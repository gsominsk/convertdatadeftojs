 /* Hal file by Sven Jansson Gnesta */
external procedure ToolWebHalGetConfCode(string,var string);
external procedure ToolWebHalWebCode(string,Boolean,Integer);
external procedure ToolWebHalWebCode2(string,Boolean);
external procedure ToolWebHalWarning();
external procedure ToolWebHalLink(Boolean,string,string);
external procedure ToolWebHalPaging(record MailVc,var LongInt,LongInt);
external procedure ToolWebHalGetDIrName(string,var string,var string,var string);
external procedure ToolWebHalTdBackground(string,string,string,string,Boolean,Boolean);
external procedure ToolWebHalPrintHeader(string);
external procedure ToolWebHalErrorMsg(integer,integer,var string,string,var Boolean,Boolean,integer,var Boolean,Boolean);
external procedure ToolWebHalAddEndTags(Integer,Integer,string);
external procedure ToolWebHalIfTrueWrite(Boolean,string,var Boolean);
external procedure ToolWebHalTrStart();
external procedure ToolWebHalTrEnd();
external procedure ToolWebHalTdEnd();
external procedure ToolWebHalTdTrEnd();
external procedure ToolWebHalTdTrTableEnd();
external procedure ToolWebHalTableEnd(Boolean);
external procedure ToolWebHalTdStart(string,string,string,string,string,string,string,string,string,Boolean);
external procedure ToolWebHalTableStart(string,string,string,string,string,string,string,string);
external procedure ToolWebHalImg(string,string,string,string,string,string,string);
external procedure ToolWebHalALinkStart(string,string);
external procedure ToolWebHalLink(Boolean,string,string);
external procedure ToolWebHalALinkEnd(Boolean);
external procedure ToolWebHalAEnd(Boolean);

global
procedure ToolWebHalTutorialParse(record MailVc Mailr, var Boolean read)
/*  ---------------------------------------------------------------------
    Description:
    A really simple variant of the same parse proc. as in the Discussion section but with less features.
    This parses the textblock in the Tutorial section. The purpose is to be
    able to show code in a nice way for the web and that it will be indented
    the right way by including [hal] "the code" [/hal]. It also gives opportunity
    to use proper html at the page by including [XHTM] "the html code" [/XHTM].
    An errormessage will be shown at the webpage if the administrator uses [hal]
    or [html] code the wrong way. For example if [hal] does not have an ending [/hal],
    [html] does not have an ending [/html] or the start and endtag is in the oppposite way.

    input args:
    Mailr   - Record of the mail in progress.

    input/output args
    read - Gives me true if there is text to parse, otherwise false and the process ends.
    --------------------------------------------------------------------- */
begin
  string 1 c;
  string 5 c2;
  string 255 errormsg;
  Boolean brack,
          slash,
          star,
          write,
          html,
          hals,
          hale,
          blocs,
          bloce,
          validf,
          errf,
          nesthalf,
          nesthtmf,
          nestslaf;
  Longint i;
  Integer j,
          res,
          cnthals,
          cnthale,
          cnthtms,
          cnthtme,
          cntslas,
          cntslae,
          mode;

  c  = "";
  i       = 0;
  res     = 0;
  mode    = 0;
  cnthals = 0; cnthale = 0; cnthtms = 0;
  cnthtme = 0; cntslas = 0; cntslae = 0;
  nesthalf = false;
  nesthtmf = false;
  validf   = true;
  errf     = false;
  write    = false;
  while (read) begin
    c = StringFromText(Mailr,i,1);
    brack = false;
    switch (c) begin
      case "": i = i - 6; read = false;
      case "[": c2 = ""; write = false; brack = true;
      case "/": c2 = ""; write = false; slash = true;
      case "*": c2 = ""; write = false; star = true;
      otherwise write = true;
    end;
    while (slash) begin
      blocs = false;
      c2 = StringFromText(Mailr,i,2);
      switch (c2) begin
        case "/*": c2 = ""; mode = 4; blocs = true; write = true; cntslas = cntslas + 1;
        otherwise c2 = ""; ; write = true; slash = false;
      end;
      if (blocs) then begin
        WebOutString("<span class=""BlockComment"">");
        slash = false;
      end;
    end;
    while (star) begin
      bloce = false;
      c2 = StringFromText(Mailr,i,2);
      switch (c2) begin
        case "*/":
          c2 = ""; i = i + 1; bloce = true; write = false; cntslae = cntslae + 1; res = mod(cntslas + cntslae,2); mode = 3;
          WebOutString("*/</span>"); star = false;
          switch (res) begin case 1: nestslaf = true; validf = false; end;
        otherwise c2 = ""; write = true; star = false;
      end;
    end;
    while (brack) begin
      hals  = false;
      hale  = false;
      html  = false;
      c2 = StringFromText(Mailr,i,5);
      switch (UpperCase(c2)) begin
        case "[HAL]": i = i + 4; c2 = ""; hals = true; write = false; cnthals = cnthals + 1; mode = 4;
        case "[/HAL": i = i + 5; c2 = ""; hale = true; write = false; cnthale = cnthale + 1; res = mod(cnthals + cnthale,2); mode = 3;
          switch (res) begin case 1: nesthalf = true; validf = false; end;
        case "[XHTM": i = i + 5; c2 = ""; write = false; html = true;  brack = false; cnthtms = cnthtms + 1;
        case "[/XHT": i = i + 6; c2 = ""; write = false; html = false; brack = false; cnthtme = cnthtme + 1; res = mod(cnthtms + cnthtme,2);
          switch (res) begin case 1: nesthtmf = true; validf = false; end;
        otherwise c2 = ""; write = true; brack = false;
      end;
      if (hals) then begin
        ToolWebHalTdTrTableEnd;
        ToolWebHalTableStart("0","100%","0","0","","","","");
        ToolWebHalTrStart;
        ToolWebHalTdStart("","","","","","top","","","",false);
        WebOutString("<br><br>");
        WebOutString("<pre>");
        brack = false;
      end;
      if (hale) then begin
        WebOutString("</pre><br>");
        ToolWebHalTdTrTableEnd;
        ToolWebHalTableStart("0","561","0","0","","","","");
        ToolWebHalTrStart;
        ToolWebHalTdStart("","","","","","top","","","",false);
        ToolWebHalImg("0","1","4","/halforum/images/spacer.gif","","","");
        brack = false;
      end;
    end;
    if (!html(and(write))and(!hals)and(c == chr(13))or(
        !html(and(write))and(!hals)and(c == chr(10)))) then begin
      WebOutString("<br />");
    end else begin
      if (!html(and(write))) then begin
        ToolWebHalWebCode(c,write,1);
      end;
    end;
    if ((html)and(write)and(!hals)) then begin
      ToolWebHalWebCode2(c,write);
    end;
    write = false;
    i = i + 1;
  end;
  ToolWebHalAddEndTags(cnthals,cnthale,"</pre><br>");
  ToolWebHalAddEndTags(cntslas,cntslae,"</span>");
  ToolWebHalErrorMsg(cntslas,cntslae,errormsg,"",validf,true,res,nestslaf,false);     if (Len(errormsg) > (220)) then begin errormsg = ""; validf = false; errf = true; end;
  ToolWebHalErrorMsg(cnthals,cnthale,errormsg,"HAL",validf,false,res,nesthalf,true);  if (Len(errormsg) > (220)) then begin errormsg = ""; validf = false; errf = true; end;
  ToolWebHalErrorMsg(cnthtms,cnthtme,errormsg,"CODE",validf,true,res,nesthtmf,false); if (Len(errormsg) > (220)) then begin errormsg = ""; validf = false; errf = true; end;
  if (!validf) then begin
    ToolWebHalWarning;
    if (errf) then begin
      errormsg = "";
      WebOutString("<br>Oups!! There are errors in the code example on this page!<br>");
      WebOutString("Please contact the administrator!");
    end else begin
      WebOutString("<br>Oups!! There are errors in the code example on this page!<br><br>" & errormsg);
      WebOutString("<br>Please contact the administrator!");
    end;
    WebOutString("</div>");
  end;
  return;
end;

global
procedure ToolWebHalTutorialShow(record MailVc Mailr,var LongInt page,LongInt looppos)
/*  ---------------------------------------------------------------------
    Description:
    the procedure that shows the tutorials,documents,Tutorial section.
    Uses paging to step between the sections.

    input args:
    Mailr   - The name of the conferance.
    looppos - the number of mails.

    input/output args:
    page - the actual page that is clicked.
    --------------------------------------------------------------------- */
begin
  Boolean read;
  string 12 code_;
  string 100 name1_,name2_,name3_;
  read = true;
  code_ = WebGetArg("dig");
  name1_ = ""; name2_ = ""; name3_ = "";
  ToolWebHalGetDIrName(code_,name1_,name2_,name3_);
  ToolWebHalPrintHeader(name3_);
  ToolWebHalTableStart("0","561","0","0","","","","");
  if (nonblank(Mailr.Header)) then begin
    ToolWebHalTrStart;
    ToolWebHalTdStart("","","","","","","","TutorialHeader","",false);  
    WebOutString(Mailr.Header);
    ToolWebHalImg("0","1","1","/halforum/images/spacer.gif","","","");
    ToolWebHalTdTrEnd;
  end;
  ToolWebHalTrStart;
  ToolWebHalTdStart("","","","","","","","","",false);
  ToolWebHalImg("0","1","9","/halforum/images/spacer.gif","","","");
  ToolWebHalTdTrTableEnd;
  ToolWebHalTableStart("0","561","0","0","","","","");
  ToolWebHalTrStart;
  ToolWebHalTdStart("","","","","","bottom","","","",false);
  if (looppos > 1) then begin
    ToolWebHalPaging(Mailr,page,looppos);
    WebOutNL;
  end;
  ToolWebHalImg("0","1","1","/halforum/images/spacer.gif","","","");
  ToolWebHalTdTrEnd;
  if (looppos > 1) then begin
    ToolWebHalTrStart;
    ToolWebHalTdStart("","","","","","","","","",false);
    ToolWebHalImg("0","1","10","/halforum/images/spacer.gif","","","");
    ToolWebHalTdTrEnd;
  end;
  ToolWebHalTrStart;
  ToolWebHalTdStart("","","","","","top","","","",false);
  ToolWebHalTutorialParse(Mailr,read);
  ToolWebHalImg("0","1","1","/halforum/images/spacer.gif","","","");
  ToolWebHalTdTrEnd;
  ToolWebHalTrStart;
  ToolWebHalTdStart
  ("","","","","","","","","",false);
  ToolWebHalImg("0","1","10","/halforum/images/spacer.gif","","","");
  ToolWebHalTdTrTableEnd;
  if (looppos > 1) then begin
    ToolWebHalPaging(Mailr,page,looppos);
  end;
  ToolWebHalTdEnd;
  return;
end;

global
procedure ToolWebHalTutorialConf(string name_,LongInt maxmail,Integer mode)
/*  ---------------------------------------------------------------------
    Description:
    Shows a specific mail from the conferance system. Reads the first mail

    input args:
    name_   - The name of the conferance.
    maxmail - the number of mails.
    mode    - not in use at the moment.

    input webargs:
    page - The first page that will be showed
    --------------------------------------------------------------------- */
begin
  record MailVc Mailr;
  string 60 ckey;
  LongInt loopcnt;
  LongInt looppos,page;
  Boolean found;
  string 60 code_;
  string 100 News;

  loopcnt = 0;
 ToolWebHalGetConfCode(name_,code_);
  ckey = "UserTime:" & code_;
  looppos = RecordsInIndex("MailVc",ckey);
  page = StringToLongint(WebGetArg("pge"));
  SetLoopPosition(Mailr,page+1);
  found = true;
  while (LoopBackKey(ckey,Mailr,1,found)) begin
    if (found) then begin
      loopcnt = loopcnt + 1;
      if (maxmail>-1) then begin
        if (loopcnt>maxmail) then begin
          found = false;
        end;
      end;
      if (found) then begin
        switch (mode) begin
          case 0: ToolWebHalTutorialShow(Mailr,page,looppos);
          case 1: ToolWebHalTutorialShow(Mailr,page,looppos);
          otherwise
        end;
      end;
    end;
  end;
  return;
end;
 /* Hal file by Sven Jansson Gnesta */