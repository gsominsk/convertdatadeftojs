global
procedure CustRouteVc_PasteCustCode(var record CustRouteVc CustRouter)
begin
  record CUVc CUr;
  
  CUr.Code = CustRouter.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    CustRouter.CustName = CUr.Name;
  end;
  
  return;
end;

global
procedure CustRouteVc_PasteDelAddrCode(var record CustRouteVc CustRouter)
begin
  record DelAddrVc DelAddrr;
  Boolean foundf;
  
  DelAddrr.DelCode = CustRouter.DelAddrCode;
  DelAddrr.CustCode = CustRouter.CustCode;
  foundf = ReadFirstMain(DelAddrr,2,true);
  if (!foundf) then begin
    DelAddrr.DelCode = CustRouter.DelAddrCode;
    DelAddrr.CustCode = "";
    foundf = ReadFirstMain(DelAddrr,2,true);
  end;
  if (foundf) then begin
    CustRouter.Comment = DelAddrr.Comment;
  end;
  
  return;
end;

global
function string 20 FindCustRoute(string CustCode,string Location,string DelAddrCode)
begin
  string 20 res;
  record CustRouteVc CustRouter;
  Boolean foundf;
  record CUVc CUr;
  
  CUr.Code = CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    res = CUr.Sorting;
    CustRouter.CustCode = CustCode;
    CustRouter.FromLocation = Location;
    CustRouter.DelAddrCode = DelAddrCode;
    foundf = ReadFirstMain(CustRouter,3,true);
    if (blank(DelAddrCode) and !foundf) then begin
      CustRouter.CustCode = CustCode;
      CustRouter.FromLocation = Location;
      foundf = ReadFirstMain(CustRouter,2,true);
    end;
    if (foundf) then begin
      res = CustRouter.DelRoute;
    end;
  end;
  
  FindCustRoute = res;
  return;
end;

global
function string 20 FindCustRouteByLocation(string FromLocation,string ViaLocation,string ToLocation)
begin
  string 20 res;
  record CustRouteVc CustRouter;
  
  CustRouter.FromLocation = FromLocation;
  CustRouter.ViaLocation = ViaLocation;
  CustRouter.ToLocation = ToLocation;
  if (ReadFirstKey("AllLocation",CustRouter,3,true)) then begin
    res = CustRouter.DelRoute;
  end;
  
  FindCustRouteByLocation = res;
  return;
end;
