external function Boolean GetItemNameStr(Integer,var string,string,string,string);
external procedure InternMovMultiLineText(record InternMovVc,record INVc,string,Integer);

procedure GetRentINNameStr(record RentINVc RentINr,string langcod2,var string comment)
begin
  row RentINVc RentINrw;
  Integer rwcnt,i;

  comment = RentINr.Name;
  rwcnt = MatRowCnt(RentINr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(RentINr,1,RentINrw);
    if (RentINrw.LangCode==langcod2) then begin
      comment = RentINrw.Text;
    end;
  end;
  return;
end;

global
function Boolean PasteInternMovItemAndSerial(var record InternMovVc Intmovr,Integer rownr,var string inwarning)
BEGIN
  record INVc INr;
  record RentINVc RentINr;
  row InternMovVc Intmovrw;
  string 255 tstr;
  Boolean res;
  Boolean rentinf;

  res = false;
  MatRowGet(Intmovr,rownr,Intmovrw);
  if (nonblank(Intmovrw.SerialNr)) then begin
    RentINr.Code = Intmovrw.ArtCode;
    RentINr.SerialNr = Intmovrw.SerialNr;
    rentinf = ReadFirstMain(RentINr,2,true);
    if (rentinf==false and blank(Intmovrw.ArtCode)) then begin
      RentINr.SerialNr = Intmovrw.SerialNr;
      rentinf = ReadFirstKey("SerialNr",RentINr,1,true);
      if (rentinf) then begin
        Intmovrw.ArtCode = RentINr.Code;
        Intmovrw.Quant = 1;
      end;
    end;
    if (rentinf) then begin
      GetRentINNameStr(RentINr,Intmovr.LangCode,tstr);
      Intmovrw.Spec = tstr;
      MatRowPut(Intmovr,rownr,Intmovrw);
      res = true;
    end;
  end;
  if (res==false) then begin
    INr.Code = Intmovrw.ArtCode;
    if (ReadFirstMain(INr,1,true)) then begin
      if (GetItemNameStr(1,tstr,Intmovr.LangCode,INr.Name,INr.Code)) then begin end;
      Intmovrw.Spec = tstr;
      MatRowPut(Intmovr,rownr,Intmovrw);
      res = true;
      if (INr.ExplodeRec==0) then begin
        InternMovMultiLineText(Intmovr,INr,Intmovr.LangCode,rownr);
      end;
      inwarning = INr.WarnText1;
      if (INr.ItemType!=1 and blank(inwarning)) then begin
        inwarning = USetStr(1301);
      end;
    end;
  end;
  PasteInternMovItemAndSerial = res;
  RETURN;
END;

global
function Boolean InternMovVc_PasteArtCode(var record InternMovVc Intmovr,Integer rownr,var string inwarning)
begin
  Boolean res;
  
  res = PasteInternMovItemAndSerial(Intmovr,rownr,inwarning);
  InternMovVc_PasteArtCode = res;
end;

global
function Boolean InternMovVc_PasteSerialNr(var record InternMovVc Intmovr,Integer rownr)
begin
  string 200 warning;
  Boolean res;
  
  res = PasteInternMovItemAndSerial(Intmovr,rownr,warning);
  InternMovVc_PasteSerialNr = res;
end;
