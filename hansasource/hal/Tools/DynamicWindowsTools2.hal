external procedure FindCCAccountID(string,string,Integer,var record CCAccountIDVc,Integer);

global
function Boolean CCTestAddress()
begin
  Boolean res;
  Integer wn;
  record IVVc IVr;
  record IVCashVc IVCashr;
  record IVCashVc Jobr;
  record IPVc IPr;
  row IPVc IPrw;
  record RestAccVc RestAccr;
  string 255 currency;
  record CCAccountIDVc CCAccountIDr;  

  wn = CurWindow;
  switch (GetWindowFileName(wn)) begin
    case "IVDClass": 
      GetWindowRecord(wn,IVr);
      currency = IVr.CurncyCode;
    case "RestAccDClass": 
      GetWindowRecord(wn,RestAccr);
      currency = "";
    case "RestAccOldDClass": 
      GetWindowRecord(wn,RestAccr);
      currency = "";
    case "IVCashDClass": 
      GetWindowRecord(wn,IVCashr);
      currency = IVCashr.CurncyCode;
    case "NPTSIVCashDClass":
      GetWindowRecord(wn,IVCashr);
      currency = IVCashr.CurncyCode;
    case "POSPIVCashDClass": 
      GetWindowRecord(wn,IVCashr);
      currency = IVCashr.CurncyCode;
    case "JobDClass": 
      GetWindowRecord(wn,Jobr);
      currency = Jobr.CurncyCode;
    case "IPDClass": 
      GetWindowRecord(wn,IPr);
      MatRowGet(IPr,WindowActiveRow(wn),IPrw)
      currency = IPrw.BankCurncy;
  end;  
  FindCCAccountID(currency,"",0,CCAccountIDr,kCardHolderAny);
  res = CCAccountIDr.TestAddress!=0;
  CCTestAddress = res;
  return;
end;

global
function Boolean ResPlan_ProdOperations()
begin
  Boolean res;
  record ResPlanBlock RPb;
  
  BlockLoad(RPb);
  res = RPb.ProdOperations;
  ResPlan_ProdOperations = res;
  return;
end;

global
function Boolean ResPlan_Rental()
begin
  Boolean res;
  record ResPlanBlock RPb;
  
  BlockLoad(RPb);
  res = RPb.Rental;
  ResPlan_Rental = res;
  return;
end;

global
function Integer ResourceWindowResourceType()
begin
  Integer res,wn;
  record RcVc RepSpec;
  
  if (CurrentCompilingWindowFileName=="RcVc") then begin
    wn = CurWindow;
    if (wn>0) then begin
      GetWindowRecord(CurWindow,RepSpec)
      res = RepSpec.flags[21];
    end;
  end;
  ResourceWindowResourceType = res;
  return;
end;

global
function Boolean CompaniesInClientMode()
begin
  record CompaniesBlock Compb;
  row CompaniesBlock Compbrw;
  Integer rwcnt,i;
  Boolean res;

  BlockLoad(Compb);
  rwcnt = MatRowCnt(Compb);
  for (i = 0;i<rwcnt;i=i+1) begin
    MatRowGet(Compb,i,Compbrw);
    LogText(0,"Compbrw.TCPIP:" & Compbrw.TCPIP & " Compbrw.ServerUUID:" & Compbrw.ServerUUID);
    if (nonblank(Compbrw.TCPIP) or blankUUID(Compbrw.ServerUUID)==false) then begin
      res = true;
    end;
  end;
  CompaniesInClientMode = res;
end;
