external procedure SetDelAddr(string,string,var string,var string,var string,var string,var string,var string,var string,var string,var string,var string,var Integer,var string);
external procedure DispatchSumup(var record DispatchVc);
external procedure SetSalesMan(var string,string);
external function Boolean GetCustAndBal(var record CUVc,var val,var val,Integer,Integer,Integer,Integer,Integer,Integer,var Boolean);
external function Boolean GetItemNameStr(Integer,var string,string,string,string);
external procedure DispatchMultiLineText(record DispatchVc,record INVc,string,Integer);
external procedure CalcSum(val,val,val,val,var val,Integer);

procedure GetRentINNameStr(record RentINVc RentINr,string langcod2,var string comment)
begin
  row RentINVc RentINrw;
  Integer rwcnt,i;

  comment = RentINr.Name;
  rwcnt = MatRowCnt(RentINr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(RentINr,1,RentINrw);
    if (RentINrw.LangCode==langcod2) then begin
      comment = RentINrw.Text;
    end;
  end;
  return;
end;

global
function Boolean DispatchVc_PasteArtCode(var record DispatchVc Dispr,Integer rownr,var string inwarning)
BEGIN
  record INVc INr;
  record RentINVc RentINr;
  row DispatchVc Disprw;
  string 255 tstr;
  Boolean res;

  res = false;
  MatRowGet(Dispr,rownr,Disprw);
  if (nonblank(Disprw.SerialNr)) then begin
    RentINr.Code = Disprw.ArtCode;
    RentINr.SerialNr = Disprw.SerialNr;
    if (ReadFirstMain(RentINr,2,true)) then begin
      GetRentINNameStr(RentINr,Dispr.LangCode,tstr);
      Disprw.Spec = tstr;
      Disprw.Value = RentINr.Value;
      MatRowPut(Dispr,rownr,Disprw);
      res = true;
    end;
  end;
  if (res==false) then begin
    INr.Code = Disprw.ArtCode;
    if (ReadFirstMain(INr,1,true)) then begin
      if (GetItemNameStr(1,tstr,Dispr.LangCode,INr.Name,INr.Code)) then begin end;
      Disprw.Spec = tstr;
      Disprw.Value = INr.InPrice;
      MatRowPut(Dispr,rownr,Disprw);
      res = true;
      if (INr.ExplodeRec==0) then begin
        DispatchMultiLineText(Dispr,INr,Dispr.LangCode,rownr);
      end;
      inwarning = INr.WarnText1;
    end;
  end;
  DispatchVc_PasteArtCode = res;
  RETURN;
END;

global
function Boolean DispatchVc_PasteSerial(var record DispatchVc Dispr,Integer rownr)
begin
  record RentINVc RentINr;
  row DispatchVc Disprw;
  string 255 tstr;
  Boolean res;

  res = false;
  MatRowGet(Dispr,rownr,Disprw);
  if (nonblank(Disprw.SerialNr)) then begin
    RentINr.Code = Disprw.ArtCode;
    RentINr.SerialNr = Disprw.SerialNr;
    if (ReadFirstMain(RentINr,2,true)) then begin
      GetRentINNameStr(RentINr,Dispr.LangCode,tstr);
      Disprw.Spec = tstr;
      Disprw.Value = RentINr.Value;
      MatRowPut(Dispr,rownr,Disprw);
      res = true;
    end;
  end;
  DispatchVc_PasteSerial = res;
  return;
end;

global
function Boolean PasteCust2InDispatch(var record DispatchVc Dispr,Boolean updatall,var string warntext)
BEGIN
  Boolean found,limitdaysf;
  record CUVc CUr;
  record INVc INr;
  val x,pr,bal,limit;
  string 255 tstr;

  CUr.Code = Dispr.CustCode;
  if (GetCustAndBal(CUr,limit,bal,0,0,0,0,0,0,limitdaysf)) then begin
    found = true;
    Dispr.Addr0 = CUr.Name;
    Dispr.ClientContact = CUr.Person;
    SetSalesMan(tstr,CUr.SalesMan);
    Dispr.SalesMan = tstr;
    Dispr.LangCode = CUr.LangCode;
    Dispr.ShipMode = CUr.ShipMode;
    Dispr.ClientContact = CUr.Person;
    if (nonblank(CUr.DelAddr0)) then begin
      Dispr.Addr0 = CUr.Name;
      Dispr.Addr1 = CUr.DelAddr0;
      Dispr.Addr2 = CUr.DelAddr1;
      Dispr.Addr3 = CUr.DelAddr2;
      Dispr.DelAddr3 = CUr.DelAddr3;
      Dispr.DelAddr4 = CUr.DelAddr4;
    end else begin
      Dispr.Addr0 = CUr.Name;
      Dispr.Addr1 = CUr.InvAddr0;
      Dispr.Addr2 = CUr.InvAddr1;
      Dispr.Addr3 = CUr.InvAddr2;
      Dispr.DelAddr3 = CUr.InvAddr3;
      Dispr.DelAddr4 = CUr.InvAddr4;
    end;
    if (updatall) then begin
      DispatchSumup(Dispr);
      if (nonblank(CUr.WarnText1)) then begin
        warntext = CUr.WarnText1;
      end;
    end;
  end;
  PasteCust2InDispatch = found;
  RETURN;
END;

global
procedure DispatchVc_PasteSite(var record DispatchVc Dispp)
begin
  string 255 name,a0,a1,a2,a3,a4,country,region,taxtemplatecode,vatcode,dummy;
  Integer exportflag;
  
  SetDelAddr(Dispp.CustCode,Dispp.Site,name,a0,a1,a2,a3,a4,country,region,taxtemplatecode,vatcode,exportflag,dummy);
  Dispp.Addr1 = name;
  Dispp.Addr2 = a0;
  Dispp.Addr3 = a1;
  Dispp.DelAddr3 = a2;
  Dispp.DelAddr4 = a3;
  return;
end;
