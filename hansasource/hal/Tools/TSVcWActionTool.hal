external function Boolean GetFirstItem(var string,var record INVc);
external procedure GetPRName(string,var string);
external procedure PasteTSObjects(string,string,string,record UserVc,Boolean,record INVc,Boolean,var string);
external procedure GetEmplName(string,var string);

global
procedure TSVc_PasteEMCode(var record TSVc TSp,Integer rownr)
begin
  string 255 tstr;
  record INVc INr;
  record UserVc Userr;
  row TSVc TSrw;
  
  MatRowGet(TSp,rownr,TSrw);
  PasteTSObjects(TSrw.EMCode,TSrw.PRCode,TSrw.ArtCode,Userr,false,INr,false,tstr);
  TSrw.Objects = tstr;
  if (rownr==0) then begin
    TSp.SalesMan = TSrw.EMCode;
  end;
  GetEmplName(TSrw.EMCode,tstr);
  TSrw.EMName = tstr;
  MatRowPut(TSp,rownr,TSrw);
  return;
end;

global
procedure TSVc_PastePRCode(var record TSVc TSp,Integer rownr)
begin
  string 255 tstr;
  Integer i,rwcnt;
  record UserVc Userr;
  row UserVc Userrw;
  record INVc INr;
  row TSVc TSrw;
  
  MatRowGet(TSp,rownr,TSrw);
  Userr.Code = TSrw.EMCode;
  if (ReadFirstMain(Userr,1,true)) then begin
    rwcnt = MatRowCnt(Userr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Userr,i,Userrw);
      if (Userrw.Code==TSrw.PRCode) then begin
        TSrw.PRCode = Userrw.PRCode;
      end;
    end;
    PasteTSObjects(TSrw.EMCode,TSrw.PRCode,TSrw.ArtCode,UserR,false,INr,false,tstr);
    TSrw.Objects = tstr;
  end;
  GetPRName(TSrw.PRCode,tstr);  
  TSrw.PRName = tstr;
  MatRowPut(TSp,rownr,TSrw);
  return;
end;

global
procedure TSDEnterSpec(var record TSVc TSp,Integer rownr,string fieldname)
BEGIN
  row TSVc TSrw,TS1rw;
  Integer i,rwcnt;
  record INVc INr;
  record UserVc Userr;
  row UserVc Userrw;
  record EGVc EGr;
  row EGVc EGrw;
  Boolean changedf;
  string 255 tstr;

  if (rownr<0) then begin
    goto LTSDEnterSpec;
  end;
  
//  if (fn>=sf) then begin
    MatRowGet(TSp,rownr,TSrw);
    if (rownr>0) then begin
      MatRowGet(TSp,rownr-1,TS1rw);
    end;
    switch (fieldname) begin
        case "date": // Date 
          if (blankdate(TSrw.date)) then begin
            if (rownr>0) then begin
              TSrw.date = TS1rw.date;
            end;
          end;
        case "EMCode": // Person 
          if (blank(TSrw.EMCode)) then begin
            if (rownr>0) then begin
              TSrw.EMCode = TS1rw.EMCode;
              Userr.Code = TSrw.EMCode;
              if (ReadFirstMain(Userr,1,true)) then begin
                TSrw.EMName = Userr.Name;
              end;  
            end;
          end;
        case "PRCode": // PRCode 
          if (blank(TSrw.PRCode)) then begin
            if (rownr>0) then begin
              TSrw.PRCode = TS1rw.PRCode;
              TSrw.PRName = TS1rw.PRName;
              changedf = true;
            end;
          end;
          if (changedf) then begin       
            Userr.Code = TSrw.EMCode;
            if (ReadFirstMain(Userr,1,true)) then begin
              rwcnt = MatRowCnt(Userr);
              for (i=0;i<rwcnt;i=i+1) begin
                MatRowGet(UserR,i,Userrw);
                if (Userrw.Code==TSrw.PRCode) then begin
                  TSrw.PRCode = Userrw.PRCode;
                end;
              end;
            end;
          end;
        case "ArtCode": // Item 
          if (blank(TSrw.ArtCode)) then begin
            if (rownr>0) then begin
               TSrw.ArtCode = TS1rw.ArtCode;
               TSrw.ItemType = TS1rw.ItemType;
               MatRowPut(TSp,rownr,TSrw);
               changedf = true;
            end;
          end;
          if (changedf) then begin
            Userr.Code = TSrw.EMCode;
            if (ReadFirstMain(Userr,1,true)) then begin
              EGr.Code = Userr.JobGroup;
              if (ReadFirstMain(EGr,1,true)) then begin
                rwcnt = MatRowCnt(EGr);
                for (i=0;i<rwcnt;i=i+1) begin
                  MatRowGet(EGr,i,EGrw);
                  if (EGrw.Code==TSrw.ArtCode) then begin
                    TSrw.ArtCode = EGrw.ArtCode;
                  end;
                end;
              end;
            end;
            if (GetFirstItem(TSrw.ArtCode,INr)) then begin
              if (INr.ItemType==3) then begin
                TSrw.ItemType = INr.ItemType;
              end else begin
                TSrw.ItemType = 0;
              end;
              TSrw.ArtName = INr.Name;
              PasteTSObjects(TSrw.EMCode,TSrw.PRCode,TSrw.ArtCode,Userr,false,INr,false,tstr);
              TSrw.Objects = tstr;
            end;  
          end;
      end;
      MatRowPut(TSp,rownr,TSrw);      
//  end;
LTSDEnterSpec:;
  RETURN;
END;

global
function Boolean TSVc_PasteArtCode(var record TSVc TSp,Integer rownr,var string inwarn)
begin
  Boolean res;
  string 255 tstr,item;
  record INVc INr;
  record UserVc Userr;
  record EGVc EGr;
  row EGVc EGrw;
  row TSVc TSrw;
  Integer i,rwcnt;
  
  res = true;
  MatRowGet(TSp,rownr,TSrw);
  item = TSrw.ArtCode;
  if (blank(item)) then begin
    if (rownr>0) then begin
      TSDEnterSpec(TSp,rownr,"ArtCode");
      MatRowGet(TSp,rownr,TSrw);
      item = TSrw.ArtCode;
    end;
  end;  
  Userr.Code = TSrw.EMCode;
  if (ReadFirstMain(Userr,1,true)) then begin
    EGr.Code = Userr.JobGroup;
    if (ReadFirstMain(EGr,1,true)) then begin
      rwcnt = MatRowCnt(EGr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(EGr,i,EGrw);
        if (EGrw.Code==item) then begin
          TSrw.ArtCode = EGrw.ArtCode;
          item = EGrw.ArtCode;
          MatRowPut(TSp,rownr,TSrw);
        end;
      end;
    end;
  end;
  if (GetFirstItem(item,INr)) then begin
    inwarn = INr.WarnText1;
    MatRowGet(TSp,rownr,TSrw);
    TSrw.ArtCode = item;
    if (INr.ItemType==3) then begin
      TSrw.ItemType = INr.ItemType;
    end else begin
      TSrw.ItemType = 0;
    end;
    TSrw.ArtName = INr.Name;
    PasteTSObjects(TSrw.EMCode,TSrw.PRCode,TSrw.ArtCode,Userr,false,INr,false,tstr);
    TSrw.Objects = tstr;
    MatRowPut(TSp,rownr,TSrw);
  end else begin
    res = false;
  end;
  TSVc_PasteArtCode = res;
  return;
end;