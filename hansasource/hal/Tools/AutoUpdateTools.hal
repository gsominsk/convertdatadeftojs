
function string 255 getserverIP()
begin
  string 255 res;
  
  res = PublicHostName;
  if (res=="0.0.0.0") then begin 
    res = ServerIPAddress;
  end;
  getserverIP = res;
  return;
end;

function string 255 GetWebClientLink()
begin
  Integer rwcnt,port;
  record CompaniesBlock CompRec;
  row CompaniesBlock comprw;
  string 255 res;

  BlockLoad(CompRec);
  rwcnt = MatRowCnt(CompRec);
  if ((CurrentCompany<=rwcnt) and (CurrentCompany>0)) then begin
    MatRowGet(CompRec,CurrentCompany-1,comprw);
    port = ServerHttpPort;
    switch (port) begin
      case 80:
        if (ServerMode) then begin 
          res = "http://" & getserverIP & "/startwebclient";
        end else begin 
          res = "http://" & comprw.TCPIP & "/startwebclient";
        end;
      otherwise
        if (ServerMode) then begin 
          res = "http://" & getserverIP & ":" & port & "/startwebclient";
        end else begin 
          res = "http://" & comprw.TCPIP & ":" & port & "/startwebclient";
        end;
    end;
  end;
  GetWebClientLink = res;
  return;
end;

global
procedure OpenWebClient()
begin
  string 255 tstr,ipstr;
  Integer wn;
  string 255 wname;
  record RcVc RepSpec;
  record LocalLoginOptBlock LOb;

  tstr = GetWebClientLink;
  if (blank(tstr)) then begin
    wn = CurWindow;
    wname = GetWindowClass(wn);
    if (wname=="IncompatibleOlderServerAppStoreWClass" or wname=="IncompatibleNewerServerAppStoreWClass" or wname=="IncompatibleServerAppStoreWClass") then begin
      GetWindowRecord(wn,RepSpec);
      if (NonBlank(RepSpec.f2)) then begin
        ipstr = RepSpec.f2;
      end else begin
        wn = FindWindow("ServerAddressTClass");
        if (wn>0) then begin
          GetWindowRecord(wn,LOb);
        end else begin
          BlockLoad(LOb);
        end;
        if (NonBlank(LOb.ServerIP)) then begin
          ipstr = LOb.ServerIP & ":";
          if (ServerHttpPort>0) then begin
            ipstr = ipstr & ServerHttpPort;
          end else begin
            ipstr = ipstr & RepSpec.long1;
          end;
        end;
      end;
      if (nonblank(ipstr)) then begin
        tstr = "http://" & ipstr & "/startwebclient";
      end;
    end;
  end;

  if (NonBlank(tstr)) then begin
    OpenWebBrowser(tstr);
  end else begin
    MessageBox(23090,"");
  end;
end;

global
procedure AddWebClientLink(var record ActVc Acptr)
begin
  row ActVc Acptrw;
  string 255 tstr;
  boolean testf;
  
  testf = ServerMode;
  if (testf==false) then begin 
    goto LAddWebClientLink;
  end;
  tstr = GetWebClientLink;
  if (NonBlank(tstr)) then begin 
    Acptrw.Text = USetStr(37450);
    MatRowInsert(Acptr,MatRowCnt(Acptr),Acptrw);
    Acptrw.Text = tstr;
    MatRowInsert(Acptr,MatRowCnt(Acptr),Acptrw);  
  end;
LAddWebClientLink:;
  return;
end;