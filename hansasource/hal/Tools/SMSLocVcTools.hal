remote updating function Boolean SaveSMSLocInServer(record SMSLocVc);
remote updating procedure UpdateChatFilterFromChatNameToContacts(var record ChatFilterVc);
remote updating procedure SaveSMSLocInServerAsync(record SMSLocVc);

global
updating procedure CopySMSLocToServer(record SMSLocVc SMSLocr,Boolean encf)
begin
  if (encf) then begin
    SMSLocr.Encryptedf = 1;
  end;
  SMSLocr.Copiedf = kMessageCopyStatusSent;
  if (RecordStore(SMSLocr,true)) then begin
  end;

  SetGlobalLongInt("SkypeMsgOut",GetGlobalLongInt("SkypeMsgOut")+1);
  asyncremote2.SaveSMSLocInServerAsync(SMSLocr);
  return;
end;

global
updating procedure OpenChatFilterWindow(record ChatFilterVc ChatFilterr,Boolean newrecf)
begin
  string 255 tstr;
  UpdateChatFilterFromChatNameToContacts(ChatFilterr);
  if (newrecf==false) then begin
    RecordStore(ChatFilterr,true);
  end;
  OpenWindow("SMSFilterDClass",0,0,"","",ChatFilterr);
  if (newrecf==false) then begin
      tstr = USetStr(20999);
      Trace("",tstr);
  end;
end;

function Boolean SkypeSyncLoop(LongInt starttick)
begin
  Boolean res;
  
  res = true;
  
  if (SkypeTurboMode) then begin
    if (GetGlobalLongInt("SkypeMsgOut")>500) then begin res = false; end;
  end else begin
    if (GetGlobalLongInt("SkypeMsgOut")>20) then begin res = false; end;
    if (GetCurTick>(starttick + 200)) then begin res = false; end;
  end;
  
  SkypeSyncLoop = res;
  return;
end;

global
updating procedure IdleCopySMSLocToServer(string arg)
begin
  record SMSLocVc SMSLocr;
  LongInt starttick;
  
  if (GetGlobalBoolean("SkypeIdleInitDone")==false) then begin
    SMSLocr.Copiedf = kMessageCopyStatusSent;
    while (LoopKey("Copiedf",SMSLocr,1,true)) begin
      if (SMSLocr.Copiedf==kMessageCopyStatusSent) then begin
        SMSLocr.Copiedf = kMessageCopyStatusNotSent;
        if (RecordStore(SMSLocr,true)) then begin
        end;
      end;
    end;
    SetGlobalBoolean("SkypeIdleInitDone",true);
  end;
  SMSLocr.Copiedf = kMessageCopyStatusNotSent;
  if (ReadFirstKey("Copiedf",SMSLocr,1,true)) then begin
    CopySMSLocToServer(SMSLocr,true);
    
    starttick = GetCurTick;
    while (LoopMain(SMSLocr,1,true) and SkypeSyncLoop(starttick)) begin
      if (SMSLocr.Copiedf==kMessageCopyStatusNotSent) then begin
        CopySMSLocToServer(SMSLocr,true);
      end;
    end;
    
    ChangeTaskTiming("CopyMessagesToServer",1);
  end else begin
    ChangeTaskTiming("CopyMessagesToServer",60);
  end;
end;

global
updating procedure SaveSMSLocInServerReply(Boolean savedf,LongInt SerNr)
begin
  record SMSLocVc SMSLocr;

  SetGlobalLongInt("SkypeMsgOut",GetGlobalLongInt("SkypeMsgOut")-1);
  if (savedf) then begin
    SMSLocr.SerNr = SerNr;
    if (ReadFirstMain(SMSLocr,1,true)) then begin
      SMSLocr.Copiedf = kMessageCopyStatusReceived;
      if (RecordStore(SMSLocr,true)) then begin
        AddSkypeSynced(1);
      end;
    end else begin
    end;
  end;
end;

global
updating procedure StoreSkypeProgress(LongInt Total)
begin
  record MsgImportProgressVc MsgImportProgressr;

  MsgImportProgressr.UserCode = CurrentUser;
  MsgImportProgressr.Total = Total;
  if (RecordStore(MsgImportProgressr,true)) then begin
  end;
end;

