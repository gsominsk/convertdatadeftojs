external procedure ClientLogoFileDownload(string,string);

procedure ExportAreaToFile(string filename,Area a)
begin
  SetFileOnServer(true);
  CreateFile(filename);
  CloseFile;
  OutAreaToFile(a,filename,0);
  CloseFile;
  SetFileOnServer(false);
  return;
end;

global
function longint RemoteGetLogoFile(var area a_logo,string filename)
begin
  longint res;
  
  res = 0;
  if (ServerMode==false) then begin 
    goto LRemoteGetLogoFile;   
  end;
  if (FileExists(filename)) then begin 
    AddFileToArea(filename,a_logo,false);
    res = 1;
  end;
 LRemoteGetLogoFile:; 
 RemoteGetLogoFile = res;
  return;
end;

global
function longint RemoteSaveAndDistributetoClients(area a_logo,string filehash,string filename)
begin
  LongInt res;
  string 255 serverfilehash;
  
  res = 38402;
  if (ServerMode==false) then begin 
    goto LRemoteSaveAndDistributetoClients;   
  end;
  if (FileExists(filename)) then begin 
    if (blank(filehash)) then begin 
      res = 38406;
      goto LRemoteSaveAndDistributetoClients;
    end;
    serverfilehash = GetFileMD5(filename);
    if (serverfilehash==filehash) then begin 
      res = 38405;
      goto LRemoteSaveAndDistributetoClients;      
    end;
  end;
  ExportAreaToFile(filename,a_logo);
  if (FileExists(filename)) then begin 
    serverfilehash = GetFileMD5(filename);
    if (serverfilehash==filehash) then begin 
      AllClientsRemoteAsync.ClientLogoFileDownload(filename,filehash);
      res = 38401;      
    end;    
  end;

LRemoteSaveAndDistributetoClients:
  RemoteSaveAndDistributetoClients = res;
  return;
end;

global 
procedure DoLoginClientLogoFileDownloadCheck()
begin
  string 255 logo1,logo2;
  
  if (ServerMode==false) then begin 
    goto LDoLoginClientLogoFileDownloadCheck;   
  end;

  logo1 = "LOGO.BMP";
  logo2 = "LOGO.PDF";

  if (FileExists(logo1)) then begin 
    ClientRemoteAsync.ClientLogoFileDownload(logo1,GetFileMD5(logo1));
  end;
  if (FileExists(logo2)) then begin 
    ClientRemoteAsync.ClientLogoFileDownload(logo2,GetFileMD5(logo2));
  end;

LDoLoginClientLogoFileDownloadCheck:;
end;
