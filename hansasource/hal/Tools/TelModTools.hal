external procedure GetTelCallType( string, var string ); //LO

global 
updating Procedure SaveTelCall(Date TransDate,Time TransTime,String Nr,Time durat,String ExtNo,String AccessCode,String Trunk,Integer Direction,Integer Rings,Integer Status,Integer Pulses)
begin
  Record TelCallsVc Calls;
  String 10 cType;
  Record ResComVc ResComr;
  Record PhoneBlock PhoneBlockr;
  Boolean savef;
  
  Blockload(PhoneBlockr);
  if (PhoneBlockr.PbxChkBit==0) then begin
    savef = true;
  end else begin
    savef = false;
    ResComr.Internal = ExtNo;
    if (ReadFirstMain(ResComr,1,true)) then begin
      if (ResComr.SaveCons<>2) then begin
        savef = true;
      end;
    end;  
  end;
  if (savef) then begin
    GetTelCallType( Nr, cType );
    Calls.TransDate = TransDate;
    Calls.TransTime = TransTime;
    Calls.Number    = Nr;
    Calls.Duration  = durat; 
    Calls.ExtNo     = ExtNo;
    Calls.RegType   = 0;
    Calls.Direction = Direction;
    // Calls.Person    = AccessCode;
    Calls.TrunkNr     = Trunk;
    Calls.Pulses    = Pulses;
    Calls.Status    = Status;
    Calls.Rings     = Rings;
    Calls.Type      = cType;
    Calls.SerNr = NextSerNr("TelCallsVc",Calls.TransDate,-1,false,""); //LO
    if RecordInsert( Calls, false ) then begin end;   
  end;
  return;
end;

global
procedure CleanUpTelNr(string Phone,var string CleanPh)
begin
  Integer length,j;
  String 2 a;
  
  CleanPh = "";
  length  = len(Phone);
  for (j=0;j<length;j=j+1) begin
    a = mid(Phone,j,1);  
    if ((a<>"-") and (a<>"/")) then begin
      CleanPh = CleanPh & a;
    end;
  end;
  return;
end;
