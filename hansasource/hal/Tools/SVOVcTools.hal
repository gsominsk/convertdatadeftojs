external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function Boolean GetPurchaseItem(string,string,var record PIVc);
external function Boolean PasteVEInPO(var record POVc,Boolean);
external procedure AddPORow(record POVc,record INVc,LongInt,string,Integer,record PIVc,val,string);
external procedure POSumup(var record POVc);

function Integer FindPO(var array record POVc aPOr,var Integer cnt,string VECode,string Objects)
begin
  Integer res,i;
  record POVc POr;
  Boolean foundf;
  
  for (i=0;i<cnt;i=i+1) begin
    POr = aPOr[i];
    if (POr.VECode==VECode) then begin
      res = i;
      i = cnt;
      foundf = true;
    end;
  end;
  if (!foundf) then begin
    RecordNew(POr);
    POr.VECode = VECode;
    PasteVEInPO(POr,false);
    POr.Objects = Objects;
    aPOr[cnt] = POr;
    res = cnt;
    cnt = cnt + 1;
  end;
  
  FindPO = res;
  return;
end;

function LongInt PasteSVORowToPO(record SVOVc SVOr,Integer rownr,var array record POVc aPOr,var Integer cnt,var string msg)
begin
  LongInt res;
  row SVOVc SVOrw;
  record INVc INr;
  record PIVc PIr;
  record POVc POr;
  Integer i;
  
  res = 0;
  MatRowGet(SVOr,rownr,SVOrw);
  if (SVOrw.ItemKind==1) then begin
    if (ReadFirstItem(SVOrw.ArtCode,INr,true,true)) then begin
      if (!GetPurchaseItem(INr.Code,"",PIr)) then begin
        res = 20863;
        msg = " " & SVOrw.ArtCode;
        goto LPasteSVORowToPO;
      end;
      i = FindPO(aPOr,cnt,PIr.VECode,SVOr.Objects);
      POr = aPOr[i];
      AddPORow(POr,INr,-1,SVOrw.ArtCode,0,PIr,SVOrw.Quant,SVOrw.Objects);
      aPOr[i] = POr;
    end;
  end;
  POSumup(POr);
  
LPasteSVORowToPO:;
  PasteSVORowToPO = res;
  return;
end;

global
updating function LongInt RecordAction_raPasteSVOInPO(record SVOVc SVOr,var record POVc POr,var Integer cnt,var string msg)
begin
  LongInt res,r;
  Integer i,rwcnt;
  array record POVc aPOr;
  
  res = 0;
  cnt = 0;
  msg = "";
  rwcnt = MatRowCnt(SVOr);
  for (i=0;i<rwcnt;i=i+1) begin
    r = PasteSVORowToPO(SVOr,i,aPOr,cnt,msg);
    if (r!=0) then begin
      res = r;
      goto LRecordAction_raPasteSVOInPO;
    end;
  end;
  for (i=0;i<cnt;i=i+1) begin
    POr = aPOr[i];
    POr.SerNr = NextSerNr("POVc",POr.TransDate,-1,false,"");
    if (POr.SerNr==-1) then begin 
      res = 1747;
      goto LRecordAction_raPasteSVOInPO; 
    end;
    POr.SVONr = SVOr.SerNr;
    if (MatRowCnt(POr)>0) then begin
      if (RecordStore(POr,false)) then begin
        CreateRecordLink(POr,CurrentCompany,SVOr,CurrentCompany);  
        CreateRecordLink(SVOr,CurrentCompany,POr,CurrentCompany);  
      end;
    end;
    aPOr[i] = POr;
  end;
  if (cnt>0) then begin
    POr = aPOr[0];
  end;
  
LRecordAction_raPasteSVOInPO:;
  RecordAction_raPasteSVOInPO = res;
  return;
end;
