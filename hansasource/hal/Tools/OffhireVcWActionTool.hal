external procedure OffHireSumUp(var record OffHireVc);
external procedure SetSalesMan(var string,string);
external function Boolean GetCustAndBal(var record CUVc,var val,var val,Integer,Integer,Integer,Integer,Integer,Integer,var Boolean);
external function Boolean GetItemNameStr(Integer,var string,string,string,string);
external procedure OffHireMultiLineText(record OffHireVc,record INVc,string,Integer);

procedure GetRentINNameStr(record RentINVc RentINr,string langcod2,var string comment)
begin
  row RentINVc RentINrw;
  Integer rwcnt,i;

  comment = RentINr.Name;
  rwcnt = MatRowCnt(RentINr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(RentINr,1,RentINrw);
    if (RentINrw.LangCode==langcod2) then begin
      comment = RentINrw.Text;
    end;
  end;
  return;
end;

global
function Boolean PasteOffHireItemAndSerial(record OffHireVc Offhr,Integer rownr,var string inwarning)
begin
  record INVc INr;
  record PUVc PUr;
  record RentINVc RentINr;
  record CostAccBlock CostAccRec;
  row PUVc PUrw;
  row OffHireVc Offhrw;
  string 255 tstr;
  Boolean res,fifofound;
  Integer rwcnt,i;
  string 20 loc2;
  val totval;
  Boolean rentinf;

  res = false;
  fifofound = false;
  MatRowGet(Offhr,rownr,Offhrw);
  if (nonblank(Offhrw.SerialNr)) then begin
    RentINr.Code = Offhrw.ArtCode;
    RentINr.SerialNr = Offhrw.SerialNr;
    rentinf = ReadFirstMain(RentINr,2,true);
    if ((rentinf==false) and (blank(Offhrw.ArtCode))) then begin
      RentINr.SerialNr = Offhrw.SerialNr;
      rentinf = ReadFirstKey("SerialNr",RentINr,1,true);
      if (rentinf) then begin
        Offhrw.ArtCode = RentINr.Code;
        Offhrw.Quant = 1;
      end;
    end;
    if (rentinf) then begin
      GetRentINNameStr(RentINr,Offhr.LangCode,tstr);
      Offhrw.Spec = tstr;
      Offhrw.Value = RentINr.Value;
      Offhrw.Status = 7;
      MatRowPut(Offhr,rownr,Offhrw);
      res = true;
    end;
  end;
  if (res==false) then begin
    INr.Code = Offhrw.ArtCode;
    if (ReadFirstMain(INr,1,true)) then begin
      if (GetItemNameStr(1,tstr,Offhr.LangCode,INr.Name,INr.Code)) then begin end;
      Offhrw.Spec = tstr;
      Offhrw.Status = 7;
      MatRowPut(Offhr,rownr,Offhrw);
      res = true;
      if (INr.ExplodeRec==0) then begin
        OffHireMultiLineText(Offhr,INr,Offhr.LangCode,rownr);
      end;
      inwarning = INr.WarnText1;
    end;
  end;
  PasteOffHireItemAndSerial = res;
  return;
end;

global
function Boolean OffHireVc_PasteArtCode(var record OffHireVc Offhr,Integer rownr,var string inwarning)
BEGIN
  Boolean res;
  
  res = PasteOffHireItemAndSerial(Offhr,rownr,inwarning);
  OffHireVc_PasteArtCode = res;
  return;
end;

global
function Boolean OffHireVc_PasteSerialNr(var record OffHireVc Offhr,Integer rownr)
BEGIN
  string 200 warning;
  Boolean res;
  
  res = PasteOffHireItemAndSerial(Offhr,rownr,warning);
  OffHireVc_PasteSerialNr = res;
  RETURN;
END;

global
function Boolean PasteCust2InOffHire(var record OffHireVc Offhr,Boolean updatall,var string warntext)
BEGIN
  Boolean found;
  record CUVc CUr;
  record INVc INr;
  val x,pr,bal,limit;
  string 255 tstr;
  Boolean limitdaysf;

  CUr.Code = Offhr.CustCode;
  if (GetCustAndBal(CUr,limit,bal,0,0,0,0,0,0,limitdaysf)) then begin
    found = true;
    Offhr.Addr0 = CUr.Name;
    Offhr.ClientContact = CUr.Person;
    SetSalesMan(tstr,CUr.SalesMan);
    Offhr.SalesMan = tstr;
    Offhr.LangCode = CUr.LangCode;
    Offhr.ClientContact = CUr.Person;
    if (updatall) then begin
      OffHireSumup(Offhr);
      if (nonblank(CUr.WarnText1)) then begin
        warntext = CUr.WarnText1;
      end;
    end;
  end;
  PasteCust2InOffHire = found;
  RETURN;
END;

