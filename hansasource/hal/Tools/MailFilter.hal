external function Boolean FindUserInMailbox(string,var string,var LongInt);

function Boolean GlobMatch(string match,string s)
begin
  Boolean res;
  LongInt l;
  string 255 glob;
  string 255 longstr;
  
  glob = uppercase(match);
  longstr = uppercase(s);

  if (left(glob,1)=="*") then begin
    l = len(glob) - 1;
    longstr = right(longstr,l);
    glob = right(glob,l);
  end;

  if (glob==longstr) then begin
    res = true;
  end else begin
    res = false;
  end;
  
  GlobMatch = res;
  return;
end;

procedure FilterMailRow(var row MailVc mailrr,string fromaddr)
begin
  record MailFilterVc mfr;
  row MailFilterVc mfrr;
  string 255 confcode;
  LongInt rwcnt,i,nr;
  
  if (FindUserInMailbox(mailrr.AddrCode,confcode,nr)==false) then begin
    goto out;
  end;
  
  mfr.MailBox = nr;
  
  if (ReadFirstMain(mfr,1,true)==false) then begin
    goto out;
  end;

  rwcnt = MatRowCnt(mfr);

  for (i = 0; i<rwcnt; i = i + 1) begin
    MatRowGet(mfr,i,mfrr);
    if (GlobMatch(mfrr.Address,fromaddr)) then begin
      switch (mfrr.Type) begin
        case 0:
        case 1: mailrr.AddrFolder = "FILTER";
      end;
      goto out;
    end;
  end;
  
out:;
  return;
end;

global
procedure FilterIncomingEmail(record MailVc mailr)
begin
  string 255 fromaddr;
  LongInt rwcnt,i;
  row MailVc mailrr;
  
  rwcnt = MatRowCnt(mailr);
  for (i = 0; i<rwcnt; i = i + 1) begin
    MatRowGet(mailr,i,mailrr);
    if (mailrr.RowTyp==1) then begin
      fromaddr = mailrr.AddrCode;
      goto foundfrom;
    end;
  end;
foundfrom:;

  for (i = 0; i<rwcnt; i = i + 1) begin
    MatRowGet(mailr,i,mailrr);
    if (mailrr.RowTyp==0 or mailrr.RowTyp==3 or mailrr.RowTyp==4) then begin
      FilterMailRow(mailrr,fromaddr);
      MatRowPut(mailr,i,mailrr);
    end;
  end;

  return;
end;