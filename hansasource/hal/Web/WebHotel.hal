external function LongInt ResourceAvail(string,Date,Time,Time,LongInt);
external function LongInt DateDiff(Date,Date);
external procedure WebFormLine(string,string,string,string,Integer,Integer);
external procedure ToolWebStartPage();
external procedure ToolWebSetMainTitle();
external procedure ToolWebStartBody(string);
external procedure ToolWebStartHeader();
external procedure ToolWebStartHeaderEnd();
external procedure ToolWebLineStart();
external procedure ToolWebLineEnd();
external procedure ToolWebEndPage(Boolean);
external procedure ToolWebOutHeader(string);
external procedure WebMainForAll();
external procedure WebLeadText2(string,string);
external procedure ToolWebRadioButtonDiv(string,string,Integer,Boolean);
external procedure ToolWebTableHdr(string,string,string);
external procedure WebPlainText(string);
external procedure AddTextLineToMail(string,record MailVc);
external function Boolean LoggedInTest();
external function integer GetGuestsInJob(record JobVc);

procedure DisplayBooking(string errormsg,string startdat,string enddat,string type,integer size,string comment)
begin
  record ResTypeVc RType;
  Boolean foundf;

  ToolWebStartPage;
  ToolWebSetMainTitle;
  ToolWebStartBody("res_startdate");
  ToolWebStartHeader;
  ToolWebOutHeader("Book Resource");
  ToolWebStartHeaderEnd;
  if (nonblank(errormsg)) then begin
    WebPlainText(errormsg);
  end;
  WebOutString("<form name=""HansaForm"" method=""POST"" action=""");
  WebOutLink("WebStoreBooking.hal?sessionid=" & WebGetArg("sessionid"));
  WebOutString(""">");
  WebOutNL;
  WebOutString("<table border=0 width=""100%"" cellspacing=0 cellpadding=1>");
  WebFormLine("Start Date:","res_startdate","text",startdat,10,10);
  WebFormLine("End Date:","res_enddate","text",enddat,10,10);
  ToolWebLineStart;
  WebLeadText2("Type:","25%");
  ToolWebLineEnd;
  ToolWebLineStart;
  WebOutString("<td width=200>");
  WebOutString("<select name=""res_type""><option></option>");
  foundf = true;
  RType.Comment = "";
  while(LoopKey("Comment",RType,1,foundf)) begin
    if (foundf) then begin
      WebOutString("<option");
      if (RType.Code==type) then begin
        WebOutString(" selected");
      end;
      WebOutString(" value=""" & RType.Code & """>" & RType.Comment & "</option>");
    end;
  end;
  WebOutString("</select></td>");
  ToolWebLineEnd;
  ToolWebLineStart;
  WebLeadText2("Size:","25%");
  ToolWebLineEnd;
  WebFormLine("Number of Guests:","res_qty","text",size,5,5);
  WebFormLine("Text:","res_comment","text",comment,66,60);
  ToolWebLineStart;
  WebOutString("<td width=""90%"" align=""left"" valign=""top""><input type=""submit"" value=""Save"" name=""b1""></td>");
  ToolWebLineEnd;
  WebOutString("</table>");
  WebOutString("</p>");
  WebOutString("</form>");
  ToolWebEndPage(true);
  return;
end;

global
procedure WebBookResource()
begin
  record JobVc Jobr;

  RecordNew(Jobr);
  if (LoggedInTest()) then begin
    DisplayBooking("",Jobr.TransDate,AddDay(Jobr.TransDate,1),"",0,"");
  end;
  return;
end;

updating procedure CreateConfirmMail(record CUVc CUr,record JobVc Jobr)
begin
  record MailVc Mailr;
  record EmailBlock EMr;
  record MailSettingsBlock MSr;
  record CYBlock CYr;
  row MailVc Mailrw;

  if (LoggedInTest()) then begin
    BlockLoad(MSr);
    BlockLoad(EMr);
    BlockLoad(CYr);
    RecordNew(Mailr);
    Mailr.TransDate = CurrentDate;
    Mailr.TransTime = CurrentTime;
    Mailr.Lifespan = 0;
    Mailr.Header = "Confirmation of reservation for " & CUr.Name;
    Mailr.LockedFlag = 0;
    Mailr.SendFlag = 1;
    Mailr.HasFileAtt = 0;
    Mailr.HasRecAtt = 0;
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = EMr.UserIP;
    if (blank(Mailrw.AddrCode)) then begin
      Mailrw.AddrCode = MSr.Postmaster;
    end;
    MatRowPut(Mailr,0,Mailrw);
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = CUr.eMail;
    if (nonblank(Mailrw.AddrCode)) then begin
      MatRowPut(Mailr,1,Mailrw);
      AddTextLineToMail("Confirmation of reservation in " & CYr.CompName,Mailr);
      AddTextLineToMail("Your reservation is confirmed.",Mailr);
      AddTextLineToMail("Your reservation nr is: " & Jobr.SerNr,Mailr);
      if (RecordInsert(Mailr,true)) then begin end;
    end;
  end;
  return;
end;

global
updating procedure WebStoreBooking()
begin
  record JobVc Jobr;
  record CUVc CUr;
  record HotelBlock HBr;
  Time endofday,startofday;
  Boolean resf;
  integer NumberOfGuests;

  BlockLoad(HBr);
  if (LoggedInTest()) then begin
    RecordNew(Jobr);
    Jobr.SerNr = NextSerNr("JobVc",Jobr.TransDate,-1,false,"");  
    Jobr.ResStatus    = HBr.DefSt;
    Jobr.Type         = WebGetArg("res_type");
    Jobr.TransDate    = WebGetArg("res_startdate");
    Jobr.EndDate      = WebGetArg("res_enddate");
    Jobr.Comment      = WebGetArg("res_comment");
    Jobr.Persons      = WebGetArg("res_qty");
    CUr.Code = CurrentCust;
    if (ReadFirstMain(CUr,1,true)==false) then begin
      CUr.Name = "";
    end;
    Jobr.CUCode       = CUr.Code; // Should possibly be customer on first row now...
    Jobr.CUName       = CUr.Name;
    endofday = HBr.CheckOut;
//    if (nonblanktime(HBr.EndOfDay)) then begin
//      endofday = HBr.EndOfDay;
//    end;
    startofday = HBr.CheckIn;
//    if (nonblanktime(HBr.StartNewDay)) then begin
//      startofday = HBr.StartNewDay;
//    end;
    NumberOfGuests = GetGuestsInJob(Jobr);
    if ((blank(Jobr.Type)) or (NumberOfGuests<=0) or (BlankDate(Jobr.TransDate)) or (BlankDate(Jobr.EndDate))) then begin
      DisplayBooking("You have to fill in all the selections",Jobr.TransDate,Jobr.EndDate,Jobr.Type,NumberOfGuests,Jobr.Comment);
    end else begin
      if (ResourceAvail(Jobr.Type,Jobr.TransDate,startofday,endofday,DateDiff(Jobr.EndDate,Jobr.TransDate))<=0) then begin
        DisplayBooking("There are no available resources at this time",Jobr.TransDate,Jobr.EndDate,Jobr.Type,NumberOfGuests,Jobr.Comment);
      end else begin
        resf = RecordInsert(Jobr,true);
        WebMainForAll;
        CreateConfirmMail(CUr,Jobr);
      end;
    end;
  end;
  return;
end;

global
procedure WebEnableResource()
begin
  Date startdat;
  
  ToolWebStartPage;
  ToolWebSetMainTitle;
  ToolWebStartBody("resource");
  ToolWebStartHeader;
  ToolWebOutHeader("Enable Resource");
  ToolWebStartHeaderEnd;
  WebOutString("<form name=""HansaForm"" method=""POST"" action=""");
  WebOutLink("WebEnableResource.hal?sessionid=" & WebGetArg("sessionid"));
  WebOutString(""">");
  WebOutNL;
  WebOutString("<table border=0 width=""100%"" cellspacing=0 cellpadding=1>");
  if (blank(WebGetArg("phase"))) then begin
    WebFormLine("Person","person","text",startdat,10,10);
    WebFormLine("Resource","resource","text",startdat,10,10);
  end;
  if (WebGetArg("phase")=="1") then begin
  
  end;
  ToolWebLineStart;
  WebOutString("<td width=""90%"" align=""left"" valign=""top""><input type=""submit"" value=""Save"" name=""b1""></td>");
  ToolWebLineEnd;
  WebOutString("</table>");
  WebOutString("</p>");
  WebOutString("</form>");
  ToolWebEndPage(true);
  return;
end;

