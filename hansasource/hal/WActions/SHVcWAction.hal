remote function Boolean GetFirstItem(var string,var record INVc);
remote procedure SHVc_PasteDelCountry(var record ORVc,string);
remote function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
remote procedure SHVc_PasteShip(var record SHVc,var Integer);
external procedure WarnFutureDate(Boolean,Date);
external function string 40 SHDClassSpecPasteNameObjects(Integer);
remote procedure SHVc_PasteShipMode(var record SHVc);
remote procedure SHVc_PastePosCode(var record SHVc,Integer);
remote procedure SHVc_PasteSerialNr(var record SHVc,Integer,var string);
external updating procedure RecordActionSH_Print(var record SHVc,string,Boolean,Integer);
external function Integer CheckSerialStatus(string,string,var string);
external function val CalculateSerialNrQuantity(string,string,string,Boolean,val,val,val,val);
remote procedure SHSumUp(var record SHVc);
remote procedure CalcSHWeight(var record SHVc,Boolean);
external function Boolean DateWarned(Date,string);
external function Boolean WarnOldRecords(Date,string,LongInt );
remote procedure RefillShipment(var record SHVc,record SHVc);
remote procedure SetDelAddr(string,string,var string,var string,var string,var string,var string,var string,var string,var string,var string,var string,var Integer,var string);
remote updating procedure OkAllStockMovementsSHr(record RcVc);
remote function string 20 FindCustRoute(string,string,string);

procedure SHChangeShip(record SHVc SHr,Integer rownr)
BEGIN
  SHVc_PasteShip(SHr,rownr);
  SHSumUp(SHr);  
  RETURN;          
END;

function boolean SHPackagesDClassPackTypeEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHPackagesVc SHPr;
  row SHPackagesVc SHPrw;
  record PackTypeVc PTr;
  
  if (changed!=0 and rownr>=0) then begin
    GetWindowRecord(wn,SHPr);
    MatRowGet(SHPr,rownr,SHPrw);
    PTr.Code = SHPrw.PackType;
    if (ReadFirstMain(PTr,1,true)) then begin
      SHPrw.PackComment = PTr.Comment;
      SHPrw.Height = PTr.Height;
      SHPrw.Length = PTr.Length;
      SHPrw.Width = PTr.Width;
      SHPrw.MeasurementSystem = PTr.MeasurementSystem;
    end else begin
      SHPrw.PackType = "";
    end;
    MatRowPut(SHPr,rownr,SHPrw);
    PutWindowRecord(wn,SHPr);
  end;
  return;
end;

function boolean SHPackagesDClassHeightEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHPackagesVc SHPr;
  row SHPackagesVc SHPrw;
  
  if (changed!=0 and rownr>=0) then begin
    GetWindowRecord(wn,SHPr);
    MatRowGet(SHPr,rownr,SHPrw);
    if (nonblank(SHPrw.PackType)) then begin
      SHPrw.PackType = "";
      MatRowPut(SHPr,rownr,SHPrw);
      PutWindowRecord(wn,SHPr);
    end;
  end;
  return;
end;

function boolean SHPackagesDClassWidthEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHPackagesVc SHPr;
  row SHPackagesVc SHPrw;
  
  if (changed!=0 and rownr>=0) then begin
    GetWindowRecord(wn,SHPr);
    MatRowGet(SHPr,rownr,SHPrw);
    if (nonblank(SHPrw.PackType)) then begin
      SHPrw.PackType = "";
      MatRowPut(SHPr,rownr,SHPrw);
      PutWindowRecord(wn,SHPr);
    end;
  end;
  return;
end;

function boolean SHPackagesDClassLengthEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHPackagesVc SHPr;
  row SHPackagesVc SHPrw;
  
  if (changed!=0 and rownr>=0) then begin
    GetWindowRecord(wn,SHPr);
    MatRowGet(SHPr,rownr,SHPrw);
    if (nonblank(SHPrw.PackType)) then begin
      SHPrw.PackType = "";
      MatRowPut(SHPr,rownr,SHPrw);
      PutWindowRecord(wn,SHPr);
    end;
  end;
  return;
end;

function boolean SHPackagesDClassMeasurementEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHPackagesVc SHPr;
  row SHPackagesVc SHPrw;
  
  if (changed!=0 and rownr>=0) then begin
    GetWindowRecord(wn,SHPr);
    MatRowGet(SHPr,rownr,SHPrw);
    if (nonblank(SHPrw.PackType)) then begin
      SHPrw.PackType = "";
      MatRowPut(SHPr,rownr,SHPrw);
      PutWindowRecord(wn,SHPr);
    end;
  end;
  return;
end;

global
function Boolean SHPackagesDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "PackType": res = SHPackagesDClassPackTypeEFAfter(wn,rownr,changed);
    case "Height": res = SHPackagesDClassHeightEFAfter(wn,rownr,changed);
    case "Width": res = SHPackagesDClassWidthEFAfter(wn,rownr,changed);
    case "Length": res = SHPackagesDClassLengthEFAfter(wn,rownr,changed);
    case "MeasurementSystem": res = SHPackagesDClassMeasurementEFAfter(wn,rownr,changed);
  end;
  SHPackagesDClassAfterEditField = res;
  RETURN;
END;

function Boolean SHDClassShipEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record SHVc SHr;
  Boolean res;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    SHChangeShip(SHr,rownr);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassShipEFAfter = res;
  return;
end;

function Boolean SHDClassPosCodeEFAfter(Integer wn,Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  Boolean res;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    SHVc_PastePosCode(SHr,rownr);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassPosCodeEFAfter = res;
  RETURN;
END;

function Boolean SHDClassUnitXvalEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  row SHVc SHrw;
  record SHVc SHr;
  Boolean res;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    MatRowGet(SHr,rownr,SHrw);
    SHrw.Ship = CalculateSerialNrQuantity(SHrw.ArtCode,"","",false,SHrw.UnitXval,SHrw.UnitYval,SHrw.UnitZval,SHrw.Ship);
    MatRowPut(SHr,rownr,SHrw);
    SHChangeShip(SHr,rownr);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassUnitXvalEFAfter = res;
  RETURN;
END;

function Boolean SHDClassUnitYvalEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  row SHVc SHrw;
  record SHVc SHr;
  Boolean res;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    MatRowGet(SHr,rownr,SHrw);
    SHrw.Ship = CalculateSerialNrQuantity(SHrw.ArtCode,"","",false,SHrw.UnitXval,SHrw.UnitYval,SHrw.UnitZval,SHrw.Ship);
    MatRowPut(SHr,rownr,SHrw);
    SHChangeShip(SHr,rownr);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassUnitYvalEFAfter = res;
  RETURN;
END;

function Boolean SHDClassUnitZvalEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  row SHVc SHrw;
  record SHVc SHr;
  Boolean res;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    MatRowGet(SHr,rownr,SHrw);
    SHrw.Ship = CalculateSerialNrQuantity(SHrw.ArtCode,"","",false,SHrw.UnitXval,SHrw.UnitYval,SHrw.UnitZval,SHrw.Ship);    
    MatRowPut(SHr,rownr,SHrw);
    SHChangeShip(SHr,rownr);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassUnitZvalEFAfter = res;
  RETURN;
END;

function Boolean SHDClassSerialNrEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  Boolean res;
  string 255 tstr;
  
  res = true;
  if ((changed!=0) and (rownr>=0)) then begin
    GetWindowRecord(wn,SHr);
    SHVc_PasteSerialNr(SHr,rownr,tstr);    
    if (nonblank(tstr)) then begin
      MessageBox(0,tstr);
    end;    
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassSerialNrEFAfter = res;
  RETURN;
END;

function Boolean SHDClassShipDateEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  Boolean res;
  string 255 tstr;
  
  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,SHr);
    if (DateWarned(SHr.ShipDate,"SHVc")) then begin
      MessageBox(1045,tstr);
    end;
    if (WarnOldRecords(SHr.ShipDate,"SHVc",SHr.SerNr)) then begin
      MessageBox(2020,tstr);
    end;
    WarnFutureDate(true,SHr.ShipDate);
//JJCS    
  end;
  SHDClassShipDateEFAfter = res;
  RETURN;
END;

function Boolean SHDClassLocationEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record SHVc SHr;
  record SHVc SH2r;
  Boolean res;
  string 255 tstr;
  Integer insertmode;
  
  insertmode = 1;//Rs_insert
  res = true;
  if (changedf) then begin
    GetWindowRecord(wn,SHr);
    if (WindowState(wn)==insertmode) then begin
      SH2r.CustCode = "NIL";
    end else begin
      GetPrevWindowRecord(wn,SH2r);
    end;
    RefillShipment(SHr,SH2r);
    SHr.Sorting = FindCustRoute(SHr.CustCode,SHr.Location,SHr.DelAddrCode);
    SHSumUp(SHr);
    PutWindowRecord(wn,SHr);
  end;
  SHDClassLocationEFAfter = res;
  RETURN;
END;

function Boolean SHDClassDelAddrCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  record SHVc SH2r;
  Boolean res;
  string 255 name,s1,s2,s3,s4,s5,country,region,taxtemplatecode,vatcode,dummy;
  Integer exportflag;

  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,SHr);
    SetDelAddr(SHr.CustCode,SHr.DelAddrCode,name,s1,s2,s3,s4,s5,country,region,taxtemplatecode,vatcode,exportflag,dummy);
    SHr.Addr0 = name;
    SHr.Addr1 = s1;
    SHr.Addr2 = s2;
    SHr.Addr3 = s3;
    SHr.DelAddr3 = s4;
    SHr.DelAddr4 = s5;
    SHr.Region = region;
    SHr.Sorting = FindCustRoute(SHr.CustCode,SHr.Location,SHr.DelAddrCode);
    PutWindowRecord(wn,SHr);    
  end;
  SHDClassDelAddrCodeEFAfter = res;
  RETURN;
END;

procedure FillFreightInfo(record SHVc SHr)
begin
  record FreightCompanyVc FCr;
  
  FCr.Code = SHr.FreightCode;
  if (ReadFirstMain(FCr,1,true)) then begin
    SHr.FreightCompany = FCr.Company;
    SHr.FreightCompanyRegNr = FCr.CompanyRegNr;
    SHr.TruckInfo = FCr.TruckInfo;
    SHr.Driver = FCr.Driver;
    SHr.CMRText = FCr.CMRText;
  end else begin
    Beep;
  end;
  return;
end;

function Boolean SHDClassFreightCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  record SHVc SH2r;
  Boolean res;

  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,SHr);
    if (nonblank(SHr.FreightCode)) then begin
      FillFreightInfo(SHr);
      PutWindowRecord(wn,SHr);
    end;
  end;
  SHDClassFreightCodeEFAfter = res;
  RETURN;
END;

function Boolean SHDClassPackageTypeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record PackTypeVc PTr;
  record SHVc SHr;
  row SHVc SHrw;
  Boolean res;

  res = true;
  if (changed!=0) then begin
    if (rownr>-1) then begin
      GetWindowRecord(wn,SHr);
      MatRowGet(SHr,rownr,SHrw);
      if (nonblank(SHrw.PackageType)) then begin
        PTr.Code = SHrw.PackageType;
        if (ReadFirstMain(PTr,1,true)) then begin
          SHrw.PackageDesc = PTr.Comment;
          MatRowPut(SHr,rownr,SHrw);
        end else begin
          Beep;
        end;
        PutWindowRecord(wn,SHr);
      end;
    end;
  end;
  SHDClassPackageTypeEFAfter = res;
  RETURN;
END;

function Boolean SHDClassRowPackagesTypeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record SHVc SHr;
  row SHVc SHrw;
  Boolean res;
  Integer i,rwcnt;

  res = true;
  if (changed!=0) then begin
    if (rownr>-1) then begin
      GetWindowRecord(wn,SHr);
      SHr.Packages = BlankVal;
      rwcnt = MatRowCnt(SHr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(SHr,i,SHrw);
        SHr.Packages = SHr.Packages + SHrw.RowPackages;
      end;
      PutWindowRecord(wn,SHr);
    end;
  end;
  SHDClassRowPackagesTypeEFAfter = res;
  RETURN;
END;

function Boolean SHDClassShipModeEFAfter(Integer wn,Boolean changedf)
BEGIN
  record SHVc SHr;
  row SHVc SHrw;
  Boolean res;
  Integer i,rwcnt;

  res = true;
  if (changedf) then begin
     GetWindowRecord(wn,SHr);
     SHVc_PasteShipMode(SHr);
     PutWindowRecord(wn,SHr);
  end;
  SHDClassShipModeEFAfter = res;
  RETURN;
END;

function Boolean SHDClassDelCountryEFAfter(Integer wn,Boolean changedf)
begin  
  record SHVc SHr;

  if (changedf) then begin  
    GetWindowRecord(wn,SHr);
    SHVc_PasteDelCountry(SHr,WindEFstr(wn))
    PutWindowRecord(wn,SHr);
  end;
  SHDClassDelCountryEFAfter = true;
  return;
end;

function Boolean SHDClassScanBarcodeEFAfter(Integer wn,Boolean changedf)
begin
  record SHVc SHr,SH2r;
    
  if (changedf) then begin 
    GetWindowRecord(wn,SH2r);
    WindowFieldGoto(wn,SH2r,-1,"scanbarcode",true);
  end;
  SHDClassScanBarCodeEFAfter = true;
  return;
end;

global
function Boolean SHDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname) begin
    case "scanbarcode": res = SHDClassScanBarcodeEFAfter(wn,changed!=0);
    case "RowPackages": res = SHDClassRowPackagesTypeEFAfter(wn,fn,rownr,changed);
    case "PackageType": res = SHDClassPackageTypeEFAfter(wn,fn,rownr,changed);
    case "DelAddrCode": res = SHDClassDelAddrCodeEFAfter(wn,fn,rownr,changed);
    case "FreightCode": res = SHDClassFreightCodeEFAfter(wn,fn,rownr,changed);
    case "Location": res = SHDClassLocationEFAfter(wn,rownr,changed!=0);
    case "ShipDate": res = SHDClassShipDateEFAfter(wn,fn,rownr,changed);
    case "SerialNr": res = SHDClassSerialNrEFAfter(wn,fn,rownr,changed);
    case "UnitZval": res = SHDClassUnitZvalEFAfter(wn,fn,rownr,changed);
    case "UnitYval": res = SHDClassUnitYvalEFAfter(wn,fn,rownr,changed);
    case "UnitXval": res = SHDClassUnitXvalEFAfter(wn,fn,rownr,changed);
    case "Ship": res = SHDClassShipEFAfter(wn,rownr,changed);
    case "PosCode": res = SHDClassPosCodeEFAfter(wn,rownr,changed);
    case "ShipMode": res = SHDClassShipModeEFAfter(wn,changed!=0);
    case "DelCountry": res = SHDClassDelCountryEFAfter(wn,changed!=0);
  end;
  SHDClassAfterEditField = res;
  return;
end;

function Boolean SHDClassBarCodeOnEnterKey(Integer wn,Integer rownr)
begin
  Boolean res;
  record SHVc SHr;
  row SHVc SHrw;
  Integer rwcnt,i,changed,UpdateRowNr;
  string 255 BarCodeStr,ItemCodeStr,warning,inwarn;
  Boolean testf;
  record INVc INr;
  
  DeselectWindow(wn,false);
  GetWindowRecord(wn,SHr);
  if (SHr.OKFlag!=0) then begin
    goto LSHDClassBarCodeOnEnterKey;
  end;
  BarCodeStr = GetWindowString(wn,"scanbarcode");
  PutWindowString(wn,"scanbarcode","");
  if (nonblank(BarCodeStr)) then begin 
    GetFirstItem(BarCodeStr,INr);
    ItemCodeStr = INr.Code;
  end;
  if (nonblank(ItemCodeStr)) then begin
    UpdateRowNr = -1;
    GetWindowRecord(wn,SHr);
    rwcnt = MatRowCnt(SHr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(SHr,i,SHrw);
      if (SHrw.stp==1) then begin
        if (SHrw.ArtCode==ItemCodeStr) then begin
          if (SHrw.Ship<SHrw.Ordered) then begin
            SHrw.Ship = SHrw.Ship + 1;
            MatRowPut(SHr,i,SHrw);
            changed = 1;
            UpdateRowNr = i;
            i = rwcnt;    
          end;
        end;  
      end; 
    end;
    PutWindowRecord(wn,SHr);
    if (UpdateRowNr>=0) then begin 
      SHDClassShipEFAfter(wn,UpdateRowNr,changed);
    end else begin
      Beep;
    end;
    WindowFieldGoto(wn,SHr,-1,"scanbarcode",false);
  end else begin
    MessageBox(8602,"");
    GetWindowRecord(wn,SHr);
    WindowFieldGoto(wn,SHr,-1,"scanbarcode",false);
  end;
LSHDClassBarCodeOnEnterKey:;  
  res = false;
  SHDClassBarCodeOnEnterKey = res;
  return;
end;

global
updating function Boolean SHDClassOnEnterKey(Integer wn,string fieldname,Integer fn,Integer rownr)
begin
  Boolean res;
  
  res = true;
  switch (fieldname) begin
    case "scanbarcode":
      res = SHDClassBarCodeOnEnterKey(wn,rownr);
  end;
LSHDClassOnEnterKey:;  
  SHDClassOnEnterKey = res;
  return;
end;

global
updating procedure PrintSHL(Integer wn,Boolean previewf)
BEGIN
  record SHVc SHr;
  Integer i;
  
  StartPrintDialogGroup;
  i = 1;
  while (GetRecordFromBrowse(SHr,wn,i)) begin
    RecordActionSH_Print(SHr,previewf,true,0);
    i = i + 1;
  end;
  EndPrintDialogGroup;
  RETURN;
END;

global
updating function Boolean SHDClassPrint(Integer wn,Boolean previewf)
BEGIN
  Boolean res;
  record SHVc SHr;
  Integer normalmode;
  
  normalmode = 0;//Rs_normal
  DeselectWindow(wn,false);
  GetWindowRecord(wn,SHr);
  if (WindowState(wn)==normalmode) then begin 
    switch (SHr.OKFlag) begin
      case 0:
        if (PrintDocument(SHr,"Sh2Form",previewf)) then begin end;
      case 1:
        RecordActionSH_Print(SHr,previewf,true,0);
//        if (PrintDocument(SHr,"Sh1Form",previewf)) then begin end;
    end;  
  end;
  res = true;
  SHDClassPrint = res;
  RETURN;
END;

global
function Boolean SHDClassInsertRowTest(Integer wn,Integer rownr)
begin
  record SHVc SHr;
  row SHVc SHrw;
  Boolean res;
  Integer updatemode;
  
  res = true;
  switch (WindowState(wn)) begin
    case Rs_normal:
      GetWindowRecord(wn,SHr);
      if (SHr.OKFlag!=0) then begin res = false; end;
    case Rs_insert:
      res = true;
    case Rs_update:
      GetPrevWindowRecord(wn,SHr);
      if (SHr.OKFlag!=0) then begin res = false; end;
    otherwise
      res = false;
  end;
  if (res) then begin
    MatRowGet(SHr,rownr,SHrw);
    switch (SHrw.stp) begin
      case kInvoiceRowTypeStructuredItemComponent:
        res = false;
    end;
  end;
  SHDClassInsertRowTest = res;
  return;
end;

global
function Boolean SHDClassDeleteRowTest(Integer wn,Integer rownr)
begin
  record SHVc SHr;
  row SHVc SHrw;
  Boolean res,puf;
  record INVc INr;
  Integer i,rwcnt;
  Integer updatemode;
  
  updatemode = 2;//Rs_normal
  res = true;
  switch (WindowState(wn)) begin
    case Rs_update:
      GetPrevWindowRecord(wn,SHr);    
      if (SHr.OKFlag!=0) then begin
        res = false;
      end;
    case Rs_insert:
      GetWindowRecord(wn,SHr);    
    case Rs_normal:
      GetWindowRecord(wn,SHr);    
  end;
  if (res) then begin
    MatRowGet(SHr,rownr,SHrw);
    switch (SHrw.stp) begin
      case kInvoiceRowTypeStructuredItemComponent:
        res = false;
    end;
  end;
  if (res) then begin
    MatRowGet(SHr,rownr,SHrw);
    if (SHrw.Ship!=0) then begin
      if (ReadFirstItem(SHrw.ArtCode,INr,false,false)) then begin
        if (INr.ItemType==kItemTypeStructured) then begin
          if (INr.ExplodeRec!=0) then begin
            rwcnt = MatRowCnt(SHr);
            i = rownr + 1;
            while (i<rwcnt) begin
              MatRowGet(SHr,i,SHrw); 
              if (SHrw.stp==kInvoiceRowTypeStructuredItemComponent) then begin
                MatRowDelete(SHr,i);
                puf = true;
              end else begin
                i = rwcnt;
              end;
            end;
          end;
        end;
      end;
    end;
    if (puf) then begin
      PutWindowRecord(wn,SHr);    
    end;
  end;
  SHDClassDeleteRowTest = res;
  return;
end;

global
function Boolean SHDClassOnOverStrike(Integer wn,Integer rownr)
begin
  record SHVc SHr;

  if (rownr>=0) then begin
    GetWindowRecord(wn,SHr);        
    SHSumup(SHr);
    PutWindowRecord(wn,SHr);   
  end;
  SHDClassOnOverStrike = true;
  return;
end;

function Boolean SHDClassButtonAction(string whichone,Integer wn,Integer value)
begin
  Boolean res;
  record SHVc SHr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,SHr);
    if (SHr.OKFlag!=0) then begin
      res = false;
      if (UserCanAction("UnOKAll",false)) then begin
        res = true;
      end;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,SHr);
    if (SHr.OKFlag!=0) then begin
      res = false;
    end;
  end;  
  SHDClassButtonAction = res;
  RETURN;
end;

global
function Boolean SHDClassOKFlagButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  
  res = SHDClassButtonAction("OKFlag",wn,value);
  SHDClassOKFlagButtonAction = res;
  RETURN;
END;

global
function Boolean SHDClassCheck1ButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  
  res = SHDClassButtonAction("Check1",wn,value);
  SHDClassCheck1ButtonAction = res;
  RETURN;
END;

global
function Boolean SHDClassCheck2ButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  
  res = SHDClassButtonAction("Check2",wn,value);
  SHDClassCheck2ButtonAction = res;
  RETURN;
END;

global
function Boolean SHDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record SHVc SHr;
  record SHVc SH2r;
  row SHVc SHrw;
  string 200 tstr;
  
  res = true;

  GetWindowRecord(wn,SHr);
  switch (wnst) begin
    case Rs_insert:
      switch (fieldname) begin
        case "TaxAdminServSeal": res = blank(SHr.TaxAdminServSeal);
      end;
    case Rs_update:
      GetPrevWindowRecord(wn,SH2r);
      if (SH2r.OKFlag!=0) then begin
        switch (fieldname) begin
          case "TransportNumber":
          case "Volyme":
          case "Weight":
          case "TotWeight":
          case "TotVolume":
          case "Packages":
          case "LangCode":
          case "CurncyCode":
          case "DeclaredVal":
          case "TaxAdminServSeal":
          case "DelAddrCode": res = HasLocalization("LTU");
          case "Addr1": res = HasLocalization("LTU");
          case "Addr2": res = HasLocalization("LTU");
          case "Addr3": res = HasLocalization("LTU");
          case "DelAddr3": res = HasLocalization("LTU");
          case "DelAddr4": res = HasLocalization("LTU");
          case "DelCountry": res = HasLocalization("LTU");
          case "PlanSendDate": res = HasLocalization("LTU");
          case "PlanSendTime": res = HasLocalization("LTU");
          case "PlanArrDate": res = HasLocalization("LTU");
          case "PlanArrTime": res = HasLocalization("LTU");
          case "FreightCode": res = HasLocalization("LTU");
          case "FreightCompany": res = HasLocalization("LTU");
          case "FreightCompanyRegNr": res = HasLocalization("LTU");
          case "TruckInfo": res = HasLocalization("LTU");
          case "Driver": res = HasLocalization("LTU");
          case "CMRText": res = HasLocalization("LTU");
          otherwise
            res = false;
        end;
      end;
  end;
  if (res) then begin
    if (rownr>-1) then begin
      switch (fieldname) begin
        case "ArtCode":
          res = false;
        case "VARList":
          res = false;
        case "Recepy":
          res = false;
        case "Ordered":
          res = false;
        case "InStock":
          res = false;
        case "Ship": 
          MatRowGet(SHr,rownr,SHrw);
          switch (SHrw.stp) begin
            case kInvoiceRowTypeStructuredItemComponent:
              res = false;
          end;
        case "Spec": 
          if (HasLocalization("PRT")) then begin
            MatRowGet(SHr,rownr,SHrw);
            if (nonblank(SHrw.ArtCode)) then begin
              res = false;
            end;
          end else begin
            if (UserCanAction("ChangeSHItemSpec",true)==false) then begin res = false; end;
          end;
        otherwise
          if (SH2r.OKFlag!=0) then begin  // previous
            res = false;
            if (changed!=0) then begin
              MessageBox(1067,tstr);
            end;
          end;
      end;
    end;
  end;
  switch (fieldname) begin
    case "BBCostPrice": res = false;
    case "Name": res = HasLocalization("PRT")==false;
    case "TaxAuthID": res = true;
  end;
  SHDClassActiveEditField = res;
  RETURN;
END;

// May not be called SpecPasteName
global
function string 40 DelAddrSClassSpecPName(string defpsname)
begin
  string 40 psname;
  record OrdSettBlock OrdSettRec;
  
  psname = defpsname;
  BlockLoad(OrdSettRec);
  if (OrdSettRec.DelAddrOpt==1) then begin
    psname = "DelAddr2SClass";
  end;
  DelAddrSClassSpecPName = psname;
  return;
end;

// May not be called SpecPasteName
global
function string 40 SerialNrSClassSpecPName(string defpsname)
begin
  string 40 psname;
  record MainStockBlock MainStockRec;
  
  psname = defpsname;
  BlockLoad(MainStockRec);
  if (MainStockRec.NoSerOnPU==1) then begin
    psname = "";
  end;
  SerialNrSClassSpecPName = psname;
  return;
end;

procedure SHDClassSpecPasteNameArtCode(Integer wn,var string psname)
begin
  record SHVc SHr;
  Integer rownr;

  GetWindowRecord(wn,SHr);
  rownr = WindowActiveRow(wn);
  DeselectWindow(wn,false);
  WindowFieldGoto(wn,SHr,rownr,"ArtCode",false);
  return;
end;

global
function string 40 SHDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 40 psname;
  
  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "DelAddrCode": psname = DelAddrSClassSpecPName(defpsname);
    case "SerialNr": psname = SerialNrSClassSpecPName(defpsname);
    case "Objects": psname = SHDClassSpecPasteNameObjects(wn);
    case "ArtCode": SHDClassSpecPasteNameArtCode(wn,psname);
  end;
  SHDClassSpecPasteName = psname;
  return;
end;

global
updating procedure OKStockMovSHrDsm()
begin
  record SHVc SHr;
  record RcVc RepSpec;
  Integer wn,i;

  wn = CurWindow;
  GetWindowRecord(wn,SHr);
  if ((WindowState(wn)==0) and (SHr.OKFlag==0)) then begin
    RepSpec.long1 = SHr.SerNr;
    OkAllStockMovementsSHr(RepSpec);
  end;
  return;
end;

global
procedure PickingListDsm()
BEGIN
  record SHVc SHr;
  record RcVc RepSpec;

  Integer wn,i;
  wn = CurWindow;
  GetWindowRecord(wn,SHr);
  RepSpec.f1 = SHr.SerNr;
  RepSpec.Media = mtScreen;
  RepSpec.repname = "PickingListRn";
  RunReport(RepSpec,0);
  RETURN;
END;

/*
global
function Boolean SHDClassOnOpenWindow(Integer wn)
begin
  record SHVc SHr;
  record eShipTaxAuthReplyVc eSTARr;
  
TaxAdminServSeal is in SHVc record  
  GetWindowRecord(wn,SHr);
  if (HasLocalization("PRT")) then begin
    eSTARr.FileName = "SHVc";
    eSTARr.TransNr = SHr.SerNr;
    ReadLastKey("FNTransNr",eSTARr,2,true);
    PutWindowString(wn,"TaxAdminServSeal",eSTARr.TaxAdminServSeal);
  end;  
  SHDClassOnOpenWindow = false;
  return;
end;
*/