external function roundmode DefaultRoundMode();
external function string 40 ObjSClassPasteSpecialWindowClass(string);
external procedure ActVcRecordDefClient(var record ActVc);
external procedure RowCalculateTaxMatrix_WSVc(var record WSVc,Integer,row WSVc);
remote procedure WSVc_PasteTaxTemplateCode(var record WSVc,Integer,var Boolean);
remote function Boolean WSVc_PasteMotherNr(var record WSVc,Integer);
remote function Boolean WSVc_PasteItemType(var record WSVc,Integer);
remote updating function Integer PasteWSInRet(var record RetVc,LongInt,string);
external function Integer OpenArtStat(Integer,record RcVc,Boolean);
remote procedure WSVc_PasteSerialNr(var record WSVc,Integer);
external procedure WarnFutureDate(Boolean,Date);
remote function Boolean PasteCUInWS(var record WSVc,var string);
remote procedure SVOVc_PasteWONr(var record WSVc,var string);
remote procedure SVOVc_PasteSVONr(var record WSVc);
external function roundmode SetRoundModeD(Integer);
external procedure FindOTforAcc(string,var string);
remote updating function Boolean MakeActFromWS(record WSVc,var record ActVc);
external procedure NormalizeObjstr(var string);
external procedure GetACShortAccount(string,var string,var string);
external function Boolean FindCustomerRelation(string,string,var record CUVc);
external function Integer CheckSerialStatus(string,string,var string);
external function string 40 SerialNrSClassSpecPName(string);
//external function Integer PasteWOItemType(LongInt,string);
remote function Boolean WSVc_PasteArtCode(var record WSVc,Integer);
remote procedure WSVc_PasteQuant(var record WSVc,Integer);
remote procedure WSVc_PasteEMCode(var record WSVc,string);
external procedure CalcProc(val,val,var val);
external procedure FindSalesExVat(record TaxMatrixVc,string,val,Integer,Integer,var val);
external procedure RecordActionWS_Print(var record WSVc,string);
external procedure SendArtStat(string,string,string,val,val,val,Date,Integer);
external procedure WSSumup(record WSVc);
external procedure CalcSum(val,val,val,val,var val,Integer);
external procedure RoundRowSum(string,string,string,var val);
remote updating function LongInt RecordAction_raPasteWSInPO(record WSVc,var array record POVc,var Integer,var string);
remote updating function Boolean RecordAction_raPasteQTInWS(var record WSVc,record QTVc);

global
function Boolean WSDchrsum(record WSVc WSr,Integer rownr,Integer UseDiscount)
begin
  Boolean chsum;
  row WSVc WSrw;
  val s,v,nilv;

  MatRowGet(WSr,rownr,WSrw);
  CalcSum(WSrw.Quant,WSrw.Price,nilv,WSrw.vRebate,s,UseDiscount);
  if (s!=WSrw.Sum) or ((s==0.00) and (blank(WSrw.Sum))) then begin
    chsum = true;
    RoundRowSum(WSr.CurncyCode,"","WSVc",s);
    WSrw.Sum = s;
    RowCalculateTaxMatrix_WSVc(WSr,rownr,WSrw);
    MatRowPut(WSr,rownr,WSrw);
  end;      
  WSDchrsum = chsum;
  return;
end;

function Boolean WSDClassArtCodeEFAfter(Integer wn,Integer fn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;

  if (changedf!=0) then begin
    GetWindowRecord(wn,WSr);
    if (WSVc_PasteArtCode(WSr,rownr)) then begin
      PutWindowRecord(wn,WSr);
    end else begin
      Beep;
    end;
  end;
  WSDClassArtCodeEFAfter = true;
  return;
end;

function Boolean WSDClassQuantEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;

  if (changedf!=0) then begin
    GetWindowRecord(wn,WSr);
    WSVc_PasteQuant(WSr,rownr);
    PutWindowRecord(wn,WSr);
  end;
  WSDClassQuantEFAfter = true;
  return;
end;

function Boolean WSDClassRecepyEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  record GeneralOptionBlock GenOptRec;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
      WSSumup(WSr);    
    end;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassRecepyEFAfter = true;
  return;
end;

function Boolean WSDClassPriceEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  record GeneralOptionBlock GenOptRec;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
      WSSumup(WSr);    
    end;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassPriceEFAfter = true;
  return;
end;

function Boolean WSDClassvRebateEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  record GeneralOptionBlock GenOptRec;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
      WSSumup(WSr);    
    end;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassvRebateEFAfter = true;
  return;
end;

function Boolean WSDClassBasePriceEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  record GeneralOptionBlock GenOptRec;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
      WSSumup(WSr);    
    end;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassBasePriceEFAfter = true;
  return;
end;

function Boolean WSDClassVATCodeEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  record GeneralOptionBlock GenOptRec;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
    end;
    WSSumup(WSr);    
    PutWindowRecord(wn,WSr);
  end;
  WSDClassVATCodeEFAfter = true;
  return;
end;

function Boolean WSDClassSumEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  row WSVc WSrw;
  record GeneralOptionBlock GenOptRec;
  val s;

  if (changedf!=0) then begin
    BlockLoad(GenOptRec);
    GetWindowRecord(wn,WSr);
    MatRowGet(WSr,rownr,WSrw);
    if ((WSrw.Quant!=0) and (blank(WSrw.Price)==false)) then begin
      s = WSrw.Sum/WSrw.Quant;
      s = WSrw.Price - s;
      s = s*100;
      s = s/WSrw.Price;
      WSrw.vRebate = Round(s,SetRoundModeD(1));
      MatRowPut(WSr,rownr,WSrw);
    end;    
    if (WSDchrsum(WSr,rownr,GenOptRec.UseDiscount)) then begin
      WSSumup(WSr);    
    end;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassSumEFAfter = true;
  return;
end;

function Boolean WSDClassMotherNrEFAfter(Integer wn,Integer rownr,Integer changedf)
begin  
  record WSVc WSr;
  row WSVc WSrw;

  if (changedf!=0) then begin
    GetWindowRecord(wn,WSr);
    WSVc_PasteMotherNr(WSr,rownr);
    PutWindowRecord(wn,WSr);
  end;
  WSDClassMotherNrEFAfter = true;
  return;
end;

function Boolean WSDClassCustCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record WSVc WSr;
  string 255 warning;

  GetWindowRecord(wn,WSr);    
  if ((changed!=0) or ((blank(WSr.Addr0)) and (nonblank(WSr.CustCode)))) then begin
    GetWindowRecord(wn,WSr);    
    DeselectWindow(wn,false);
    if (PasteCUInWS(WSr,warning)) then begin
      WSSumup(WSr);
      PutWindowRecord(wn,WSr);
      if (nonblank(warning)) then begin
        MessageBox(0,warning);
      end;
    end else begin
      Beep;
    end;
  end;
  WSDClassCustCodeEFAfter = true;
  return;
end;

updating function Boolean WSDClassWONrEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record WSVc WSr;
  string 255 warning;

  if (changed!=0) then begin
    GetWindowRecord(wn,WSr);    
    SVOVc_PasteWONr(WSr,warning);
    if (nonblank(warning)) then begin
      MessageBox(0,warning);
    end;
  end;
  WSDClassWONrEFAfter = true;
  return;
end;

function Boolean WSDClassSVONrEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record WSVc WSr;
  record WOVc WOr;
  string 255 warning;

  if (changed!=0) then begin
    DeselectWindow(wn,false);
    GetWindowRecord(wn,WSr);    
    SVOVc_PasteSVONr(WSr);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassSVONrEFAfter = true;
  return;
end;

function Boolean WSDClassCustContactEFAfter(Integer wn,Integer changed)
BEGIN
  record WSVc WSr;
  record CUVc ContactCUr;
  string 255 warning;

  if (changed!=0) then begin
    GetWindowRecord(wn,WSr);    
    DeselectWindow(wn,false);
    if (FindCustomerRelation(WSr.CustCode,WSr.CustContact,ContactCUr)) then begin
      WSr.Phone = ContactCUr.Phone;
      WSr.Fax = ContactCUr.Fax;
    end;
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassCustContactEFAfter = true;
  return;
end;

function Boolean WSDClassEMCodeEFAfter(Integer wn,Integer changed)
BEGIN
  record WSVc WSr;
  string 255 warning;

  if (changed!=0) then begin
    GetWindowRecord(wn,WSr);    
    DeselectWindow(wn,false);
    WSVc_PasteEMCode(WSr,WindEFstr(wn));
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassEMCodeEFAfter = true;
  return;
end;

function Boolean WSDClassPriceListEFAfter(Integer wn,Integer changed)
BEGIN
  record WSVc WSr;
  record PLDefVc PLDefr;
  string 255 warning;

  if (changed!=0) then begin
    DeselectWindow(wn,false);
    GetWindowRecord(wn,WSr);    
    PLDefr.Code = WSr.PriceList;
    if (ReadFirstMain(PLDefr,1,true)) then begin
      WSr.InclVAT = PLDefr.InclVAT;
    end;
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassPriceListEFAfter = true;
  return;
end;

function Boolean WSDClassSerialNrEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record WSVc WSr;
  Boolean res;
  
  res = true;
  if ((changedf) and (rownr>=0)) then begin
    GetWindowRecord(wn,WSr);
    WSVc_PasteSerialNr(WSr,rownr);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassSerialNrEFAfter = res;
  RETURN;
END;

function Boolean WSVc_PasteACShort(var record WSVc WSr,Integer rownr)
BEGIN
  row WSVc WSrw;
  string 255 tstr,obj;
  Boolean res;
  
  if (rownr>=0) then begin
    MatRowGet(WSr,rownr,WSrw);
    GetACShortAccount(WSrw.ACShort,tstr,obj);
    WSrw.CostAcc = tstr;
    WSrw.Objects = obj;
    if (nonblank(obj)) then begin
      if (nonblank(WSrw.Objects)) then begin
        if (obj<>WSrw.Objects) then begin
          obj = obj & "," & WSrw.Objects;
          NormalizeObjstr(obj);
        end;  
      end;        
      WSrw.Objects = obj;
    end;
    MatRowPut(WSr,rownr,WSrw);
    res = true;
  end else begin
    GetACShortAccount(WSr.ACShort,tstr,obj);
    WSr.CostAcc = tstr;
    WSr.Objects = obj;
    if nonblank(obj) then begin
      if nonblank(WSr.Objects) then begin
        if (obj<>WSr.Objects) then begin
          obj = obj & "," & WSr.Objects;
          NormalizeObjstr(obj);
        end;  
    end;        
      WSr.Objects = obj;
    end;
    res = true;
    
  end;  
  WSVc_PasteACShort = res;
  RETURN;
END;

function Boolean WSDClassACShortEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record WSVc WSr;
  Boolean res;
  
  if (changed!=0) then begin
    GetWindowRecord(wn,WSr);
    res = WSVc_PasteACShort(WSr,rownr);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassACShortEFAfter = res;
  RETURN;
END;

function Boolean WSDClassTransDateEFAfter(Integer wn,Boolean changedf)
begin
  record WSVc WSr;
  Boolean res;
  
  if (changedf) then begin
    GetWindowRecord(wn,WSr);
    WarnFutureDate(true,WSr.TransDate);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassTransDateEFAfter = res;
  return;
end;

function Boolean WSDClassItemTypeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record WSVc WSr;
  Boolean res;
  
  if (changedf) then begin
    GetWindowRecord(wn,WSr);
    res = WSVc_PasteItemType(WSr,rownr);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassItemTypeEFAfter = res;
  return;
end;

global
function Boolean WSDClassTaxTemplateCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin  
  record WSVc WSr;
  Boolean chsum;

  if (changedf) then begin  
    GetWindowRecord(wn,WSr);
    WSVc_PasteTaxTemplateCode(WSr,rownr,chsum);
    if (chsum) then begin
//      WSDchsum(WSr,rownr);
    end;    
    WSSumup(WSr);
    PutWindowRecord(wn,WSr);
  end;  
  WSDClassTaxTemplateCodeEFAfter = true;
  return;
end;

global
updating function Boolean WSDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "ArtCode": res = WSDClassArtCodeEFAfter(wn,fn,rownr,changed);
    case "Quant": res = WSDClassQuantEFAfter(wn,rownr,changed);
    case "Recepy": res = WSDClassRecepyEFAfter(wn,rownr,changed);
    case "Price": res = WSDClassPriceEFAfter(wn,rownr,changed);
    case "vRebate": res = WSDClassvRebateEFAfter(wn,rownr,changed);
    case "BasePrice": res = WSDClassBasePriceEFAfter(wn,rownr,changed);
    case "VATCode": res = WSDClassVATCodeEFAfter(wn,rownr,changed);
    case "Sum": res = WSDClassSumEFAfter(wn,rownr,changed);
    case "MotherNr": res = WSDClassMotherNrEFAfter(wn,rownr,changed);    
    case "CustCode": res = WSDClassCustCodeEFAfter(wn,fn,rownr,changed);
    case "WONr": res = WSDClassWONrEFAfter(wn,fn,rownr,changed);
    case "SVONr": res = WSDClassSVONrEFAfter(wn,fn,rownr,changed);
    case "CustContact": res = WSDClassCustContactEFAfter(wn,changed);
    case "EMCode": res = WSDClassEMCodeEFAfter(wn,changed);
    case "PriceList": res = WSDClassPriceListEFAfter(wn,changed);
    case "SerialNr": res = WSDClassSerialNrEFAfter(wn,rownr,changed!=0);
    case "ACShort": res = WSDClassACShortEFAfter(wn,fn,rownr,changed);
    case "TransDate": res = WSDClassTransDateEFAfter(wn,changed!=0);
    case "ItemType": res = WSDClassItemTypeEFAfter(wn,rownr,changed!=0);
    case "TaxTemplateCode": res = WSDClassTaxTemplateCodeEFAfter(wn,rownr,changed!=0);
  end;
  WSDClassAfterEditField = res;
  RETURN;
END;

global
procedure PrintWSL(Integer wn,Boolean previewf)
BEGIN
  record WSVc WSr;
  Integer i;
  
  StartPrintDialogGroup;
  i = 1;
  while (GetRecordFromBrowse(WSr,wn,i)) begin
    RecordActionWS_Print(WSr,previewf);
    i = i + 1;
  end;
  EndPrintDialogGroup;
  RETURN;
END;

global
function Boolean WSDClassSwitchRow(Integer wn,Integer rownr)
begin        
  record WSVc WSr;  
  row WSVc WSrw; 
  Integer rwcnt;
  Boolean res;
  val t,tproc,unitprdisc,s,rowsum,sum;
  string 255 recepy;
  record TaxMatrixVc TMr;

  res = true;
  GetWindowRecord(wn,WSr);
  rwcnt = MatRowCnt(WSr);  
  if ((rownr<rwcnt) and (rownr>=0)) then begin
    MatRowGet(WSr,rownr,WSrw);
    if (WSrw.stp==1) then begin
      t = WSrw.Quant * WSrw.BasePrice;

      FindSalesExVat(TMr,WSrw.VATCode,WSrw.Sum,0,0,rowsum);
      CalcProc(rowsum,t,tproc);    
      unitprdisc = WSrw.Sum/WSrw.Quant;
      unitprdisc = Round(unitprdisc,DefaultRoundMode);
      recepy = WSrw.Recepy;    
/*  This makes one extra call, I can see why but we need another solution
      if (blank(recepy)) then begin
        GetRecepy(WSrw.ArtCode,recepy);
      end;  
    
*/  
      SendArtStat(WSrw.ArtCode,WSr.Location,recepy,t,tproc,unitprdisc,WSr.TransDate,0);
    end;
  end;
  WSDClassSwitchRow = res;  
  return;
end;

global
procedure ItemStatusWSDsm()
BEGIN
  Integer wn,nwn;
  Boolean testf;
  record RcVc RepSpec;
  
  wn = CurWindow;
  nwn = OpenArtStat(wn,RepSpec,false);
  testf = WSDClassSwitchRow(wn,WindowActiveRow(wn));
  RETURN
END;

global
procedure OpenWOFromWSDsm()
BEGIN
  record WSVc WSr;
  record WOVc WOr;
  Integer wn,wn2;

  wn = CurWindow;
  GetWindowRecord(wn,WSr);
  WOr.SerNr = WSr.WONr;
  if (ReadFirstMain(WOr,1,true)) then begin
    wn2 = OpenWindow("WODClass",1,0,"","",WOr);
  end;
  RETURN;
END;

global
function Boolean WSDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record WSVc WSr;
  row WSVc WSrw;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  if (WindowState(wn)==Rs_update) then begin
    res = false;
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag==0) then begin
      res = true;
      switch (fieldname) begin
        case "CostAcc": 
          if (UserCanAction("EditingWSCostAcc",true)==false) then begin res = false; end;
        case "CustCode": 
          if (WSr.SVONr>0) then begin
            res = false;
          end;
        case "PriceList": 
          res = UserCanAction("ChangePrices",true);
        case "Price": 
          res = UserCanAction("ChangeUnitPrices",true);
        case "vRebate": 
          res = UserCanAction("ChangeUnitPrices",true);
        case "Sum": 
          res = UserCanAction("ChangeUnitPrices",true);      
      end;
      goto LWSDClassActiveEditField;
    end;
    switch (fieldname) begin
      case "LangCode": res = true;
    end;
  end else begin
    switch (fieldname) begin
      case "PriceList": 
        res = UserCanAction("ChangePrices",true);
      case "Price": 
        res = UserCanAction("ChangeUnitPrices",true);
      case "vRebate": 
        res = UserCanAction("ChangeUnitPrices",true);
      case "Sum": 
        res = UserCanAction("ChangeUnitPrices",true);      
    end;
  end;
LWSDClassActiveEditField:;  
  switch (fieldname) begin
    case "Returned": res = false;
    case "Invd": res = false;
    case "Spec": 
      if (HasLocalization("PRT")) then begin
        GetWindowRecord(wn,WSr);
        MatRowGet(WSr,rownr,WSrw);
        if (nonblank(WSrw.ArtCode)) then begin
          res = false;
        end;
      end;
  end;
  WSDClassActiveEditField = res;
  RETURN;
END;

global
function Boolean WSDClassPrelOKButtonAction(Integer wn,Integer value)
begin
  Boolean res;
  record WSVc WSr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;  
  WSDClassPrelOKButtonAction = res;
  return;
end;

global
function Boolean WSDClassOKFlagButtonAction(Integer wn,Integer value)
begin
  Boolean res;
  record WSVc WSr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
      if (UserCanAction("UnOKAll",false)) then begin
        res = true;
      end;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;
  WSDClassOKFlagButtonAction = res;
  return;
end;

global
function Boolean WSDClassInvFlagButtonAction(Integer wn,Integer value)
begin
  Boolean res;
  record WSVc WSr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;  
  WSDClassInvFlagButtonAction = res;
  return;
end;

global 
function Boolean WSDClassDeleteRowTest(Integer wn,Integer rownr)
begin
  record WSVc WSr;
  Boolean res;
  Integer updatemode;

  res = true;
  updatemode = 2;//Rs_update
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin res = false; end;
  end;
  WSDClassDeleteRowTest = res;
  return;
end;

global
function Boolean WSDClassOnOverStrike(Integer wn,Integer rownr)
BEGIN
  record WSVc WSr;

  if (rownr>=0) then begin
    GetWindowRecord(wn,WSr);    
    WSSumUp(WSr);
    PutWindowRecord(wn,WSr);    
  end;
  WSDClassOnOverStrike = true;
  RETURN;
END;

/* Not needed anymore...
global
procedure WSDEnterSpec(var record WSVc WSp,Integer rownr,string fieldname)
BEGIN
  row WSVc WSrw,WS1rw;
  Integer i,rwcnt;
  record UserVc Userr;
  row UserVc Userrw;
  record EGVc EGr;
  row EGVc EGrw;
  Boolean changedf;

  if (rownr<0) then begin
    goto LWSDEnterSpec;
  end;
  
//  if (fn>=sf) then begin
    MatRowGet(WSp,rownr,WSrw);
    if (rownr>0) then begin
      MatRowGet(WSp,rownr-1,WS1rw);
    end;
    switch (fieldname) begin
      case "ArtCode":
        if (rownr>0) then begin
          WSrw.ArtCode = WS1rw.ArtCode;
          WSrw.ItemType = WS1rw.ItemType;
          WSrw.MotherNr = WS1rw.MotherNr;
          MatRowPut(WSp,rownr,WSrw);
        end;
    end;    
LWSDEnterSpec:;    
  RETURN;
END;

global
function Boolean WSDClassOnEnterKey(Integer wn,string fieldname,Integer fn,Integer rownr)
BEGIN
  Boolean res;
  record WSVc WSr;
  
  res = false;
  if ((WindowState(wn)==2) or (WindowState(wn)==1)) then begin//Rs_update,Rs_insert
    GetWindowRecord(wn,WSr);
    WSDEnterSpec(WSr,rownr,fieldname);
    res = true;
    PutWindowRecord(wn,WSr);
  end;
  WSDClassOnEnterKey = true;
  RETURN;
END;
*/

global 
function Boolean WSDClassInsertRowTest(Integer wn,Integer rownr)
begin
  record WSVc WSr;
  Boolean res;

  res = true;
  if (WindowState(wn)==2) then begin//Rs_update
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin res = false; end;
  end;
  WSDClassInsertRowTest = res;
  return;
end;

global 
function Boolean WSDClassOverStrikeTest(Integer wn,Integer rownr)
begin
  record WSVc WSr;
  Boolean res;
  Integer updatemode,rwcnt;

  if (rownr<0) then begin
    goto LWSDClassOverStrikeTest;
  end;
  GetWindowRecord(wn,WSr);
  updatemode = 2;//Rs_update
  rwcnt = MatRowCnt(WSr);
  if (WindowState(wn)!=updatemode) then begin
//    MessageBox(1050,"");
    goto LWSDClassOverStrikeTest;
  end;
  if (WindowOldRowcnt(wn)==rwcnt) then begin
//    MessageBox(1050,"");
    goto LWSDClassOverStrikeTest;
  end;
  if (rownr<WindowOldRowcnt(wn)) then begin res = true; end;
LWSDClassOverStrikeTest:;
  WSDClassOverStrikeTest = res;
  return;
end;

function string 40 WSDClassSpecPasteNameObjects(Integer wn)
begin
  record WSVc WSr;
  row WSVc WSrw;
  Integer rownr;
  string 200 typ;
  string 255 psname,accnr;

  GetWindowRecord(wn,WSr);
  rownr = WindowActiveRow(wn);
  typ="";
  if (rownr>=0) then begin
    MatRowGet(WSr,rownr,WSrw);
    accnr = WSrw.CostAcc;
  end;
  if (blank(accnr)) then begin
    accnr = WSr.CostAcc;
  end;
  FindOTforAcc(accnr,typ);
  if (nonblank(typ)) then begin
    psname = "TRObjSClass";
  end else begin    
    psname = ObjSClassPasteSpecialWindowClass("ObjSClass");
  end;
  WSDClassSpecPasteNameObjects = psname;
  return;
end;

global
function string 40 WSDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 255 psname;
  
  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "SerialNr": psname = SerialNrSClassSpecPName(defpsname);
    case "Objects": psname = WSDClassSpecPasteNameObjects(wn);
  end;
  WSDClassSpecPasteName = psname;
  return;
end;

global
function Boolean WSDClassExportFlagButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  record WSVc WSr;
  Integer normalmode,updatemode;

  res = false;
  WSDClassExportFlagButtonAction = res;
  RETURN;
END;

global
updating procedure ActFromWSDsm()
begin
  record WSVc WSr;
  record ActVc Actr;
  Integer wn,nwn;
  
  wn = CurWindow;
  if (WindowState(wn)==0) then begin//normal
    GetWindowRecord(wn,WSr);
    RecordNew(Actr);
    ActVcRecordDefClient(Actr);
    if (MakeActFromWS(WSr,Actr)) then begin
      nwn = OpenWindow("ActDClass",1,0,"","",Actr);
      SetRLink(wn,true);  
    end;
  end;
  return;
end;

global
updating procedure WSVcCreateActivity()
begin
  ActFromWSDsm;
  return;
end;

global
updating function POFromWSDsm()
begin
  record WSVc WSr;
  record POVc POr;
  array record POVc aPOr;
  Integer wn,nwn,cnt;
  LongInt r;
  string 255 msg;
  
  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    if (UserCanAction("WSToPO",true)) then begin // TODO: to add to access rights
      GetWindowRecord(wn,WSr);
      r = RecordAction_raPasteWSInPO(WSr,aPOr,cnt,msg);
      if (r==0) then begin
        switch (cnt) begin
          case 0:
            msg = ": 0";
            MessageBox(20273,msg);
          case 1:
            POr = aPOr[0];
            nwn = OpenWindow("PODClass",1,0,"","",POr);
          otherwise
            msg = ": " & cnt;
            msg = msg & chr(10);
            msg = msg & USetStr(20274);
            MessageBox(20273,msg);
        end;
      end else begin
        MessageBox(r,msg);
      end;
    end else begin
      MessageBox(1274,StringFromStringSet(3,"WSToPO"));
    end;
  end else begin
    Beep;
  end;
  return;
end;

global
function Boolean WSDClassUpdStockFlagButtonAction(Integer wn,Integer value)
begin
  Boolean res;
  record WSVc WSr;
  Integer normalmode,updatemode;

  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  GetWindowRecord(wn,WSr);  
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      res = false;
    end;
  end; 
  if (res) then begin
    res = UserCanAction("AllowWSUpdateStockChange",false);
  end;
  WSDClassUpdStockFlagButtonAction = res;
  return;
end;

global
updating procedure RetFromWSDsm()
begin
  record RetVc Retr;
  record WSVc WSr;
  Integer wn,r,nwn;
  record LocalMachineBlock LMb;

  wn = CurWindow;
  DeselectWindow(wn,false);
  if (WindowState(wn)==Rs_normal) then begin
    BlockLoad(LMb);
    GetWindowRecord(wn,WSr);
    if (WSr.OKFlag!=0) then begin
      if (UserCanAction("WSToRet",true)) then begin
        r = PasteWSInRet(Retr,WSr.SerNr,LMb.DefReturnLocation);      
        switch (r) begin
          case 0:
            nwn = OpenWindow("RetDClass",1,0,"","",Retr);
          otherwise 
            MessageBox(r,"");
        end;                
      end else begin
        MessageBox(1274,StringFromStringSet(3,"WSToRet"));
      end;
    end;
  end else begin
    Beep;
  end;
  return;
end;

global
updating procedure WSFromQTDsm()
BEGIN
  record WSVc WSr;
  record QTVc QTr;
  record WSVc WS2r;
  Integer nwn,wn;
  Boolean res;

  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    GetWindowRecord(wn,QTr);
    if (QTr.SVOSerNr>0) then begin
      WS2r.SVONr = QTr.SVOSerNr;
      res = RecordAction_raPasteQTInWS(WSr,QTr);
      if (res==true) then begin
        nwn = OpenWindow("WSDClass",1,0,"","",WSr);
      end else begin
        Beep;
      end;
    end;  
  
  end else begin
    Beep;
  end;
  RETURN;
END;

global
procedure OpenVatMatrixWSDsm()
begin
  record TaxMatrixVc TMr;
  record WSVc WSr;
  row WSVc WSrw;
  longint nwn,wn,rowno;
  
  wn = CurWindow;
  GetWindowRecord(wn,WSr);
  rowno = WindowActiveRow(wn);
  
  if (rowno>=0) then begin
    MatRowGet(WSr,rowno,WSrw);
    UnpackRowFieldMatrix(WSrw,"TaxMatrix",TMr);
  end else begin
    UnpackFieldMatrix(WSr,"TaxMatrix",TMr);
  end;
  
  nwn = OpenWindow("TaxMatrixVClass",0,wn,"","",TMr);
  PutWindowString(nwn,"TaxMatrixActiveRow",rowno);
  
  PutWindowRecord(nwn,TMr);
  PutWindowPrevRecord(nwn,TMr);
  SetWindowState(nwn,Rs_normal);
  
  return;
end;
