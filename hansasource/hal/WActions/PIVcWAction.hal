remote function Boolean PIVc_PasteVECode(var record PIVc);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure GetSalesGroup(string,var string);

function Boolean PIDClassItemCodeEFAfter(Integer wn,Integer changed)
BEGIN
  Boolean res;
  record PIVc PIr;
  record INVc INr;
  
  if (changed!=0) then begin
    res = true;
    DeselectWindow(wn,false);
    GetWindowRecord(wn,PIr);
    if (ReadFirstItem(PIr.ItemCode,INr,true,true)) then begin
      PIr.Comment = INr.Name;
      PIr.PurPrice = INr.InPrice;
      PutWindowRecord(wn,PIr);        
    end else begin
      Beep;
    end;
  end;
  PIDClassItemCodeEFAfter = res;
  RETURN;
END;

function Boolean PIDClassSalesManEFAfter(Integer wn,Integer changed)
BEGIN
  Boolean res;
  record PIVc PIr;
  string 255 tstr;
  
  if (changed!=0) then begin
    res = true;
    DeselectWindow(wn,false);
    GetWindowRecord(wn,PIr);
    GetSalesGroup(PIr.SalesMan,tstr);
    PIr.SalesGroup = tstr;
    PutWindowRecord(wn,PIr);        
  end;
  PIDClassSalesManEFAfter = res;
  RETURN;
END;

function Boolean PIDClassVECodeEFAfter(Integer wn,Integer changed)
begin
  Boolean res;
  record PIVc PIr;
  record PIVc PI2r;
  
  if (changed!=0) then begin
    res = true;
    DeselectWindow(wn,false);
    GetWindowRecord(wn,PIr);
    if (PIVc_PasteVECode(PIr)) then begin
      PutWindowRecord(wn,PIr);        
    end;
  end;
  PIDClassVECodeEFAfter = res;
  return;
end;

global
function Boolean PIDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "ItemCode": res = PIDClassItemCodeEFAfter(wn,changed);
    case "SalesMan": res = PIDClassSalesManEFAfter(wn,changed);
    case "VECode": res = PIDClassVECodeEFAfter(wn,changed);
  end;
  PIDClassAfterEditField = res;
  RETURN;
END;

procedure PIDClassSpecPasteNameItemCode(Integer wn,var string psname)
begin
  record PIVc PIr;
  Integer rownr;

  GetWindowRecord(wn,PIr);
  rownr = WindowActiveRow(wn);
  DeselectWindow(wn,false);//to get VARINSClass working
  WindowFieldGoto(wn,PIr,rownr,"ItemCode",false);
  return;
end;

global
function string 40 PIDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 40 psname;

  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "ItemCode": PIDClassSpecPasteNameItemCode(wn,psname);
  end;
  PIDClassSpecPasteName = psname;
  return;
end;
