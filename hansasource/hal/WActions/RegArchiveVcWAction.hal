remote procedure ReFillAffectedRegs(var record RegArchiveVc);

function Boolean RegArchiveDClassTransDateEFAfter(Integer wn,Integer fn,Integer rownr,Integer changedf)
begin
  Boolean res;
  record RegArchiveVc RegArchiver;
  row RegArchiveVc RegArchiverw;
  row RegArchiveVc RegArchive2rw;
  Integer i,rwcnt;
  
  res = true;
  if (changedf!=0) then begin
    GetWindowRecord(wn,RegArchiver);
    DeselectWindow(wn,false);
    rwcnt = MatRowCnt(RegArchiver);
    if (rownr<0) then begin
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(RegArchiver,i,RegArchiverw);
        RegArchiverw.TransDate = RegArchiver.TransDate;
        MatRowPut(RegArchiver,i,RegArchiverw);
      end;
    end else begin
      MatRowGet(RegArchiver,rownr,RegArchive2rw);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(RegArchiver,i,RegArchiverw);
        if (SetInSet(RegArchive2rw.FileName,RegArchiverw.ConnectTo)) then begin
          RegArchiverw.TransDate = RegArchive2rw.TransDate;
          MatRowPut(RegArchiver,i,RegArchiverw);
        end;
      end;
    end;
    PutWindowRecord(wn,RegArchiver);
  end;
  RegArchiveDClassTransDateEFAfter = res;
  return;
end;

global
function Boolean RegArchiveDClassAfterEditField(Integer wn,string fieldname,Integer fn,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  
  res = false;
  switch (fieldname) begin
    case "TransDate": res = RegArchiveDClassTransDateEFAfter(wn,fn,rownr,changed);
  end;
  RegArchiveDClassAfterEditField = res;
  RETURN;
END;

global
function Boolean RegArchiveDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record RegArchiveVc RegArchiver;
  row RegArchiveVc RegArchiverw;
  
  res = true;
  GetWindowRecord(wn,RegArchiver);
  switch (fieldname) begin
    case "FileName": res = false;
    case "Text": res = false;
    case "TransDate":
      if (rownr>=0) then begin
        MatRowGet(RegArchiver,rownr,RegArchiverw);
        if (nonblank(RegArchiverw.ConnectTo)) then begin
          res = false;
        end;
      end;
  end;
  RegArchiveDClassActiveEditField = res;
  RETURN;
END;

global
procedure ReFillAffectedRegDsm()
begin
  record RegArchiveVc RegArchiver;
  Integer wn;

  wn = CurWindow;
  DeselectWindow(wn,false);
  GetWindowRecord(wn,RegArchiver);
  while (MatRowCnt(RegArchiver)>0) begin
    MatRowDelete(RegArchiver,0);
  end;
  ReFillAffectedRegs(RegArchiver);
  PutWindowRecord(wn,RegArchiver);
  return;
end;
