global
procedure OpenWebClientCompanies()
begin
  record RcVc RepSpec;
  
  if (IsLoggedIn) then begin
    OpenWindow("WebClientCompWClass",1,0,"","",RepSpec);
  end;
  return;
end;

global
function Boolean WebClientCompWClassOnOKWindow(Integer wn)
begin
  string 255 url;
  LongInt line;
  Boolean res;
  
  res = true;
  line = SelectedListLine(wn);
  if (line>=0) then begin
    url = GetListTag(wn,line);
    OpenWebBrowser(url & "/startwebclient");
  end;
  WebClientCompWClassOnOKWindow = res;
  return;
end;

global
procedure OpenSecondaryServerWebClient()
begin
  Integer wn;
  string 255 url;
  LongInt sel;
  
  wn = CurWindow;
  sel = SelectedListLine(wn);
  if (sel>=0) then begin
    url = GetListTag(wn,sel);
    OpenWebBrowser(url & "/startwebclient");
  end;
  return;
end;

global
procedure GetSecondaryServerNativeClient()
begin
  Integer wn;
  string 255 url;
  LongInt sel;
  
  wn = CurWindow;
  sel = SelectedListLine(wn);
  if (sel>=0) then begin
    url = GetListTag(wn,sel);
    OpenWebBrowser(url & "/install");
  end;
  return;
end;

global
function Boolean WebClientCompWClassOnOpenWindow(Integer wn)
begin
  Integer i,rwcnt;
  record WebClientCompBlock WCCb;
  row WebClientCompBlock WCCbrw;
  
  BlockLoad(WCCb);
  rwcnt = MatRowCnt(WCCb);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(WCCb,i,WCCbrw);
    SetListStringAndTag(wn,i,WCCbrw.Name,WCCbrw.Url);  
  end;
  WebClientCompWClassOnOpenWindow = false;
  return;
end;
