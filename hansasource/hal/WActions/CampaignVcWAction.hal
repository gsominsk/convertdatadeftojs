remote updating function Integer CreateLetFromCampaign(record CampaignVc,var record LetVc,var string);
remote procedure CampaignPasteLeadCode(var record CampaignVc,Integer);

function Boolean CampaignDClassStatusEFAfter(Integer wn,Boolean changedf)
begin
  Boolean res;
  record CampaignVc Campaignr;
  record CampaignStatusVc CSr;

  res = true;
  if (changedf) then begin
    GetWindowRecord(wn,Campaignr);
    CSr.Code = Campaignr.Status;
    ReadFirstMain(CSr,1,true);
    Campaignr.StatusComment = CSr.Comment;
    PutWindowRecord(wn,Campaignr);
  end;
  CampaignDClassStatusEFAfter = res;
  return;
end;

function Boolean CampaignDClassMediaTypeEFAfter(Integer wn,Boolean changedf)
begin
  Boolean res;
  record CampaignVc Campaignr;
  record MediaTypeVc MTr;

  res = true;
  if (changedf) then begin
    GetWindowRecord(wn,Campaignr);
    MTr.Code = Campaignr.MediaType;
    ReadFirstMain(MTr,1,true);
    Campaignr.MediaComment = MTr.Comment;
    PutWindowRecord(wn,Campaignr);
  end;
  CampaignDClassMediaTypeEFAfter = res;
  return;
end;

function Boolean CampaignDClassCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  Boolean res;
  record CampaignVc Campaignr;

  res = true;
  if (changedf) then begin
    GetWindowRecord(wn,Campaignr);
    CampaignPasteLeadCode(Campaignr,rownr);
    PutWindowRecord(wn,Campaignr);
  end;
  CampaignDClassCodeEFAfter = res;
  return;
end;

global
function Boolean CampaignDClassAfterEditField(Integer wn,string fieldname,Integer fn,Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname) begin
    case "Status": res = CampaignDClassStatusEFAfter(wn,changed!=0);
    case "MediaType": res = CampaignDClassMediaTypeEFAfter(wn,changed!=0);
    case "Code": res = CampaignDClassCodeEFAfter(wn,rownr,changed!=0);
  end;
  CampaignDClassAfterEditField = res;
  return;
end;

global
function string 40 CampaignDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 255 psname;
  record CampaignVc Campaignr;
  row CampaignVc Campaignrw;
  Integer rownr;
  
  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "Code":
      GetWindowRecord(wn,Campaignr);
      rownr = WindowActiveRow(wn);
      if (rownr>=0) then begin
        MatRowGet(Campaignr,rownr,Campaignrw);
      end;
      switch (Campaignrw.CodeType) begin
        case kCampaignRowCodeTypeLead: psname = "CULeadSClass";
        case kCampaignRowCodeTypeCU: psname = "CUSClass";
      end;
  end;
  CampaignDClassSpecPasteName = psname;
  return;
end;

global
updating procedure NewLetCampaignDsm()
begin
  record CampaignVc Campaignr;
  record LetVc Letr;
  Integer wn,nwn,err;
  string 255 warning;
  
  wn = CurWindow;
  GetWindowRecord(wn,Campaignr);
  err = CreateLetFromCampaign(Campaignr,Letr,warning);
  switch (err) begin
    case 0:    
      nwn = OpenWindow("LetDClass",1,0,"","",Letr);  
    otherwise
      MessageBox(err,"");
  end;
  return;
end;
