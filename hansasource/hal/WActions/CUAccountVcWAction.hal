global
function Boolean CUAccountDClassCustCodeEFAfter(Integer wn,Integer rownr,Integer changedf)
begin
  record CUAccountVc CUAccountr;
  record CUVc CUr;
  
  if (changedf!=0) then begin  
    GetWindowRecord(wn,CUAccountr);
    CUr.Code = CUAccountr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      CUAccountr.Name = CUr.Name;
    end else begin
      CUAccountr.Name = "";
    end;
    PutWindowRecord(wn,CUAccountr);
  end;
  CUAccountDClassCustCodeEFAfter = true;
  return;
end;

global
function Boolean CUAccountDClassInvToCustEFAfter(Integer wn,Integer rownr,Integer changedf)
begin
  record CUAccountVc CUAccountr;
  record CUVc CUr;
  
  if (changedf!=0) then begin  
    GetWindowRecord(wn,CUAccountr);
    CUr.Code = CUAccountr.InvToCust;
    if (ReadFirstMain(CUr,1,true)) then begin
      CUAccountr.InvToCustName = CUr.Name;
    end else begin
      CUAccountr.InvToCustName = "";
    end;
    PutWindowRecord(wn,CUAccountr);
  end;
  CUAccountDClassInvToCustEFAfter = true;
  return;
end;

global
function Boolean CUAccountDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "CustCode": res = CUAccountDClassCustCodeEFAfter(wn,rownr,changed);
    case "InvToCust": res = CUAccountDClassInvToCustEFAfter(wn,rownr,changed);
  end;
  CUAccountDClassAfterEditField = res;
  RETURN;
END;

global
procedure CUAccountDsm()
BEGIN
  record RcVc RepSpec;
  record CUAccountVc CUAccountr;

  GetWindowRecord(CurWindow,CUAccountr);
  RepSpec.f1 = CUAccountr.CustCode;
  RepSpec.f2 = CUAccountr.FileName;
  RepSpec.long1 = CUAccountr.SerNr;
  RepSpec.flags[1] = 1; // Don't print others events on accounts paying for you...
  RepSpec.Media = mtScreen;
  RepSpec.repname = "CUAccountRn";
  RunReport(RepSpec,0);
  RETURN;
END;

// This is best effort only, could be more precise....
global
procedure DblCUAccountVc(string dblstr,string l,Integer currepwn)
begin
  Integer wn;
  record CUAccountVc CUAccountr;
  
  CUAccountr.CustCode = dblstr;
  CUAccountr.ThisAccount = StringToLongInt(FirstInRange(l,20));
  CUAccountr.InvToCust = LastInRange(l,20);
  if (ReadFirstKey("InvToCust",CUAccountr,3,true)) then begin
    wn = OpenWindow("CUAccountDClass",1,0,"","",CUAccountr);
  end; 
  return;
end;

