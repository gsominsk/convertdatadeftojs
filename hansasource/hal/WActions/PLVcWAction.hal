remote function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);

function Boolean PLDClassArtCodeEFAfter(Integer wn,Integer changed)
BEGIN
  Boolean res;
  record PLVc PLr;
  record INVc INr;
  
  if (changed!=0) then begin
    res = true;
    DeselectWindow(wn,false);
    GetWindowRecord(wn,PLr);
    if (ReadFirstItem(PLr.ArtCode,INr,true,false)) then begin
      PLr.Comment = INr.Name;
    end;
    PutWindowRecord(wn,PLr);        
  end;
  PLDClassArtCodeEFAfter = res;
  RETURN;
END;

function Boolean PLDClassPLCodeEFAfter(Integer wn,Integer changed)
begin
  Boolean res;
  record PLVc PLr;
  record PLDefVc PLDr;
  
  if (changed!=0) then begin
    res = true;
    DeselectWindow(wn,false);
    GetWindowRecord(wn,PLr);
    PLDr.Code = PLr.PLCode;
    if (ReadFirstMain(PLDr,1,true)) then begin
      if (PLDr.DepPrice==2) then begin
        PLr.BBGenTrans = 1;
      end;
    end;
    PutWindowRecord(wn,PLr);        
  end;
  PLDClassPLCodeEFAfter = res;
  return;
end;

global
function Boolean PLDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "ArtCode": res = PLDClassArtCodeEFAfter(wn,changed);
    case "PLCode": res = PLDClassPLCodeEFAfter(wn,changed);
  end;
  PLDClassAfterEditField = res;
  RETURN;
END;

procedure PLDClassSpecPasteNameArtCode(Integer wn,var string psname)
begin
  record PLVc PLr;
  Integer rownr;

  GetWindowRecord(wn,PLr);
  rownr = WindowActiveRow(wn);
  DeselectWindow(wn,false);//to get VARINSClass working
  WindowFieldGoto(wn,PLr,rownr,"ArtCode",false);
  return;
end;

global
function string 40 PLDClassSpecPasteName(Integer wn,string defpsname)
begin
  string 40 psname;

  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "ArtCode": PLDClassSpecPasteNameArtCode(wn,psname);
  end;
  PLDClassSpecPasteName = psname;
  return;
end;
