external function Boolean GiftReceiptPrint(var record GiftReceiptVc);
remote updating procedure RecordActionGiftReceipt_Print(var record GiftReceiptVc,string,string,Boolean);
external procedure GiftReceiptDClass_RefreshStringList(Integer,record GiftReceiptVc);
external function integer GetSelectedPOSItemRowIndex(integer);

global
procedure GiftReceiptSumup(var record GiftReceiptVc GRr)
begin
  row GiftReceiptVc GRrw;
  Integer i,rwcnt;

  GRr.Sum4 = blankval;
  rwcnt = MatRowCnt(GRr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(GRr,i,GRrw);
    GRr.Sum4 = GRr.Sum4 + GRrw.Sum;
  end;
  return;
end;

global 
function Boolean GiftReceiptDClassDeleteRowTest(Integer wn,Integer rownr)
begin
  record GiftReceiptVc GRr;
  row GiftReceiptVc GRrw;
  Boolean res;
  Integer i,rwcnt;

  res = true;
  switch (WindowState(wn)) begin
    case Rs_update:
      GetPrevWindowRecord(wn,GRr);    
      if (GRr.OKFlag!=0) then begin res = false; end;
  end;
LGiftReceiptDClassDeleteRowTest:;  
  GiftReceiptDClassDeleteRowTest = res;
  return;
end;

global
updating procedure GiftReceiptTouchScreenDeleteRow()
begin
  Integer wn,matrowix;
  record GiftReceiptVc GRr;

  wn = CurWindow;
  if (SelectedListLine(wn)>=0) then begin
    DeselectWIndow(wn,true);
    GetWindowRecord(wn,GRr);

    matrowix = GetSelectedPOSItemRowIndex(wn);
    if (matrowix>=0) then begin
      if (GiftReceiptDClassDeleteRowTest(wn,matrowix)) then begin
        MatRowDelete(GRr,matrowix);
        GiftReceiptSumup(GRr);
        PutWindowRecord(wn,GRr);
      end;
      GiftReceiptDClass_RefreshStringList(wn,GRr);
      WindowDoOK(wn,0);
      SetSelectedListLine(wn,0);
    end;
  end;
  return;
end;

global
procedure GiftReceiptTouchScreenAddressDetails()
begin
  Integer wn,nwn;
  record RcVc RepSpec;
  record GiftReceiptVc GRr;

  wn = CurWindow;
  GetWindowRecord(wn,GRr);
  nwn = OpenWindow("GRAddressDetNPTSSClass",1,wn,"","",RepSpec);
  DeselectWindow(nwn,false);
  RepSpec.f1 = GRr.Addr0;
  RepSpec.f2 = GRr.Addr1;
  RepSpec.f3 = GRr.Addr2;
  RepSpec.f4 = GRr.Addr3;
  RepSpec.f5 = GRr.InvAddr3;
  RepSpec.f6 = GRr.InvAddr4;
  PutWindowRecord(nwn,RepSpec);
  WindowFieldGoto(nwn,RepSpec,-1,"f1",false);
  return;
end;

global
updating procedure ProceedGRAddressDetNPTSSClass()
begin
  Integer wn,mwn;
  record RcVc RepSpec;
  record GiftReceiptVc GRr;

  wn = CurWindow;
  mwn = MotherWindow(wn);
  if (mwn>0) then begin
    DeselectWindow(wn,false);
    GetWindowRecord(wn,RepSpec);
    GetWindowRecord(mwn,GRr);
    GRr.Addr0 = RepSpec.f1;
    GRr.Addr1 = RepSpec.f2;
    GRr.Addr2 = RepSpec.f3;
    GRr.Addr3 = RepSpec.f4;
    GRr.InvAddr3 = RepSpec.f5;
    GRr.InvAddr4 = RepSpec.f6;
    PutWindowRecord(mwn,GRr);
    GiftReceiptDClass_RefreshStringList(mwn,GRr);
    WindowDoOK(mwn,0);
    ReRunWindowDef(mwn);
    CloseWindow(wn);
  end;
  return;
end;

global
updating function Boolean GiftReceiptDClassPrint(Integer wn,Boolean previewf)
begin
  record GiftReceiptVc GRr;
  string 20 docname;
  Boolean testf;
  Integer tf;

  DeselectWindow(wn,false);
  GetWindowRecord(wn,GRr); 
  testf = true;
  if (testf) then begin
    if ((WindowState(wn)==Rs_normal) and (previewf==false)) then begin
      RecordActionGiftReceipt_Print(GRr,"GiftReceiptForm","",true);
    end else begin  
      tf = GRr.OKFlag;
      if (previewf==false) then begin
        GRr.OKFlag = 0;
      end else begin
        GRr.OKFlag = 1;
      end;
      if (PrintDocument(GRr,docname,true)) then begin end;
      GRr.OKFlag = tf;
    end;
  end;
  GiftReceiptDClassPrint = true;
  return;
end;

global
updating procedure GiftReceiptTouchScreenFinish()
begin
  Integer wn;
  record GiftReceiptVc GRr;

  wn = CurWindow;
  DeselectWindow(wn,false);
  GetWindowRecord(wn,GRr);
  GRr.OKFlag = 1;
  PutWindowRecord(wn,GRr);
  GiftReceiptDClass_RefreshStringList(wn,GRr);
  if (WindowDoOK(wn,0)) then begin
    GetWindowRecord(wn,GRr);
    GiftReceiptPrint(GRr);
  end;
//  ReRunWindowDef(wn);
  CloseWindow(wn);
  return;
end;

global
updating procedure GiftReceiptTouchScreenCancel()
begin
  Integer wn;
  record GiftReceiptVc GRr;

  wn = CurWindow;
  DeselectWindow(wn,false);
  GetWindowRecord(wn,GRr);
  if (GRr.OKFlag==0) then begin
    RecordDelete(GRr);
    CloseWindow(wn);
  end;
  return;
end;


global
updating function Boolean NPTSGiftReceiptDClassFunctionKey(LongInt keycode,Boolean shflag,Boolean ctrlflag)
begin
  Integer wn;
  Boolean res;
    
  wn = CurWindow;
  switch (keycode) begin
    case 1: 
      GiftReceiptTouchScreenFinish;
      res = true;
    case 2: 
      GiftReceiptTouchScreenCancel;
      res = true;
    case 3: 
      GiftReceiptTouchScreenAddressDetails;
      res = true;
    case 4: 
      GiftReceiptTouchScreenDeleteRow;
      res = true;
  end;
  NPTSGiftReceiptDClassFunctionKey = res;
  return;
end;
