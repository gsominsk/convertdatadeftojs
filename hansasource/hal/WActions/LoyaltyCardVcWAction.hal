function Boolean LoyaltyCardDClassCustCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
begin
  record LoyaltyCardVc LoyaltyCardr;
  record CUVc CUr;

  GetWindowRecord(wn,LoyaltyCardr);    
  if (changed!=0) then begin
    GetWindowRecord(wn,LoyaltyCardr);    
    DeselectWindow(wn,false);
    CUr.Code = LoyaltyCardr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin 
      LoyaltyCardr.CustName = CUr.Name;
      PutWindowRecord(wn,LoyaltyCardr);
    end else begin
      Beep;
    end;
  end;
  LoyaltyCardDClassCustCodeEFAfter = true;
  return;
end;

global
function Boolean LoyaltyCardDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;
  
  switch (fieldname) begin
    case "CustCode": res = LoyaltyCardDClassCustCodeEFAfter(wn,fn,rownr,changed);
  end;
  LoyaltyCardDClassAfterEditField = res;
  return;
end;

function Boolean LoyaltyCardDClassCustCodeEFActive(Integer wn,Integer fn,Integer wnst,Integer ef)
begin
  Boolean res;
  record PointsHistVc PointsHistr;
  record LoyaltyCardVc LoyaltyCardr;
  
  res = true;
  
  if (wnst==2) then begin//updatemode
    GetPrevWindowRecord(wn,LoyaltyCardr);
    PointsHistr.LMCardNr = LoyaltyCardr.SerNr;
    PointsHistr.CustCode = LoyaltyCardr.CustCode;
    if (ReadFirstKey("CustCode",PointsHistr,2,true)) then begin 
      res = false;
    end;
    if (LoyaltyCardr.Closed!=0) then begin
      res = false;
    end;
  end;

  LoyaltyCardDClassCustCodeEFActive = res;
  return;
end;

function Boolean LoyaltyCardDClassStartDateEFActive(Integer wn,Integer fn,Integer wnst,Integer ef)
begin
  Boolean res;
  record PointsHistVc PointsHistr;
  record LoyaltyCardVc LoyaltyCardr;
  
  res = true;
  
  if (wnst==2) then begin//updatemode
    GetPrevWindowRecord(wn,LoyaltyCardr);
    PointsHistr.LMCardNr = LoyaltyCardr.SerNr;
    PointsHistr.CustCode = LoyaltyCardr.CustCode;
    if (ReadFirstKey("CustCode",PointsHistr,2,true)) then begin 
      res = false;
    end;  
  end;
  LoyaltyCardDClassStartDateEFActive = res;
  return;
end;

global
function Boolean LoyaltyCardDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
begin
  Boolean res;
  record LoyaltyCardVc LoyaltyCardr;
  
  res = true;
  switch (fieldname) begin
    case "CustCode": res = LoyaltyCardDClassCustCodeEFActive(wn,fn,wnst,changed);
    case "StartDate": res = LoyaltyCardDClassStartDateEFActive(wn,fn,wnst,changed);
    otherwise
      switch (wnst) begin
        case Rs_update:
          GetPrevWindowRecord(wn,LoyaltyCardr);  
          if (LoyaltyCardr.Closed!=0) then begin
            res = false;
          end;
      end;
  end;
  LoyaltyCardDClassActiveEditField = res;
  return;
end;