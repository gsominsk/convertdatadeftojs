external procedure Rahaxi_Msg_Event_Cancel(string,val);

global
procedure PasteCredCardInCoupon(var Record CouponVc Couponr)
begin
  record CreditCardVc CCr;
  record CredCardTypeVc CCTr;

  CCr.CreditCardNr = Couponr.CreditCardNr;
  if ReadFirstMain(CCr,1,true) then begin
    if (CCr.Invalid==0) then begin
      Couponr.CardType = CCr.CredCardType;
      Couponr.CUCode = CCr.CUCode;
      Couponr.CUName = CCr.CUName;
      CCTr.Code = CCr.CredCardType;
      if ReadFirstMain(CCTr,1,true) then begin
        Couponr.Direct = CCTr.Direct;
      end;
    end else begin
      beep;
    end;  
  end;
  return;   
end;

global
function Boolean CouponDClassCUCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record CouponVc Couponr;
  record CUVc CUr;
  
  if (changed!=0) then begin
    GetWindowRecord(wn,Couponr);
    CUr.Code = Couponr.CUCode;
    if ReadFirstMain(CUr,1,true) then begin
      Couponr.CUName = CUr.Name;
    end;
    PutWindowRecord(wn,Couponr);
  end;  
  CouponDClassCUCodeEFAfter = res;
  RETURN;
END;

global
function Boolean CouponDClassCreditCardNrEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
Begin
  Boolean res;
  record CouponVc Couponr;
  
  if (changed!=0) then begin
    GetWindowRecord(wn,Couponr);

    PasteCredCardInCoupon(Couponr);
    
    PutWindowRecord(wn,Couponr);
  end;  
  CouponDClassCreditCardNrEFAfter = res;
  RETURN;
END;


global
function Boolean CouponConDClassAccNumberEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;
  record AccVc Accr;
  record CouponConVc CConr;
  Row CouponConVc CConrw;
  
  if (changed!=0) then begin
    GetWindowRecord(wn,CConr); 
    Matrowget(CConr,rownr,CConrw);
    Accr.AccNumber = CConrw.AccNumber;
    if ReadFirstMain(Accr,1,true) then begin
      CConrw.Comment = Accr.Comment;
    end;
    Matrowput(CConr,rownr,CConrw);
    PutWindowRecord(wn,CConr);
  end;  
  CouponConDClassAccNumberEFAfter = res;
  Return;
end;

global
function Boolean CouponDClassStatusButtonAction(Integer wn,Integer ns)
begin
  Boolean res;
  record CouponVc Couponr;
  Integer os;
    
  res = false;
  switch (WindowState(wn)) begin
    case 0://normalmode
      GetWindowRecord(wn,Couponr);
      os = Couponr.Status;
    case 1://insertmode
      GetWindowRecord(wn,Couponr);
      os = 0;
    case 2://updatemode
      GetPrevWindowRecord(wn,Couponr);     
      os = Couponr.Status;
  end;
  if (os==ns) then begin res = true; end;
    
  if ((Couponr.Direct==0) or (Couponr.Direct==1)) then begin // circuito tarjetas de credito
    switch (os) begin
      case 0 : if (ns==1) then begin res = true; end;
      case 1 : if (ns==2) then begin res = true; end;
               if (ns==6) then begin res = true; end;
      case 2 : if (ns==3) then begin res = true; end;
               if (ns==6) then begin res = true; end;
      case 3 : if (ns==4) then begin res = true; end;
               if (ns==5) then begin res = true; end;
    end;
  end;
  if (Couponr.Direct==2) then begin // circuito tarjetas de debito
    switch (os) begin
      case 0 : if (ns==1) then begin res = true; end;
      case 1 : if (ns==2) then begin res = true; end;
               if (ns==6) then begin res = true; end;
      case 2 : if (ns==4) then begin res = true; end;
    end;
  end;

  res = true;
  if (not res) then begin Beep; end; 
  CouponDClassStatusButtonAction = res;
  Return;
end;

global
function Boolean CouponDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record CouponVc Couponr;
  
  res = true;
  GetWindowRecord(wn,Couponr);
  switch (fieldname) begin
    case "Amount"        : if (Couponr.Status>0) then begin res = false; end;
    case "CUCode"        : if (Couponr.Status>0) then begin res = false; end;
    case "CreditCardNr"  : if (Couponr.Status>0) then begin res = false; end;
    case "CardType"      : if (Couponr.Status>0) then begin res = false; end;
    case "IVNr"          : if (Couponr.Status>0) then begin res = false; end;
    case "TransDate"     : if (Couponr.Status>0) then begin res = false; end;
    case "AuthDate"      : if (Couponr.Status>1) then begin res = false; end;
    case "AuthorizationCode"  : if (Couponr.Status>1) then begin res = false; end;
    case "ConsNr"        : if (Couponr.Status>3) then begin res = false; end;
  end;
  CouponDClassActiveEditField = res;
  RETURN;
END;

global
function Boolean CouponDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "CUCode": res = CouponDClassCUCodeEFAfter(wn,fn,rownr,changed);
    case "AccNumber": res = CouponConDClassAccNumberEFAfter(wn,fn,rownr,changed);
    case "CreditCardNr": res = CouponDClassCreditCardNrEFAfter(wn,fn,rownr,changed);
  end;
  CouponDClassAfterEditField = res;
  RETURN;
END;

global
procedure CouponVcCancelDsm()
begin
  Integer wn,nwn;
  record CouponVc Couponr;
  record RcVc RepSpec;
  
  wn = CurWindow;
  GetWindowRecord(wn,Couponr);
  nwn = OpenWindow("CouponVcCancelDsmYesNoWClass",0,wn,"","",RepSpec);
  RepSpec.f1 = Couponr.CouponNr;
  RepSpec.vals0 = Couponr.Amount;
  PutWindowRecord(nwn,RepSpec);

//  Rahaxi_Msg_Event_Cancel(Couponr.CouponNr,Couponr.Amount);
  return;
end;

global
procedure CouponVcCancelDsmYesNoWClassYES()
begin
  Integer wn;
  record RcVc RepSpec;

  wn = CurWindow;
  GetWindowRecord(wn,RepSpec);
  CloseWindow(wn);      
  Rahaxi_Msg_Event_Cancel(RepSpec.f1,RepSpec.vals0);
  return;
end;

global
procedure CouponVcCancelDsmYesNoWClassNO()
begin
  Integer wn;

  wn = CurWindow;
  CloseWindow(wn);      
  return;
end;
