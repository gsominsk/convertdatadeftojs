remote procedure ResTypeDClass_BICharts_remote_Async(Integer,record ResVc);

global
procedure ResTypeLsm()
BEGIN
  record ResTypeVc ResTyper;
  record RcVc RepSpec;
  Integer wn,nwn;

  wn = CurWindow;
  if (ReadMarkedRecord(wn,ResTyper)) then begin  
    nwn = OpenWindow("ResMonWClass",0,0,"","",RepSpec);
    DeselectWindow(nwn,false)
    RepSpec.sStartDate = CurrentDate;
    RepSpec.f1 = ResTyper.Code;
    PutWindowRecord(nwn,RepSpec);
    SelectWindow(nwn);
  end;
  RETURN;
END;

global
procedure ResTypeDClass_BICharts_remote_AsyncCallback(Integer wn,val maxv,Array val voccup,Array Integer vmonth,vector val vrooms)
begin
  Integer i;
  Array string 255 arooms;
  val maxy;
  
  ChartBegin(wn,"$ResTypeDClass_BIChart_Occup",kChartTypeColumn,kChartLegendTypeNone,0,-1);
  ChartTheme("MiamiNice");
  ChartXAxis(0,12.5,UsetStr(21371));
  ChartYAxis(0,maxv,UsetStr(21372));  

  ChartDataSeriesBegin("ResTypeDClass_BIChartOccupation");
  for (i=1;i<=12;i=i+1) begin
    ChartXAxisLabel(i,USetStr(2870+vmonth[i]));
    ChartDatum(voccup[i],"");  
  end;
  EndChartDataSeries;
  EndChart;

  GetVectorTags(vrooms,arooms);
  SortStringArray(arooms);

  ChartBegin(wn,"$ResTypeDClass_BIChart_Rooms",kChartTypeColumn,kChartLegendTypeNone,0,-1);
  ChartTheme("MiamiNice");
  for (i=0;i<arooms.length;i=i+1) begin
    if (maxy<vrooms[arooms[i]]) then begin maxy = vrooms[arooms[i]]; end;
  end;

  ChartXAxis(0,arooms.length-0.5,USetStr(21373));
  ChartYAxis(0,maxy,USetStr(21374));  

  ChartDataSeriesBegin("ResTypeDClass_BIChartRooms");

  for (i=1;i<arooms.length;i=i+1) begin
    ChartXAxisLabel(i,arooms[i]);
    ChartDatum(vrooms[arooms[i]],"");  
  end;
  EndChartDataSeries;
  EndChart;
  return;
end;

global
function Boolean ResTypeDClassOnOpenWindow(Integer wn)
begin
  record ResTypeVc RTr;

  GetWindowRecord(wn,RTr);
  threadremoteasync.ResTypeDClass_BICharts_remote_Async(wn,RTr);
  ResTypeDClassOnOpenWindow = false;
  return;
end;

global
procedure ResTypeDClassOnWindowRecordChange(Integer wn)
begin
  record ResTypeVc RTr;

  GetWindowRecord(wn,RTr);
  threadremoteasync.ResTypeDClass_BICharts_remote_Async(wn,RTr);
  return;
end;
