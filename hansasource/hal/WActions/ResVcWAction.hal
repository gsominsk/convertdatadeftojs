external function Boolean IsEnterprise();
remote procedure ResDCLass_BIChart_remote_Async(Integer,record ResVc);

function Boolean ResDClassTypeEFAfter(Integer wn,Boolean changedf)
begin
  Boolean res;
  record ResVc Resr;
  record ResTypeVc RTr;

  res = true;
  if (changedf) then begin
    GetWindowRecord(wn,Resr);  
    RTr.Code = Resr.Type;
    if (ReadFirstMain(RTr,1,true)) then begin
      Resr.DefArtCode = RTr.DefArtCode;
      Resr.InvoiceBy = RTr.InvoiceBy;
      Resr.Sort = RTr.Sort;
    end else begin
      if (IsStandardProduct) then begin
        Resr.InvoiceBy = 1;
      end;
    end;
    PutWindowRecord(wn,Resr);
  end;
  ResDClassTypeEFAfter = res;
  return;
end;

global
function Boolean ResDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname) begin
    case "Type": res = ResDClassTypeEFAfter(wn,changed!=0);
  end;
  ResDClassAfterEditField = res;
  return;
end;

global
function boolean ResDClassOnOKWindow(Integer wn)
begin
  record ResVc Resr;
  Boolean res;

  res = true;
  ResDClassOnOKWindow = res;
end;

global
procedure ResDCLass_BIChart_remote_AsyncCallback(Integer wn,val maxv,Array val voccup,Array Integer vmonth)
begin
  Integer i;
  
  ChartBegin(wn,"$ResDCLass_BIChart",kChartTypeColumn,kChartLegendTypeNone,0,-1);
  ChartTheme("MiamiNice");
  ChartXAxis(0,12.5,UsetStr(21371));
  ChartYAxis(0,maxv,UsetStr(21372));  

  ChartDataSeriesBegin("ResDCLass_BIChartOccupation");
  for (i=1;i<=12;i=i+1) begin
    ChartXAxisLabel(i,USetStr(2870+vmonth[i]));
    ChartDatum(voccup[i],"");  
  end;
  EndChartDataSeries;
  EndChart;
  return;
end;

global
function Boolean ResDCLassOnOpenWindow(Integer wn)
begin
  record ResVc Resr;

  GetWindowRecord(wn,Resr);
  threadremoteasync.ResDCLass_BIChart_remote_Async(wn,Resr);
  ResDCLassOnOpenWindow = false;
  return;
end;

global
procedure ResDClassOnWindowRecordChange(Integer wn)
begin
  record ResVc Resr;

  GetWindowRecord(wn,Resr);
  threadremoteasync.ResDCLass_BIChart_remote_Async(wn,Resr);
  return;
end;
