external function LongInt UpgradeGetBaseFolders(var array string,Boolean);
external function string 255 ExtName(string);
external function LongInt FindSubFoldersWithHDBs(string,var array string);
external function Boolean CheckForHDBs(string,var string);

global
function Boolean UpdateExistingInstallationSelectTClassOnOpenWindow(Integer wn)
begin
  array string 255 afolders,appsearchdirs;
  string 255 appsearchdir,dbname;
  LongInt i,n,t,j,dirs;
  record RcVc RepSpec;

  GetWindowRecord(wn,RepSpec);
  appsearchdir = GetHomeDir & "/Library/Application Support/HansaWorld/";
  n = CountFoldersInDir(appsearchdir);
  t = 0;
  for (i = 0; i<n; i = i + 1) begin
    afolders[i] = GetFolderNameInDir(appsearchdir,i);
//    LogText(0,"UpdateExistingInstallationSelectTClassOnOpenWindow 1 afolders[i]:" & afolders[i] & " " & i & "/"& n & " t:" & t);
    SetListStringAndTag(wn,t,afolders[i],appsearchdir & afolders[i]);
    t = t + 1;
  end;
  if (WindowsMode) then begin
    dirs = UpgradeGetBaseFolders(appsearchdirs,true);
    for (j = 0 ; j<dirs; j = j +1) begin
      if (CheckForHDBs(appsearchdirs[j],dbname)) then begin
        dbname = BaseName(DirName(dbname));
//      LogText(0,"UpdateExistingInstallationSelectTClassOnOpenWindow 2 dbname:" & dbname & " appsearchdirs[j]:" & appsearchdirs[j] & " " & i & "/"& n & " t:" & t);
        SetListStringAndTag(wn,t,dbname,appsearchdirs[j] & dbname);
        t = t+1;
      end;
      n = FindSubFoldersWithHDBs(appsearchdirs[j],afolders);
      for (i = 0; i<n; i = i + 1) begin
//      LogText(0,"UpdateExistingInstallationSelectTClassOnOpenWindow 3 afolders[i]:" & afolders[i] & " appsearchdirs[j]:" & appsearchdirs[j] & " " & i & "/"& n & " t:" & t);
        SetListStringAndTag(wn,t,afolders[i],appsearchdirs[j] & afolders[i]);
        t = t+1;
      end;
    end;
  end;
  PutWindowRecord(wn,RepSpec);

  UpdateExistingInstallationSelectTClassOnOpenWindow = true;
  return;
end;

global
function Boolean UpdateExistingInstallationSelectTClassOnOkWindow(Integer wn)
begin
  LongInt l,j,dirs;
  array string 255 appsearchdirs;
  string 255 seltag,ext,dbname,bname,appname;
  Integer mwn;
  record RcVc RepSpec;

  l = SelectedListLine(wn);
  seltag = GetListTag(wn,l);
  mwn = MotherWindow(wn);
  DeselectWindow(mwn,true);
  GetWindowRecord(mwn,RepSpec);
  RepSpec.f1 = seltag;
  dbname = GetListString(wn,l);
  ext = ExtName(dbname);
  if (len(ext)==3) then begin
    bname = left(dbname,len(dbname)-4);
  end else begin
    bname = dbname;
  end;
//  LogText(0,"UpdateExistingInstallationSelectTClassOnOkWindow bname:" & bname & " ext:" & ext & " dbname:" & dbname);
  if (WindowsMode) then begin
    dirs = UpgradeGetBaseFolders(appsearchdirs,false);
    for (j = 0 ; j<dirs; j = j +1) begin
      appname = appsearchdirs[j] & bname & "\\" & bname & ".exe";
//    LogText(0,"UpdateExistingInstallationSelectTClassOnOkWindow Looking for: " & appname);
      if (FileExists(appname)) then begin
        RepSpec.f2 = appname;
      end;
      appname = appsearchdirs[j] & bname & "\\HansaWorld.exe";
//    LogText(0,"UpdateExistingInstallationSelectTClassOnOkWindow Looking for: " & appname);
      if (FileExists(appname)) then begin
        RepSpec.f2 = appname;
      end;
    end;
  end else begin
    appname = "/Applications/" & bname & ".app";
    if (FileExists(appname)) then begin
      RepSpec.f2 = appname;
    end;
  end;
  PutWindowRecord(mwn,RepSpec);
  CloseWindow(wn);

  UpdateExistingInstallationSelectTClassOnOkWindow = true;
end;