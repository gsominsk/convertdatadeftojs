remote updating function LongInt SetupFollowUpAct(Record FollowUpVc,String,String );
external function LongInt DateM42Days(Date);
remote Function Val GetCustAccount(LongInt,String,Boolean,Boolean);
external function Boolean CheckFollowUp(record JobVc,String,var String);

updating procedure SetUpFollowUpProgram(Record JobVc Jobr,String PayDeal)
begin
  Record ProgFollowUpsVc PrFUr;
  Row ProgFollowUpsVc PrFUrw;
  Record FollowUpVc FollowUpr;
  Val TotalSum;
  Integer i,rwcnt,daytype;
  Date vDate;
  
  PrFUr.Code = PayDeal;
  if readfirstmain(PrFUr,1,true) then begin
    TotalSum = GetCustAccount(Jobr.SerNr,"",false,false);
    rwcnt = MatRowCnt(PrFUr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(PrFUr,i,PrFUrw);
      switch (PrFUrw.DayRef) begin
       case 0 :   vDate = AddDay(Jobr.ConfDate,PrFUrw.Days);
       case 1 :   vDate = AddDay(Jobr.TransDate,PrFUrw.Days);
       case 2 :   vDate = AddDay(Jobr.EndDate,PrFUrw.Days);
      end;
      daytype = GetDateID(vDate);
      if (daytype==6) then begin vDate = AddDay(vDate,-1);  end;
      if (daytype==7) then begin vDate = AddDay(vDate,1);   end;
      FollowUpr.TransDate    = vDate;
      FollowUpr.SerNr        = NextSerNr("FollowUpVc",FollowUpr.TransDate,-1,false,"");
      FollowUpr.Comment      = PrFUrw.Comment;
      FollowUpr.MainPersons  = PrFUrw.MainPersons;
      FollowUpr.Amount       = (TotalSum * PrFUrw.Percent)/100.00;
      FollowUpr.NReservationStatus = PrFUrw.NReservationStatus;
      FollowUpr.JobNr        = Jobr.SerNr;
      FollowUpr.ActNr        = SetupFollowUpAct(FollowUpr,PrFUrw.ActType,Jobr.Source);
      if (RecordStore(FollowUpr,false)) then begin end;
    end;
  end;
  return;
end;

global
updating procedure FollowUpsm()
begin
  record JobVc Jobr;
  record RcVc RepSpec;
  Integer wn;
  String 50 tstr;
  Boolean Bool;

  wn = CurWindow;
  GetWindowRecord(wn,Jobr);
  Bool = CheckFollowUp(Jobr,CurrentUser,tstr);
  if (Bool) then begin
    SetUpFollowUpProgram(Jobr,tstr);
  end;
  RepSpec.long1   = Jobr.SerNr;
  RepSpec.repname = "JobFollowUpRn";
  RepSpec.Media   = mtScreen;    
  RunReport(RepSpec,0);
  return;
end;

global
procedure FollowUpActOpen()
begin
  record FollowUpVc FollowUpr;
  record ActVc Actr;
  Integer wn;

  wn = CurWindow;
  GetWindowRecord(wn,FollowUpr);
  Actr.SerNr = FollowUpr.ActNr; 
  if (ReadFirstMain(Actr,1,true)) then begin
    wn = OpenWindow("ActDClass",1,0,"","",Actr);
  end;
  return;
end;
