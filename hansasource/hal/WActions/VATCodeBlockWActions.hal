remote function Boolean VATCodeIsAllowedToChange(string);

function Boolean VATCodeTClassVATCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record VATCodeBlock VATCodeb;
  row VATCodeBlock VATCodebrw;

  if (changedf) then begin
    if (rownr>=0) then begin
      GetWindowRecord(wn,VATCodeb);        
      MatRowGet(VATCodeb,rownr,VATCodebrw);
      VATCodebrw.InclinTotal = kTaxTransInclinTotalYes;
      VATCodebrw.VATRateTier = StringFromStringSet(633,"Basic_Level");
      MatRowPut(VATCodeb,rownr,VATCodebrw);
      PutWindowRecord(wn,VATCodeb);    
    end;
  end;
  VATCodeTClassVATCodeEFAfter = true;
  return;
end;

global
function Boolean VATCodeTClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;

  switch (fieldname) begin
    case "VATCode": res = VATCodeTClassVATCodeEFAfter(wn,rownr,changed!=0);
  end;
  VATCodeTClassAfterEditField = res;
  return;
end;

function Boolean AllowedToChange(Integer wn,Integer rownr)
begin
  Boolean res;
  record VATCodeBlock VATCodeb;
  row VATCodeBlock VATCodebrw;
  
  res = true;
  if (HasLocalization("PRT")) then begin
    GetWindowRecord(wn,VATCodeb);        
    MatRowGet(VATCodeb,rownr,VATCodebrw);
    if (nonblank(VATCodebrw.VATCode)) then begin
      if (VATCodeIsAllowedToChange(VATCodebrw.VATCode)==false) then begin
        res = false;
      end;
    end;
  end;
  AllowedToChange = res;
  return;
end;

global
function Boolean VATCodeTClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
begin
  Boolean res;

  res = true;
  switch (fieldname) begin
    case "VATCode": res = AllowedToChange(wn,rownr);
    case "IncVatpr": res = AllowedToChange(wn,rownr);
    case "ExVatpr": res = AllowedToChange(wn,rownr);
    case "Comment": res = AllowedToChange(wn,rownr);
    case "Tax1pr": res = AllowedToChange(wn,rownr);
  end;
  VATCodeTClassActiveEditField = res;
  return;
end;

global 
function Boolean VATCodeTClassDeleteRowTest(Integer wn,Integer rownr)
begin
  Boolean res;

  res = true;
  if (rownr>=0) then begin
    res = AllowedToChange(wn,rownr);
  end;
  VATCodeTClassDeleteRowTest = res;
  return;
end;
