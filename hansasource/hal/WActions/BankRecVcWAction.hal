external procedure DefaultPeriod(var string);
remote procedure YodleeRegisterUser(record YodleeUserVc);
remote procedure YodleeUserImportYodleeData(string,string,Date,Date);
remote procedure PayModeBankAccountImportYodleeData(string,string,Date,Date);
external function string 60 AddStringToStringList(string,string);
external function string 255 ServerDateFormat();
external function string 255 DateToStr(Date);
external function Date DateFromString(string,string);
remote updating procedure BankAccountReconWClass_ReconcileBankTransactionRemote(string);
remote procedure BankAccountReconWClass_FillDataAsync(Integer,string,Date,Date);
external procedure OpenVcFromTR(record TRVc);
external function Integer TypeOfCurncy(var string,var Integer);
remote updating procedure BankRecVc_PasteReconSum(var record BankRecVc);
remote updating procedure BankRecVc_PasteTyp(var record BankRecVc);
remote updating procedure BankRecVc_PasteCurncyCode(var record BankRecVc);
remote updating procedure BankRecVc_PasteRecDate(var record BankRecVc);
remote updating procedure BankRecVc_PasteAccNumber(var record BankRecVc);
remote updating procedure BankRecVc_PasteObjects(var record BankRecVc);
remote updating procedure BankRecVc_PastePeriod2Str(var record BankRecVc);
remote updating procedure UpdateBankRecW(var record BankRecVc);
remote updating function Integer ReconsBankRecW(var record BankRecVc);
remote updating procedure UpdateOnAmountBankRecW(var record BankRecVc);
remote updating procedure UpdateOnReconCodeBankRecW(var record BankRecVc);
external procedure ToStrTRCode(var string,Integer,LongInt);
external procedure In2Period(var string,var Date,var Date);


procedure BankRecVc_PasteReconsf(var record BankRecVc BankRecp,Integer rownr)
BEGIN
  row BankRecVc brrw;
  Integer oldstyle;
  
  MatRowGet(BankRecp,rownr,brrw);
  if (brrw.Reconsf!=0) and (brrw.Reconsf!=1) then begin
    brrw.Reconsf = 0;
    goto LBankRecVc_PasteReconsf;
  end;
  if (nonblank(BankRecp.CurncyCode)) then begin
    if (BankRecp.CurncyCode==brrw.CurncyCode) then begin
      switch (TypeOfCurncy(brrw.CurncyCode,oldstyle)) begin
        case 1:
          if (brrw.Reconsf==0) then begin
            BankRecp.RecVal = BankRecp.RecVal - brrw.Val;
          end else begin
            BankRecp.RecVal = BankRecp.RecVal + brrw.Val;
          end;
        case 2:
          if (brrw.Reconsf==0) then begin
            BankRecp.RecVal = BankRecp.RecVal - brrw.Val2;
          end else begin
            BankRecp.RecVal = BankRecp.RecVal + brrw.Val2;
          end;
        case 3:
          if (brrw.Reconsf==0) then begin
            BankRecp.RecVal = BankRecp.RecVal - brrw.Val;
          end else begin
            BankRecp.RecVal = BankRecp.RecVal + brrw.Val;
          end;
        otherwise
          if (brrw.Reconsf==0) then begin
            BankRecp.RecVal = BankRecp.RecVal - brrw.CurVal;
          end else begin
            BankRecp.RecVal = BankRecp.RecVal + brrw.CurVal;
          end;
      end;
    end;
  end else begin
    if (brrw.Reconsf==0) then begin
      BankRecp.RecVal = BankRecp.RecVal - brrw.Val;
    end else begin
      BankRecp.RecVal = BankRecp.RecVal + brrw.Val;
    end;
  end;
LBankRecVc_PasteReconsf:;  
  MatRowPut(BankRecp,rownr,brrw);
  BankRecp.EndBal = BankRecp.StartBal + BankRecp.RecVal;
  BankRecp.Diff = BankRecp.EndBal - BankRecp.BankBal;
  RETURN;
END;

global
procedure BankRecSearch()
BEGIN
  Integer wn,nwn;
  record BankRecVc BankRecr;
  record RcVc RepSpec;
  
  wn = CurWindow;
  GetWindowRecord(wn,BankRecr);
  ReportDefaults(RepSpec,"RecSearchRClass");
//  RepSpec.Media = mtScreen;
  nwn = OpenWindow("RecSearchRClass",0,0,"","",RepSpec);
  RepSpec.UsedOnly = wn;
  PutWindowRecord(nwn,RepSpec);  
  SelectWindow(nwn);
  RETURN;
END;

function Boolean RecSearchRClassf1EFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  record RcVc RepSpec;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;//BankRecRowType
  Integer i,rwcnt;
  Boolean res;
  string 255 rstr;
  
  res = true;
  GetWindowRecord(wn,RepSpec);
  if (RepSpec.UsedOnly>0) then begin
    GetWindowRecord(RepSpec.UsedOnly,BankRecr);
    DeselectWindow(RepSpec.UsedOnly,false);
    rwcnt = MatRowCnt(BankRecr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(BankRecr,i,BankRecrw);
      switch (RepSpec.flags[0]) begin
        case 0:
          ToStrTRCode(rstr,BankRecrw.IntYc,BankRecrw.Number);        
          if (Left(rstr,len(RepSpec.f1))==RepSpec.f1) then begin
            WindowFieldGoto(RepSpec.UsedOnly,BankRecr,i,"Number",true);
            goto LRecSearchRClassf1EFAfter;
          end;
        case 1:
          if (Left(BankRecrw.Comment,len(RepSpec.f1))==RepSpec.f1) then begin
            WINDOWFIELDGOTO(RepSpec.UsedOnly,BankRecr,i,"Comment",true);
            goto LRecSearchRClassf1EFAfter;
          end;
      end;
    end;
  end;
  WindowFieldGoto(RepSpec.UsedOnly,BankRecr,0,"Comment",true);
LRecSearchRClassf1EFAfter:;  
  RecSearchRClassf1EFAfter = res;
  RETURN;
END;

global
function Boolean RecSearchRClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "f1": res = RecSearchRClassf1EFAfter(wn,fn,rownr,changed);
  end;
  RecSearchRClassAfterEditField = res;
  RETURN;
END;

updating function Boolean BankRecWClassPeriod2StrEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      BankRecVc_PastePeriod2Str(BankRecr);    
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassPeriod2StrEFAfter = res;
  RETURN;
END;

updating function Boolean BankRecWClassd1EFAfter(Integer wn,Boolean changedf)
begin
  record BankRecVc BankRecr;
  Boolean res;
  Date td;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (blankdate(BankRecr.d1)) then begin
      td.day = 1; td.month = 1; td.year = 1980;
      BankRecr.d1 = td;
    end;
    BankRecr.Period2Str = BankRecr.d1;
    BankRecr.Period2Str = BankRecr.Period2Str & ":";
    BankRecr.Period2Str = BankRecr.Period2Str & BankRecr.d2;
    BankRecVc_PastePeriod2Str(BankRecr);
    PutWindowRecord(wn,BankRecr);
  end;  
  BankRecWClassd1EFAfter = res;
  return;
end;

updating function Boolean BankRecWClassd2EFAfter(Integer wn,Boolean changedf)
begin
  record BankRecVc BankRecr;
  Boolean res;
  Date td;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (blankdate(BankRecr.d1)) then begin
      td.day = 1; td.month = 1; td.year = 1980;
      BankRecr.d1 = td;
    end;
    BankRecr.Period2Str = BankRecr.d1;
    BankRecr.Period2Str = BankRecr.Period2Str & ":";
    BankRecr.Period2Str = BankRecr.Period2Str & BankRecr.d2;
    BankRecVc_PastePeriod2Str(BankRecr);
    PutWindowRecord(wn,BankRecr);
  end;  
  BankRecWClassd2EFAfter = res;
  return;
end;

updating function Boolean BankRecWClassCompCodeEFAfter(Integer wn,Boolean changedf)
begin
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      UpdateBankRecW(BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassCompCodeEFAfter = res;
  return;
end;

updating function Boolean BankRecWClassAccNumberEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      BankRecVc_PasteAccNumber(BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassAccNumberEFAfter = res;
  RETURN;
END;

procedure ApplySelected(var record BankRecVc BRr,record BankRecVc oldBRr)
begin
  row BankRecVc brrw;
  row BankRecVc oldbrrw;
  Integer orwcnt,rwcnt,i,oi;

  orwcnt = MatRowCnt(oldBRr);
  rwcnt = MatRowCnt(BRr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(BRr,i,brrw);
    for (oi=0;oi<orwcnt;oi=oi+1) begin
      MatRowGet(oldBRr,oi,oldbrrw);
      if (oldbrrw.IntYc==brrw.IntYc) and (oldbrrw.Number==brrw.Number) and (brrw.Reconsf!=oldbrrw.Reconsf) then begin
        brrw.Reconsf = oldbrrw.Reconsf;
        MatRowPut(BRr,i,brrw);
        oi = orwcnt;
        BankRecVc_PasteReconsf(BRr,i);
      end;
    end;
  end;
  return;
end;

procedure BuildPerpetuateReconList(integer wn,record BankRecVc BankRecr)
begin
  string 255 BRWnStr,tmpstr;
  row BankRecVc brrw;
  integer rwcnt,i;

  rwcnt = MatRowCnt(BankRecr);
  for (i=0; i<rwcnt; i=i+1) begin
    MatRowGet(BankRecr,i,brrw);
    if (brrw.Reconsf!=0) then begin
      BRWnStr = AddStringToStringList(BRWnStr,brrw.IntYc & ";" & brrw.Number);
/*      
      if (nonblank(BRWnStr)) then begin 
        BRWnStr = BRWnStr & ",";
      end; 
      BRWnStr = BRWnStr & brrw.IntYc & ";" & brrw.Number;
*/      
    end;
  end;
  PutWindowString(wn,"gAccRec_ReconfListOK",BRWnStr);  
end;

procedure PastePerpetuateReconList(integer wn,var record BankRecVc BankRecr)
begin
  string 255 BRWnStr,BankRecMatStr,intyc,number;
  row BankRecVc brrw;
  longint charpos,charpos2;
  integer rwcnt,i;

  BRWnStr = GetWindowString(wn,"gAccRec_ReconfListOK"); 
  charpos = 0;
  while (GetNextSubstring(BRWnStr,charpos,",",BankRecMatStr)) begin
    if (nonblank(BankRecMatStr)) then begin
      charpos2 = 0;
      GetNextSubstring(BankRecMatStr,charpos2,";",intyc);
      GetNextSubstring(BankRecMatStr,charpos2,";",number);
      rwcnt = MatRowCnt(BankRecr);
      for (i=0; i<rwcnt; i=i+1) begin
        MatRowGet(BankRecr,i,brrw);
        if (brrw.IntYc==StringToInt(intyc) and brrw.Number==StringToInt(number)) then begin
          brrw.Reconsf = 1;
          MatRowPut(BankRecr,i,brrw);
          BankRecVc_PasteReconsf(BankRecr,i);
          i = rwcnt;               
        end;
      end;
    end;
  end;
end;

updating function Boolean BankRecWClassReconSumEFAfter(Integer wn,Boolean changedf)
begin
  record BankRecVc BankRecr;
  record BankRecVc oBRr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      RecordCopy(oBRr,BankRecr);
      if (nonblank(BankRecr.ReconSum)) then begin 
        BuildPerpetuateReconList(wn,BankRecr);
      end; 
      BankRecVc_PasteReconSum(BankRecr);
      ApplySelected(BankRecr,oBRr);
      PastePerpetuateReconList(wn,BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassReconSumEFAfter = res;
  return;
end;

function Boolean BankRecWClassBankBalEFAfter(Integer wn,Boolean changedf)
begin
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    DeselectWindow(wn,false);
    BankRecr.Diff = BankRecr.EndBal - BankRecr.BankBal;
    PutWindowRecord(wn,BankRecr);
  end;  
  BankRecWClassBankBalEFAfter = res;
  return;
end;

updating function Boolean BankRecWClassCurncyCodeEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      BankRecVc_PasteCurncyCode(BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassCurncyCodeEFAfter = res;
  RETURN;
END;

updating function Boolean BankRecWClassObjectsEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      BankRecVc_PasteObjects(BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassObjectsEFAfter = res;
  RETURN;
END;

updating function Boolean BankRecWClassRecDateEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    DeselectWindow(wn,false);
    BankRecVc_PasteRecDate(BankRecr);
    PutWindowRecord(wn,BankRecr);
  end;  
  BankRecWClassRecDateEFAfter = res;
  RETURN;
END;

function Boolean BankRecWClassReconsfEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    DeselectWindow(wn,false);
    BankRecVc_PasteReconsf(BankRecr,rownr);
    PutWindowRecord(wn,BankRecr);
  end;  
  BankRecWClassReconsfEFAfter = res;
  RETURN;
END;

updating function Boolean BankRecWClassTypEFAfter(Integer wn,Boolean changedf)
BEGIN
  record BankRecVc BankRecr;
  Boolean res;

  if (changedf) then begin
    GetWindowRecord(wn,BankRecr);
    if (BankRecr.Recedf!=0 or BankRecr.Unrecedf!=0) then begin
      BankRecVc_PasteTyp(BankRecr);
      PutWindowRecord(wn,BankRecr);
    end;
  end;  
  BankRecWClassTypEFAfter = res;
  RETURN;
END;

global
updating function Boolean BankRecWClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
//    case "d1": res = BankRecWClassd1EFAfter(wn,changed!=0);
//    case "d2": res = BankRecWClassd2EFAfter(wn,changed!=0);
    case "Period2Str": res = BankRecWClassPeriod2StrEFAfter(wn,changed!=0);
    case "AccNumber": res = BankRecWClassAccNumberEFAfter(wn,changed!=0);
    case "Objects": res = BankRecWClassObjectsEFAfter(wn,changed!=0);
    case "RecDate": res = BankRecWClassRecDateEFAfter(wn,changed!=0);
    case "Reconsf": res = BankRecWClassReconsfEFAfter(wn,rownr,changed!=0);
    case "CurncyCode": res = BankRecWClassCurncyCodeEFAfter(wn,changed!=0);
    case "Typ": res = BankRecWClassTypEFAfter(wn,changed!=0);
    case "ReconSum": res = BankRecWClassReconSumEFAfter(wn,changed!=0);
    case "BankBal": res = BankRecWClassBankBalEFAfter(wn,changed!=0);
    case "CompCode": res = BankRecWClassCompCodeEFAfter(wn,changed!=0);
  end;
  BankRecWClassAfterEditField = res;
  RETURN;
END;

global
function string 40 BankRecWClassSpecPasteName(Integer wn,string defpsname)
begin
  string 255 psname;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  Integer rownr;
  
  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "CompCode":
      GetWindowRecord(wn,BankRecr);
      rownr = WindowActiveRow(wn);
      if (rownr>=0) then begin
        MatRowGet(BankRecr,rownr,BankRecrw);
      end;
      switch (BankRecr.Typ) begin
        case 1: psname = "CUSClass";
        case 2: psname = "VESClass";
        otherwise psname = "";
      end;
  end;
  BankRecWClassSpecPasteName = psname;
  return;
end;

global
updating function Boolean BankRecWClassRecedfButtonAfter(Integer wn,Boolean changedf)
begin        
  record BankRecVc BankRecr;  
  Boolean res;

  DeselectWindow(wn,false);
  GetWindowRecord(wn,BankRecr);
  UpdateBankRecW(BankRecr);
  PutWindowRecord(wn,BankRecr);
  BankRecWClassRecedfButtonAfter = res;  
  return;
end;

global
updating function Boolean BankRecWClassUnrecedfButtonAfter(Integer wn,Boolean changedf)
begin        
  record BankRecVc BankRecr;  
  Boolean res;
  record AccVc Accr;

  DeselectWindow(wn,false);
  GetWindowRecord(wn,BankRecr);
  Accr.AccNumber = BankRecr.AccNumber;
  if (ReadFirstMain(Accr,1,true)) then begin
    BankRecr.CurncyCode = Accr.Curncy;
  end;
  UpdateBankRecW(BankRecr);
  PutWindowRecord(wn,BankRecr);
  BankRecWClassUnrecedfButtonAfter = res;  
  return;
end;

global
function Boolean BankRecWClassIncompletefButtonAction(Integer wn,Integer value)
begin
  Boolean res;
 
  res = false;
  BankRecWClassIncompletefButtonAction = res;
  return;
end;

global 
function Boolean BankRecWClassDeleteRowTest(Integer wn,Integer rownr)
begin
  Boolean res;

  res = false;
  BankRecWClassDeleteRowTest = res;
  return;
end;

global 
function Boolean BankRecWClassInsertRowTest(Integer wn,Integer rownr)
begin
  Boolean res;

  res = false;
  BankRecWClassInsertRowTest = res;
  return;
end;

global
function Boolean BankRecWClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  
  res = true;
  if (rownr>=0) then begin  
    switch (fieldname) begin
      case "Reconsf": res = true;
      case "ReconCode": res = true;
      case "ReconDate": res = true;
      otherwise res = false;
    end;
  end;
  BankRecWClassActiveEditField = res;
  RETURN;
END;

procedure ResetReconState(record BankRecVc BankRecr,record BankRecVc prevBankRecr)
begin
  row BankRecVc BankRecrw;
  row BankRecVc prevBankRecrw;
  Integer i,rwcnt;
  Integer pi,prwcnt;
  
  prwcnt = MatRowCnt(prevBankRecr);
  rwcnt = MatRowCnt(BankRecr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(BankRecr,i,BankRecrw);
    for (pi=0;pi<prwcnt;pi=pi+1) begin
      MatRowGet(prevBankRecr,pi,prevBankRecrw);
      if ((BankRecrw.IntYc==prevBankRecrw.IntYc) and (BankRecrw.Number==prevBankRecrw.Number)) then begin
        BankRecrw.Reconsf = prevBankRecrw.Reconsf;
        BankRecrw.ReconCode = prevBankRecrw.ReconCode;
        if (BankRecrw.Reconsf!=0) then begin
          BankRecr.RecVal = BankRecr.RecVal + BankRecrw.Val;
        end;
        MatRowPut(BankRecr,i,BankRecrw);
        pi = prwcnt;
      end;
    end;
  end;
  BankRecr.EndBal = BankRecr.StartBal + BankRecr.RecVal;
  BankRecr.Diff = BankRecr.EndBal - BankRecr.BankBal;
  return;
end;

global
updating procedure UpdateBankRecWsm()
BEGIN
  Integer wn;
  record BankRecVc BankRecr;
  record BankRecVc prevBankRecr;
  
  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,BankRecr);
  GetWindowRecord(wn,prevBankRecr);
  BuildPerpetuateReconList(wn,BankRecr);
  UpdateBankRecW(BankRecr);
  ResetReconState(BankRecr,prevBankRecr);
  PastePerpetuateReconList(wn,BankRecr);  
  PutWindowRecord(wn,BankRecr);
  RETURN;
END;

global
updating procedure CommitBankRecWsm()
BEGIN
  Integer wn,err;
  record BankRecVc BankRecr;
  
  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,BankRecr);
  err = ReconsBankRecW(BankRecr);
  if (err!=0) then begin
    MessageBox(err,"");
    goto LCommitBankRecWsm;
  end;
  UpdateBankRecW(BankRecr);
  PutWindowRecord(wn,BankRecr);
LCommitBankRecWsm:;  
  RETURN;
END;

global
updating procedure SortAmountBankRecWsm()
BEGIN
  Integer wn;
  record BankRecVc BankRecr;
  
  wn = CurWindow;
  DeselectWindow(wn,false);
  GetWindowRecord(wn,BankRecr);
  BuildPerpetuateReconList(wn,BankRecr);
  UpdateOnAmountBankRecW(BankRecr);
  PastePerpetuateReconList(wn,BankRecr);
  PutWindowRecord(wn,BankRecr);
  RETURN;
END;

global
updating procedure SortReconCodeBankRecWsm()
begin
  Integer wn;
  record BankRecVc BankRecr;
  
  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,BankRecr);
  BuildPerpetuateReconList(wn,BankRecr);
  UpdateOnReconCodeBankRecW(BankRecr);
  PastePerpetuateReconList(wn,BankRecr);
  PutWindowRecord(wn,BankRecr);
  return;
end;

global
updating function Boolean BankRecWClassOnOpenWindow(Integer wn)
begin
  record BankRecVc BankRecr;
  record BankRecBlock BRb;
  
  BlockLoad(BRb);
  GetWindowRecord(wn,BankRecr);
  BankRecr.RecDate = CurrentDate;
  if (GetDay(BankRecr.RecDate)!=1) then begin
    BankRecr.RecDate = AddMonth(BankRecr.RecDate,1);
    BankRecr.RecDate = AddDay(BankRecr.RecDate,-GetDay(BankRecr.RecDate)+1);
  end;
  if (nonblankdate(BRb.LastBankRecDate)) then begin
    BankRecr.RecDate = BRb.LastBankRecDate;
  end;
  BankRecr.Typ = kTransactionRowTypeAny;
  UpdateBankRecW(BankRecr);
  PutWindowRecord(wn,BankRecr);
  BankRecWClassOnOpenWindow = false;
  return;
end;

global
procedure MarkAsRecBankRecWsm()
BEGIN
  Integer wn;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  Integer i,rwcnt;  
  
  wn = CurWindow;
  DeselectWindow(wn,true);
  GetWindowRecord(wn,BankRecr);
  rwcnt = MatRowCnt(BankRecr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(BankRecr,i,BankRecrw);
    BankRecrw.Reconsf = 1;
    MatRowPut(BankRecr,i,BankRecrw);
    BankRecVc_PasteReconsf(BankRecr,i);
  end;
  PutWindowRecord(wn,BankRecr);
  RETURN;
END;

global
procedure TRBankRecWsm()
BEGIN
  Integer wn,nwn;
  record BankRecVc BankRecr;
  record TRVc TRr;
  
  RecordNew(TRr);
  nwn = OpenWindow("TRDClass",0,0,"","",TRr);
  PutWindowRecord(nwn,TRr);  
  SelectWindow(nwn);
  RETURN;
END;

global
procedure OpenTRBankRecWsm()
BEGIN
  Integer wn,nwn;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  record TRVc TRr;
  Integer rownr;

  wn = CurWindow;
  GetWindowRecord(wn,BankRecr);
  rownr = WindowActiveRow(wn);
  if (rownr>=0) then begin
    MatRowGet(BankRecr,rownr,BankRecrw);
    TRr.Number = BankRecrw.Number;
    TRr.IntYc = BankRecrw.IntYc;
    if (ReadFirstMain(TRr,2,true)) then begin
      nwn = OpenWindow("TRDClass",1,0,"","",TRr);
    end;
  end;
  RETURN;
END;

global
procedure ACReconRecWsm()
begin
  Integer wn;
  record BankRecVc BankRecr;
  record RcVc RepSpec;

  wn = CurWindow;
  GetWindowRecord(wn,BankRecr);
  ReportDefaults(RepSpec,"ACReconRClass");
  RepSpec.repname = "ACRecRn";
  In2Period(BankRecr.Period2Str,RepSpec.sStartDate,RepSpec.sEndDate);

  RepSpec.f1 = BankRecr.AccNumber;
  RepSpec.ObjStr = BankRecr.Objects;
  RepSpec.RegDate = BankRecr.RecDate;
  RepSpec.FirstAcc = BankRecr.ReconCode;
  RepSpec.Media = mtScreen;
  RepSpec.flags[1] = BankRecr.Recedf;
  RepSpec.flags[2] = BankRecr.Unrecedf;

  RepSpec.flags[4] = 0;
  RepSpec.flags[5] = BankRecr.ShowRefStr;
  RepSpec.basecurncy = 0;
  RunReport(RepSpec,0);  
  RETURN;
END;

/*
global
procedure RecordActionBankRec_Print(record BankRecVc BankRecr,string arg1)
BEGIN
  Boolean previewf;

  if (nonblank(arg1)) then begin
    if (StringToInt(arg1)!=0) then begin
      previewf = true;
    end;
  end;   
//  if (PrintDocument(BankRecr,"BankRecForm",previewf)) then begin end;
  RETURN;
END;

global
function Boolean BankRecWClassPrint(Integer wn,Boolean previewf)
BEGIN
  Boolean res;
  record BankRecVc BankRecr;
  Integer normalmode;
  
  normalmode = 0;//Rs_normal
  DeselectWindow(wn,false);
  GetWindowRecord(wn,BankRecr);
  if ((WindowState(wn)==normalmode) and (previewf==false)) then begin 
    RecordActionBankRec_Print(BankRecr,"");
  end else begin
//    if (PrintDocument(BankRecr,"BankRecForm",previewf)) then begin end;
  end;
  res = true;
  BankRecWClassPrint = res;
  RETURN;
END;
*/  

global
updating function Boolean BankRecWClassOnEnterKey(var Integer wn,string fieldname,Integer fn,Integer rownr)
begin
  Boolean res;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  
  res = true;
  GetWindowRecord(wn,BankRecr);
  switch (fieldname) begin
    case "ReconSum":
      if (nonblank(BankRecr.ReconSum)) then begin
        if (MatRowCnt(BankRecr)>0) then begin
          WindowFieldGoto(wn,BankRecr,0,"Reconsf",true);
          res = false;
        end else begin
          Beep;
          WindowFieldGoto(wn,BankRecr,-1,"ReconSum",true);
          res = false;
        end;
      end;
    case "Reconsf":
      if (nonblank(BankRecr.ReconSum)) then begin
        MatRowGet(BankRecr,rownr,BankRecrw);
        if (BankRecrw.Reconsf==0) then begin
          BankRecrw.Reconsf = 1;
          MatRowPut(BankRecr,rownr,BankRecrw);
          ReconsBankRecW(BankRecr);
          UpdateBankRecW(BankRecr);
          PutWindowRecord(wn,BankRecr);
          WindowFieldGoto(wn,BankRecr,-1,"ReconSum",true);
          res = false;
        end;
      end;
  end;
LBankRecWClassOnEnterKey:;  
  BankRecWClassOnEnterKey = res;
  return;
end;

global
function Boolean BankRecWClassOpenRecord(Integer wn,string fieldname,Integer fn,Integer rownr)
begin
  Boolean res;
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  record TRVc TRr;
  
  switch (fieldname) begin
    case "Number":
      GetWindowRecord(wn,BankRecr);  
      MatRowGet(BankRecr,rownr,BankRecrw);
      TRr.Number = BankRecrw.Number;
      TRr.IntYc = BankRecrw.IntYc;
      if (ReadFirstMain(TRr,2,true)) then begin
        if (TRr.IntYc>=IVYc) then begin
          OpenVcFromTR(TRr);
        end else begin
          OpenWindow("TRDClass",1,0,"","",TRr);          
        end;
      end;
  end;
  BankRecWClassOpenRecord = res;
  return;
end;

global
updating procedure CreateTemplTransBankRecWsm()
begin
  record BankRecVc BankRecr;
  row BankRecVc BankRecrw;
  record DanTRVc DTr;
  row DanTRVc DTrw;
  Integer wn,nwn;
  
  wn = CurWindow;
  GetWindowRecord(wn,BankRecr);
  
  RecordNew(DTr);

  ClearRow(DTr,DTrw,1);
  DTrw.RowTransDate = BankRecr.RecDate;
  DTrw.AccNumber1 = BankRecr.AccNumber;
  MatRowPut(DTr,0,DTrw);
  
  nwn = OpenWindow("DanTRDClass",1,0,"","",DTr);
  
  return;
end;

//

procedure InPeriod(string period,var Date sd,var Date ed)
begin
  LongInt pos;
  string 255 tstr;
  
  pos = 0;
  GetNextSubstring(period,pos," ",tstr);
//  sd = DateFromString(tstr,"DD-MM-YYYY");
  sd = DateFromString(tstr,ServerDateFormat);
  GetNextSubstring(period,pos,",",tstr);
//  ed = DateFromString(tstr,"DD-MM-YYYY");
  ed = DateFromString(tstr,ServerDateFormat);
  return;
end;

procedure BankAccountReconWClassPeriod(Integer wn,var Date sd,var Date ed)
begin
  string 255 tstr;
  
  sd = "";
  ed = "";
  tstr = GetWindowString(wn,"gAccRcn_vperiod");
  if (nonblank(tstr)) then begin
    InPeriod(tstr,sd,ed);
  end;
  if (blankdate(sd)) then begin
    sd = CurrentDate;
    sd.day = 1;
  end;
  if (blankdate(ed)) then begin
    ed = sd;
    ed.day = DaysInMonth(ed.year,ed.month);  
  end;
//  tstr = DateToString(sd,"DD-MM-YYYY") & " " & DateToString(ed,"DD-MM-YYYY");
  tstr = DateToStr(sd) & " " & DateToStr(ed);
  PutWindowString(wn,"gAccRcn_vperiod",tstr);
  return;
end;

procedure BankAccountReconWClassPMAccNr(Integer wn,var string pmaccnr)
begin
  record PMBlock PMb;
  row PMBlock PMbrw;
  Integer i,rwcnt;

  BlockLoad(PMb);
  rwcnt = MatRowCnt(PMb);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PMb,i,PMbrw);
    i = rwcnt;
  end;
  pmaccnr = PMbrw.AccNr;
  PutWindowString(wn,"gAccRcn_vpmaccnr",pmaccnr);
  return;
end;

global
function Boolean BankAccountReconWClassOnOpenWindow(Integer wn)
begin
  Date sd,ed;
  string 255 pmaccnr;
  
  BankAccountReconWClassPeriod(wn,sd,ed);
  BankAccountReconWClassPMAccNr(wn,pmaccnr);
  threadremoteasync.BankAccountReconWClass_FillDataAsync(wn,pmaccnr,sd,ed);
  BankAccountReconWClassOnOpenWindow = false;
  return;
end;

global
procedure BankAccountReconWClassReload(string arg)
begin
  Date sd,ed;
  string 255 pmaccnr;

  BankAccountReconWClassPeriod(CurWindow,sd,ed);
  BankAccountReconWClassPMAccNr(CurWindow,pmaccnr);
  threadremoteasync.BankAccountReconWClass_FillDataAsync(CurWindow,pmaccnr,sd,ed);
  return;
end;

global
procedure BankAccountReconWClass_FillDataAsyncCallback(Integer wn,string html)
begin
  SetWebPageData(wn,"$BankAccountRecon",html);
  return;  
end;

global
procedure BankAccountReconWClass_CreateReceipt(string btnr)
begin
  record BankTRVc BTRr;
  record IPVc IPr;
  
  BTRr.SerNr = btnr;
  if (ReadFirstMain(BTRr,1,true)) then begin
    RecordNew(IPr);
    OpenWindow("IPDClass",0,0,"","",IPr);
  end;
  return;
end;

global
procedure BankAccountReconWClass_CreatePayment(string btnr)
begin
  record BankTRVc BTRr;
  record OPVc OPr;
  
  BTRr.SerNr = btnr;
  if (ReadFirstMain(BTRr,1,true)) then begin
    RecordNew(OPr);
    OpenWindow("OPDClass",0,0,"","",OPr);
  end;
  return;
end;

global
procedure BankAccountReconWClass_CreatePersonalPayment(string btnr)
begin
  record BankTRVc BTRr;
  record PPVc PPr;
  
  BTRr.SerNr = btnr;
  if (ReadFirstMain(BTRr,1,true)) then begin
    RecordNew(PPr);
    OpenWindow("PPDClass",0,0,"","",PPr);
  end;
  return;
end;

global
procedure BankAccountReconWClass_CreateTransaction(string btnr)
begin
  record BankTRVc BTRr;
  record TRVc TRr;
  
  BTRr.SerNr = btnr;
  if (ReadFirstMain(BTRr,1,true)) then begin
    RecordNew(TRr);
    OpenWindow("TRDClass",0,0,"","",TRr);
  end;
  return;
end;

global
procedure BankAccountReconWClass_Create(string args)
begin
  string 255 filename,banktransnr;
  LongInt pos;

  pos = 0;
  GetNextSubstring(args,pos,",",filename);
  GetNextSubstring(args,pos,",",banktransnr);
  switch (filename) begin
    case "IPVc": BankAccountReconWClass_CreateReceipt(banktransnr);
    case "OPVc": BankAccountReconWClass_CreatePersonalPayment(banktransnr);
    case "PPVc": BankAccountReconWClass_CreatePersonalPayment(banktransnr);
    case "TRVc": BankAccountReconWClass_CreateTransaction(banktransnr);
  end;
  return;
end;


global
procedure BankAccountReconWClass_OpenRecordToReconcile(string arg)
begin
  record PPVc PPr;
  record OPVc OPr;
  record IPVc IPr;
  record TRVc TRr;
  string 255 filename,transnr,intyc;
  LongInt pos;

  pos = 0;
  GetNextSubstring(arg,pos,",",filename);
  GetNextSubstring(arg,pos,",",transnr);
  GetNextSubstring(arg,pos,",",intyc);
  
  switch (filename) begin
    case "IPVc":
      IPr.SerNr = StringToLongInt(transnr);
      if (ReadFirstMain(IPr,1,true)) then begin
        OpenWindow("IPDClass",0,0,"","",IPr);
      end;
    case "OPVc":
      OPr.SerNr = StringToLongInt(transnr);
      if (ReadFirstMain(OPr,1,true)) then begin
        OpenWindow("OPDClass",0,0,"","",OPr);
      end;
    case "PPVc":
      PPr.SerNr = StringToLongInt(transnr);
      if (ReadFirstMain(PPr,1,true)) then begin
        OpenWindow("PPDClass",0,0,"","",PPr);
      end;
    case "TRVc":
      TRr.Number = StringToLongInt(transnr);
      TRr.IntYc = StringToLongInt(intyc);
      if (ReadFirstMain(TRr,2,true)) then begin
        OpenWindow("TRDClass",0,0,"","",TRr);
      end;
  end;
  return;
end;

global
procedure BankAccountReconWClass_OpenBankTransaction(string btnr)
begin
  record BankTRVc BTRr;
  
  BTRr.SerNr = btnr;
  if (ReadFirstMain(BTRr,1,true)) then begin
    OpenWindow("BankTRDClass",0,0,"","",BTRr);
  end;
  return;
end;

global
updating procedure BankAccountReconWClass_ReconcileBankTransaction(string arg)
begin
  Date sd,ed;
  string 255 tstr;

  BankAccountReconWClass_ReconcileBankTransactionRemote(arg);
  tstr = GetWindowString(CurWindow,"gAccRcn_vperiod");
  InPeriod(tstr,sd,ed);
  tstr = GetWindowString(CurWindow,"gAccRcn_vpmaccnr");
  BankAccountReconWClass_FillDataAsync(CurWindow,tstr,sd,ed);
  return;
end;

global
procedure BankAccountReconWClass_RefillData(string arg)
begin
  Date sd,ed;
  string 255 tstr;
  Integer wn;

  if (nonblank(arg)) then begin
    wn = StringToInt(arg);
  end else begin
    wn = CurWindow;
  end;
  if (FindWindow("BankAccountReconWClass")>0) then begin
    tstr = GetWindowString(wn,"gAccRcn_vperiod");
    InPeriod(tstr,sd,ed);
    tstr = GetWindowString(wn,"gAccRcn_vpmaccnr");
    BankAccountReconWClass_FillDataAsync(wn,tstr,sd,ed);
  end;
  return;
end;

global
procedure BankAccountRecon_RefreshStart(string arg)
begin
  Time t;

  t = CurrentTime;
  t.second = t.second + 10;
  NewTimedTask("RefreshBankAccountRecon","Reconciliation Refresh","BankAccountRecon_Refresh",arg,CurrentDate,t,"","");
  return;
end;

global
procedure BankAccountRecon_Refresh(string arg)
begin
  record BankTRVc BTRr;
  Time t;
  string 255 lastBankTRNr,cntstr,curwinstr;
  LongInt pos;
  
  pos = 0;
  GetNextSubstring(arg,pos,",",lastBankTRNr);
  GetNextSubstring(arg,pos,",",cntstr);
  GetNextSubstring(arg,pos,",",curwinstr);

  BTRr.SerNr = 2147483647;
  ReadLastMain(BTRr,1,false);
  if (StringToLongInt(lastBankTRNr)!=BTRr.SerNr) then begin
    BankAccountReconWClass_RefillData(curwinstr);
  end else begin
    if (StringToInt(cntstr)<=10) then begin
      t = CurrentTime;
      t.second = t.second + 1;
      cntstr = StringToInt(cntstr) + 1;
      NewTimedTask("BankAccountRecon_RefreshStart","Reconciliation Refresh","BankAccountRecon_RefreshStart",BTRr.SerNr & "," & cntstr,CurrentDate,t,"","");
    end;
  end;
  return;
end;

global
procedure BankAccountReconWClass_ImportYodleeData(string arg)
begin
  Date sd,ed;
  string 255 tstr;
  record BankTRVc BTRr;
  Time t;

  if (UserCanAction("YodleeData",true)) then begin
    tstr = GetWindowString(CurWindow,"gAccRcn_vperiod");
    InPeriod(tstr,sd,ed);
    tstr = GetWindowString(CurWindow,"gAccRcn_vpmaccnr");
    BTRr.SerNr = 2147483647;
    ReadLastMain(BTRr,1,false);

    PayModeBankAccountImportYodleeData(CurrentUser,tstr,sd,ed);
    t = CurrentTime;
    t.second = t.second + 1;
    NewTimedTask("BankAccountRecon_RefreshStart","Reconciliation Refresh","BankAccountRecon_RefreshStart",BTRr.SerNr & "," & "1" & "," & CurWindow,CurrentDate,t,"","");
  end else begin
    MessageBox(1274,StringFromStringSet(3,"YodleeData"));
  end;
  
  return;
end;

global
procedure RegisterYodleeUserDsm()
begin
  Integer wn;
  record YodleeUserVc YUr;
  
  if (UserCanAction("YodleeData",true)) then begin
    wn = CurWindow;
    GetWindowRecord(wn,YUr);
    if (blank(YUr.YodleeUsername) or blank(YUr.YodleePassword)) then begin
      MessageBox(40150,"");
    end else begin
      YodleeRegisterUser(YUr);
    end;
  end else begin
    MessageBox(1274,StringFromStringSet(3,"YodleeData"));
  end;
  return;
end;

global
procedure ImportDataYodleeUserDsm()
begin
  Integer wn;
  record YodleeUserVc YUr;
  string 255 tstr;
  Date sd,ed;
  
  if (UserCanAction("YodleeData",true)) then begin
    wn = CurWindow;
    GetWindowRecord(wn,YUr);
    if (blank(YUr.YodleeUsername) or blank(YUr.YodleePassword)) then begin
      MessageBox(40150,"");
    end else begin
      DefaultPeriod(tstr);
      InPeriod(tstr,sd,ed);
      YodleeUserImportYodleeData(YUr.UserCode,YUr.AllowedBankAccs,sd,ed);
    end;
  end else begin
    MessageBox(1274,StringFromStringSet(3,"YodleeData"));
  end;
  return;
end;

global
procedure ImportDataYodleeUserLsm()
begin
  Integer wn;
  record YodleeUserVc YUr;
  string 255 tstr;
  Date sd,ed;
  Integer i;
  
  if (UserCanAction("YodleeData",true)) then begin
    DefaultPeriod(tstr);
    InPeriod(tstr,sd,ed);
    wn = CurWindow;
    i = 1;
    while (GetRecordFromBrowse(YUr,wn,i)) begin
      if (blank(YUr.YodleeUsername) or blank(YUr.YodleePassword)) then begin
      end else begin
        YodleeUserImportYodleeData(YUr.UserCode,YUr.AllowedBankAccs,sd,ed);
      end;
      i = i + 1;
    end;
  end else begin
    MessageBox(1274,StringFromStringSet(3,"YodleeData"));
  end;
  return;
end;

global
procedure BankAccountReconWClassChangeEditField(string arg)
begin
  Date sd,ed;
  string 255 tstr,fieldname,fieldvalue,prevvpmaccnr,vpmaccnr,vperiod;
  LongInt pos;

  pos = 0;
  GetNextSubstring(arg,pos,",",fieldname);
  GetNextSubstring(arg,pos,",",fieldvalue);
  
  vperiod = GetWindowString(CurWindow,"gAccRcn_vperiod");
  prevvpmaccnr = GetWindowString(CurWindow,"gAccRcn_vpmaccnr");
  
  switch (fieldname) begin
    case "AccRcn_pmaccnr":
      pos = 0;
      GetNextSubstring(fieldvalue,pos," ",vpmaccnr);
      
      if (prevvpmaccnr!=vpmaccnr and nonblank(fieldvalue)) then begin
        InPeriod(vperiod,sd,ed);
        PutWindowString(CurWindow,"gAccRcn_vpmaccnr",vpmaccnr);
        BankAccountReconWClass_FillDataAsync(CurWindow,vpmaccnr,sd,ed);
      end;
    case "AccRcn_period":
      if (vperiod!=fieldvalue and nonblank(fieldvalue)) then begin
        InPeriod(fieldvalue,sd,ed);
        PutWindowString(CurWindow,"gAccRcn_vperiod",fieldvalue);
        BankAccountReconWClass_FillDataAsync(CurWindow,prevvpmaccnr,sd,ed);
      end;
  end;

  return;
end;