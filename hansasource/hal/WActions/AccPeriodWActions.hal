function Boolean AccPeriodDClassCodeEFAfter(Integer wn,Integer fn,Integer rownr,Integer changedf)
begin
  Boolean res;
  record AccPeriodVc AccPeriodr;
  row AccPeriodVc AccPeriodrw;
  record YearBlock YBr;
  row YearBlock YBrw;
  Integer rwcnt,i;
  Date thedat;
  
  res = true;
  if (changedf!=0) then begin
    BlockLoad(YBr);
    GetWindowRecord(wn,AccPeriodr);
    rwcnt = MatRowCnt(YBr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(YBr,i,YBrw);
      if (YBrw.YearCode==AccPeriodr.Code) then begin
        rwcnt = MatRowCnt(AccPeriodr);
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowDelete(AccPeriodr,0);
        end;
        AccPeriodr.FiscalStartDate = YBrw.StartDate;
        AccPeriodr.FiscalEndDate = YBrw.EndDate;
        thedat = AccPeriodr.FiscalStartDate;
        while (thedat<AccPeriodr.FiscalEndDate) begin
          AccPeriodrw.StartDate = thedat;
          thedat = AddMonth(AccPeriodrw.StartDate,1);
          AccPeriodrw.EndDate = AddDay(thedat,-1);
          MatRowPut(AccPeriodr,MatRowCnt(AccPeriodr),AccPeriodrw);
        end;
        PutWindowRecord(wn,AccPeriodr);
        goto LAccPeriodDClassCodeEFAfter;
      end;
    end;
    AccPeriodr.FiscalStartDate = thedat;
    AccPeriodr.FiscalEndDate = thedat;
    rwcnt = MatRowCnt(AccPeriodr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowDelete(AccPeriodr,0);
    end;
    PutWindowRecord(wn,AccPeriodr);
  end;
LAccPeriodDClassCodeEFAfter:;
  AccPeriodDClassCodeEFAfter = res;
  return;
end;

global
function Boolean AccPeriodDClassAfterEditField(Integer wn,string fieldname,Integer fn,Integer rownr,Integer changed)
BEGIN
  Boolean res;
  
  res = false;
  switch (fieldname) begin
    case "Code": res = AccPeriodDClassCodeEFAfter(wn,fn,rownr,changed);
  end;
  AccPeriodDClassAfterEditField = res;
  RETURN;
END;

