external procedure OutRegisterTag(string);

global
procedure ExpActEn(record RcVc RepSpec)
begin
  record ActVc Actr;
  Row ActVc Actrw;
  Boolean found;
  string 255 ti;
  Integer rwcnt,i;
  Boolean testf,firstf;

  firstf = true;
  Actr.TransDate = RepSpec.sStartDate;
  found = true;
  while (LoopKey("TransDate",Actr,1,found)) begin
   testf = true;
   if (Actr.TransDate>RepSpec.sEndDate) then begin
    found = false;
   end;
   if (nonblank(RepSpec.f2)) then begin
    if (SetInSet(RepSpec.f2,Actr.MainPersons)==false) then begin
      testf = false;
    end;
   end;
   if (nonblank(RepSpec.f1)) then begin
     if (RepSpec.f1<>Actr.CUCode) then begin
       testf = false;
     end;
   end;
   if (nonblank(RepSpec.f3)) then begin
    if RepSpec.f3<>Actr.ActType then begin
      testf = false;
    end;
   end;
   if (found==false) then begin
    testf = false;
   end;
   if (testf) then begin
    ti="";
    rwcnt = MatRowCnt(Actr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Actr,i,Actrw);
      if (Actrw.TextCode==RepSpec.AccStr) then begin
       ti=Actrw.Text;
      end;
    end;
    if (firstf) then begin
      OutRegisterTag("ActVc");
      NewLine;
      firstf = false;
    end;
    ExportString(Actr.CUCode);
    ExportDate(Actr.TransDate);
    ExportString(Actr.StartTime);
    ExportString(Actr.MainPersons);
    ExportString(Actr.Contact);
    ExportString(Actr.ActType);
    ExportString(Actr.Comment);
    ExportLongInt(Actr.OKFlag);
    ExportString(Actr.CUName);
    ExportString(ti);
    NewLine;
    UserTrace(Actr.TransDate,0);
   end;
  end;
  return;
end;

