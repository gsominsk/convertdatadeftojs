/* Bankfile For Finland */

#ifdef HAL

Global
Procedure BankExpFinland0(Record OPVc OPr,Integer today)
Begin
  Record VIVc VIr;
  Record CYBlock CompYear;
  Record CUVc VEr;
  Row OPVc OPrw;
  String 100 tstr;
  Val temp;
  Val sumfakt,sumkred;
  Integer cntkred,cntfakt;
  Integer i,rwcnt;
  Integer rowcount;
  Boolean testf;
  Boolean VEf;
  
  BlockLoad(CompYear);
  sumfakt = 0;
  sumkred = 0;
  cntkred = 0;
  cntfakt = 0;

  ExportPadString("LM  00",6,"",false);
  ExportPadString(ConvertSpecStr(OPr.BankAcc,"FINBANKACC"),14," ",false);
  ExportPadString(StripNonDigits(CompYear.OrgNr),9,"0",true);
  If (today==1) Then Begin
    ExportPadString(DateToString(OPr.TransDate,"YYMMDD"),6," ",false);
  End Else Begin
    ExportPadString(DateToString(CurrentDate,"YYMMDD"),6," ",false);
  End;
  tstr = CurrentTime;
  ExportPadString(StripNonDigits(tstr),2," ",false);
  ExportPadString(tstr,2," ",false);
  ExportPadString(ConvertSpecStr(OPr.BankAcc,"FINBANKGROUP"),2," ",false);
  If (today==1) Then Begin
    ExportPadString(DateToString(OPr.TransDate,"YYMMDD"),6," ",false);
  End Else Begin
    ExportPadString(DateToString(CurrentDate,"YYMMDD"),6," ",false);
  End;
  tstr = " ";
  NewLine;

  rowcount = 0;
  rwcnt = MatRowCnt(OPr);
  For (i=0; i<rwcnt; i=i+1) Begin
    MatRowGet(OPr,i,OPrw);
    testf = false;
    If (OPrw.ovst==0) AND (OPrw.stp==1) Then Begin
      testf = true;
    End;
    VIr.SerNr = OPrw.VISerNr;
    If (ReadFirstMain(VIr,1,true)==false) Then Begin
      testf = false;
    End;
    if (testf) then begin
      if (VIr.Invalid<>0) then begin testf = false; end;
    end;
    If (testf==true) Then Begin
      VEr.Code = VIr.VECode;
      VEf = ReadFirstMain(VEr,1,true);
      If (OPrw.RecVal<0) Then Begin
        cntkred = cntkred + 1;
        temp = 0 - OPrw.RecVal;
        sumkred = sumkred + temp;
      End Else Begin
        cntfakt = cntfakt + 1;
        sumfakt = sumfakt + OPrw.RecVal;
      End;
      ExportPadString("LM  00",6," ",false);
      ExportPadString(ConvertSpecStr(OPr.BankAcc,"FINBANKACC"),14," ",false);
      If (VEf) Then Begin
        ExportPadString(VEr.Name,30," ",false);
        ExportPadString(VEr.InvAddr0,20," ",false);
        ExportPadString(VEr.InvAddr0,20," ",false);
      End Else Begin
        ExportPadString(VIr.VEName,30," ",false);
        ExportPadString(" ",20," ",false);
        ExportPadString(" ",20," ",false);
      End;
      ExportPadString(ConvertSpecStr(OPrw.BankAcc,"FINBANKACC"),14,"",false);
      ExportPadString(" 89",3," ",false);
      If (nonblank(VIr.RefStr)) Then Begin
        ExportPadString("1",1," ",false);
        ExportPadString(VIr.RefStr,20,"0",true);          
        ExportPadString(" ",40," ",false);
      End Else Begin
        ExportPadString("2",1," ",false);
        If (VEf) Then Begin
          ExportPadString(VEr.VECustID,10," ",false);
        End Else Begin
          ExportPadString(" ",10," ",false);
        End;
        ExportPadString(VIr.InvoiceNr,15," ",false);
        ExportPadString(DateToString(OPr.TransDate,"YYMMDD"),6," ",false);
        ExportPadString(" ",29," ",false);
      End;
      ExportPadString(DateToString(OPr.TransDate,"YYMMDD"),6," ",false);
      ExportPadString(" ",12," ",false);
      If (OPrw.RecVal<0) Then Begin
        temp = 0 - OPrw.RecVal;
      End Else Begin
        temp = OPrw.RecVal;
      End;
      ExportPadString(ValToString(temp,M4Val,"","",0),12,"0",true);
      ExportPadString(" ",102," ",false);
      NewLine;
    End;
  End;
  UserTrace("" & OPr.SerNr,2);
  ExportPadString("LM  90",6," ",false);
  ExportPadString(ConvertSpecStr(OPr.BankAcc,"FINBANKACC"),14," ",false);
  ExportPadString(StripNonDigits(CompYear.OrgNr),9,"0",true);
  if (today==1) then begin
    tstr = DateToString(OPr.PayDate,"YYMMDD");
  end else begin
    tstr = DateToString(CurrentDate,"YYMMDD");
  end;
  ExportPadString(tstr,6," ",false);
  ExportPadString("" & cntfakt,6,"0",true);
  ExportPadString(ValToString(sumfakt,M4Val,"","",0),13,"0",true);
  ExportPadString("" & cntkred,6,"0",true);
  ExportPadString(ValToString(sumkred,M4Val,"","",0),13,"0",true);
  if (sumfakt>=sumkred) then begin  // if invoices > credits
    temp = sumfakt - sumkred;
    ExportPadString(ValToString(temp,M4Val,"","",0),13,"0",true);
  end else begin
    ExportPadString("",13," ",false);
  end;
  if (sumkred>=sumfakt) then begin  // if credits > invoices
    temp = sumfakt - sumkred; // Is this correct
    ExportPadString(ValToString(temp,M4Val,"","",0),13,"0",true);
  end else begin
    ExportPadString("",13," ",false);
  end;
  ExportPadString("",201," ",false);
  NewLine;
  return;
End;

#endif



