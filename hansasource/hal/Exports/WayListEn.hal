external function string 100 GetWayListDistUnit;

global
updating procedure WayListEn(record RcVc RepSpec)
begin
  record WLSettingsBlock WLSettingsRec;
  record WayListVc WayListr;
  row WayListVc WayListrw;
  boolean loop;
  string 100 DistUnit;
  
  BlockLoad(WLSettingsRec);
  WLSettingsRec.ExportFileName = RepSpec.f1;
  BlockStore(WLSettingsRec);
  DistUnit = GetWayListDistUnit;
  
  CreateFile(RepSpec.f1);
  ExportString(USetStr(23068));
  ExportString(USetStr(23069));
  ExportString(USetStr(23070));
  ExportString(USetStr(23071));
  ExportString(USetStr(23072));
  ExportString(USetStr(23073));
  ExportString(USetStr(23074));
  ExportString(DistUnit & USetStr(23075));
  ExportString(DistUnit & USetStr(23076));
  ExportString(USetStr(23077) & DistUnit);
  ExportString(USetStr(23078) & DistUnit);
  ExportString(USetStr(23079));
  NewLine;
  
  WayListr.TransDate = RepSpec.sStartDate;
  loop = true;
  
  while (LoopKey("TransDate", WayListr,1,loop)) begin
    if (WayListr.TransDate <= RepSpec.sEndDate) then begin
      if (MatRowCnt(WayListr)>0) then begin
        MatRowGet(WayListr,0,WayListrw);
      end;
      ExportLongint(WayListr.SerNr);
      ExportDate(WayListr.TransDate);
      ExportString(WLSettingsRec.UserName);
      ExportString(WayListr.CarCode);
      ExportString(WayListrw.WDistCode);
      ExportString(WayListrw.WComment);
      ExportString(WayListr.Purpose);
      ExportVal(WayListrw.Stkm,M4UVal);
      ExportVal(WayListrw.Endkm,M4UVal);
      ExportVal(WayListrw.WDistVal,M4Val);
      ExportVal(WayListr.CostPerkm,M45Val);
      ExportVal(WayListr.TotalCost,M4Val);
      NewLine;
    end else begin
      loop = false;
    end;
  end;
  
  CloseFile;
  
  return;
end;

global
function boolean WayListEClassOnOpenWindow(integer wn)
begin
  record RcVc RepSpec;
  record WLSettingsBlock WLSettingsRec;
  
  BlockLoad(WLSettingsRec);
  GetWindowRecord(wn, RepSpec);
  ReportDefaults(RepSpec, "WayListEClass");
  RepSpec.sStartDate = CurrentDate;
  RepSpec.sEndDate = CurrentDate;
  RepSpec.f1 = WLSettingsRec.ExportFileName;
  PutWindowRecord(wn, RepSpec);
  
  WayListEClassOnOpenWindow = false;
  return;
end;

global
updating function boolean WayListEClassOnOKWindow(integer wn)
begin
  boolean res;
  record RcVc RepSpec;
  
  GetWindowRecord(wn, RepSpec);
  if (RepSpec.f1<>"") then begin;
    CloseWindow(wn);
    WayListEn(RepSpec);
    MessageBox(23051,"");
  end;
  WayListEClassOnOKWindow = res;
  return;
end;
