#ifdef HAL

external function Boolean CheckPlanShip(string,string);

function
Boolean HasShippedQty(record POVc POp)
BEGIN
  row POVc POrw;
  Boolean res;
  Integer i,rwcnt;
  val t;
  
  res = false;
  rwcnt = MatRowCnt(POp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(POp,i,POrw);
    t = t + POrw.Shipd1;
  end;
  if (t!=0) then begin
    res = true;
  end;  
  HasShippedQty = res;
  RETURN;
END;

procedure OutPOOrdRows(record RcVc RepSpec,record POVc POp)
BEGIN
  row POVc POrw;
  record INVc INr;
  string 20 fromin,toin;
  string 20 fromacc,toacc;
  Integer i,rwcnt;
  val t;
  Boolean testf;
  string 255 tstr;
  
  fromin = FirstInRange(RepSpec.f3,20);
  toin = LastInRange(RepSpec.f3,20);    
  fromacc = FirstInRange(RepSpec.LastAcc,10);
  toacc = LastInRange(RepSpec.LastAcc,10);    
  testf = true;
  rwcnt = MatRowCnt(POp);
  for (i = 0 ; i<rwcnt ;i=i+1) begin
    MatRowGet(POp,i,POrw);
    if (nonblank(RepSpec.f3)) then begin
      INr.Code = POrw.ArtCode;
      if (ReadFirstMain(INr,1,true)) then begin
        if (INr.Group<fromin) then begin testf = false; end;
        if (INr.Group>toin) then begin testf = false; end;
      end;
    end;
    if (nonblank(RepSpec.LastAcc)) then begin      
      if (POrw.ArtCode<fromacc) then begin testf = false; end;
      if (POrw.ArtCode>toacc) then begin testf = false; end;
    end;
    if (testf) then begin
      ExportLongInt(POp.SerNr);
      ExportDate(POp.TransDate);
      tstr = StringToDate(POrw.PlanShipRow);
      if (blank(tstr)) then begin tstr = POp.PlanShip; end;
      ExportString(tstr);
      ExportString(POp.VECode);
      ExportString(POp.Addr0);
      ExportString(POrw.ArtCode);
      ExportString(POrw.VEArtCode);
      ExportString(POrw.Spec);
      ExportVal(POrw.Quant,M4Qty);
      t = POrw.Quant - POrw.Shipd1;
      ExportVal(t,M4Val);
      ExportVal(POrw.Price,M4Val);
      ExportVal(POrw.Sum,M4Val);
      ExportVal(POrw.vRebate,M41Val);
      ExportString(POrw.VATCode);
      NewLine;
    end;  
  end;
  RETURN;
END;

global
procedure POOrdRowEn(record RcVc RepSpec)
BEGIN
  record POVc PO;
  Boolean TrHs;
  string 255 fromcust,tocust;
  string 255 ckey;
  Boolean testf,testf1;
  LongInt long1,long2;
  
  long1 = FirstInRange(RepSpec.f1,20);
  long2 = LastInRange(RepSpec.f1,20);  
  fromcust = FirstInRange(RepSpec.f2,20);
  tocust = LastInRange(RepSpec.f2,20);  
  if (RepSpec.flags[5]==0) then begin
    ckey = "SerNr";
    PO.SerNr = long1;
  end;
  if (RepSpec.flags[5]==1) then begin
    ckey = "VECode";
    PO.VECode=fromcust;
  end;
  TrHs = true;
  while (LoopKey(ckey,PO,1,TrHs)) begin
		testf = true;
					
		testf1=false;
		if (RepSpec.flags[4]==1) then begin
			testf1 = true;
		end;
		if (RepSpec.flags[2]==1) then begin
			if (PO.PUFlag==0) then begin
				testf1 = true;
			end;
		end;    
		if (RepSpec.flags[3]==1) then begin
			if ((PO.PUFlag==0) and (HasShippedQty(PO))) then begin
				testf1 = true;
			end;
		end;    
		if (testf1==false) then begin
			testf=false;
		end;  

		if (nonblank(RepSpec.Stext)) then begin
			if (CheckPlanShip(RepSpec.Stext,PO.PlanShip)==false) then begin
				testf = false;
			end;
		end;    
		if (RepSpec.flags[5]==0) then begin
			if (nonblank(RepSpec.f1)) then begin
				if (PO.SerNr>long2) then begin 
					TrHs = false;testf = false; 
				end;
			end;  
			if (nonblank(RepSpec.f2)) then begin
        if ((PO.VECode<fromcust) or (PO.VECode>tocust)) then begin 
					testf = false; 
        end;
      end; 
		end else begin
		  if (nonblank(RepSpec.f2)) then begin
        if ((PO.VECode<fromcust) or (PO.VECode>tocust)) then begin 
					TrHs = false;testf = false; 
        end;
      end;
      if (nonblank(RepSpec.f1)) then begin
				if (PO.SerNr<long1) or (PO.SerNr>long2) then begin 
				  testf = false; 
				end;
			end;   
    end;     
		
		if (DateInRange(PO.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
		  testf = false;
		end;
  
    if (testf) then begin
      OutPOOrdRows(RepSpec,PO);
      UserTrace(PO.SerNr,M4Long);
    end;     
  end;
  NewLine;
  RETURN;
END;
#endif