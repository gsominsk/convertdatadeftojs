procedure OutTSRows(record TSVc TSp, Integer fl1)
begin
  Integer i,rwcnt;
  row TSVc TSrw;
  record PRVc PRr;
  Boolean testf;

  rwcnt = MatRowCnt(TSp);
  for (i = 0 ; i<rwcnt ;i=i+1) begin
    MatRowGet(TSp,i,Tsrw);
    testf = true;
    if (fl1==1) then begin
      if (TSrw.ItemType<>3) then begin testf = false; end;
    end;
    if (testf) then begin
      PRr.Code = TSrw.PRCode;
      if ReadFirstMain(PRr,1,true) then begin
        ExportLongInt(TSp.SerNr);
        ExportDate(TSp.RegDate);
        ExportString(TSp.Comment);
        ExportDate(TSrw.date);
        ExportString(TSrw.EMCode);
        ExportString(TSrw.PRCode);
        ExportString(TSrw.ArtCode);
        ExportVal(TSrw.Qty,M4Qty);
        ExportString(TSrw.Spec);
        ExportString(PRr.Leader);
        ExportString(TSrw.TimeClass);
        ExportString(TSrw.Objects);
        NewLine;
      end;
    end;
  end;
  return;
end;

global
procedure TSRowEn(record RcVc RepSpec)
begin
  record TSVc MR;
  Boolean TrHs,testf;
  LongInt frnr,tonr;

  frnr = FirstInRange(RepSpec.f1,20);
  tonr = LastInRange(RepSpec.f1,20);
  MR.SerNr = frnr;
  TrHs = true;
  while (LoopMain(MR,1,TrHs)) begin
    testf = true;
    if (TrHs) then begin
      if (tonr<>-1) then begin
        if (MR.SerNr>tonr) then begin TrHs = false; end;
      end;
    end;
    if (RepSpec.flags[2]==0) then begin
      if (Mr.OKFlag<>0) then begin testf = false; end;
    end;
    if (RepSpec.flags[3]==0) then begin
      if (Mr.OKFlag==0) then begin testf = false; end;
    end;
    if (TrHs and testf) then begin
      if ((MR.RegDate>=RepSpec.sStartDate) and (MR.RegDate<=RepSpec.sEndDate)) then begin
        OutTSRows(MR, RepSpec.flags[1]);
        UserTrace(MR.SerNr,0);
      end;
    end;
  end;
  NewLine;
  return;
end;

