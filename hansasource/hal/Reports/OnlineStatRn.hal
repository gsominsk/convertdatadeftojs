procedure GetUserEvent(LongInt mailBoxNr,Integer eventv,var Date datp,var Time timp,var string LastIP)
BEGIN
  record UserEventVc UEr;
  Date blankd;
  Time blankt;

  UEr.mailBoxNr = mailBoxNr;
  datp = blankd;
  timp = blankt;
  LastIP = "";
  if (ReadFirstMain(UEr,2,true)) then begin
    switch (eventv) begin
      case 1: // Login 
        datp = UEr.LastLoginDate;
        timp = UEr.LastLoginTime;
      case 2: // Controlled Logout 
        datp = UEr.LastLogoutDate;
        timp = UEr.LastLogoutTime;
      case 3: // Login from Web  
        datp = UEr.LastLoginWebDate;
        timp = UEr.LastLoginWebTime;
      case 4: // Controlled Logout from Web  
        datp = UEr.LastLogoutWebDate;
        timp = UEr.LastLogoutWebTime;
      case 5: // Timeout Logout from Web 
        datp = UEr.LastLogout2Date;
        timp = UEr.LastLogout2Time;
    end;
    LastIP = UEr.LastIP;
  end;
  RETURN;
END;

global
procedure OnlineStatRn(record RcVc RepSpec)
BEGIN
  string 255 tcpaddr,tstr;
  record ConfVc Confr;
  Boolean TrHs,testf,onlinef;
  Date td,wtd;
  Time tim,wtim;
  
  StartReportJob(1593);//8630);
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(1595),false);
  OutString(170,0,USetStr(1596),false);
  OutString(280,0,USetStr(1597),false);
  OutString(480,0,USetStr(1598),true);
  EndFormat;
  Gray_Divider(0,1);
  TrHs = true;
  Confr.AddrName = "";
  while (LoopKey("AddrName",Confr,1,TrHs)) begin
    onlinef = false;
    testf = true;
    if (blank(Confr.AddrCode)) then begin testf = false; end;
    if (TrHs==false) then begin testf = false; end;
//    if (FindUserByName(Confr.AddrName)>-1) then onlinef = true;
    if (testf) then begin
      GetUserEvent(Confr.SerNr,1,td,tim,tcpaddr);
      GetUserEvent(Confr.SerNr,3,wtd,wtim,tstr);
      StartFormat(15);
      if (onlinef) then begin
        OutString(0,"DblInvite",Confr.AddrName,false);
        OutString(120,0,1594,false);
      end else begin
        OutString(0,0,Confr.AddrName,false);
      end;
      OutDate(170,0,td,false);
      if (blankdate(td)) then begin
        tstr = "";
      end else begin
        tstr = tim;
      end;  
      OutString(230,0,tstr,false);
      OutDate(280,0,wtd,false);
      if (blankdate(wtd)) then begin
        tstr = "";
      end else begin
        tstr = wtim;
      end;  
      OutString(340,0,tstr,false);
      OutString(480,0,tcpaddr,true);
      EndFormat;
    end;
  end;
  EndJob;
  RETURN;
END;