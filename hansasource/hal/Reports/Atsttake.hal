external procedure HTDepartment(string,var string);
external procedure HTPers(string,var string);
external procedure HTAsset(string,var string);
external procedure HTAssetClass(string,var string);

global
procedure ATStockTakeRn(record RcVc RepSpec)
begin
  Record ATVc ATp;
  Boolean TrHs;
  Boolean testf;
  String 20 fromat,toat;
  val totval;
  Integer rw;
  String 255 tstr;

  fromat = FirstInRange(RepSpec.f1,20);
  toat   = LastInRange(RepSpec.f1,20);
  totval = 0;
  StartReportJob(USetStr(5441));
  rw = 1;
  HTDepartment(RepSpec.AccStr,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTPers(RepSpec.f2,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;  
  HTAsset(RepSpec.f1,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTAssetClass(RepSpec.f3,tstr);  
  Header(rw,tstr,1);  
  EndHeader;
  StartFormat(15);
  OutString(15,0,USetStr(5442),false);
  OutString(100,0,USetStr(5443),false);
  OutString(300,0,USetStr(5444),true);
  OutString(450,0,USetStr(5445),true);
  EndFormat;
  ATp.Code = fromat;
  Gray_Divider(0,1);
  TrHs = true;
  while LoopMain(ATp,1,TrHs) begin
    if (nonblank(toat)) then begin
      if (ATp.Code>toat) then begin
        TrHs = false;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f2)) then begin
        if (ATp.Owner<>RepSpec.f2) then begin
         testf = false;
        end;
      end;
      if (nonblank(RepSpec.f3)) then begin
        if (ATp.ATClass<>RepSpec.f3) then begin
         testf = false;
        end;
      end;
      if (nonblank(RepSpec.AccStr)) then begin
        if (ATp.Department<>RepSpec.AccStr) then begin
            testf = false;
        end;
      end;
      if (nonblankdate(RepSpec.d1)) then begin
        if (RepSpec.d1<ATp.StockDate) then begin
          testf = false;
        end;
      end;
      if (ATp.Inactive==1) then begin testf = false; end;
      if (testf) then begin
        StartFormat(15);
        OutString(15,0,ATp.Code,false);
        OutString(100,0,ATp.Description,false);
        OutVal(300,0,ATp.Quantity,M4Qty,true);
        OutString(450,0,USetStr(2766),true);
        EndFormat;
      end;
    end;
  end;
  Gray_Divider(0,1);
  EndJob;
  return;
end;

