external procedure FindProdOrdOutItem(record ProdOrderVc, var string,var val);

global 
procedure ProdQueueRn (record RcVc RepSpec)
begin
  record ProdOrderVc ProdOrderr;
  boolean TrHs, testf,firstf;
  LongInt sernr;
  val diff,divby;
  string 5 unit;
  integer noofrows;
  integer ttype;
  string 255 item,textitem,tmp; 
  string 255 lastmachine;
  string 255 tstr;
  integer rw;
  
  rw = 1;
  sernr = FirstInRange(RepSpec.f3,10);
  StartReportJob(USetStr(15000));
  if (nonblank(RepSpec.f1)) then begin
    tstr = USetStr(15005) & RepSpec.f1;
    Header(rw,tstr,1);
  end;
  if (blank(RepSpec.f1)) then begin
    tstr = USetStr(15007);
    Header(rw,tstr,1);
  end;
  EndHeader;
  StartFormat(15);   
  OutString(0,0,USetStr(15001),false);
  OutString(70,0,USetStr(15002),false);
  OutString(150,0,USetStr(15003),false);
  OutString(330,0,USetStr(15004),true);
  OutString(410,0,USetStr(15005),false);
  OutString(1,0,USetStr(15006),true);
  EndFormat;
  Gray_Divider(0,1);
  
  TrHs = true;
  firstf = true;
  sernr = FirstInRange(RepSpec.f3,10);
  ProdOrderr.Machine = RepSpec.f1;
  while (LoopKey("Queue",ProdOrderr,1,TrHs)) begin
    if (TrHs) then begin
      if (nonblank(RepSpec.f1)) then begin
        if (ProdOrderr.Machine <> RepSpec.f1) then begin
          TrHs = false;
        end;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(lastmachine)) then begin
        if (ProdOrderr.Machine <> lastmachine) then begin
          firstf = true;
        end;
      end;
      if (ProdOrderr.DoneFlag <> 0) then begin
        testf = false;
      end;
      if (ProdOrderr.StatusFlag < 1) or (ProdOrderr.StatusFlag > 2) then begin
        testf = false;
      end;
      if (sernr<> -1) then begin
        if (sernr <> ProdOrderr.SerNr) then begin
          testf = false;
        end;
      end;
      if (testf) then begin
        if (firstf) then begin
          FindProdOrdOutItem(ProdOrderr,tmp,divby);
          StartFormat(15);
          OutString(0,"DblProdOrderVc",ProdOrderr.SerNr,false)
          OutString(70,0,ProdOrderr.Recipe,false);
          OutString(150,0,ProdOrderr.RecName,false);
          diff = (ProdOrderr.Qty/divby) - ProdOrderr.Finished;
          Outval(330,0,diff,M4Qty,true);
          OutString(410,0,ProdOrderr.Machine,false);
          OutVal(1,0,ProdOrderr.QueuePos,M40Val,true);
          EndFormat;
          firstf = false;
          lastmachine = ProdOrderr.Machine;
       end;
      end; 
    end;
  end;
  EndJob;
  Return;
end;
