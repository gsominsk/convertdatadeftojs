external procedure HT2Per(Date, Date , var string);
external function Boolean CheckifAT2UnitSold(string,Date);
external function Boolean CheckDepAndDate(record AT2UnitVc,string,Date);
external procedure HTInventoryNrs(string,string,var string);
external procedure HTDetailLevel(Integer, var string);
external procedure HTDepartment(string,var string);
external procedure HTCategories(string,string,var string);

procedure PrintAT2ListHeader(record RcVc RepSpec,string frinv,string toinv)
BEGIN
  Integer rw;
  string 255 tstr;
  
  rw = 1;
  HTInventoryNrs(frinv,toinv,tstr);
  Header(rw, tstr, 1);
  rw = rw + 1;
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw, tstr, 0);
  HTCategories(RepSpec.f2,RepSpec.f2,tstr);
  Header(rw, tstr, 1);
  rw = rw + 1;
  if (nonblank(RepSpec.TransStr)) then begin
    tstr = USetStr(9201);
    tstr = tstr & " ";
    tstr = tstr & RepSpec.TransStr;
    Header(rw, tstr, 0);
    rw = rw + 1;
  end;    
  RETURN;      
END;

global
procedure AT2RespRn(record RcVc RepSpec)
BEGIN
  record AT2UnitVc ATUnitr;
  record UserVc Userr;
  Boolean TrHs,testf;
  string 20 frinv,toinv;
  string 20 frcat,tocat;
  string 20 lastuser;
  string 200 ckey;
  Date blankd;
  Boolean firstf;
  
  frinv = FirstInRange(RepSpec.f1,20);
  toinv = LastInRange(RepSpec.f1,20);
  frcat = FirstInRange(RepSpec.f2,20);
  tocat = LastInRange(RepSpec.f2,20);
  StartReportJob(USetStr(9005));
  PrintAT2ListHeader(RepSpec,frinv,toinv);
  EndHeader;
  if (RepSpec.flags[2]==0) then begin
    StartFormat(15);
    OutString(0,0,USetStr(9001),false);
    OutString(100,0,USetStr(9002),false);
    OutString(1,0,USetStr(11535),true);
    EndFormat;
    ckey = "InventoryNr";
    ATUnitr.InventoryNr = frinv;
  end else begin
    StartFormat(15);
    OutString(0,0,USetStr(9001),false);
    OutString(100,0,USetStr(9002),false);
    OutString(1,0,USetStr(8776),true);
    EndFormat;
    ckey = "RespPerson";
    ATUnitr.RespPerson = RepSpec.TransStr;
  end;
  firstf = true;
  TrHs = true;
  while (LoopKey(ckey,ATUnitr,1,TrHs)) begin
    if (RepSpec.flags[2]==0) then begin
      if (nonblank(RepSpec.f1)) then begin
        if (ATUnitr.InventoryNr>toinv) then begin
          TrHs = false; 
        end;
      end;
    end else begin
      if (nonblank(RepSpec.TransStr)) then begin
        if (ATUnitr.RespPerson!=RepSpec.TransStr) then begin
          TrHs = false; 
        end;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (RepSpec.flags[2]==0) then begin
        if (nonblank(RepSpec.TransStr)) then begin
          if (ATUnitr.RespPerson<>RepSpec.TransStr) then begin
            testf = false;
          end;
        end;
      end else begin
        if (nonblank(RepSpec.f1)) then begin
          if (ATUnitr.InventoryNr<frinv) then begin
            testf = false;
          end;
          if (ATUnitr.InventoryNr>toinv) then begin
            testf = false;
          end;
        end;
      end;
      if (RepSpec.flags[4]==0) then begin
        if (ATUnitr.Activef == 1) then begin
           testf= false;
        end;
      end; 
      if (nonblank(RepSpec.f2)) then begin
        if (ATUnitr.AT2Code<frcat) then begin
          testf = false;
        end;
        if (ATUnitr.AT2Code>tocat) then begin
          testf = false;
        end;
      end;
      if (RepSpec.flags[6]==0) then begin
        if (testf) then begin
          if (CheckifAT2UnitSold(ATUnitr.InventoryNr,blankd)) then begin
            testf = false;
          end;
        end;  
      end;
      if (testf) then begin
        if (RepSpec.flags[2]==0) then begin
          StartFormat(15);
          OutString(0,"DblAT2UnitVc",ATUnitr.InventoryNr,false);
          OutString(100,0,ATUnitr.Description,false);
          OutString(1,0,ATUnitr.RespPerson,true);
          EndFormat;
        end else begin
          if (lastuser!=ATUnitr.RespPerson) then begin
            Userr.Code = ATUnitr.RespPerson;
            if (ReadFirstMain(Userr,1,true)) then begin
              Gray_Divider(0,1);
              StartFormat(15);
              OutString(0,0,Userr.Code,false)
              OutString(70,0,Userr.Name,false)
              OutString(300,0,Userr.Phone1,false)
              EndFormat;
              Gray_Divider(0,1);
            end;
            lastuser = ATUnitr.RespPerson;
          end;
          if ((firstf) and (blank(ATUnitr.RespPerson))) then begin
            firstf = false;
            Gray_Divider(0,1);
            StartFormat(15);
            OutString(0,0,USetStr(9978),false);
            EndFormat;
            Gray_Divider(0,1);
          end;
          StartFormat(15);
          OutString(0,"DblAT2UnitVc",ATUnitr.InventoryNr,false);
          OutString(100,0,ATUnitr.Description,false);
          OutDate(1,0,ATUnitr.PurchaseDate,true);
          EndFormat;
        end;
      end;
    end;
  end;
  Gray_Divider(0,1);
  EndJob;
  RETURN;
END;
