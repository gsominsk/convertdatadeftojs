external function Boolean SetInSet2(string,string);
// Commented out from the interface now, we might need to make a new one for Contacts

global
procedure ContactListRn(record RcVc RepSpec)
begin
  record ContactVc Contactr;
  record CUVc CUr;
  Integer xkey;
  Integer rw;
  Boolean TrHs;
  Boolean testf;
  String 255 tstr;
  record MailSettingsBlock MBr;
  LongInt cucnt;

  BlockLoad(MBr);
  cucnt = 0;
  rw = 1;
  StartReportJob(USetStr(7303));
  if (RepSpec.f1<>"") then begin
    tstr = USetStr(7113);
    tstr = tstr & RepSpec.f1;
    Header(rw,tstr,1);
    rw = rw + 1;
  end else begin
    tstr = USetStr(7302);
    Header(rw,tstr,1);
  end;
  EndHeader;
  SetRepCol(2,70);
  SetRepCol(3,200);
  SetRepCol(4,300);
  SetRepCol(5,420);
  StartFormat(15);
   OutString(0,0,USetStr(2447),false); //customer
   OutString(2,0,USetStr(2448),false); //name
   OutString(3,0,USetStr(7023),false); //contact name
   if (RepSpec.ArtMode==2) then begin
     OutString(4,0,USetStr(7026),false); //fax
   end else begin
     OutString(4,0,USetStr(7114),false); //job desc
   end;
   OutString(1,0,USetStr(7025),true); //tel No
  EndFormat
  if (RepSpec.ArtMode==1) then begin
    StartFormat(15);
    OutString(2,0,USetStr(2305),false); //aadress
    OutString(3,0,USetStr(7300),false); //mail
    OutString(4,0,USetStr(10831),false); //job title
    OutString(1,0,USetStr(7026),true); //fax
    EndFormat;
  end;
  Gray_Divider(0,1);
  TrHs = true;
  while (LoopMain(Contactr,1,TrHs)) begin
   testf = true;
   if (TrHs) then begin
     if (nonblank(RepSpec.f1)) then begin
       testf = (SetinSet2(RepSpec.f1,Contactr.Classification));
     end;
     if (Contactr.Closed<>0) then begin testf = false; end;
   end;
   if (TrHs) then begin
     if (nonblank(RepSpec.f2)) then begin
       CUr.Code = Contactr.Company;
       if (ReadFirstMain(CUr,1,true)) then begin
         if (CUr.CustCat<>RepSpec.f2) then begin
           testf = false;
         end;
       end;
     end;
   end;
   if (nonblank(RepSpec.f3)) then begin
     if (Contactr.Name<>RepSpec.f3) then begin
       testf = false;
     end;
   end;
   if (testf) then begin
      cucnt = cucnt + 1;
      StartFormat(15);
       OutString(0,0,Contactr.Company,false);
       OutString(2,0,Contactr.CompName,false);
       OutStringID(3,"DblContVc",Contactr.Name,false,Contactr.Company);
       if (RepSpec.ArtMode==2) then begin
         OutString(4,0,Contactr.Fax,false);
       end else begin
         OutString(4,0,Contactr.JobDesc,false);
       end;
       OutString(1,0,Contactr.Phone,true);
      EndFormat;
      if (RepSpec.ArtMode==1) then begin
       if (nonblank(Contactr.Adress0) or nonblank(Contactr.Email) or nonblank(Contactr.Title) or nonblank(Contactr.Fax)) then begin
          StartFormat(15);
          OutString(2,0,Contactr.Adress0,false);
          if (blank(Contactr.Email)) then begin
            OutString(3,0,USetStr(2289),false);
          end else begin
            if (MBr.ExclEmail==0) then begin
              OutString(3,0,Contactr.Email,false);
            end;
          end;
          OutString(4,0,Contactr.Title,false);
          OutString(1,0,Contactr.Fax,true);
          EndFormat;
        end;
        if (nonblank(Contactr.Adress1) or nonblank(Contactr.Mobile)) then begin
          StartFormat(15);
          OutString(2,0,Contactr.Adress1,false);
          OutString(1,0,Contactr.Mobile,true);
          EndFormat;
        end;
        if (nonblank(Contactr.Adress2)) then begin
          StartFormat(15);
          OutString(2,0,Contactr.Adress2,false);
          EndFormat;
        end;
        if (nonblank(Contactr.Adress3)) then begin
          StartFormat(15);
          OutString(2,0,Contactr.Adress3,false);
          EndFormat;
        end;
        if (nonblank(Contactr.Adress4)) then begin
          StartFormat(15);
          OutString(2,0,Contactr.Adress4,false);
          EndFormat;
        end;
        Black_Divider(0,1);
      end;
    end;
  end;
  StartFormat(15);
  EndFormat;
  Gray_Divider(0,1);
  StartFormat(15);
  OutString(0,0,USetStr(7305),false);
  OutString(100,0,cucnt,false);
  EndFormat;
  EndJob;
return;
end;


