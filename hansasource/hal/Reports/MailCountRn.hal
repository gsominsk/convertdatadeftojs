/* Made By HA 2001-04-04 */
/* Count mail per mailbox  version 3.9 */


global
procedure CountAttFiles(record MailVc Mailr, var LongInt mbcnt , var longint attcnt)
begin
  record RLinkVc RLr;
  record Attach2Vc Attach2r;
  LongInt a;
  longint b,cnt;
  
  a = 0; // MBcnt
  b = 0; // Attcnt
  cnt = 1;   
  while (ReadRecordLink(Mailr,cnt,Attach2r,RLr)) begin
    cnt = cnt + 1;
    a = a + Attach2r.FileSize;
    b = b + 1;    
  end;
  
  mbcnt = mbcnt + a;
  attcnt = attcnt + b;
	
  return;	
end;

global
procedure CountMailLoop(record ConfVc Confr,record RcVc RepSpec,var longint k,var longint l,var LongInt m)
begin
  record MailVc Mailr;
  String 300 ckey;
  String 100 classname;
  LongInt mbcnt;
  longint mailcnt,attcnt;
  Boolean foundf,testf;
  
  mailcnt = 0;
  mbcnt = 0;
  attcnt = 0;
  foundf = true;
  ckey = "UserSer:" & Confr.SerNr;
  while (LoopKey(ckey,Mailr,1,foundf)) begin
    testf = true;
    if (nonblankdate(RepSpec.d1)) then begin
      if (Mailr.TransDate<RepSpec.d1) then begin
        testf = false;
      end;
    end;
    if (nonblankdate(RepSpec.d2)) then begin
      if (Mailr.TransDate>RepSpec.d2) then begin
        testf = false;
      end;
    end;
    if (testf) then begin
      mailcnt = mailcnt + 1;
      CountAttFiles(Mailr,mbcnt,attcnt);
    end;
  end;
  
  k = mailcnt;
  l = attcnt;
  m = mbcnt;
  
  return;
end;

global
procedure ConfCntRn(record RcVc RepSpec) 
begin
  record ConfVc Confr;
  Boolean foundf;
  Boolean testf;
  Boolean classf;
  String 255 tstr;
  Integer rw;
  String 80 ckey;
  Integer class;
  longint tqnt,tattcnt;
  LongInt tmbcnt,m;
  longint k,l;
  String 100 classname;
  array string 20 a_sn;
  array string 100 a_name;
  array string 30 a_classname;
  array longint a_quant;
  array longint a_files;
  array longint a_size;
  longint ar,i,j,templi;
  string 255 tempstr;
  Boolean isswap;
  
  
  rw = 1;
  
  StartReportJob(USetStr(10855));  
  if ( nonblank(RepSpec.f1) ) then begin
    Header(rw,RepSpec.f1,1);
  end else begin
    Header(rw,USetStr(10856),1);  
  end;
  if (RepSpec.flags[2]==1) then begin
    rw = rw + 1;
    Header(rw,USetStr(10857),1);
  end;
  if (RepSpec.flags[3]==1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10858),1);
  end;
  if (RepSpec.flags[4]==1) then begin
    rw = rw + 1;
    Header(rw,USetStr(10859),1);
  end;  
  if (RepSpec.flags[5]==1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10860),1);
  end;       
  if (RepSpec.flags[6]==1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10861),1);
  end;  
  if (RepSpec.flags[7]==1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10862),1);
  end;
  if (RepSpec.flags[8]==1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10863),1);
  end;
  if (rw == 1) then begin
    rw = rw + 1;  
    Header(rw,USetStr(10864),1);
  end;
  EndHeader;
  
  SetRepCol(2,60);
  SetRepCol(3,200);
  SetRepCol(4,300);
  SetRepCol(5,375);
  SetRepCol(6,480);
  	
  StartFormat(15);
  OutString(0,0,USetStr(10865),false);
  OutString(2,0,USetStr(10866),false);
  OutString(3,0,USetStr(10867),false);
  OutString(4,0,USetStr(10868),false);
  OutString(5,0,USetStr(10869),false);
  OutString(6,0,USetStr(10870),true);
  EndFormat;
  Gray_Divider(0,1);
  
  Confr.AddrName = RepSpec.f1;
  ckey = "AddrName";
  
  tqnt = 0;
  tattcnt = 0;
  tmbcnt = 0;

  foundf = true;
  ar=0;
  
  while(LoopKey(ckey,Confr,1,foundf)) begin
    classf = true;
    class = Confr.Class;
    testf = true;
    k = 0;
    l = 0;
    m = 0;

    if(nonblank(RepSpec.f1)) then begin
      if(Confr.AddrName<>RepSpec.f1) then begin
        foundf = false;
      end;	
    end;
    
    if (RepSpec.flags[2]==1 and class == 0 and foundf == true) then begin classf = false; end;
    if (RepSpec.flags[3]==1 and class == 1 and foundf == true) then begin classf = false; end;
    if (RepSpec.flags[4]==1 and class == 2 and foundf == true) then begin classf = false; end;  
    if (RepSpec.flags[5]==1 and class == 3 and foundf == true) then begin classf = false; end;       
    if (RepSpec.flags[6]==1 and class == 4 and foundf == true) then begin classf = false; end;  
    if (RepSpec.flags[7]==1 and class == 5 and foundf == true) then begin classf = false; end;
    if (RepSpec.flags[8]==1 and class == 6 and foundf == true) then begin classf = false; end;   

	if(foundf == true and classf == true and testf == true) then begin
       CountMailLoop(Confr,RepSpec,k,l,m)    
       classname = "";
       if ( Confr.Class==kConfClassConference)  then begin classname = USetStr(10871); end;
       if ( Confr.Class==kConfClassNews)        then begin classname = USetStr(10872); end;
       if ( Confr.Class==kConfClassBillboard)   then begin classname = USetStr(10873); end;
       if ( Confr.Class==kConfClassLibrary)     then begin classname = USetStr(10874); end;
       if ( Confr.Class==kConfClassFolder)      then begin classname = USetStr(10875); end;
       if ( Confr.Class==kConfClassMailbox)     then begin classname = USetStr(10876); end;     
       if ( Confr.Class==kConfClassArchive)     then begin classname = USetStr(10877); end;  
       a_sn[ar]=Confr.SerNr;
       a_name[ar]=Confr.AddrName;
       a_classname[ar]= classname;
       a_quant[ar]=k;
       a_files[ar]=l;
       a_size[ar]=m;
       ar=ar+1;
       tqnt = tqnt + k;
       tattcnt = tattcnt + l;
       tmbcnt = tmbcnt + m;    
    end;
  end;
  if (RepSpec.flags[1]<>0) then begin
      for (i=0;i<ar;i=i+1) begin
          for (j=i+1; j<ar; j=j+1) begin
              isswap=false;
              if (RepSpec.flags[1]==1) and (a_quant[i]<a_quant[j]) then begin
                    isswap=true;          
              end;
               if (RepSpec.flags[1]==2) and (a_files[i]<a_files[j]) then begin
                    isswap=true;          
              end;
               if (RepSpec.flags[1]==3) and (a_size[i]<a_size[j]) then begin
                    isswap=true;          
              end;
              if (isswap) then begin
                  tempstr=a_sn[i];a_sn[i]=a_sn[j];a_sn[j]=tempstr;
                  tempstr=a_name[i];a_name[i]=a_name[j];a_name[j]=tempstr;
                  tempstr=a_classname[i];a_classname[i]=a_classname[j];a_classname[j]=tempstr;
                  templi=a_quant[i];a_quant[i]=a_quant[j];a_quant[j]=templi;
                  templi=a_files[i];a_files[i]=a_files[j];a_files[j]=templi;
                  templi=a_size[i];a_size[i]=a_size[j];a_size[j]=templi;
              end;          
          end;
      end;
  end;
  
  for (i=0;i<ar;i=i+1) begin 

       StartFormat(15);
	   OutString(0,0,a_sn[i],false);
	   OutString(2,0,a_name[i],false);
	   OutString(3,0,a_classname[i],false);
	   OutString(4,0,a_quant[i],false);
	   OutString(5,0,a_files[i],false);
	   OutString(6,0,a_size[i],true);
	   EndFormat;
  end;
  
  Gray_Divider(0,1);
  StartFormat(15);
  OutString(0,0,"",false);
  OutString(2,0,"",false);
  OutString(3,0,USetStr(10878) & ": ",false);
  OutString(4,0,tqnt,false);
  OutString(5,0,tattcnt,false);
  OutString(6,0,tmbcnt,true);
  EndFormat;

  EndJob;
  return;
end;

