external procedure AutomatedSalesOrderly(string);
external function Boolean DatePrel(Date);
external procedure LstRegDat(var string);
external procedure HT2Per(Date,Date,var string);

procedure ToStrTRCode(var string rstr,Integer ycp,LongInt nrp)
begin
  string 255 t2;
  
  t2 = nrp;
  rstr = StrTRCode(ycp);
  if (nonblank(rstr)) then begin
    rstr = rstr & ".";
  end;  
  rstr = rstr & t2;
  return;
end;

procedure FormCorr(Integer tc,record TRVc TRp,Date dt,LongInt vn,Integer yc)
begin
  string 255 tstr;

  ToStrTRCode(tstr,TRp.IntYc,TRp.Number);
  StartFormat(15);
  OutStringID(0,"DblTrans",tstr,false,CurrentCompany);
  OutDate(90,0,TRp.TransDate,false);
  if (tc==1) then begin
        OutString(170,0,USetStr(3102),false);
        OutDate(400,0,dt,false);
  end;
  if (tc==2) then begin
        OutString(170,0,USetStr(3103),false);
        OutDate(400,0,dt,false);
  end;
  if (tc==3) then begin
    ToStrTRCode(tstr,yc,vn);
    OutString(170,0,USetStr(3104),false);
    OutStringID(340,"DblTrans",tstr,true,CurrentCompany);
    OutDate(400,0,dt,false);
  end;
  EndFormat;
  return;
end;

procedure PrintCorrectedTransactions(record RcVc RepSpec)
BEGIN
  row TRVc TRrw;
  Integer rwcnt;
  Integer i;
  Boolean dt;
  record TRVc TRr;
  Boolean TrHs,testf,printedf;
  string 255 tstr,t2;
  string 20 fracc,toacc;

  dt = false;
  if (nonblankdate(RepSpec.RegDate)) then begin dt = true; end;
  TRr.TransDate = RepSpec.sStartDate;

  fracc = FirstInRange(RepSpec.f1,10);
  toacc = LastInRange(RepSpec.f1,20);

  StartFormat(20);
  OutString(0,0,USetStr(3105),false);
  OutString(90,0,USetStr(3106),false);
  OutString(170,0,USetStr(3107),false);
  OutString(400,0,USetStr(3108),false);
  EndFormat;
  TrHs = true;
  while (LoopKey("TransDate",TRr,1,TrHs)) begin
    if (TrHs) then begin
      if (TRr.TransDate>RepSpec.sEndDate) then begin TrHs = false; end;
    end;  
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f1)) then begin
        testf = false;
        rwcnt = MatRowCnt(TRr);
        for (i = 0 ; i<rwcnt ;i=i+1) begin
          MatRowGet(TRr,i,TRrw);
          if (TRrw.stp==1) then begin
            if (nonblank(RepSpec.f1)) then begin
              if (TRrw.AccNumber>=fracc) and (TRrw.AccNumber<=toacc) then begin 
                testf = true; 
                i = rwcnt;              
              end;
            end;
          end;
        end;
      end;
      if (testf) then begin
        printedf = false;
        rwcnt = MatRowCnt(TRr);
        for (i = 0 ; i<rwcnt ;i=i+1) begin
          MatRowGet(TRr,i,TRrw);
          if (TRrw.stp==2) then begin
            if ((dt==false) or (TRrw.UpdDate>=RepSpec.RegDate)) then begin
              FormCorr(2,TRr,TRrw.UpdDate,0,0);
              printedf = true;
            end;  
          end;
          if (TRrw.stp==3) then begin
            if ((dt==false) or (TRrw.CorrDate>=RepSpec.RegDate)) then begin
              FormCorr(3,TRr,TRrw.CorrDate,TRrw.CorrNr,TRrw.CorrYc);
              printedf = true;
            end;  
          end;
        end;
        if (printedf==false) then begin
          if (RepSpec.flags[4]==1) then begin
            if ((dt==false) or (TRr.RegDate>=RepSpec.RegDate)) then begin
              FormCorr(1,TRr,TRr.RegDate,0,0);
            end;  
          end;
        end;
      end;
    end;
  end;
  RETURN;
END;

global
procedure CorrRn(record RcVc RepSpec)
begin
  string 255 tstr,t2;
  Integer rw;

  rw = 1;
  StartReportJob(USetStr(3101));
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  LstRegDat(tstr);
  rw = rw +1;
  Header(rw,tstr,0);
  tstr = USetStr(3022);
  t2 = PackStrDate(RepSpec.RegDate);
  tstr = tstr & t2;
  Header(rw,tstr,1);
  rw = rw +1;
  if (DatePrel(RepSpec.sEndDate)) then begin
    tstr = USetStr(3023);
    Header(rw,tstr,1);
  end;
  if (RepSpec.flags[4] == 0) then begin
    tstr = UsetStr(3109);     
    Header(rw,tstr,0);
  end;
  EndHeader;

  PrintCorrectedTransactions(RepSpec);
  EndJob;
  AutomatedSalesOrderly("HasIntegratedNL+Run_CorrRn");
  return;
end;

global
procedure AllCompaniesCorrRn(record RcVc RepSpec)
begin
  record CompaniesBlock CompaniesRec;
  row CompaniesBlock comprw;
  Integer i,rwcnt;
  Integer compnr,curcomp;
  string 255 tstr,t2;
  Integer rw;

  rw = 1;
  StartReportJob(USetStr(3100));
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  LstRegDat(tstr);
  rw = rw +1;
  Header(rw,tstr,0);
  tstr = USetStr(3022);
  t2 = PackStrDate(RepSpec.RegDate);
  tstr = tstr & t2;
  Header(rw,tstr,1);
  rw = rw +1;
  if (DatePrel(RepSpec.sEndDate)) then begin
    tstr = USetStr(3023);
    Header(rw,tstr,1);
  end;
  if (RepSpec.flags[4] == 0) then begin
    tstr = UsetStr(3109);     
    Header(rw,tstr,0);
  end;
  EndHeader;

  curcomp = CurrentCompany;  
  BlockLoad(CompaniesRec);
  rwcnt = MatRowCnt(CompaniesRec);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(CompaniesRec,i,comprw);
    if (SetCompanyCode(comprw.CompCode,false)) then begin
      StartFormat(15);
       OutString(0,0,comprw.CompName,false);
      EndFormat;      
      PrintCorrectedTransactions(RepSpec);
      Black_Divider(0,1);
    end;
    ResetCompany(curcomp);
  end;
  
  EndJob;
  AutomatedSalesOrderly("HasIntegratedNL+Run_AllCompaniesCorrRn");
  return;
end;  
