external function Boolean PasteRentResItemAndSerial(record RentResVc,var string,string,Boolean);
external procedure HTArtGroup(string, var string);
external function Boolean HasItemGroups();
external function Boolean RentalItemInItemGroupOrClass(string,string,string);

procedure PrintRentHeader(record RcVc RepSpec)
begin
  StartFormat(15);
  OutString(0,0,USetStr(12661),false);
  OutString(2,0,USetStr(12667),false);
  OutString(3,0,"",false);
  OutString(4,0,USetStr(12662),false);
//  OutString(5,0,USetStr(12664),false);
  OutString(1,0,USetStr(12665),true);
  EndFormat;
  Gray_Divider(0,1);
  return;
end;

procedure PrintRentRow(string rentcode,string serialnr,string invitem,string agreetype,val invsum)
begin
  record RentINVc RentINr;

  RentINr.Code = rentcode;
  RentINr.SerialNr = serialnr;
  if (ReadFirstMain(RentINr,2,true)) then begin
  end;
  StartFormat(15);
  OutString(0,0,rentcode,false);
  OutString(2,0,serialnr,false);
  OutString(3,0,RentINr.Name,false);
  OutString(4,0,invitem,false);
//  OutString(5,0,agreetype,false);
  OutVal(1,0,invsum,M4Val,true);
  EndFormat;
  return;
end;

global
procedure RentResAccesPL2Rn(record RcVc RepSpec)
BEGIN
  record RentResVc RentResr;
  record RentResVc RentRes2r;
  row RentResVc RentResrw;
  Integer rwcnt,i;
  Integer rw;
  string 255 tstr;
  val tot;

  if (RepSpec.long1>0) then begin
    RentResr.SerNr = RepSpec.long1;
    if (ReadFirstMain(RentResr,1,true)) then begin
      StartReportJob(USetStr(12660));
      rw = 1;
      tstr = USetStr(12663) & " " & RentResr.AgreeType;
      Header(rw,tstr,1);
      rw = rw + 1;
      EndHeader;
      SetRepCol(2,80);
      SetRepCol(3,150);
      SetRepCol(4,340);
      SetRepCol(5,370);
      PrintRentHeader(RepSpec);
      PrintRentRow(RentResr.Code,RentResr.SerialNr,RentResr.InvItem,RentResr.AgreeType,RentResr.Sum);
      Gray_Divider(5,1);
      StartFormat(15);
      OutString(5,0,USetStr(12666),false);
      OutVal(1,0,RentResr.Sum,M4Val,true);
      EndFormat;
      rwcnt = MatRowCnt(RentResr);
      if (rwcnt>0) then begin
        StartFormat(15);
        EndFormat;
        StartFormat(15);
        OutString(0,0,USetStr(12668),false);
        EndFormat;
        PrintRentHeader(RepSpec);
        tot = 0;
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(RentResr,i,RentResrw);
          RecordCopy(RentRes2r,RentResr);
          RentRes2r.Code = RentResrw.ArtCode;
          RentRes2r.SerialNr = RentResrw.SerialNr;
          RentRes2r.RentQuant = RentResrw.Quant;
          if (PasteRentResItemAndSerial(RentRes2r,tstr,"",false)) then begin end;
          PrintRentRow(RentRes2r.Code,RentRes2r.SerialNr,RentRes2r.InvItem,RentRes2r.AgreeType,RentRes2r.Sum * RentRes2r.RentQuant);
          tot = tot + (RentRes2r.Sum * RentRes2r.RentQuant);
        end;
        Gray_Divider(5,1);
        StartFormat(15);
        OutString(5,0,USetStr(12666),false);
        OutVal(1,0,tot,M4Val,true);
        EndFormat;
      end;
      EndJob;
    end;
  end;
  RETURN;
END;

global
procedure RentResAccesPLRn(record RcVc RepSpec)
BEGIN
  record RentINVc RentINr;
  record RentResVc RentResr;
  Boolean foundf,testf;
  Integer rw;
  string 255 tstr;
  string 40 frin,toin;

  StartReportJob(USetStr(12660));
  rw = 1;
  tstr = USetStr(12663) & " " & RepSpec.f3;
  Header(rw,tstr,1);
  rw = rw + 1;
  if (HasItemGroups) then begin
    HTArtGroup(RepSpec.f5,tstr);
    if (nonblank(tstr)) then begin
      Header(rw,tstr,1);
      rw = rw + 1;
    end;
  end;  
  EndHeader;
  frin = FirstInRange(RepSpec.f1,20);
  toin = LastInRange(RepSpec.f1,20);
  SetRepCol(2,80);
  SetRepCol(3,150);
  SetRepCol(4,340);
  SetRepCol(5,370);
  PrintRentHeader(RepSpec);
  foundf = true;
  RentINr.Code = frin;
  while (LoopMain(RentINr,1,foundf)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (RentINr.Code>toin) then begin
        foundf = false;
      end;
    end;
    testf = foundf;
    if (testf) then begin
      testf = RentalItemInItemGroupOrClass(RentINr.Code,RepSpec.f5,RepSpec.f6);
    end;    
    if (testf) then begin
      RecordNew(RentResr);
      RentResr.Code = RentINr.Code;
      RentResr.SerialNr = RentINr.SerialNr;
      RentResr.AgreeType = RepSpec.f3;
      RentResr.RentQuant = 1;
      if (PasteRentResItemAndSerial(RentResr,tstr,RepSpec.f2,false)) then begin end;
      PrintRentRow(RentResr.Code,RentResr.SerialNr,RentResr.InvItem,RentResr.AgreeType,RentResr.Sum);
    end;
  end;
  EndJob;
  RETURN;
END;
