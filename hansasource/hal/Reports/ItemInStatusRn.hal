external procedure HTCustCat(string,var string);
external procedure HTCustClass(string,var string);
external function Boolean ContactInGroupClass(string,string,string,string,string,string);

global
procedure ItemInStatusRn(record RcVc RepSpec)
begin
  record OffHireVc OffHirer;
  boolean TrHs,testf;
  string 255 tstr;
  val rw, qty;
  
  StartReportJob(USetStr(13060));
  rw = 1;
  if (nonblank(RepSpec.f1)) then begin
    tstr = USetStr(13061) & RepSpec.f1;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  if (nonblank(RepSpec.f2)) then begin
    tstr = USetStr(13062) & RepSpec.f2;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTCustCat(RepSpec.f3,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTCustClass(RepSpec.f4,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  EndHeader;

  StartFormat(15);
  OutString(0,0,USetStr(13063),false);
  OutString(60,0,USetStr(13064),false);
  OutString(150,0,USetStr(13065),false);
  OutString(250,0,USetStr(13066),false);
  if (blank(RepSpec.f1)) then begin
    OutString(440,0,USetStr(13068),false);
  end;
  EndFormat;
  Gray_Divider(0,1);  
  TrHs = true;
  OffHirer.OKFlag = 0;
  while LoopKey("OKFlag",OffHirer,1,TrHs) begin
    if (TrHs) then begin
      if (OffHirer.OKFlag > 0) then begin
        TrHs = false;  
      end;
    end;
    if (TrHs) then begin
      testf = true;     
      if (nonblank(RepSpec.f1)) then begin
        if (RepSpec.f1 <> Offhirer.Location) then begin
           testf = false;      
        end;    
      end;
      if (nonblank(RepSpec.f2)) then begin
        if (Offhirer.CustCode <> RepSpec.f2) then begin
          testf = false;
        end;
      end;
      if (testf) then begin 
        testf = ContactInGroupClass(Offhirer.CustCode,RepSpec.f3,RepSpec.f4,"CUST","","");
      end; 
      if (testf) then begin
        StartFormat(15);
        OutStringID(0,"DblOffHireVc",Offhirer.SerNr,false,Offhirer.SerNr);
        OutString(60,0,OffHirer.TransDate,false);
        OutString(150,"DblAgreementVc",OffHirer.AgreementNr,false);  
        OutString(250,0,OffHirer.CustCode,false);
        OutString(300,0,OffHirer.Addr0,false);
        if (blank(RepSpec.f1)) then begin
          OutString(440,0,OffHirer.Location,false);
        end;
        EndFormat;
        qty = qty + 1;
      end;    
  
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
  OutString(0,0,USetStr(13067),false);
  OutVal(80,0,qty,M40Val,false);
  EndJob;
return;
end;