
function Boolean PrintUserLog(record RcVc RepSpec,record UserLogVc UserLogr,record CompaniesBlock CompaniesRec,Integer rwcnt,var string olduser)
begin
  record UserVc Userr;
  row CompaniesBlock comprw;
  Boolean testf;
  string 255 tstr;
  Boolean foundf;

  foundf = true;
  if (nonblank(RepSpec.f1)) then begin
    if (UserLogr.User!=RepSpec.f1) then begin
      foundf = false;
    end;
  end;
  if (DateInRange(UserLogr.evDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
    foundf = false;
  end;
  testf = foundf;
  if (testf) then begin
    if (UserLogr.Company-1<rwcnt) then begin
      MatRowGet(CompaniesRec,UserLogr.Company-1,comprw);
    end else begin
      comprw.ShortName = "";
    end;
  end;
  if (testf) then begin
    StartFormat(15);
    OutString(0,0,UserLogr.User,false);
    OutString(2,0,comprw.ShortName,false);
    OutString(3,0,UserLogr.evDate,false);
    OutString(4,0,Left(UserLogr.evTime,5),false);
    OutString(5,0,UserLogr.What,false);
    if (UserLogr.Allowedf) then begin
      OutString(6,0,USetStr(30280),true);
    end else begin
      OutString(6,0,USetStr(30279),true);
    end;
    OutString(7,0,UserLogr.Oldinfo,false);
    OutString(8,0,UserLogr.Newinfo,false);
    EndFormat;
    olduser = UserLogr.User;
  end;
  PrintUserLog = foundf;
  return;
end;

global
procedure UserLogRn(record RcVc RepSpec)
begin
  record UserLogVc UserLogr;
  record CompaniesBlock CompaniesRec;
  Boolean foundf;
  Integer rwcnt;
  string 20 olduser;

  BlockLoad(CompaniesRec);
  rwcnt = MatRowCnt(CompaniesRec);
  olduser = "xxxxxxxxxxxxxxxxxxxx";
  StartReportJob(USetStr(30270));
  EndHeader;
  SetRepCol(2,30);
  SetRepCol(3,80);
  SetRepCol(4,140);
  SetRepCol(5,170);
  SetRepCol(6,375);
  SetRepCol(7,380);
  SetRepCol(8,440);
  StartFormat(15);
  OutString(0,0,USetStr(30271),false);
  OutString(2,0,USetStr(30272),false);
  OutString(3,0,USetStr(30273),false);
  OutString(4,0,USetStr(30274),false);
  OutString(5,0,USetStr(30275),false);
  OutString(6,0,USetStr(30276),true);
  OutString(7,0,USetStr(30277),false);
  OutString(8,0,USetStr(30278),false);
  EndFormat;
  Gray_Divider(0,1);
  foundf = true;
  UserLogr.User = RepSpec.f1;
  UserLogr.evDate = RepSpec.sEndDate;
  if (nonblank(UserLogr.User)) then begin
    while (LoopBackKey("User",UserLogr,2,foundf)) begin
      foundf = PrintUserLog(RepSpec,UserLogr,CompaniesRec,rwcnt,olduser);
    end;
  end else begin
    while (LoopKey("evDate",UserLogr,1,foundf)) begin
      foundf = PrintUserLog(RepSpec,UserLogr,CompaniesRec,rwcnt,olduser);
    end;
  end;
  EndJob;
  return;
end;
