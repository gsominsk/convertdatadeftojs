external procedure HTCustCat(string,var string);
external procedure HTCustClass(string,var string);
external function Boolean ContactInGroupClass(string,string,string,string,string,string);
external procedure HTLocations(string, string, var string);
external procedure HTCusts(string, string, var string);


procedure PrintHeader(string AgreementNr)
begin
  record AgreementVc Agreementr;
  Agreementr.SerNr = AgreementNr;
  if (ReadFirstMain(Agreementr,1,true)) then begin
    Gray_Divider(0,1);
    StartFormat(15);
    OutString(0,0,USetStr(12806),false);
    OutString(80,0,Agreementr.CustCode,false);
    EndFormat;
    StartFormat(15);
    OutString(0,0,USetStr(12807),false);
    OutString(80,0,Agreementr.Addr0,false);
    EndFormat;
    StartFormat(15);
    OutString(0,0,USetStr(12808),false);
    OutString(80,0,Agreementr.Addr1,false);
    EndFormat;
    Gray_Divider(0,1);
  end;
 return;
end;

// Might need rewriting now when SiteVc is replaced by DelAddrVc with double segments to the Main key
global 
procedure ItemAtSiteRn (record RcVc RepSpec)
begin
  record DelAddrVc DelAddrr;
  record RentResVc Rentr;
  record AgreementVc Agreementr;
  boolean TrHs, testf,firstf,firstf2,sitefoundf;
  string 20 frsite,tosite;
  string 20 oldsite;  
  Integer rw;
  string 255 tstr;

  firstf2 = true;
  TrHs = true;

  frsite = FirstInRange(RepSpec.f4,20);
  tosite = LastInRange(RepSpec.f4,20);

  StartReportJob(USetStr(12800));
  rw = 1;
  HTCusts(RepSpec.f1,RepSpec.f1,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTCustCat(RepSpec.f3,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTCustClass(RepSpec.f6,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTLocations(RepSpec.f5,RepSpec.f5,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  
  if nonblank(RepSpec.f4) then begin
    tstr = USetStr(11408) & " " & frsite;
    if (frsite<>tosite) then begin
      tstr = tstr & ":" & tosite;
    end;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;  
  EndHeader;
  
  StartFormat(15);
  OutString(0,0,USetStr(12801),false);
  OutString(90,0,USetStr(12802),false);
  OutString(250,0,USetStr(12803),false);
  OutString(320,0,USetStr(12804),false);
  OutString(400,0,USetStr(12805),false);
  EndFormat;
  TrHs = true;

  Rentr.Site = frsite;
  while (LoopKey("Site",Rentr,1,TrHs)) begin
    firstf = true;
    if (TrHs) then begin
      if (nonblank(RepSpec.f4)) then begin
        if (Rentr.Site > tosite) then begin
          TrHs = false;
        end;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f1) or nonblank(RepSpec.f3) or nonblank(RepSpec.f6)) then begin
        Agreementr.SerNr = Rentr.AgreementNr;
        if (ReadFirstMain(Agreementr,1,true)) then begin
          if nonblank(RepSpec.f1) then begin 
            if (Agreementr.CustCode <> RepSpec.f1) then begin
              testf = false;
            end;
          end;
          if (testf) then begin 
            testf = ContactInGroupClass(Agreementr.CustCode,RepSpec.f3,RepSpec.f6,"CUST","","");
          end; 
        end;
      end; 
      if (nonblank(RepSpec.f5)) then begin 
        if (Rentr.Location <> RepSpec.f5) then begin
          testf = false;
        end;
      end;
      if (Rentr.RentResRet > 0) then begin
        testf = false;
      end;

      if (testf) then begin
        if (oldsite!=Rentr.Site) then begin
          sitefoundf = false;
          DelAddrr.DelCode  = Rentr.Site;
          DelAddrr.CustCode = Agreementr.CustCode;
          if (ReadFirstMain(DelAddrr,2,true)) then begin
            sitefoundf = true;
          end else begin
            DelAddrr.DelCode  = Rentr.Site;
            DelAddrr.CustCode = "";
            if (ReadFirstMain(DelAddrr,2,true)) then begin
              sitefoundf = true;
            end;
          end;
          if (sitefoundf) then begin
            Gray_Divider(0,1);
            if (firstf2==false) then begin
              StartFormat(15);
              EndFormat;
            end;
            StartFormat(15);
            OutString(0,0,USetStr(11408),false);
            OutString(80,0,DelAddrr.DelCode,false);
            OutString(150,0,DelAddrr.Comment,false);
            EndFormat;
            if (nonblank(DelAddrr.DelAddr0)) then begin
              StartFormat(15);
              OutString(80,0,DelAddrr.DelAddr0,false);
              EndFormat;
            end;
            if (nonblank(DelAddrr.DelAddr1)) then begin
              StartFormat(15);
              OutString(80,0,DelAddrr.DelAddr1,false);
              EndFormat;
            end;
            if (nonblank(DelAddrr.DelAddr2)) then begin
              StartFormat(15);
              OutString(80,0,DelAddrr.DelAddr2,false);
              EndFormat;
            end;
          end;
          oldsite = Rentr.Site;
        end;
        if (firstf) then begin
          PrintHeader(Rentr.AgreementNr);
          firstf = false;
        end;
        StartFormat(15);
        OutString(0,0,Rentr.Code,false);
        OutString(90,0,left(Rentr.Name,25),false);
        OutString(250,0,Rentr.SerialNr,false);
        OutStringID(320,"DblRentResVc",Rentr.SerNr,false,Rentr.SerNr);
        OutString(400,0,Rentr.CustOrdNr,false);
        EndFormat;
        firstf2 = false;
      end;
    end;
  end;
  EndJob;
  return;
end;