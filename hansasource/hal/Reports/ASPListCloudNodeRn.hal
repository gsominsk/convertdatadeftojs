global
procedure ASPListCloudNodeRn(record RcVc RepSpec)
begin
  record CloudNodeVc CNr;
  Boolean found,testf;
  Integer rw;
  string 255 tstr;

  StartReportJob("List Cloud Nodes");
  rw = 1;
  if (nonblank(RepSpec.f1)) then begin
    tstr = "Cloud Node: " & RepSpec.f1;
    Header(rw,tstr,1);
  end;
  if (nonblank(RepSpec.long1)) then begin
    Header(rw,"Free Disk Space (MB): " & RepSpec.long1,1); rw = rw + 1;
  end;
  if (nonblank(RepSpec.long2)) then begin
    Header(rw,"Free Disk Space %: " & RepSpec.long2,1); rw = rw + 1;
  end;
  EndHeader;

  SetRepCol(2,80);
  SetRepCol(3,130);
  SetRepCol(4,320);
  SetRepCol(5,370);
  SetRepCol(6,420);

  StartFormat(15);
  OutString(0,0,"Server Name",false);
  OutString(2,0,"Instance Type",false);
  OutString(3,0,"Host Name",false);
  OutString(4,0,"Disk Total",false);
  OutString(5,0,"Disk Free",false);
  OutString(6,0,"Disk Max Used",false);
  EndFormat;
  Gray_Divider(0,1);
  
  found = true;
  CNr.Code = RepSpec.f1;
  while (LoopMain(CNr,1,found)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (CNr.Code!=RepSpec.f1) then begin found = false; end;
    end;
    if (found) then begin
      testf = true;
      if (CNr.Closed!=0) then begin testf = false; end;
      if (nonblank(RepSpec.long1)) then begin
        if (CNr.ServerDiskFree<RepSpec.long1) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.long2)) then begin
        if ((CNr.ServerDiskFree/CNr.ServerDiskTotal)*100<RepSpec.long2) then begin testf = false; end;
      end;
      if (testf) then begin
        StartFormat(15);
         OutString(0,0,CNr.Code,false);
         OutString(2,0,CNr.AmazonInstanceType,false);
         OutString(3,0,CNr.HostName,false);
         OutString(4,0,CNr.ServerDiskTotal,false);
         OutString(5,0,CNr.ServerDiskFree,false);
         OutString(6,0,CNr.ServerDiskMaxUsed,false);
        EndFormat;
      end;
    end;
  end;
  EndJob;
  return;
end;