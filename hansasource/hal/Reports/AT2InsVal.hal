external function Boolean GetInventoryNrWrofQuant(string,var val);
external function Boolean AT2ExcludeCategory(string);

global
procedure AT2InsValRn(record RcVc RepSpec)
begin
  record AT2UnitVc ATUnitr;
  Boolean TrHs;
  Boolean testf;
  String 30 frinv,toinv;
  val totval,wq,t;
  
  frinv = FirstInRange(RepSpec.f1,20);
  toinv = LastInRange(RepSpec.f1,20);

  StartReportJob(USetStr(5421));
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(5422),false);
  OutString(100,0,USetStr(5423),false);
  OutString(480,0,USetStr(5424),true);
  EndFormat;
  ATUnitr.InventoryNr = frinv;
  Gray_Divider(0,1);
  TrHs = true;
  while (LoopMain(ATUnitr,1,TrHs)) begin
      if (nonblank(toinv)) then begin
        if (ATUnitr.InventoryNr>toinv) then begin
          TrHs = false;
        end;
      end;    
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f3)) then begin
        if (ATUnitr.AT2Code<>RepSpec.f3) then begin testf = false; end;
      end;  
      if (nonblank(RepSpec.AccStr)) then begin
        if (ATUnitr.DepCode<>RepSpec.AccStr) then begin testf = false;  end;
      end;  
      if (nonblank(RepSpec.TransStr)) then begin
        if (ATUnitr.RespPerson<>RepSpec.TransStr) then begin testf = false;  end;
      end;  
      if (ATUnitr.Activef==1) then begin testf = false; end;
      if (testf) then begin
        testf = !AT2ExcludeCategory(ATUnitr.AT2Code);
      end;
      if (testf) then begin
        if (GetInventoryNrWrofQuant(ATUnitr.InventoryNr,wq)) then begin
          if (wq>=ATUnitr.Quantity) then begin testf = false; end;
        end;
      end;
      if (testf) then begin
        t = (ATUnitr.Quantity-wq)*ATUnitr.InsuranceVal;
        StartFormat(15);
        OutString(15,"DblAT2UnitVc",ATUnitr.InventoryNr,false);
        OutString(100,0,ATUnitr.Description,false);
        OutVal(480,0,t,M4Val,true);
        EndFormat;
        totval = totval + t;
      end;
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(10);
  EndFormat;
  StartFormat(15);
  OutString(200,0,USetStr(5425),false);
  OutVal(480,0,totval,M4Val,true);
  EndFormat;
  Gray_Divider(0,1);
  EndJob;
  return;
end;
