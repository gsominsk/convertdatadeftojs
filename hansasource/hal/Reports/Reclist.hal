external procedure HTArtGroup(string, var string);
external procedure HTArts(string, string, var string);

global
procedure RecRn(var record RcVc RepSpec)
begin
  record RecVc recr;
  Row RecVc Recrw;
  Boolean TrHs;
  String 20 fromart,toart;
  String 20 fromart2,toart2;
  String 20 frit,toit;
  String 255 tstr;
  val isum,tisum,t;
  Boolean testf;
  Integer i,rwcnt;
  Integer rw;
  Integer thecomprow;

  thecomprow = -1;
  testf = true;
  fromart = FirstInRange(RepSpec.f1,20);
  toart   = LastInRange(RepSpec.f1,20);
  fromart2 = FirstInRange(RepSpec.f2,20);
  toart2   = LastInRange(RepSpec.f2,20);
  frit = FirstInRange(RepSpec.f3,20);
  toit = LastInRange(RepSpec.f3,20);
  if ((blank(RepSpec.f2)) and (blank(RepSpec.f3))) then begin
    if (RepSpec.flags[1]==1) then begin
      RepSpec.flags[1] = 0;
    end;
  end;
  StartReportJob(USetStr(5380));
  rw = 1;
  if (blank(fromart) and blank(toart)) then begin
    tstr = USetStr(5385);
  end else begin
    tstr = USetStr(5106);
    tstr = tstr & " ";
    tstr = tstr & fromart;
    if (fromart<>toart) then begin
      tstr = tstr & " : ";
      tstr = tstr & toart;
    end;
  end;
  Header(rw,tstr,1);
  rw = rw+1;
  HTArts(fromart2, toart2,tstr);
  Header(rw,tstr,1);
  rw = rw+1;
  HTArtGroup(RepSpec.f3, tstr);
  Header(rw,tstr,1);
  EndHeader;
  if (RepSpec.flags[1]==0) then begin
    StartFormat(15);
    OutString(0,0,USetStr(5381),false);
    OutString(90,0,USetStr(5104),false);
    OutString(320,0,USetStr(5382),true);
    OutString(410,0,USetStr(5383),true);
    OutString(460,0,USetStr(5384),true);
    EndFormat;
  end else begin
    StartFormat(15);
    OutString(0,0,USetStr(5381),false);
    OutString(60,0,USetStr(5391),false);
    OutString(120,0,USetStr(5104),false);
    OutString(280,0,USetStr(5388),true);
    OutString(290,0,USetStr(5389),false);
    OutString(350,0,USetStr(5104),false);
    OutString(1,0,USetStr(5388),true);
    EndFormat;
    Gray_Divider(0,1);
  end;
  recr.Code = fromart;
  TrHs = true;
  while (LoopMain(recr,1,TrHs)) begin
    TrHs = true;
    isum = 0;
    if (nonblank(fromart)) then begin
      if (recr.Code > toart) then begin
        TrHs = false;
      end;
    end;
    if ((TrHs) and (nonblank(fromart2))) then begin
      testf = false;
      rwcnt = MatRowCnt(recr);
      for (i = 0 ; i<rwcnt ;i=i+1) begin
        MatRowGet(recr,i,Recrw);
        if (Recrw.Item >= fromart2) then begin
          if (Recrw.Item<=toart2) then begin
            thecomprow = i;
            testf = true;
            i = rwcnt;
          end;
        end;
      end;
    end;
    if ((TrHs) and (nonblank(RepSpec.f3))) then begin
      record INVc INr;

      testf = false;
      rwcnt = MatRowCnt(recr);
      for (i = 0 ; i<rwcnt ;i=i+1) begin
        MatRowGet(recr,i,Recrw);
        INr.Code = Recrw.Item;
        if (ReadFirstMain(INr,1,true)) then begin
          if (INr.Group>=frit) then begin
            if (INr.Group<=toit) then begin
              thecomprow = i;
              testf = true;
              i = rwcnt;
            end;
          end;
        end;
      end;
    end;
    if ((TrHs) and (testf)) then begin
      if (RepSpec.flags[1]==0) then begin
        Gray_Divider(0,1);
        StartFormat(15);
        OutString(0,0,recr.Code,false);
        OutString(90,0,recr.Comment,false);
        OutString(320,0,recr.MinProdQty,true);
        OutString(410,0,recr.NormProdQty,true);
        EndFormat;
        StartFormat(15);
        EndFormat;
        StartFormat(15);
        OutString(0,0,USetStr(5107),false);
        OutString(280,0,USetStr(5108),true);
        OutString(330,0,USetStr(5109),true);
        OutString(380,0,USetStr(5111),true);
        OutString(420,0,USetStr(5112),true);
        OutString(480,0,USetStr(5146),true);
        EndFormat;
        isum = 0;
        tisum = 0;
        rwcnt = MatRowCnt(recr);
        for (i = 0 ; i<=rwcnt-1 ;i=i+1) begin
          MatRowGet(recr,i,Recrw);
          StartFormat(15);
          OutString(0,0,Recrw.Item,false);
          OutString(90,0,Recrw.Comment,false);
          OutVal(280,0,Recrw.InQty,M4UVal,true);
          OutVal(330,0,Recrw.OutQty,M4UVal,true);
          OutString(380,0,Recrw.ItemCost,true);
          OutString(420,0,Recrw.ExtraCost,true);
          if (Recrw.InQty>0) then begin
            t = Recrw.InQty*Recrw.ItemCost;        
          end else begin
            t = Recrw.OutQty*Recrw.ItemCost;        
          end;
          t = t + Recrw.ExtraCost;
          OutString(480,0,t,true);
          EndFormat;
          if (Recrw.InQty>0) then begin
            isum = isum + Recrw.ItemCost;
          end else begin
            tisum = tisum + t;
          end;
        end;
        Black_Divider(300,1);
        StartFormat(15);
        OutVal(380,0,isum,M4Val,true);
        OutVal(480,0,tisum,M4Val,true);
        EndFormat;
      end else begin
        StartFormat(15);
        OutString(0,0,recr.Code,false);
        rwcnt = MatRowCnt(recr);
// Means that a recepy only shows first out item in this report... should be ok
        for (i = 0 ; i<rwcnt ;i=i+1) begin
          MatRowGet(recr,i,Recrw);
          if (Recrw.OutQty>0) then begin
            OutString(60,0,Recrw.Item,false);
            OutString(120,0,Recrw.Comment,false);
            OutVal(280,0,Recrw.OutQty,M4UVal,true);
            i = rwcnt;
          end;
        end;
        if (thecomprow>-1) then begin
          MatRowGet(recr,thecomprow,Recrw);
          OutString(290,0,Recrw.Item,false);
          OutString(350,0,Recrw.Comment,false);
          OutVal(480,0,Recrw.InQty,M4UVal,true);
          i = rwcnt;
        end;
        EndFormat;
      end;
    end;
  end;
  Gray_Divider(0,1);
  EndJob;
  return;
end;
