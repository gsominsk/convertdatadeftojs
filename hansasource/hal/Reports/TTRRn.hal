external function Integer GetIntYc(Date);
external procedure ToStrTRCode(var string,Integer,LongInt);

procedure PrintTTRs(record RcVc RepSpec,LongInt frnr,LongInt tonr)
BEGIN
  record TTRVc TTRr;
  Boolean found;
  string 40 ckey;
  Integer keys,endIntYc;
  LongInt lasttrnr;
  string 255 tstr;
  
  endIntYc = RepSpec.flags[0];
  switch (RepSpec.flags[0]) begin
    case 0: 
      TTRr.TRIntYc = GetIntYc(RepSpec.sStartDate);
      endIntYc = GetIntYc(RepSpec.sEndDate);
    otherwise 
      TTRr.TRIntYc = RepSpec.flags[0];
  end;
  if (nonblank(RepSpec.f1)) then begin
    TTRr.TRNumber = frnr;
    ckey = "TRNumber";
    keys = 2;
  end else begin
    ckey = "TRIntYcDate";
    TTRr.TransDate = RepSpec.sStartDate;
    keys = 2;
  end;
  found = true;
  while (LoopKey(ckey,TTRr,2,found)) begin
    if (TTRr.TRIntYc>endIntYc) then begin found = false; end;
    if (nonblank(RepSpec.f1)) then begin
      if (TTRr.TRNumber>tonr) then begin found = false; end;
    end else begin
      if (TTRr.TransDate>RepSpec.sEndDate) then begin found = false; end;
    end;
    if (found) then begin
      if (lasttrnr!=TTRr.TRNumber) then begin
        ToStrTRCode(tstr,TTRr.TRIntYc,TTRr.TRNumber);
        StartFormat(15);
         OutString(0,"DblTrans",tstr,false);
        EndFormat;
      end;
      StartFormat(15);
       OutLongInt(15,"DblTTRVc",TTRr.SerNr,false);
       OutDate(2,0,TTRr.TransDate,false);
       OutString(3,0,TTRr.AccNumber,false);
       OutVal(8,0,TTRr.CurVal,M4Val,true);
       OutVal(1,0,TTRr.B1Val,M4Val,true);
      EndFormat;
      lasttrnr = TTRr.TRNumber;
    end;
  end;
  RETURN;
END;

global
procedure TTRRn(record RcVc RepSpec)
BEGIN
  LongInt frnr,tonr;
  
  frnr = FirstInRange(RepSpec.f1,20);
  tonr = FirstInRange(RepSpec.f1,20);
  StartReportJob(USetStr(16120));

  EndHeader;
  SetRepCol(2,80);
  SetRepCol(3,140);
  SetRepCol(8,420);
  StartFormat(15);
   OutString(0,0,USetStr(16121),false);
   OutString(2,0,USetStr(16122),false);
   OutString(3,0,USetStr(16123),false);
   OutString(8,0,USetStr(16124),true);
   OutString(1,0,USetStr(16125),true);
  EndFormat;
  Gray_Divider(0,1);

  PrintTTRs(RepSpec,frnr,tonr);  
  EndJob;
  RETURN;
END;

