global
procedure POPlanInfoRn(record RcVc RepSpec)
begin
  record POPlanVc POPr;
  row POPlanVc POPrw;
  Integer i,rwcnt;
  Boolean found,testf;
  LongInt fr,to;
  
  fr = FirstInRange(RepSpec.f1,10);
  StartReportJob(USetStr(16390));    
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(16391),false);
  OutString(100,0,USetStr(16392),false);
  EndFormat;
  Gray_Divider(0,1);

  POPr.SerNr = fr;
  found = true;
  while (LoopMain(POPr,1,found)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (POPr.SerNr!=fr) then begin found = false; end;
    end;
    if (found) then begin
      rwcnt = MatRowCnt(POPr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(POPr,i,POPrw);
        testf = true;
        if (nonblank(POPrw.Qty)) then begin
          if (POPrw.Qty==0) then begin testf = false; end;
        end else begin
          if (POPrw.SugQty==0) then begin testf = false; end;
        end;
        if (testf) then begin
          StartFormat(15);
          OutString(0,0,POPrw.ItemCode,false);
          OutLongInt(100,"DblPOVc",POPrw.PONr,false);
          EndFormat;
        end;
      end;
    end;
  end;
  EndJob;  
  return;
end;

