/*
Done by Tony Marshal
*/
// Report commented out from interface, so if you comment it in, rewrite the Contacts...

global
procedure Detail2DoRn(record RcVc RepSpec)
BEGIN
  record ActVc Actr;
  record ActTypeVc ActTyper;
  record ActTypeGrVc ActTypeGrr;
  record CUVc CUr;
  record ContactVc Contactr;
  Row ActVc Actrw;
  record RcVc Repspec2;
  Boolean found;
  string 255 tstr;
  string 255 corange,drange;
  string 10 fdate,tdate,curuser,comp,controller,dept;
  Integer rwcnt,i,j,outrow;
  Boolean testf;
  Integer rw,actcnt;
  String 50 Fcus,Tcus,sort;
  
  Fcus = "";
  Tcus = "";
  Fcus = Firstinrange(RepSpec.f1,50);
  Tcus = Lastinrange(RepSpec.f1,50);
//  fdate = RepSpec.sStartDate;        //is start of date range
//  tdate = RepSpec.sEndDate;          //is end date in range
  if nonblankdate(RepSpec.d1) then begin
    tdate = RepSpec.d1;
  end else begin
    tdate=CurrentDate;
  end;
  rw = 1;
  j = 0;
  actcnt = 0;
  outrow = 20; //Set number of rows you wish to display
  drange = USetStr(10110);// & tdate;
  curuser = currentuser;
  if (fcus <> "") then begin
    corange = USetStr(10116) & fcus;
  end;
  if (tcus <> "") then begin
    corange = corange & USetStr(10117) & tcus;
  end;
  startReportJob(drange);
  if (corange<>"") then begin
    Header(rw,corange,1);
    rw=rw+1;
  end;
  if nonblank(RepSpec.f2) then begin
    Header(rw,USetStr(10111) & RepSpec.f2,1);
  end else begin
    Header(rw,USetStr(10111) & curuser,1);
  end;
  rw=rw+1;
  if (RepSpec.long1!=-1) then begin
    if (RepSpec.flags[3] == 0) then begin
      Actr.PrioLevel=-5;
      tstr = USetStr(10112) & RepSpec.long1;
    end;
    if (RepSpec.flags[3] == 1) then begin
      Actr.PrioLevel=RepSpec.long1;
      tstr = USetStr(10113) & RepSpec.long1;
    end;
    Header(rw,tstr,1);
    rw=rw+1;
  end;
  EndHeader;
  StartFormat(15);
  Outstring(0,"DblActDetailed",USetStr(6689),false);
  Outstring(280,"DblTodoNew",USetStr(6666),true);
  endFormat;
  StartFormat(20);
  OutString(0,0,USetStr(6684),false);
  OutString(60,0,USetStr(3173),false);
  OutString(110,0,USetStr(9561),false);
  OutString(340,0,USetStr(2304),false);
  endformat;
  Black_Divider(0,1);
  
  Actr.OKFlag = 0;
  found = true;
  if (RepSpec.flags[2]==0) then begin
    sort = "PrioLevel";
    Actr.PrioLevel=-5;
  end;
  if (RepSpec.flags[2]==1) then begin
    sort = "TransDate";
    Actr.TransDate="01/01/1997";//## ??
  end;
  while (LoopKey(sort,Actr,1,found)) begin
    testf = true;
    if (RepSpec.flags[2]==0) then begin
      if (RepSpec.long1!=-1) then begin
        if (Actr.PrioLevel>RepSpec.long1) then begin
          found=false;
          testf=false;
        end;
      end;
      if nonblankdate(RepSpec.d1) then begin
        if  RepSpec.d1 < Actr.TransDate then begin
          testf = false;
        end;
      end else begin
        if  CurrentDate < Actr.TransDate then begin
          testf=false;
        end;
      end;
    end;
    if (RepSpec.flags[2]==1) then begin
      if nonblankdate(RepSpec.d1) then begin
        if  RepSpec.d1 < Actr.TransDate then begin
          testf = false;
          found = false;
        end;
      end else begin
        if  CurrentDate < Actr.TransDate then begin
          testf=false;
          found=false;
        end;
      end;
    end;
    if (RepSpec.long1!=-1) then begin
      if (RepSpec.flags[2]==1) then begin
        if (RepSpec.flags[3]==0) then begin
          if (Actr.PrioLevel>RepSpec.long1) then begin
            testf=false;
          end;
        end;
        if (RepSpec.flags[3]==1) then begin
          if (Actr.PrioLevel<>RepSpec.long1) then begin
            testf=false;
          end;
        end;
      end;
    end;
    if (RepSpec.flags[1] == 0) then begin
      if (Actr.OKFlag<>0) then begin
        testf=false;
       end;
    end;
    if (Actr.TodoFlag <> 1) then begin
      testf = false;
    end;
    if nonblank(RepSpec.f1) then begin
      if (Actr.CUCode < fcus) or (Actr.CUCode > tcus) then begin
        testf = false;
      end;
    end;
    if nonblank(RepSpec.f2) then begin
      if ((SetInSet(RepSpec.f2,Actr.MainPersons)==false) and
        (SetInSet(RepSpec.f2,Actr.CCPersons)==false)) then begin
        testf = false;
      end;
    end else begin
      if ((SetInSet(curuser,Actr.MainPersons)==false) and
           (SetInSet(curuser,Actr.CCPersons)==false)) AND nonblank(Actr.MainPersons) then begin
        testf = false;
      end;
    end;
    if nonblank(RepSpec.f3) then begin
      if RepSpec.f3<>Actr.ActType then begin
        testf = false;
      end;
    end;
    if (RepSpec.f4!="") then begin
      if (Actr.ActType=="") then begin
        testf = false;
      end else begin
        ActTyper.Code = Actr.ActType;
        if (ReadFirstMain(ActTyper,1,true)==true) then begin
          if (RepSpec.f4!=ActTyper.ActTypeGr) then begin
            testf = false;
          end;
        end;
      end;
    end;
    if nonblank(RepSpec.ObjStr) then begin
      if (Actr.CUCode=="") then begin
        testf = false;
      end else begin
        CUr.Code = Actr.CUCode;
        if (ReadFirstMain(CUr,1,true)==true) then begin
          if (RepSpec.ObjStr<>CUr.CustCat) then begin
            testf = false;
          end;
        end;
      end;
    end;
    if nonblank(RepSpec.Stext) then begin
      if (Actr.CUCode=="") then begin
        testf = false;
      end else begin
        CUr.Code = Actr.CUCode;
        if (ReadFirstMain(CUr,1,true)==true) then begin
          if (SetInSet(RepSpec.Stext,CUr.Classification)==false) then begin
            testf = false;
          end;
        end;
      end;
    end;
    if (testf) then begin
      actcnt=actcnt+1;
      rwcnt = MatRowCnt(Actr);
      StartFormat(10);
      EndFormat;
      StartFormat(15)
      OutString(0,0,Actr.MainPersons,false);
      OutString(60,"DblCUVc",Actr.CUCode,false);
      OutString(110,0,Actr.CUName,false);
      CUr.Code = Actr.CUCode;
      if (Actr.Phone == "") then begin
        if (ReadFirstMain(CUr,1,true)==true) then begin
          OutString(370,370,CUr.Phone,true);
        end;
      end else begin
        OutString(370,370,Actr.Phone,true);
      end;
      
      OutString(380,0,Actr.Contact,false);
      EndFormat;
      StartFormat(15) 
      OutString(0,0,USetStr(7253),false);
      OutDate(60,60,Actr.TransDate,false);
      OutString(265,0,USetStr(6687),true);
      OutString(270,"DblActVc",Actr.SerNr,false);
      OutString(355,0,USetStr(10114),true);
      if nonblank(Actr.PrioLevel) then begin
        OutString(360,0,Actr.PrioLevel,false);
      end;
      if (Actr.OKFlag<>0) then begin
        OutString(460,0,USetStr(10115),false);
      end;
      EndFormat
      StartFormat(15)
      OutString(0,0,USetStr(9339),false);
      OutString(35,0,Actr.ActType,false);
      OutString(85,0,USetStr(2406),false);
      OutString(110,0,Actr.Comment,false);
      EndFormat;
      If(RepSpec.ArtMode==1) then begin
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(Actr,i,Actrw);
          StartFormat(15);
          OutString(35,0,Actrw.TextCode,false);
          OutString(110,0,Actrw.Text,False);
          EndFormat;
        end;
      end;
      StartFormat(10);
      Gray_Divider(0,1);
      EndFormat;
    end;
  end;
  StartFormat(15);
  OutString(20,0,USetStr(6690) & "  " & Actcnt,false);
  EndFormat;
  StartFormat(10);
  Gray_Divider(0,1);
  EndFormat;
  EndJob;


  RETURN;
END;


