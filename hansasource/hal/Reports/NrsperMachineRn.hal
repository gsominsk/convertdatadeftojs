global
procedure NrsperMachineRn(record RcVc RepSpec)
begin
  record SynkDBIDRegVc SynkDBIDRegr;
  row SynkDBIDRegVc SynkDBIDRegrw;
  Integer i,rwcnt;
  Boolean found;
  string 255 frma,toma;
  
  frma = FirstInRange(RepSpec.f1,10);
  toma = LastInRange(RepSpec.f1,10);
  StartReportJob(USetStr(27080));
  EndHeader;
  
  SetRepCol(2,80);
  SetRepCol(3,200);
  SetRepCol(4,300);
  SetRepCol(5,400);
  StartFormat(15);
   OutString(0,0,USetStr(27081),false);
   OutString(2,0,USetStr(27082),false);
   OutString(3,0,USetStr(27083),false);
   OutString(4,0,USetStr(27084),false);
   OutString(5,0,USetStr(27085),false);
  EndFormat;
  Gray_Divider(0,1);
  found = true;
  SynkDBIDRegr.LocalMachineCode = frma;
  while (LoopKey("LocalMachineCode",SynkDBIDRegr,1,found)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (RepSpec.f1!="*") then begin
        if (SynkDBIDRegr.LocalMachineCode<frma) then begin found = false; end;
        if (SynkDBIDRegr.LocalMachineCode>toma) then begin found = false; end;
      end;
    end;
    if (found) then begin
      rwcnt = MatRowCnt(SynkDBIDRegr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(SynkDBIDRegr,i,SynkDBIDRegrw);
        StartFormat(15);
         OutString(0,0,SynkDBIDRegr.LocalMachineCode,false);
         OutString(2,0,SynkDBIDRegr.DBIDCode,false);
         OutString(3,0,SynkDBIDRegrw.FileName,false);
         OutString(4,0,SynkDBIDRegrw.From & ":" & SynkDBIDRegrw.To,false);
         OutString(5,0,SynkDBIDRegrw.LastNumber,false);
        EndFormat;
      end;
    end;
  end;
  EndJob;
  return;
end;