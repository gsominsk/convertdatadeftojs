global
procedure RCldSvrNoArchvRn(record RcVc RepSpec)
begin
  Integer rw;
  string 255 tstr,index;
  record RemoteCloudServerVc RCSr;
  record ASPArchiveVc ASPAr;
  Boolean found,testf;

  StartReportJob("Servers without Cloud Archive");
  rw = 1;
  EndHeader;
  StartFormat(15);
   OutString(0,0,"Name",false);
   OutString(200,0,"Customer",false);
   OutString(300,0,"Closed",false);
  EndFormat;
  Gray_Divider(0,1);

  found = true;
  index = "MainKey";  
  while (LoopKey(index,RCSr,1,found)) begin
    if (found) then begin
      testf = true;
      if (RepSpec.flags[0]==0) then begin
        if (RCSr.Closed!=0) then begin testf = false; end;
      end;
      if (testf) then begin
        ASPAr.ServerUUID = RCSr.UUID;
        if (ReadFirstKey("ServerUUID",ASPAr,1,true)==false) then begin
          StartFormat(15);
           OutStringID(0,"DblRemoteCloudServerVc",RCSr.Name,false,RCSr.UUID);
           OutString(200,0,RCSr.CUCode,false);
           OutString(300,0,RCSr.Closed,false);
          EndFormat;
        end;
      end;
    end;
  end;
  EndJob;
  return;
end;