procedure ExportLine(string artcode,val qty,record VEStoreIDVc VESr,record BaseCurBlock BCb)
begin
  record INVc INr;

  INr.Code = artCode;
  ReadFirstMain(INr,1,true);
  StartFormat(15);
   OutString(0,0,DateToString(CurrentDate,"YYYYMMDD"),false);//REPORTING DATE
   OutString(0,0,VESr.VEStoreID,false);
   OutString(0,0,"",false);//CUSTOMER ID
   OutString(0,0,"",false);//CUSTOMER NAME
   OutString(0,0,"",false);//CUSTOMER ADDRESS
   OutString(0,0,"",false);//CUSTOMER CITY
   OutString(0,0,"",false);//CUSTOMER PROVINCE
   OutString(0,0,"",false);//CUSTOMER ZIP CODE
   OutString(0,0,VESr.Region,false);//CUSTOMER COUNTRY CODE
   OutString(0,0,"",false);//VAT NUMBER
   OutString(0,0,"",false);//INVOICE NUMBER
   OutString(0,0,"",false);//INVOICE DATE
   OutString(0,0,"",false);//INVOICE LINE
   OutString(0,0,INr.Code,false);//APPLE MARKETING PART NUMBER
   OutString(0,0,INr.Name,false);//APPLE MARKETING PART NUMBER DESCRIPTION
   OutVal(0,0,qty,M4Val,false);//SOLD QUANTITY - TAGTODO
   OutString(0,0,"",false);//RETURNED QUANTITY
   OutString(0,0,"",false);//UNIT PRICE
   OutString(0,0,BCb.BaseCur1,false);//CURRENCY
   OutString(0,0,"0",false);//FUNCTIONAL DISCOUNT
   OutString(0,0,"",false);//SERIAL NUMBERS
  EndFormat;
  return;
end;

procedure PrintLine(string artcode,val qty,record VEStoreIDVc VESr)
begin
  record INVc INr;

  INr.Code = artCode;
  ReadFirstMain(INr,1,true);
  StartFormat(15);
   OutString(0,0,DateToString(CurrentDate,"YYYYMMDD"),false);
   OutString(80,0,VESr.VEStoreID,false);
   OutString(140,0,VESr.Region,false);
   OutString(180,0,INr.Code,false);
   OutString(260,0,Left(INr.Name,25),false);
   OutVal(1,0,qty,M4Val,true);
  EndFormat;
  return;
end;

procedure PrintLineHeader()
begin
  StartFormat(15);
   OutString(0,0,USetStr(17601),false);
   OutString(80,0,USetStr(17602),false);
   OutString(140,0,USetStr(17603),false);
   OutString(180,0,USetStr(17604),false);
   OutString(260,0,USetStr(17605),false);
   OutString(1,0,USetStr(17606),true);
  EndFormat;
  return;
end;

procedure ExportLineHeader()
begin
  StartFormat(15);
   OutString(0,0,USetStr(17601),false);
   OutString(0,0,USetStr(17602),false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,USetStr(17603),false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,USetStr(17604),false);
   OutString(0,0,USetStr(17605),false);
   OutString(0,0,USetStr(17606),false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
   OutString(0,0,USetStr(17607),false);
   OutString(0,0,"",false);
   OutString(0,0,"",false);
  EndFormat;
  return;
end;

procedure AddToItemArray(string artcode,val qty,Array string aartcodes,Array val aqtys,var Integer acnt)
begin
  Integer i;
  
  for (i=0;i<acnt;i=i+1) begin
    if (artcode==aartcodes[i]) then begin
      aqtys[i] = aqtys[i] - qty;
      goto LAddToItemArray;
    end;
  end;
  aartcodes[acnt] = artcode;
  aqtys[acnt] = -qty;
  acnt = acnt + 1;
LAddToItemArray:;  
  return;
end;

procedure SortItemArray(Array string aartcodes,Array val aqtys,Integer acnt)
begin
  Integer i;
  Integer changing;
  string 255 artcode;
  val qty;

  changing = 1;
  while (changing==1) begin
    changing = 0;
    for (i=0;i<acnt-1;i=i+1) begin
      if (aartcodes[i]<aartcodes[i+1]) then begin
        changing = 1;
        artcode = aartcodes[i];
        qty = aqtys[i];
                
        aartcodes[i] = aartcodes[i+1];
        aqtys[i] = aqtys[i+1];
        
        aartcodes[i+1] = artcode;
        aqtys[i+1] = qty;        
      end;
    end;
  end;
  return;
end;

procedure AppleGDVSales(record RcVc RepSpec,record VEStoreIDVc VESr,record BaseCurBlock BCb,Array string aartcodes,Array val aqtys,var Integer acnt)
begin
  Boolean found,foundih,testf,foundin;
  record ItemHistVc IHr;
  record INVc INr;

  IHr.FileName = "IVVc";
  IHr.Location = VESr.Location;
  IHr.TransDate = RepSpec.sStartDate;
  foundih = true;
  while (LoopKey("FNLocation",IHr,3,foundih)) begin
    if (IHr.FileName!="IVVc") then begin foundih = false; end;
    if (IHr.Location!=VESr.Location) then begin foundih = false; end;
    if (DateInRange(IHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin foundih = false; end;   
    if (foundih) then begin   
      testf = true;
      if (IHr.Invalid!=0) then begin testf = false; end;          
      if (nonblank(RepSpec.f1)) then begin
        INr.Code = IHr.ArtCode;
        ReadFirstMain(INr,1,true);   
        if (SetInSet(RepSpec.f1,INr.DispGroups)==false) then begin testf = false; end;
      end;
      if (IHr.Invalid!=0) then begin testf = false; end;
      if (testf) then begin
        AddToItemArray(IHr.ArtCode,IHr.Qty,aartcodes,aqtys,acnt);
      end;
    end;
  end;
  return;
end;

global
procedure AppleGDVSellRn(record RcVc RepSpec)
begin
  record BaseCurBlock BCb;
  record VEStoreIDVc VESr;
  record VEStoreIDVc lastVESr;
  Boolean found;
  string 255 lastVEStoreID;
  Array string 255 aartcodes;
  Array val aqtys;
  Integer acnt,i;
  
  BlockLoad(BCb);
  StartReportJob(USetStr(17600));
  EndHeader;
  switch (RepSpec.Media) begin
    case mtFile:
      ExportLineHeader;
    case mtExcel:
      ExportLineHeader;
    otherwise
      PrintLineHeader;
  end;
  Gray_Divider(0,1);
  found = true;
  VESr.VECode = RepSpec.FirstAcc;
  while (LoopKey("VEStoreID",VESr,2,found)) begin
    if (nonblank(RepSpec.FirstAcc)) then begin
      if (VESr.VECode!=RepSpec.FirstAcc) then begin found = false; end;
    end;
    if (found) then begin
      if (nonblank(lastVESr.VEStoreID)) then begin
        if (VESr.VEStoreID!=lastVESr.VEStoreID) then begin
          SortItemArray(aartcodes,aqtys,acnt);
          for (i=0;i<acnt;i=i+1) begin
            if (aqtys[i]!=0) then begin
              switch (RepSpec.Media) begin
                case mtFile:
                  ExportLine(aartcodes[i],aqtys[i],lastVESr,BCB);
                case mtExcel:
                  ExportLine(aartcodes[i],aqtys[i],lastVESr,BCB);
                otherwise
                  PrintLine(aartcodes[i],aqtys[i],lastVESr);
              end;
            end;
          end;
          for (i=0;i<acnt;i=i+1) begin
            aartcodes[i] = "";
            aqtys[i] = blankval;
          end;
          acnt = 0;
        end;
      end;
      AppleGDVSales(RepSpec,VESr,BCb,aartcodes,aqtys,acnt);
      RecordCopy(lastVESr,VESr);
    end;
  end;  
  if (acnt>0) then begin
    SortItemArray(aartcodes,aqtys,acnt);
    for (i=0;i<acnt;i=i+1) begin
      if (aqtys[i]!=0) then begin
        switch (RepSpec.Media) begin
          case mtFile:
            ExportLine(aartcodes[i],aqtys[i],lastVESr,BCB);
          case mtExcel:
            ExportLine(aartcodes[i],aqtys[i],lastVESr,BCB);
          otherwise
            PrintLine(aartcodes[i],aqtys[i],lastVESr);
        end;
      end;
    end;
  end;
  EndJob;
  return;
end;
