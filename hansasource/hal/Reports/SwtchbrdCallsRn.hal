external procedure HT2Per(Date, Date , var string);

procedure PrintActivities(record RcVc RepSpec,Integer t,string acttype)
begin
  record ActVc Actr;
  Boolean found,testf,printedf;
  val cnt;
  
  found = true;
  Actr.ActType = acttype;
  Actr.TransDate = RepSpec.sStartDate;
  while (LoopKey("ActType",Actr,2,found)) begin
    if (Actr.ActType!=acttype) then begin found = false; end;
    if (DateInRange(Actr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin found = false; end;
    if (found) then begin
      testf = true;
      if (nonblank(RepSpec.AccStr)) then begin
        if (Actr.PBXQueues!=RepSpec.AccStr) then begin testf = false; end;
      end;
      if (testf) then begin
        if (printedf==false) then begin
          StartFormat(15);
          OutString(0,0,USetStr(38370+t),false);
          EndFormat;
          StartFormat(15);
          OutString(0,0,USetStr(38375),false);
          OutString(2,0,USetStr(38376),false);
          OutString(3,0,USetStr(38377),false);
          OutString(4,0,USetStr(38378),false);
          OutString(5,0,USetStr(38379),true);
          OutString(6,0,USetStr(38380),false);
          OutString(7,0,USetStr(38381),false);
          EndFormat;
          Gray_Divider(0,1);
          printedf = true;
        end;
        StartFormat(15);
        OutDateID(0,"DblActVc",Actr.TransDate,false,Actr.SerNr);
        OutString(2,0,Actr.StartTime,false);
        OutString(3,0,Actr.CUCode,false);
        OutString(4,0,Actr.Contact,false);
        OutString(5,0,Actr.CostTime,true);
        OutString(6,0,Actr.MainPersons,false);
        OutString(7,0,Actr.Comment,false);
        EndFormat;
        cnt = cnt + 1;
      end;
    end;
  end;
  if (printedf) then begin
    Gray_Divider(0,1);
    StartFormat(15);
     OutString(6,0,USetStr(6690),false);
     OutVal(1,0,cnt,M40Val,true);
    EndFormat;
    StartFormat(5);
    EndFormat;
  end;
  return;
end;

global
procedure SwtchbrdCallsRn(record RcVc RepSpec)
begin
  Integer rw;
  string 255 tstr;
  record ASTBlock ASTb;

  BlockLoad(ASTb);
  StartReportJob(USetStr(38370));  
  rw = 1;
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  EndHeader;
  SetRepCol(2,50);
  SetRepCol(3,90);
  SetRepCol(4,150);
  SetRepCol(5,260);
  SetRepCol(6,270);
  SetRepCol(7,320);

  if (RepSpec.flags[0]!=0) then begin
    PrintActivities(RepSpec,1,ASTb.AsteriskCalls);
  end;
  if (RepSpec.flags[1]!=0) then begin
    PrintActivities(RepSpec,2,ASTb.MissedAsteriskCalls);
  end;
  if (RepSpec.flags[2]!=0) then begin
    PrintActivities(RepSpec,3,ASTb.ServerAsteriskCalls);
  end;
  if (RepSpec.flags[3]!=0) then begin
    PrintActivities(RepSpec,4,ASTb.ServerMissedAsteriskCalls);
  end;
  EndJob;
  return;
end;