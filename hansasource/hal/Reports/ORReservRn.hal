external function val FindReseverdQty(string,string,string);
external function Boolean AllowedToTakeFromThisLoc(record LocationVc);
//external function val GetStockQty(string,string,Date,Boolean);
external function val GetPORowReserv(LongInt,string,var val,Boolean);
external function val GetORRowReserv(LongInt,string,string,var val,var string,var string,Boolean);
external function val GetORRowReserv2(LongInt,string,string,var val,var string,var string,Boolean,var val,Boolean);
external procedure FindStockValueInclStockReserv(LongInt,string,string,string,var record ItemStatusVc,var val);
external procedure FindStockValue(string,string,var record ItemStatusVc);

procedure FindAndPrintPO(record ORVc ORr,string artcode)
begin
  record POVc POr;
  row POVc POrw;
  Integer rwcnt,i;
  Boolean foundf;
  Boolean firstf;
  val resrv,ordqty;
  
  POr.PUFlag = 0;
  foundf = true;
  firstf = true;
  while (LoopKey("PUFlag",POr,1,foundf)) begin
    if (POr.PUFlag!=0) then begin
      foundf = false;
    end;
    if (foundf) then begin
      rwcnt = MatRowCnt(POr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(POr,i,POrw);
        if (POrw.ArtCode==artcode) then begin
          if (POrw.Quant>POrw.Shipd1) then begin
            resrv = GetPORowReserv(POr.SerNr,POrw.ArtCode,ordqty,false);
            if (resrv<POrw.Quant-POrw.Shipd1) then begin
              if (firstf) then begin
                StartFormat(15);
                OutString(0,0,USetStr(16149),false);
                OutString(2,0,USetStr(16145),false);
                OutString(5,0,USetStr(16137),true);
                OutString(6,0,USetStr(16146),true);
                EndFormat;
                Gray_Divider(0,1);
                firstf = false;
              end;
              StartFormat(15);
              OutString(0,"DblPOVc",POr.SerNr,false);
              OutString(2,0,POr.PlanShip,false);
              OutVal(5,0,POrw.Quant-POrw.Shipd1,M4Qty,true);
              OutVal(6,0,resrv,M4Qty,true);
              if (ORr.Closed==0) then begin
                OutStringID(1,"DblORNewReserv",USetStr(16141),true,"1," & POr.SerNr & "," & i & "," & ORr.SerNr);
              end else begin
                OutString(1,0,USetStr(16157),true);
              end;
              EndFormat;
            end;
          end;
        end;
      end;
    end;
  end;
  if (firstf==false) then begin
    Gray_Divider(0,1);
    StartFormat(15);
    EndFormat;
  end;
  return;
end;

procedure FindAndPrintStock(record ORVc ORr,string artcode,string serialnr)
begin
  record INVc INr;
  record SerBalVc SBr;
  record LocationVc Locr;
  record ItemStatusVc ISr;
  Boolean foundf;
  Boolean firstf;
  Date bldat;
  val instock,stockreservq;
  Boolean sernrf;
  
  INr.Code = artcode;
  if (ReadFirstMain(INr,1,true)) then begin
    if (INr.SerNrf>0) then begin
      sernrf = true;
    end;
  end;
  foundf = true;
  firstf = true;
  Locr.Code = "";
  while (LoopMain(Locr,1,foundf)) begin
    if (foundf) then begin
      FindStockValueInclStockReserv(-1,artcode,serialnr,Locr.Code,ISr,stockreservq);
//      FindStockValue(artcode,Locr.Code,ISr);
      instock = ISr.Instock; // - stockreservq;
      if (instock - stockreservq > 0) then begin
        if (firstf) then begin
          StartFormat(15);
          OutString(0,0,USetStr(16158),false);
          OutString(2,0,USetStr(16138),false);
          OutString(5,0,USetStr(16168),true);
          OutString(6,0,USetStr(16146),true);
          OutString(7,0,USetStr(16172),true);
          EndFormat;
          Gray_Divider(0,1);
          firstf = false;
        end;
        StartFormat(15);
        OutString(0,0,Locr.Code,false);
        OutString(2,0,Locr.Name,false);
        OutVal(5,0,instock,M4Qty,true);
        OutVal(6,0,stockreservq,M4Qty,true);
        OutVal(7,0,instock - stockreservq,M4Qty,true);
        if (AllowedToTakeFromThisLoc(Locr)==false) then begin
          OutString(1,0,USetStr(16167),true);
        end else begin
          if (ORr.Closed==0) then begin
            OutStringID(1,"DblORNewReservStock",USetStr(16141),true,Locr.Code & "," & artcode & "," & ORr.SerNr & "," & serialnr);
          end else begin
            OutString(1,0,USetStr(16157),true);
          end;
        end;
        EndFormat;
      end;
    end;
  end;
  if (firstf==false) then begin
    Gray_Divider(0,1);
    StartFormat(15);
    EndFormat;
  end;
  return;
end;

procedure ReserveNextPO(record ORVc ORr,string artcode,string serialnr)
begin
  StartFormat(15);
  EndFormat;
  StartFormat(15);
  if (ORr.Closed==0) then begin
    OutStringID(1,"DblORNewReservNext",USetStr(16161),true,artcode & "," & ORr.SerNr & "," & serialnr);
  end else begin
    OutString(1,0,USetStr(16157),true);
  end;
  EndFormat;
  return;
end;

procedure ReserveBackToBackPO(record ORVc ORr,string artcode)
begin
  StartFormat(15);
  EndFormat;
  StartFormat(15);
  if (ORr.Closed==0) then begin
    OutStringID(1,"DblORNewBackToBackPO",USetStr(16126),true,artcode & ":" & ORr.SerNr);
  end else begin
    OutString(1,0,USetStr(16157),true);
  end;
  EndFormat;
  return;
end;

global
procedure ORReservDrillRn(record RcVc RepSpec)
BEGIN
  record ORVc ORr;
  row ORVc ORrw;
  Integer i,rwcnt;
  Boolean foundf;
  LongInt fror,toor;
  val resrv,ordqty,quantdone;
  string 60 comment,location;
  
  StartReportJob(USetStr(16130));
  EndHeader;
  SetRepCol(2,65);
  SetRepCol(4,170);
  SetRepCol(5,280);
  SetRepCol(6,350);
  SetRepCol(7,410);
  SetRepCol(8,230);
  ORr.SerNr = RepSpec.f1;
  if (ReadFirstMain(ORr,1,true)) then begin
    rwcnt = MatRowCnt(ORr);
    i = StringToInt(RepSpec.f2);
    if (i<rwcnt) then begin
      MatRowGet(ORr,i,ORrw);
      resrv = GetORRowReserv(ORr.SerNr,ORrw.ArtCode,ORrw.SerialNr,ordqty,comment,location,true);
      if (resrv!=ordqty) then begin
        StartFormat(15);
        OutString(0,0,USetStr(16136),false);
        OutString(2,0,USetStr(16138),false);
        OutString(5,0,USetStr(16153),true);
        OutString(6,0,USetStr(16146),true);
        OutString(7,0,USetStr(16154),true);
        EndFormat;
        Gray_Divider(0,1);
        StartFormat(15);
        OutString(0,"DblINVc",ORrw.ArtCode,false);
        OutString(2,0,Left(ORrw.Spec,30),false);
        OutString(5,0,ordqty,true);
        OutString(6,0,resrv,true);
        OutString(7,0,ordqty-resrv,true);
        EndFormat;
        Gray_Divider(0,1);
        StartFormat(15);
        EndFormat;
        FindAndPrintStock(ORr,ORrw.ArtCode,ORrw.SerialNr);
        if (blank(ORrw.SerialNr)) then begin
          FindAndPrintPO(ORr,ORrw.ArtCode);
          ReserveNextPO(ORr,ORrw.ArtCode,ORrw.SerialNr);
          ReserveBackToBackPO(ORr,ORrw.ArtCode);
        end;
      end;
    end;
  end;
  EndJob;
  RETURN;
END;

global
procedure ORReservRn(record RcVc RepSpec)
BEGIN
  record StockReservBlock SRb;
  record ORVc ORr;
  row ORVc ORrw;
  row ORVc ORrw2;
  Integer i,j,rwcnt;
  Boolean foundf,testf;
  LongInt fror,toor;
  val resrv,ordqty,ordrtot,rsrvtot,quantdone,quantdonetot;
  string 60 comment,location;
  
  BlockLoad(SRb);
  fror = FirstInRange(RepSpec.f1,10);
  toor = LastInRange(RepSpec.f1,10);
  StartReportJob(USetStr(16130));
  EndHeader;
  SetRepCol(2,15);
  SetRepCol(3,90);
  SetRepCol(4,130);
  SetRepCol(5,260);
  SetRepCol(6,320);
  SetRepCol(7,400);
  SetRepCol(8,250);
  SetRepCol(9,440);
  foundf = true;
  ORr.SerNr = fror;
  while (LoopMain(ORr,1,foundf)) begin
    if (ORr.SerNr>toor) then begin
      foundf = false;
    end;
    if (foundf) then begin
      StartFormat(15);
      OutString(0,0,USetStr(16144),false);
      OutString(70,0,USetStr(16132),false);
      OutString(130,0,USetStr(16133),false);
      OutString(200,0,USetStr(16134),false);
      OutString(410,0,USetStr(16135),false);
      EndFormat;
      Black_Divider(0,1);
      StartFormat(15);
      OutLongInt(0,"DblORVc",ORr.SerNr,false);
      OutDate(70,0,ORr.OrdDate,false);
      OutString(130,"DblCUVc",ORr.CustCode,false);
      OutString(200,0,ORr.Addr0,false);
      OutString(410,0,ORr.PlanShip,false);
      EndFormat;
      StartFormat(15);
      EndFormat;
      StartFormat(15);
      OutString(2,0,USetStr(16136),false);
      OutString(3,0,USetStr(16138),false);
      OutString(5,0,USetStr(16137),true);
//      OutString(5,0,USetStr(16139),true);
      OutString(6,0,USetStr(16140),true);
      OutString(380,0,USetStr(16160),true);
      OutString(7,0,USetStr(16142),false);
      EndFormat;
      Gray_Divider(0,1);
      rwcnt = MatRowCnt(ORr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(ORr,i,ORrw);
        testf = true;
        for (j=0;j<i;j=j+1) begin
          MatRowGet(ORr,j,ORrw2);
          if (ORrw.ArtCode==ORrw2.ArtCode and ORrw.SerialNr==ORrw2.SerialNr) then begin
            testf = false;
            j = i;
          end;
        end;
        if (ORrw.stp==1 and testf) then begin
          resrv = GetORRowReserv2(ORr.SerNr,ORrw.ArtCode,ORrw.SerialNr,ordqty,comment,location,false,quantdone,true);
          if (ordqty==0) then begin quantdone = blankval; end;
          ordrtot = ordrtot + ordqty;
          rsrvtot = rsrvtot + resrv;
          quantdonetot = quantdonetot + quantdone;
          StartFormat(15);
          OutString(2,"DblINVc",ORrw.ArtCode,false);
          OutString(3,0,Left(ORrw.Spec,23),false);
          OutVal(5,0,ordqty,M4Qty,true);
          OutVal(6,0,resrv,M4Val,true);
          OutVal(380,0,quantdone,M4Val,true);
          if (ordqty==resrv) then begin
            if (ORr.Closed==0) then begin
              OutStringID(7,"DblORReserv",USetStr(16162),false,"" & ORr.SerNr & ":" & i);
            end else begin
              OutString(7,0,USetStr(16157),false);
            end;
            OutStringID(9,"DblORReleaseLine",USetStr(16169),false,"" & ORr.SerNr & ":" & i);
          end else begin
            if (ORr.Closed==0) then begin
              OutStringID(7,"DblORReserv",USetStr(16176),false,"" & ORr.SerNr & ":" & i);
              OutStringID(9,"DblORAutoReserveLine",USetStr(16173),false,"" & ORr.SerNr & ":" & i);
            end else begin
              OutString(7,0,USetStr(16157),false);
              OutString(9,0,"",false);
            end;
          end;
          EndFormat;
          if (nonblank(ORrw.SerialNr)) then begin
            StartFormat(15);
            OutString(3,0,ORrw.SerialNr,false);
            EndFormat;
          end;
        end;
      end;
      if (SRb.AutoLevel!=0) then begin
        StartFormat(15);
        EndFormat;
        if (ordrtot!=rsrvtot) then begin
          if (ORr.Closed==0) then begin
            StartFormat(15);
            OutStringID(1,"DblORAutoReserveAll",USetStr(16170),true,ORr.SerNr);
            EndFormat;
          end;
          StartFormat(15);
          EndFormat;
        end;
        if (rsrvtot>0) then begin
          StartFormat(15);
          OutStringID(1,"DblORReleaseAll",USetStr(16171),true,ORr.SerNr);
          EndFormat;
        end;
      end;
      Gray_Divider(0,1);
    end;
  end;
  EndJob;
  RETURN;
END;

