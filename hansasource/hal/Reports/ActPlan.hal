#ifdef HAL
external procedure ExtractObj(string,var Integer,var string);


procedure FindActByTime(record RcVc RepSpec,integer imode,string usercode,date wdate,integer i,var string tstr)
begin
  record ActVc Actr;
  string 255 keystr;
  boolean found;
  boolean TrHs;
  integer mode;

  keystr = "UserMain:" & usercode;
  Actr.TransDate = wdate;
  Actr.TodoFlag = 0;
  mode = 2-imode;
  found = false;
  TrHs = true;
  tstr = "";
  while (LoopKey(keystr,Actr,2,TrHs)) begin
    if (SetInSet(usercode,Actr.MainPersons)==false) then begin
      if (SetInSet(usercode,Actr.CCPersons)==false) then begin
        TrHs=false;
      end;
    end;
    if (Actr.TransDate <> wdate) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      if (Actr.CalTimeFlag==mode) then begin
        if ((TimeToval(Actr.StartTime)>=i) and (timetoval(Actr.StartTime)<i+1) or
            (TimeToval(Actr.StartTime)<i) and (timetoval(Actr.EndTime)>i))  then begin
          tstr = Actr.Comment;
          found = true;
          TrHs = false;
        end;
		  end;
	  end;
  end;    
  return;
end;

global 
procedure ActWeekRn(record RcVc RepSpec)
begin
	date wdate;
	string 255 tstr, namecodes;
	string 5 usercode,hh;
	integer i,ar,pos,stime,etime,ostrlen;
	record CalDispGroupBlock DispGroupRec;
	row CalDispGroupBlock DispGrouprw;
	boolean grfound,tcfound;
	record TimeClassVc TimeClassr;
		
	BlockLoad(DispGroupRec);
	ar = MatRowCnt(DispGroupRec);
	grfound = false;
	tcfound = false;	
	if (ar>0) then begin
	  for (i=0;i<ar;i=i+1) begin
      MatRowGet(DispGroupRec,i,DispGrouprw);
      if (RepSpec.AccStr==DispGrouprw.Code) then begin
        namecodes = DispGrouprw.DefStr;
        grfound = true;
      end;   
    end;
	end;
	if (nonblank(RepSpec.f2)) then begin
    TimeClassr.Code=RepSpec.f2; 
    if (ReadFirstMain(TimeClassr,1,true)) then begin
      stime = timetoval(TimeClassr.StartTime);
      etime = timetoval(TimeClassr.EndTime);
      tcfound = true;
    end;
	end;
	StartReportNoHeaderJob(USetStr(10815));
	if (grfound==false) then begin
	  StartFormat(15);
	  OutString(0,0,USetStr(10816),false);
	  EndFormat;
	end;
	
	wdate = RepSpec.sStartDate;
	if (tcfound==false) then begin
    stime = 8;
    etime = 19;
	end;
	ostrlen = 7;//lenght of word
	while ((wdate<=RepSpec.sEndDate) and (grfound)) begin
    StartFormat(15);
     OutDate(0,0,wdate,false);
    EndFormat;
	  StartFormat(15);
      OutString(0,0,USetStr(10817),false);
      for (i=stime;i<etime;i=i+1) begin
	      hh = i;
        OutString(40+(i-stime)*40,0,hh & ":00",false);
	    end;                        
	  EndFormat;
	  pos = 0;        
	  ExtractObj(namecodes,pos,usercode);
	  while (nonblank(usercode)) begin
      if (RepSpec.ArtMode<>2) then begin
        Black_Divider(0,1);
        StartFormat(15);
         OutString(0,0,usercode,false);
         for (i=stime;i<etime;i=i+1) begin
           FindActByTime(RepSpec,RepSpec.ArtMode,usercode,wdate,i,tstr);
           OutString(40+(i-stime)*40,0,left(tstr,ostrlen),false);
         end;                					          
        EndFormat;
	    end else begin
        Black_Divider(0,1);
        StartFormat(15);
         OutString(0,0,usercode,false);
         for (i=stime;i<etime;i=i+1) begin
           FindActByTime(RepSpec,0,usercode,wdate,i,tstr);
           OutString(40+(i-stime)*40,0,left(tstr,ostrlen),false);
         end;                					          
        EndFormat;
        Gray_Divider(0,1);
        StartFormat(15);
         OutString(0,0,usercode,false);
         for (i=stime;i<etime;i=i+1) begin
           FindActByTime(RepSpec,1,usercode,wdate,i,tstr);
           OutString(40+(i-stime)*40,0,left(tstr,ostrlen),false);
         end;                					          
        EndFormat;
      end;
	    ExtractObj(namecodes,pos,usercode);
	  end;    
	  wdate = addday(wdate,1);
	  StartFormat(30);
	  EndFormat;
	end;
	endjob;
	return;
end;

#endif
