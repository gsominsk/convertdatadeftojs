#ifdef HAL
external procedure GetUserName(string,var string);

procedure PrintUsersWithNoAlises(string frus,string tous)
BEGIN
  record UserVc Userr;
  record ConfVc Confr;
  record EmailAliasVc EMAsr;
  Boolean foundus,firstf;
  string 255 errstr;

  firstf = true;
  foundus = true;
  Userr.Code = frus;
  while (LoopMain(Userr,1,foundus)) begin
    if (nonblank(tous)) then begin
      if (Userr.Code>tous) then begin
        foundus = false;
      end;
    end;  
    if (foundus) then begin
      errstr = "";
      Confr.AddrCode = Userr.Code;
      if (ReadFirstKey("AddrCode",Confr,1,true)) then begin 
        EMAsr.InternalName = Confr.AddrName;
        if (ReadFirstKey("InternalName",EMAsr,1,true)==false) then begin 
          errstr = USetStr(34540);
        end;    
      end else begin
        errstr = USetStr(34541);
      end;
      if (nonblank(errstr)) then begin
        if (firstf) then begin
          StartFormat(15);
           OutString(0,0,USetStr(34542),false);
          EndFormat;    
          firstf = false;
        end;
        StartFormat(15);
         OutString(11,0,Userr.Name,false);
         OutString(200,0,errstr,false);
        EndFormat;    
      end;
    end;  
  end;
  RETURN;
END;

global
procedure EMailAliasRn(record RcVc RepSpec)
BEGIN
  record ConfVc Confr;
  record EmailAliasVc EMAsr;
  Boolean foundconf,foundalias,firstf;
  string 255 tstr;
  string 5 frus,tous;
  string 5 frconf,toconf;
  
  StartReportJob(USetStr(34543));
  EndHeader;
  StartFormat(15);
   OutString(0,0,USetStr(34544),false);
   OutString(100,0,USetStr(34545),false);
   OutString(200,0,USetStr(34546),false);
  EndFormat;
  frus = FirstInRange(RepSpec.f1,5);
  tous = LastInRange(RepSpec.f1,5);
  frconf = FirstInRange(RepSpec.f2,5);
  toconf = LastInRange(RepSpec.f2,5);
  Gray_Divider(0,1);
  foundconf = true;
  Confr.AddrName = frconf;
  while (LoopKey("AddrName",Confr,1,foundconf)) begin
    ResetLoop(EMAsr);
    EMAsr.InternalName = Confr.AddrName;
    firstf = true;
    foundalias = true;
    if (nonblank(RepSpec.f2)) then begin
      if (Confr.AddrName>toconf) then begin foundalias = false; end;
    end;  
    if (nonblank(RepSpec.f1)) then begin
      if (Confr.AddrCode<frus) then begin foundalias = false; end;
      if (Confr.AddrCode>tous) then begin foundalias = false; end;
    end;  
    while (LoopKey("InternalName",EMAsr,1,foundalias)) begin
      if (EMAsr.InternalName!=Confr.AddrName) then begin foundalias = false; end;
      if (foundalias) then begin
        if (firstf) then begin
          StartFormat(15);
           OutString(0,0,EMAsr.InternalName,false);
           GetUserName(Confr.AddrCode,tstr);
           OutString(200,0,tstr,false);
          EndFormat;
          Gray_Divider(0,300);
          firstf = false;
        end;  
        StartFormat(15);
         OutString(11,0,EMAsr.Pop3Serv,false);
         OutString(100,0,EMAsr.Field,false);
         OutString(200,0,EMAsr.MatchText,false);
        EndFormat; 
      end;  
    end;
    if (firstf==false) then begin
      Gray_Divider(0,1);               
    end;
  end;
  PrintUsersWithNoAlises(frus,tous);
  EndJob;  
  RETURN;
END;
#endif
