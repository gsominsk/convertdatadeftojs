external procedure HT2Per(Date, Date , var string);

global
procedure HTContacts(string a, string b, var string res)
begin
  if (blank(a) and blank(b)) then begin
     res = USetStr(16576);
  end else begin
     res = USetStr(16575);
     res = res & " " & a;
     if (a <> b) then begin
        res = res & " : " & b;
     end;
  end;
  return;
end;

procedure CallHistHeader(record RcVc RepSpec,string frcu,string tocu)
begin
  Integer rw;
  string 255 tstr;
  
  rw = 1;
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTContacts(frcu,tocu,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  return;
end;

global
procedure CallHistRn(record RcVc RepSpec)
begin
  record ActVc Actr;
  Boolean found,testf;
  string 255 frcu,tocu;
  
  frcu = FirstInRange(RepSpec.f1,20);
  tocu = LastInRange(RepSpec.f1,20);
  StartReportJob(USetStr(16570));
  CallHistHeader(RepSpec,frcu,tocu);
  EndHeader;
  StartFormat(15);
   OutString(0,0,USetStr(16571),false);
   OutString(70,0,USetStr(16572),false);
   OutString(120,0,USetStr(16573),false);
   OutString(190,0,USetStr(16575),false);
   OutString(260,0,USetStr(16574),false);
  EndFormat;
  Gray_Divider(0,1);
  found = true;
  Actr.CUCode = frcu;
  Actr.TransDate = RepSpec.sStartDate;
  while (LoopKey("CUCode",Actr,2,found)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (Actr.CUCode>tocu) then begin found = false; end;
    end;
    if (nonblank(RepSpec.f1)) then begin
      if (DateInRange(Actr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
        found = false;
      end;
    end;
    if (found) then begin
      testf = true;
      if (blank(RepSpec.f1)) then begin
        if (DateInRange(Actr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
          testf = false;
        end;
      end;
      if (BuildProductCode!="StandardCRM") then begin
        if (Actr.SymbNr!=1) then begin
          testf = false;
        end;
      end;
      if (Actr.PrivateFlag!=0) then begin
        testf = false;
      end;
      if (Actr.Invalid!=0) then begin
        testf = false;
      end;
      if (testf) then begin
        StartFormat(15);
        OutDateID(0,"DblActVc",Actr.TransDate,false,Actr.SerNr);
        OutString(70,0,Actr.StartTime,false);
        OutString(120,0,Actr.EndTime,false);
        OutString(190,0,Actr.CUCode,false);
        OutString(260,0,Actr.Comment,false);
        EndFormat;
      end;
    end;
  end;
  Gray_Divider(0,1);
  EndJob;
  return;
end;