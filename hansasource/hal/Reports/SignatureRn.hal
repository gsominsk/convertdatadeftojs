global
procedure SignatureRn(record RcVc RepSpec) 
begin
  record ConfVc Confr;
  record ConfSignVc ConfSignr;
  record ConfPresVc ConfPresr;
  Boolean foundf,testf,signf,presf;
  Integer rw;
  
  StartReportJob(USetStr(10879));  
  rw = 1;
  if (nonblank(RepSpec.f1) ) then begin
    Header(rw,RepSpec.f1,1);
  end else begin
    Header(rw,USetStr(10856),1);  
  end;
  EndHeader;
  SetRepCol(2,175);
  SetRepCol(3,370);
  SetRepCol(4,445);
  SetRepCol(5,50);
  Confr.AddrName = RepSpec.f1;
  foundf = true;
  while(LoopKey("AddrName",Confr,1,foundf)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (Confr.AddrName<>RepSpec.f1) then begin
        foundf = false;
      end;	
    end;
    testf = foundf;
    if (Confr.Class!=kConfClassMailbox) then begin
      testf = false;
    end;
    if (Confr.Closed!=0) then begin
      testf = false;
    end;
    if (testf) then begin
      ConfSignr.SerNr = Confr.SerNr;
      signf = ReadFirstMain(ConfSignr,1,true);
      if ((signf) and (nonblank(RepSpec.f2))) then begin
        testf = false;
        if (StringInText(RepSpec.f2,ConfSignr)) then begin
          testf = true;
        end;
      end;
    end;
    if ((signf==false) and (RepSpec.flags[0]==1)) then begin
      testf = false;
    end;
    if (testf) then begin
      ConfPresr.SerNr = Confr.SerNr;
      presf = ReadFirstMain(ConfPresr,1,true);
      StartFormat(15);
      OutString(0,0,Confr.AddrName,false);
      OutString(2,0,Confr.Comment,false);
      if (signf==false) then begin
        OutString(3,0,USetStr(10881),false);
      end;
      if (presf) then begin
        OutStringID(4,"DblOpenPres",USetStr(10880),false,ConfPresr.SerNr);
      end;
      EndFormat;
      if (signf) then begin
        Gray_Divider(5,1);
        OutText(5,72,ConfSignr);
        Gray_Divider(5,1);
      end;
    end;
  end;
  EndJob;
  return;
end;

