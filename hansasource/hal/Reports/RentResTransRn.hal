procedure FindTransactions(record LocationVc Locationr,record RcVc RepSpec,var val ton, var val toff, var val tcurr)
begin
  record RentResVc Rentr;
  val onrental, offrental,current;
  boolean TrHs, testf;
  
  TrHs = true; 
  Rentr.Location = Locationr.Code;
  while (LoopKey("Location",Rentr,1,TrHs)) begin
    if (TrHs) then begin
      if (Rentr.Location > Locationr.Code) then begin
        TrHs = false;
      end;
    end;
    if (TrHs) then begin
      testf = true;  
      if (Rentr.Price == 0) then begin
        testf = false;
      end;
      if (testf) then begin
        if (DateInRange(Rentr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)) then begin
          onrental = onrental + 1;
        end;
        if (nonblankdate(Rentr.EndDate)) then begin
          if (DateInRange(Rentr.EndDate,RepSpec.sStartDate,RepSpec.sEndDate)) then begin
            offrental = offrental + 1;
          end;
        end;
        if (blankdate(Rentr.EndDate)) or (Rentr.EndDate > RepSpec.sEndDate) then begin
           current = current + 1;       
        end;     
      end;
    end;        
  end;
  if (onrental > 0) or (offrental > 0) or (current > 0) then begin
    StartFormat(15);
    OutString(0,0,Locationr.Code,false);
    OutVal(100,0,onrental,M40Val,true);
    OutVal(140,0,offrental,M40Val,true);
    Outval(200,0,current,M40Val,true);
    EndFormat;
  end;
  ton = ton + onrental;
  toff = toff + offrental;
  tcurr = tcurr + current;
  return;
end;  

global 
procedure RentResTransRn(record RcVc RepSpec)
begin
  record LocationVc Locationr;
  boolean TrHs, testf;
  integer qty;
  val ton, toff, tcurr;
  StartReportJob(USetStr(12860));
  EndHeader;
  
  StartFormat(15);
  OutString(0,0,USetStr(12861),false);
  OutString(100,0,USetStr(12862),true);
  OutString(140,0,USetStr(12863),true);
  OutString(200,0,USetStr(12864),true);
  OutString(300,0,USetStr(12865),true);
  OutString(1,0,USetStr(12866),true);
  EndFormat;
  Gray_Divider(0,1);
  TrHs = true;
  Locationr.Code = RepSpec.f1;
  while (LoopMain(Locationr,1,TrHs)) begin
    if (TrHs) then begin
      if (nonblank(RepSpec.f1)) then begin
        if (Locationr.Code > RepSpec.f1) then begin
          TrHs = false;
        end;
      end;
    end;
    if (TrHs) then begin
      FindTransactions(Locationr,RepSpec,ton,toff,tcurr);  
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
  OutString(0,0,USetStr(12867),false);
  OutVal(100,0,ton,M40Val,true);
  OutVal(140,0,toff,M40Val,true);
  OutVal(200,0,tcurr,M40Val,true);
  EndFormat;
  EndJob;
  return;
end;

