// Not used any more, checkins and checkouts don't generate activties
global
procedure HotelEventsRn(record RcVc RepSpec)
begin
  Integer i;
  record ActVc Actr;
  Boolean TsHr,testf;
  record HotelBlock HSr;
  Integer p1,p2,p3,p4,p5,p6;
  Integer keys;
  String 20 mykey;
  String 20 tstr;
  LongInt tt;

  p1= 0;
  p2= 70;
  p3= 125;
  p4= 180;
  p5= 260;
  p6= 350;
  tt = 0;
  Blockload(HSr);  
  StartReportJob(USetStr(9915));
  EndHeader;
  if ((blank(HSr.CheckoutMes)) and (blank(HSr.CheckinMes))) then begin
    StartFormat(15);
    OutString(p1,0,USetStr(9914),false);
    EndFormat;
    goto LHotelEventsRn;
  end;

  StartFormat(15);
  OutString(p1,0,USetStr(9916),false);
  OutString(p2,0,USetStr(9917),false);
  OutString(p3,0,USetStr(9918),false);
  OutString(p4,0,USetStr(9919),false);
  OutString(p5,0,USetStr(9920),false);  
  OutString(p6,0,USetStr(9921),false);  
  EndFormat;
  
  Gray_Divider(0,1);
  TsHr = true;
  mykey = "TransDate";
  Actr.TransDate = RepSpec.sStartDate;
  while (LoopKey(mykey,Actr,1,TsHr)) begin
    testf = true;
    if (DateInRange(Actr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
      TsHr = false;
    end;
    if (TsHr==false) then begin
      testf = false;
    end;
    if ((nonblank(RepSpec.f1)) and (RepSpec.f1!=Actr.MainPersons)) then begin
      testf = false;
    end;
    if (testf) then begin
      testf = false;
      if (Actr.ActType==HSr.CheckoutMes) then begin
        tstr = USetStr(9922);
        testf = true;
        if (RepSpec.flags[0]==2) then begin
          testf = false;
        end;   
      end;
      if (Actr.ActType==HSr.CheckinMes) then begin
        tstr = USetStr(9923);
        testf = true;
        if (RepSpec.flags[0]==1) then begin
          testf = false;
        end;
      end;
    end;
    if (testf) then begin
      StartFormat(15);        
      OutDate(p1,0,Actr.TransDate,false);
      OutString(p2,0,Actr.StartTime,false);
      OutString(p3,0,tstr,false);
      OutString(p4,0,Actr.Resources,false);
      OutString(p5,0,Actr.MainPersons,false);
      OutString(p6,0,Actr.CUName,false);
      EndFormat;
      tt = tt + 1;
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
  OutString(p1,0,USetStr(9924),false);
  OutLongInt(p3,0,tt,true);
  EndFormat;
LHotelEventsRn:;
  EndJob;
  return;
end;

