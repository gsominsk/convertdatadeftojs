external procedure ExtractObj(string,var Integer,var string);
external procedure HT2Per(Date, Date , var string);
external procedure HTPers(string,var string);

global
procedure TargTimeJRn(record RcVc RepSpec)
BEGIN
  Integer rw;
  string 255 tstr;
  string 10 frpers,topers,usercode;
  record TargTimeVc TargTimer;
  row TargTimeVc TargTimerw;
  Boolean TrHs,testf;
  Integer keys;
  record CalDispGroupBlock CalDispGroupRec;
  row CalDispGroupBlock CalDispGrouprw;
  Integer i,rwcnt,pos; 
  string 255 dispgr;
  
  if (nonblank(RepSpec.AccStr)) then begin
    BlockLoad(CalDispGroupRec);
    rwcnt = MatRowCnt(CalDispGroupRec);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(CalDispGroupRec,i,CalDispGrouprw);
      if (CalDispGrouprw.Code==RepSpec.AccStr) then begin
        dispgr = CalDispGrouprw.DefStr;
        goto LBREAK;
      end;
    end;
LBREAK:;  
  end;
  frpers = FirstInRange(RepSpec.f1,10);
  topers = LastInRange(RepSpec.f1,10);
  StartReportJob(USetStr(14400));
  rw = 1;
   HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
   Header(rw,tstr,1);
   rw = rw + 1;
   HTPers(RepSpec.f1,tstr);
   Header(rw,tstr,1);
   rw = rw + 1;
   if (nonblank(RepSpec.AccStr)) then begin
     tstr = USetStr(14401) & ": " & RepSpec.AccStr; 
     Header(rw,tstr,1);
   end;
  EndHeader;
  StartFormat(15);
   OutString(0,0,USetStr(14402),false);
   OutString(80,0,USetStr(14403),false);
   OutString(160,0,USetStr(14404),false);
  EndFormat;
  StartFormat(15);
   OutString(15,0,USetStr(14405),false);
   OutString(95,0,USetStr(14406),false);
   OutString(375,0,USetStr(14407),false);
   OutString(425,0,USetStr(14408),false);
  EndFormat;
  Gray_Divider(0,1);
  keys = 1;
  if (nonblank(RepSpec.f1)) then begin keys = 2; end;
  TrHs = true;
  TargTimer.StartDate = RepSpec.sStartDate;
  TargTimer.Person = frpers;
  while (LoopKey("StartDate",TargTimer,keys,TrHs)) begin
    if (TargTimer.StartDate>RepSpec.sEndDate) then begin
      TrHs = false;
    end;
    if (nonblank(RepSpec.f1)) then begin
      if (TargTimer.Person<frpers) then begin TrHs = false; end;
      if (TargTimer.Person>topers) then begin TrHs = false; end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(dispgr)) then begin
        testf = false;
        pos = 0;
        ExtractObj(dispgr,pos,usercode);
        while (nonblank(usercode)) begin
          if (TargTimer.Person==usercode) then begin 
            testf = true;
            goto L88;
          end;
          ExtractObj(dispgr,pos,usercode);
        end;
L88:;      
      end;          
      if (testf) then begin
        StartFormat(15);
         OutString(0,0,TargTimer.Person,false);
         OutString(80,0,TargTimer.StartDate,false);
         OutString(160,0,TargTimer.Name,false);
        EndFormat;
        rwcnt = MatRowCnt(TargTimer);
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(TargTimer,i,TargTimerw);
          StartFormat(15);
           OutString(15,0,TargTimerw.ActType,false);
           OutString(95,0,TargTimerw.Text,false);
           OutString(375,0,TargTimerw.Days,false);
           OutString(425,0,TargTimerw.Hours,false);
          EndFormat;          
        end;
        StartFormat(15);
        EndFormat;
      end;
    end;
  end;
  EndJob;
  RETURN;
END;