external procedure FindUserMailboxName(string,var string,var string);
external function Integer CheckConfAccess(record ConfVc,record ConfVc);

procedure PrintConferencePath(record ConfVc Mailboxr,Array record ConfVc aCnfr,var Integer acnt)
begin
  record ConfVc Cnfr;
  Integer i,pos;
  string 255 dblstr;
  
  pos = 0;
  for (i=acnt-1;i>=0;i=i-1) begin
    pos = (acnt - 1 - i)*11;
    RecordCopy(Cnfr,aCnfr[i]);
    dblstr = "";
    switch (Cnfr.Class) begin
      case kConfClassFolder:
        dblstr = "DblConferenceW";
      case kConfClassArchive:
        dblstr = "DblArchive";
      otherwise
        dblstr = "DblConference";
    end;
    if (CheckConfAccess(Mailboxr,Cnfr)==0) then begin
      dblstr = "";
    end;
    StartFormat(15);
     OutStringID(pos,dblstr,Cnfr.AddrName,false,Cnfr.SerNr);
    EndFormat;
  end;
  return;
end;

procedure FindConferencePath(record ConfVc Cnfr,Array record ConfVc aCnfr,var Integer acnt)
begin
  record ConfVc MCnfr;
  Integer l;
  
  aCnfr[acnt] = Cnfr;
  acnt = acnt + 1;
  if (Cnfr.MotherConf>=0) then begin
    MCnfr.SerNr = Cnfr.MotherConf;
    if (ReadFirstMain(MCnfr,1,true)) then begin
      if (MCnfr.MotherConf>=0) then begin
        FindConferencePath(MCnfr,aCnfr,acnt);
      end;
    end;
  end;  
  return;
end;

global
procedure FindConfRn(record RcVc RepSpec)
begin
  record ConfVc Cnfr;
  Array record ConfVc aCnfr;
  record ConfVc Mailboxr;
  Integer rw;
  Integer acnt;
  string 255 cstr,tstr;
  
  StartReportJob(USetStr(39350));
  rw = 1;
  Header(rw,USetStr(39351) & RepSpec.f1,1);
  EndHeader;
  
  Cnfr.AddrName = RepSpec.f1;
  if (ReadFirstKey("AddrName",Cnfr,1,true)) then begin
    FindConferencePath(Cnfr,aCnfr,acnt);
    FindUserMailboxName(CurrentUser,cstr,tstr);
    Mailboxr.SerNr = StringToLongInt(cstr);
    ReadFirstMain(Mailboxr,1,true);
    PrintConferencePath(Mailboxr,aCnfr,acnt);
  end else begin
    StartFormat(15);
     OutString(0,0,USetStr(39352),false);
    EndFormat;
  end;
  EndJob;
  return;
end;