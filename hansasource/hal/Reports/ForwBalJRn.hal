#ifdef HAL

global
procedure ForwBalJRn(record RcVc RepSpec)
begin
  Record FBVc FBr;
  row FBVc FBrw;
  boolean TrHs,testf;
  string 60 keyname;
  date sdate,edate;
  longint fn,ln;
  integer i,ar,rw;
  string 255 tstr;
  
  sdate=RepSpec.sStartDate;
  edate=RepSpec.sEndDate;
  fn=FirstInRange(RepSpec.f1,15);
  ln=LastInRange(RepSpec.f1,15);
  
  StartReportJob(USetStr(10940));
  tstr = "Period: " & PackStrDate(sdate) & ":" & PackStrDate(edate); 
  Header(1,tstr,1);
  rw=2;
  if (nonblank(RepSpec.f1)) then begin
    tstr=USetStr(10943) & ": " & RepSpec.f1;
    Header(rw,tstr,1);
    rw=rw+1;
  end;
  if (nonblank(RepSpec.f2)) then begin
    tstr=USetStr(10942) & ": " & RepSpec.f2;
    Header(rw,tstr,1);
    rw=rw+1;
  end;
  if (RepSpec.flags[1]==1) then begin
    tstr=USetStr(10941);
    Header(rw,tstr,1);
    rw=rw+1;
  end;
  if (RepSpec.ArtMode==0) then begin
    tstr=USetStr(10944) & " 1";
  end else begin
    tstr=USetStr(10944) & " 2";
  end;
  Header(rw,tstr,1);
  EndHeader;
  
  if (blank(RepSpec.f1)) then begin
    keyname="TransSerNr";
    FBr.TransDate=sdate;
  end else begin
    keyname="SerNr";
    FBr.SerNr=fn;
  end;
  
  SetRepCol(2,50);
  SetRepCol(3,120);
  SetRepCol(4,220);
  SetRepCol(5,380);
  
  StartFormat(15);
  OutString(0,0,USetStr(10945),false);
  OutString(2,0,USetStr(10946),false);
  OutString(3,0,USetStr(10947),false);
  OutString(4,0,USetStr(10948),false);
  OutString(1,0,USetStr(10949),true);
  EndFormat;
  StartFormat(15);
  OutString(2,0,USetStr(10950),false);
  OutString(3,0,USetStr(10951),false);
  OutString(4,0,USetStr(10952),false);
  OutString(5,0,USetStr(10953),true);
  OutString(1,0,USetStr(10954),true);
  EndFormat;
  
  Black_Divider(0,1);
  
  
  TrHs=true;
  while (LoopKey(keyname,FBr,1,TrHs)) begin
    testf=true;    
    if (keyname=="TransSerNr") then begin
      if (FBr.TransDate>edate) then begin
        TrHs=false;testf=false;
      end;
    end;
    if (keyname=="SerNr") and (ln>0) then begin
      if (FBr.SerNr>ln) then begin
        TrHs=false;testf=false;
      end;
      if (FBr.TransDate>edate) or (FBr.TransDate<sdate) then begin
        testf=false;
      end;
    end;
    if (testf) then begin
      if (RepSpec.flags[1]==0) and (FBr.SimFlag==1) then begin
        testf=false;
      end;
      if (nonblank(RepSpec.f2)) and (SetInSet(RepSpec.f2,FBr.Objects)==false) then begin
        testf=false;
      end;
    end;
    
    if (testf) then begin
      StartFormat(15);
      OutString(0,"DblFBVc",FBr.SerNr,false);
      OutDate(2,0,FBr.TransDate,false);
      OutString(3,0,FBr.Objects,false);
      OutString(4,0,FBr.Comment,false);
      if (FBr.SimFlag==1) then begin
        OutString(1,0,USetStr(10955),true);
      end;  
      EndFormat;
      ar=MatRowCnt(FBr);
      for (i=0;i<ar;i=i+1) begin
        MatRowGet(FBr,i,FBrw);
        Gray_Divider(2,1);
        StartFormat(15);
        outstring(2,0,FBrw.AccNumber,false);
        OutString(3,0,FBrw.Comment,false);
        if (RepSpec.ArtMode==0) then begin
          OutVal(5,0,FBrw.DebVal,M4Val,true);
          OutVal(1,0,FBrw.CredVal,M4Val,true);
        end else begin
          OutVal(5,0,FBrw.Deb2Val,M4Val,true);
          OutVal(1,0,FBrw.Cred2Val,M4Val,true);
        end; 
        endformat;
        if (nonblank(FBrw.CurncyCode)) then begin
          StartFormat(15);
            OutString(4,0,FBrw.CurncyCode,false);
            OutVal(5,0,FBrw.CurDebVal,M4Val,true);
            OutVal(1,0,FBrw.CurCredVal,M4Val,true);
          endformat;
        end;
      end;
      Black_Divider(0,1);   
    end;
  end;
  
  
  EndJob;
  
  return;
end;


#endif