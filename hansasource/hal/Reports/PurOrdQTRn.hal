external function Boolean ContactInGroupClass2(string,string,string,string,string,string,string);
external procedure HTSuppGroup(string,var string);
external procedure BeginCurncyTotals(var Integer);
external procedure AddCurncyTotals(var array string,var array val,var Integer,string,val);
external procedure PrintCurncyTotals(array string,array val,array val,array val,Integer,Integer,Integer,Integer,Integer,Boolean,Integer);
external procedure HTDetailLevel(Integer, var string);
external procedure HTSupplier(string, var string);
external procedure HTNr(Integer,Integer,LongInt,LongInt,var string);
external procedure HTVendClass(string,var string);
external procedure HTCustClassType(string,var string);
external function Boolean SetInSet2(string,string);
external procedure ClassTypef(string,string,var Boolean);

procedure RepPOQT( record POQTVc POQTp)
begin
  Integer i,rwcnt;
  row POQTVc POQTrw;

  StartFormat(15);
  OutLongInt(0,"DblPOQTVc",POQTp.SerNr,false);
  OutDate(130,0,POQTp.TransDate,false);
  OutString(220,0,POQTp.PayDeal,false);
  EndFormat;
  StartFormat(15);
  OutString(50,0,POQTp.VECode,false);
  OutString(200,0,POQTp.Addr0,false);
  EndFormat;
  StartFormat(15);
  OutString(200,0,POQTp.Addr1,false);
  StartFormat(15);
  EndFormat;
  StartFormat(15);
  OutString(50,0,POQTp.OurContact,false);     
  OutString(200,0,POQTp.Addr2,false);
  EndFormat;
  if nonblank(POQTp.Addr3) then begin
    StartFormat(15);
    OutString(200,0,POQTp.Addr3,false);
    EndFormat;
  end;
  if nonblank(POQTp.VEContact) then begin
    StartFormat(15);
    OutString(200,0,POQTp.VEContact,false);     
    EndFormat;
  end;
  if nonblank(POQTp.CurncyCode) then begin
    StartFormat(15);
    OutString(460,0,POQTp.CurncyCode,false);
    EndFormat;
  end;
  rwcnt = MatRowCnt(POQTp);
  for (i = 0 ; i<rwcnt ;i=i+1) begin
    MatRowGet(POQTp,i,POQTrw);
    if (POQTrw.stp==1) then  begin
      StartFormat(15);
      OutString(20,0,POQTrw.ArtCode,false);
      OutVal(124,0,POQTrw.Quant,M4UVal,true);
      OutString(130,0,POQTrw.Spec,false);
      OutVal(340,0,POQTrw.Price,M423Val,true);
      OutVal(370,0,POQTrw.vRebate,M41Val,true);
      OutVal(460,0,POQTrw.Sum,M4Val,true);
      OutString(480,0,POQTrw.VATCode,true);
      EndFormat;
    end;
  end;
  Black_Divider(380,460);
  StartFormat(15);
  if (POQTp.ExportFlag!=0) then begin
    OutString(20,0,USetStr(4948),false);
  end;
  OutVal(260,0,POQTp.Sum1,M4Val,true);
  OutVal(360,0,POQTp.Sum3,M4Val,true);
  OutVal(460,0,POQTp.Sum4,M4Val,true);
  EndFormat;
return;
end;


global
procedure PurOrdQTJRn(record RcVc RepSpec)
begin
  record POQTVc POQTr;
  record CUVc VEr;
  Boolean TrHs;
  val iv;
  val TotSum;
  Boolean testf;
  LongInt afr,ato;
  string 255 tstr;
  Integer rw;
  array val av1;
  array val av2;
  array val av3;
  array string 20 acur;
  Integer curcnt;

  rw = 1;
  afr = FirstInRange(RepSpec.f1,30);
  ato = LastInRange(RepSpec.f1,30);

  TotSum = 0;
  BeginCurncyTotals(curcnt);
  StartReportJob(USetStr(17950));
  if nonblank(RepSpec.f1) then begin
    HTNr(17952,17951,afr,ato,tstr);
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTDetailLevel(RepSpec.ArtMode,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  if (nonblank(RepSpec.FirstAcc)) then begin
      tstr = USetStr(17953);
      tstr = tstr & ": " & RepSpec.FirstAcc;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTSupplier(RepSpec.f2,tstr);
  if nonblank(tstr) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  if (nonblank(RepSpec.f3)) then begin
    tstr = USetStr(2768);
    tstr = tstr & ": " & RepSpec.f3;
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  HTSuppGroup(RepSpec.f9,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTVendClass(RepSpec.f4,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  HTCustClassType(RepSpec.f5,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  EndHeader;
  if ((RepSpec.ArtMode==2) or (RepSpec.ArtMode==0)) then  begin
    StartFormat(15);
    OutString(0,0,USetStr(4942),false);
    OutString(60,0,USetStr(4943),false);
    OutString(140,0,USetStr(4944),false);
    OutString(1,0,USetStr(4947),true);
    EndFormat;
  end;
  if (RepSpec.ArtMode==1)  then  begin
    StartFormat(15);
    OutString(0,0,USetStr(4942),false);
    OutString(130,0,USetStr(4943),false);
    EndFormat;
  end;
  POQTr.SerNr = afr;
  if (RepSpec.ArtMode==0) then begin
    Gray_Divider(0,1);
  end;
  TrHs = true;
  while LoopMain(POQTr,1,TrHs) begin
    if (ato<>-1) then begin
      if (POQTr.SerNr>ato) then begin
        TrHs = false;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f2) and (RepSpec.f2<>POQTr.VECode)) then begin
         testf = false;
      end;
      if (nonblank(RepSpec.FirstAcc) and (RepSpec.FirstAcc<>POQTr.POClass)) then begin
         testf = false;
      end;
      if ((nonblank(RepSpec.f3)) and (RepSpec.f3<>POQTr.Location)) then begin 
        testf = false; 
      end;
      if (DateInRange(POQTr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin testf = false; end;
      if (testf) then begin
        testf = ContactInGroupClass2(POQTr.VECode,RepSpec.f9,RepSpec.f4,RepSpec.f5,"SUPP","","");
      end; 
      if (testf) then begin
        if ((RepSpec.ArtMode==0) or (RepSpec.ArtMode==1)) then begin
          iv = POQTr.Sum4;
        end;
        if (RepSpec.ArtMode==2) then begin
          iv = POQTr.Sum1;
        end;
        TotSum = TotSum + iv;
        AddCurncyTotals(acur,av1,curcnt,POQTr.CurncyCode,iv);
        if  (RepSpec.ArtMode==1) then begin
          Gray_Divider(0,1);
          RepPOQT(POQTr);
        end;
        if ((RepSpec.ArtMode==0) or (RepSpec.ArtMode==2)) then begin
          StartFormat(15);
          OutLongInt(0,"DblPOQTVc",POQTr.SerNr,false);
          OutDate(60,0,POQTr.TransDate,false);
          OutString(140,0,POQTr.Addr0,false);
          OutVal(440,0,iv,M4Val,true);
          OutString(445,0,POQTr.CurncyCode,false);
          EndFormat;
         end;
      end;
    end;
  end;
  Gray_Divider(0,1);
  if (RepSpec.ArtMode==1) then begin
    PrintCurncyTotals(acur,av1,av2,av3,curcnt,460,-1,-1,140,true,RepSpec.PocketPC);
  end else begin
    PrintCurncyTotals(acur,av1,av2,av3,curcnt,440,-1,-1,140,true,RepSpec.PocketPC);
  end; 
  EndJob;
  return;
end;
