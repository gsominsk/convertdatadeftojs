procedure SerialInInternalStock(string item,string ser,var string location,var val q)
BEGIN
  record IntSerBalVc SBr;
  record MainStockBlock MainStockRec;
  
  q = blankval;
  SBr.Item = item;
  SBr.Serial = ser;
  if (ReadFirstKey("ItemSerial",SBr,2,true)) then begin
    q = SBr.Quant;
    if (q > 0) then begin
      location = SBr.Location;
    end;
  end;
  RETURN;
END;

global 
procedure DeptAssetRn(record RcVc RepSpec)
begin
  record AT2UnitVc AT2r;
  record RentINVc Rentr;
  boolean TrHs, testf;
  string 255 ItemCode,location,orglocation;
  val instock;

  StartReportJob(USetStr(12810));
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(12811),false);
  OutString(90,0,USetStr(12812),false);
  OutString(160,0,USetStr(12813),false);
  OutString(350,0,USetStr(12814),false);
  OutString(420,0,USetStr(12815),false);
  EndFormat;
  Gray_Divider(0,1);
  TrHs = true;
  AT2r.DepCode = RepSpec.f1;
  while (LoopKey("DepCode",AT2r,1,TrHs)) begin
    if (TrHs) then begin
      if (nonblank(RepSpec.f1)) then begin
        if (AT2r.DepCode > RepSpec.f1) then begin
          TrHs = false;
        end;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (blank(AT2r.SerialNr)) then begin
        testf = false;
      end;
      if (AT2r.Activef <> 0) then begin
        testf = false;
      end;
      if (testf) then begin
        Rentr.SerialNr = AT2r.SerialNr;
        if (ReadFirstKey("SerialNr",Rentr,1,true)) then begin
          if ((Rentr.SerialNr==AT2r.SerialNr) and (Rentr.InventoryNr==AT2r.InventoryNr)) then begin
            ItemCode = Rentr.Code;
            orglocation = Rentr.OrgLocation;
            testf = true;
          end else begin
            testf = false;
          end;
        end else begin
          testf = false;
        end;
        if (testf) then begin
          if (nonblank(AT2r.DepCode)) then begin
            location = AT2r.DepCode;
          end;
          SerialInInternalStock(ItemCode,AT2r.SerialNr,location,instock); 
          if (instock > 0) then begin
            if (location <> AT2r.DepCode) then begin   
              StartFormat(15);
              OutString(0,0,AT2r.InventoryNr,false);
              OutString(90,0,AT2r.SerialNr,false);
              OutString(160,0,AT2r.Description,false);
              OutString(350,0,AT2r.DepCode,false);
              OutString(420,0,location,false);
              EndFormat;
            end;
          end;
          if (instock <= 0) then begin
            if (orglocation <> AT2r.DepCode) then begin   
              StartFormat(15);
              OutString(0,0,AT2r.InventoryNr,false);
              OutString(90,0,AT2r.SerialNr,false);
              OutString(160,0,AT2r.Description,false);
              OutString(350,0,AT2r.DepCode,false);
              OutString(420,0,orglocation,false);
              EndFormat;
            end;
          end;
        end;
      end;
    end;
  end;
  EndJob; 
  return;
end;