external function Boolean WrapAndPrint(Integer,string,Integer);
external procedure HTNr(Integer,Integer,LongInt,LongInt,var string);
external procedure PrintRentQTDetail(record RentQTVc);
external procedure PrintRentQTDetailHeader();

procedure PrintActivity(record ActVc Actr)
BEGIN
  Integer i,rwcnt;
  row ActVc Actrw;
  
  StartFormat(15);
   OutStringID(0,"DblActRec",Actr.TransDate,false,Actr.SerNr);
   OutString(70,0,Actr.ActType,false);
   OutString(120,"DblPersAct",Actr.MainPersons,false);
   OutString(160,0,Actr.Contact,false);
   if (WrapAndPrint(280,Actr.Comment,28)) then begin
     EndFormat;
   end;
  rwcnt = MatRowCnt(Actr);
  for (i=0; i<rwcnt ;i=i+1) begin
    MatRowGet(Actr,i,Actrw);
    StartFormat(15);
    // OutString(80,0,Actrw.TextCode,false);
    if (WrapAndPrint(15,Actrw.Text,100)) then begin
      EndFormat;
    end;
  end;
  Gray_Divider(0,1);
  if (rwcnt>1) then begin
    StartFormat(10);
    EndFormat;
  end;
  RETURN;
END;

procedure PrintActivityLinkedActivities(record ActVc Actr)
BEGIN
  record ActVc LinkActr;
  record RLinkVc RLinkr;
  Integer actnr;

  actnr = 1;
  while (ReadRecordLink(Actr,actnr,LinkActr,RLinkr)) begin
    PrintActivity(LinkActr);
    actnr = actnr + 1;
  end;  
  RETURN;
END;

procedure PrintQuotationActivities(record RentQTVc RentQTr)
BEGIN
  record ActVc Actr;
  record RLinkVc RLinkr;
  Integer actnr;
  Boolean firstf;

  firstf = true;
  actnr = 1;
  while (ReadRecordLink(RentQTr,actnr,Actr,RLinkr)) begin
    if (firstf) then begin
      StartFormat(15);
      EndFormat;
      StartFormat(15);
      OutString(0,0,USetStr(6682),false);
      OutString(70,0,USetStr(6687),false);
      OutString(120,0,USetStr(6684),false);
      OutString(160,0,USetStr(6688),false);
      OutString(280,0,USetStr(6685),false);
      EndFormat;
      Gray_Divider(0,1);
      firstf = false;
    end;
    PrintActivity(Actr);
    PrintActivityLinkedActivities(Actr);
    actnr = actnr + 1;
  end;  
  RETURN;
END;

global
procedure RentQTStatusRn(record RcVc RepSpec)
BEGIN
  record RentQTVc RentQTr;
  Boolean TrHs;
  LongInt afr,ato;
  string 255 tstr;
  Integer rw;

  afr = FirstInRange(RepSpec.f1,10);
  ato = LastInRange(RepSpec.f1,10);
  StartReportJob(USetStr(11292));
  rw = 1;
  HTNr(2689,2690,afr,ato,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  EndHeader;
  RentQTr.SerNr = afr;
  TrHs = true;
  while (LoopMain(RentQTr,1,TrHs)) begin
    if ((ato!=-1) and (RentQTr.SerNr>ato)) then begin TrHs = false; end;
    if (TrHs) then begin
      PrintRentQTDetailHeader;
      Gray_Divider(0,1);
      PrintRentQTDetail(RentQTr);
      PrintQuotationActivities(RentQTr);
      Black_Divider(0,1);
    end;
  end;
  EndJob;
  RETURN;
END;