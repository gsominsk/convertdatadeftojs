external function Boolean SetInSet2(string,string);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure HTItemClass(string,var string);
external procedure HTArtGroup(string, var string);    
external procedure HTCustCat(string,var string);
external procedure HTCustClass(string,var string);
external function Boolean HasItemClassification();
external function Boolean HasItemGroups();   
external function Boolean HasContactClassification();
external function Boolean HasCategories();

global
function boolean COINStatisticsRClassOnOKWindow(Integer wn)
begin
  record RcVc RepSpec;
  boolean res;
  
  GetWindowRecord(wn,RepSpec);
  if (blank(RepSpec.f1) or blank(RepSpec.f2) or blank(RepSpec.f3)) then begin
    MessageBox(14803,"");
    res = false;
  end else begin
    res = true;
  end;
  
  COINStatisticsRClassOnOKWindow = res;
  return;
end;

global
procedure COINStatisticsRn(record RcVc RepSpec)
begin
  record COVc COr;
  row COVc COrw;
  boolean testf,test2f;
  integer c1,c2,c3,c4,c5,c6;
  record CUVc CUr;
  record INVc INr;
  integer cnt,i,rw;
  val q,totq;
  string 255 tstr,itgroupfr,itgroupto;
  
  itgroupfr = FirstInRange(RepSpec.f4,20);
  itgroupto = LastInRange(RepSpec.f4,20);  
  
  StartReportJob(USetStr(26450));
  rw = 1;
  if (HasItemGroups) then begin
    HTArtGroup(RepSpec.f4,tstr);
    if (nonblank(tstr)) then begin
      Header(rw,tstr,1);
      rw = rw + 1;
    end;
  end;  
  if (HasItemClassification) then begin
    HTItemClass(RepSpec.f5,tstr);
    if (nonblank(tstr)) then begin
      Header(rw,tstr,1);
      rw = rw + 1;
    end; 
  end;
  if (HasCategories) then begin 
    HTCustCat(RepSpec.f3,tstr);
    if (nonblank(tstr)) then begin
      Header(rw,tstr,1);
      rw = rw + 1;
    end; 
  end;
  if (HasContactClassification) then begin 
    HTCustClass(RepSpec.f4,tstr);
    if (nonblank(tstr)) then begin
      Header(rw,tstr,1);
      rw = rw + 1;
    end;  
  end;  
  EndHeader;

  if (blank(RepSpec.f1) or blank(RepSpec.f2) or blank(RepSpec.f3)) then begin
    StartFormat(15);
    OutString(0,0,USetStr(1565),false);
    EndFormat;
  end else begin
    c1 = 0;
    c2 = 80;
    c3 = 160;
    c4 = 220;
    c5 = 290;
    c6 = 480;
    StartFormat(15);
    OutString(c1,0,USetStr(26451),false);
    OutString(c2,0,RepSpec.f1,false);
    EndFormat;
    StartFormat(15);    
    OutString(c1,0,USetStr(26387),false);
    OutString(c2,0,RepSpec.f2,false);
    EndFormat;
    StartFormat(15);    
    OutString(c1,0,USetStr(9788),false);
    OutString(c2,0,RepSpec.f3,false);
    EndFormat;
    Gray_Divider(0,1);
    StartFormat(15);    
    OutString(c1,0,USetStr(17742),false);
    OutString(c2,0,USetStr(17732),false);
    OutString(c5,0,USetStr(15015),true);
    EndFormat;
    Gray_Divider(0,1);
    
    totq = 0;
    while (LoopMain(COr,1,true)) begin
      if (COr.ContractClass==RepSpec.f1) then begin
        CUr.Code = COr.CustCode;
        if (ReadFirstMain(CUr,1,true)) then begin
          test2f = true;
          if (CUr.CustCat!=RepSpec.f2) then begin
            test2f = false;
          end;
          if (test2f) then begin 
            if (nonblank(RepSpec.Stext)) then begin
              if (SetInSet2(RepSpec.Stext,CUr.Classification)==false) then begin
                test2f = false;
              end;
            end;  
          end;
          if (test2f) then begin
            cnt = MatRowCnt(COr);
            q = 0;
            for (i=0;i<cnt;i=i+1) begin
              MatRowGet(COr,i,COrw);
              testf = true;
              if (ReadFirstItem(COrw.ArtCode,INr,true,false)==false) then begin  testf = false; end;
              if (testf) then begin
                if (nonblank(itgroupfr)) then begin      
                  if (INr.Group<itgroupfr) then begin testf = false; end;
                end;
                if (nonblank(itgroupto)) then begin 
                  if (INr.Group>itgroupto) then begin testf = false; end;
                end;
              end;
              if (testf) then begin
                if (nonblank(RepSpec.f5)) then begin
                  if (SetInSet2(RepSpec.f5,INr.DispGroups)==false) then begin
                    testf = false;
                  end;
                end;
              end;               
              if (testf) then begin
                if (COrw.ArtCode!=RepSpec.f3) then begin
                  testf = false;  
                end;
              end;
              if (testf) then begin 
                q = q + COrw.Quant;
              end;
            end;
            if (q!=0) then begin
              totq = totq + q;
              StartFormat(15);
              OutString(c1,0,COr.CustCode,false);
              OutString(c2,0,COr.Addr0,false);
              OutVal(c5,0,q,M4Val,true);
              EndFormat;
            end;
          end;
        end;
      end;
    end;

    if (totq>0) then begin
      Black_Divider(0,1);
      StartFormat(15);
      OutString(c1,0,USetStr(11021),false);
      OutVal(c5,0,totq,M4Val,true);
      EndFormat;
    end;
  end;
  
  EndJob;
  return;
end;