external procedure HT2Per(Date, Date , var string);
external function Boolean SetInSet2(string,string);

global
procedure NoActCURn(record RcVc RepSpec)
BEGIN
  record CUVc CUr;
  record ActVc Actr;
  Boolean cufoundf;
  Boolean actfoundf;
  Boolean testf;
  Boolean printf;
  string 255 tstr;
  Integer rw,keys;
  LongInt cucnt;
  
  switch (RepSpec.Comparison) begin
    case 0:
      StartReportJob(USetStr(9353));
    otherwise
      StartReportJob(USetStr(9355));
  end;
   rw = 1;
   HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
   Header(rw,tstr,1);
   rw = rw + 1;
   if (nonblank(RepSpec.ObjType)) then begin
     tstr = USetStr(9324);
     tstr = tstr & ": " &  RepSpec.ObjType;
     Header(rw,tstr,1);
     rw = rw + 1;
   end;
  EndHeader;
  StartFormat(15);
   OutString(0,0,USetStr(8104),false);
   OutString(100,0,USetStr(8105),false);  
  EndFormat;
  Gray_Divider(0,1);
  cucnt = 0;
  cufoundf = true;
  while (LoopMain(CUr,1,cufoundf)) begin
    testf = true;
    if (nonblank(RepSpec.FirstAcc)) then begin
      if (RepSpec.FirstAcc!=CUr.CustCat) then begin
        testf = false;
      end;  
    end;  
    if (nonblank(RepSpec.LastAcc)) then begin
      if (RepSpec.LastAcc!=CUr.SalesMan) then begin
        testf = false;
      end;  
    end;
    if (nonblank(RepSpec.Stext)) then begin
      if (SetInSet2(RepSpec.Stext,CUr.Classification)==false) then begin
        testf = false;
      end;  
    end;
    if (CUr.CUType==0) then begin
      testf = false;
    end;
    if (cufoundf==false) then begin
      testf = false;
    end;
    if (testf) then begin
      switch (RepSpec.Comparison) begin
        case 0:
          printf = true;
        otherwise
          printf = false;
      end;
      actfoundf = true;
      ResetLoop(Actr);
      Actr.CUCode = CUr.Code;
      keys = 1;
      if (RepSpec.flags[1]==0) then begin
        Actr.TransDate = RepSpec.sStartDate;    
        keys = 2;
      end;
      while (LoopKey("CUCode",Actr,keys,actfoundf)) begin
        testf = true;
        if (Actr.CUCode!=CUr.Code) then begin
          actfoundf = false;
        end;
        if (RepSpec.flags[1]==0) then begin
          if (Actr.EndDate>RepSpec.sEndDate) then begin
            actfoundf = false;
          end;
        end;
//        if ((nonblank(RepSpec.ObjType)) and (RepSpec.ObjType!=Actr.ActType)) then begin      
//          testf = false;
//        end;
        if (nonblank(RepSpec.ObjType)) then begin      
          if (RepSpec.ObjType!=Actr.ActType) then begin
            testf = false;
          end;
        end;
        if (nonblank(RepSpec.TransStr)) then begin      
          if (RepSpec.TransStr==Actr.ActType) then begin
            testf = false;
          end;
        end;
        if (actfoundf) and (testf) then begin
          switch (RepSpec.Comparison) begin
            case 0:
              printf = false;
            otherwise
              printf = true;
          end;
        end;
      end;
      if (printf) then begin
        StartFormat(15);
        OutString(0,"DblCUVc",CUr.Code,false);
        OutString(100,0,CUr.Name,false);
        EndFormat;
        cucnt = cucnt + 1;
      end;
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
   OutString(0,0,USetStr(9354),false);
   OutLongInt(100,0,cucnt,false);
  EndFormat;
  EndJob;
  RETURN;
END;
