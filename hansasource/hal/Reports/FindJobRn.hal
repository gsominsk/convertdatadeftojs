external function string 20 FindJobCustomerCode(record JobVc);
external function string 60 FindJobCustomerName(record JobVc);

procedure PrintSubreservations(LongInt mother)
BEGIN
  record JobVc Jobr;
  Boolean foundf;
  string 40 keystr;
  
  keystr = "Mother:";
  keystr = keystr & mother;
  foundf = true;
  while (LoopKey(keystr,Jobr,1,foundf)) begin
    if (Jobr.Mother!=mother) then begin foundf = false; end;
    if (foundf) then begin
      StartFormat(15);
      OutLongInt(15,"DblJobVc",Jobr.SerNr,false);
      OutDate(80,0,Jobr.TransDate,false);
      OutDate(160,0,Jobr.EndDate,false);
      OutString(240,0,Jobr.ResStatus,false);
      OutString(280,0,FindJobCustomerCode(Jobr),false);
      OutString(360,0,FindJobCustomerName(Jobr),false);
      EndFormat;
    end;
  end;
  RETURN;
END;

function
Boolean CheckProfileinJob(record JobVc Jobr,string frprof,string toprof)
BEGIN
  Integer i,rwcnt;
  row JobVc Jobrw;
  Boolean res;
  
  rwcnt = MatRowCnt(Jobr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Jobr,i,Jobrw);
    if ((Jobrw.GuestCode>=frprof) and (Jobrw.GuestCode<=toprof)) then begin
      res = true;
      goto LCheckProfileinJob;
    end;
  end;
LCheckProfileinJob:;  
  CheckProfileinJob = res;
  RETURN;
END;

global
procedure FindJobRn(record RcVc RepSpec)
BEGIN
  Boolean foundf,testf;
  LongInt frjob,tojob;
  record JobVc Jobr;
  string 20 frres,tores;
  string 20 frcu,tocu;
  string 20 frag,toag;
  string 20 frprof,toprof;
  string 255 ckey;
  Integer rw;
  
  frjob = FirstInRange(RepSpec.f1,20);
  tojob = LastInRange(RepSpec.f1,20);
  frres = FirstInRange(RepSpec.FirstAcc,20);
  tores = LastInRange(RepSpec.FirstAcc,20);
  frprof = FirstInRange(RepSpec.f2,20);
  toprof = LastInRange(RepSpec.f2,20);
  frcu = FirstInRange(RepSpec.f3,20);
  tocu = LastInRange(RepSpec.f3,20);
  frag = FirstInRange(RepSpec.f4,20);
  toag = LastInRange(RepSpec.f4,20);
  StartReportJob(USetStr(8175));
  rw = 1;
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(8177),false);
  OutString(80,0,USetStr(8188),false);
  OutString(160,0,USetStr(8196),false);
  OutString(240,0,USetStr(8197),false);
  OutString(280,0,USetStr(8198),false);
  OutString(360,0,USetStr(8199),false);
  EndFormat;

  ckey = "SerNr";
  Jobr.SerNr = frjob;
  foundf = true;
  while (LoopKey(ckey,Jobr,1,foundf)) begin
    if (ckey=="SerNr") then begin
      if (tojob!=-1) then begin
        if (Jobr.SerNr>tojob) then begin 
          foundf = false;
        end;
      end;    
    end;
    testf = true;  
    if (foundf==false) then begin testf = false; end;
    if (testf) then begin
      if (nonblank(RepSpec.FirstAcc)) then begin
        if (Jobr.ResCode<frres) then begin testf = false; end;
        if (Jobr.ResCode>tores) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.f3)) then begin
        if (FindJobCustomerCode(Jobr)<frcu) then begin testf = false; end;
        if (FindJobCustomerCode(Jobr)>tocu) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.f4)) then begin
        if (Jobr.Source<frag) then begin testf = false; end;
        if (Jobr.Source>toag) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.LastAcc)) then begin
        if (Jobr.ResStatus!=RepSpec.LastAcc) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.ObjStr)) then begin
        if (Jobr.RefStr!=RepSpec.ObjStr) then begin testf = false; end;
      end;
    end;
    if (testf) then begin
      if ((RepSpec.flags[0]!=0) or (RepSpec.flags[1]!=0)) then begin testf = false; end;
      if (RepSpec.flags[0]!=0) then begin
        if (DateInRange(Jobr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)) then begin testf = true; end;
      end;
      if (RepSpec.flags[1]!=0) then begin
        if (DateInRange(Jobr.EndDate,RepSpec.sStartDate,RepSpec.sEndDate)) then begin testf = true; end;
      end;
    end;
    if ((testf) and (nonblank(RepSpec.f2))) then begin    
      testf = CheckProfileinJob(Jobr,frprof,toprof);
    end;
    if (testf) then begin
      StartFormat(15);
      OutLongInt(0,"DblJobVc",Jobr.SerNr,false);
      OutDate(80,0,Jobr.TransDate,false);
      OutDate(160,0,Jobr.EndDate,false);
      OutString(240,0,Jobr.ResStatus,false);
      OutString(280,0,FindJobCustomerCode(Jobr),false);
      OutString(360,0,FindJobCustomerName(Jobr),false);
      EndFormat;
      if (RepSpec.flags[2]!=0) then begin
        PrintSubreservations(Jobr.SerNr);
      end;
    end;
  end;
  EndJob;
  RETURN;
END;