external procedure HTOKF(Integer,Integer, var string);
external procedure HT2Per(Date,Date,var string);

global
procedure AddToPlanArray(LongInt sernr,string itemcode,Date orddat,Date needdat,LongInt days,val qty,var array LongInt aprodplan,var array string aitem,var array Date aorddat,var array Date aneeddat,var array LongInt adays,var array val aqty,var Integer acnt)
begin
  Integer i;
  Boolean foundf;
  array string 20 atempitem;
  
  foundf = false;
  for (i=acnt;i>0;i=i-1) begin
    if (aitem[i-1]>itemcode) then begin
      aitem[i] = aitem[i-1];
      aprodplan[i] = aprodplan[i-1];
      aorddat[i] = aorddat[i-1];
      aneeddat[i] = aneeddat[i-1];
      adays[i] = adays[i-1];
      aqty[i] = aqty[i-1];
    end else begin
      aitem[i] = itemcode;
      aprodplan[i] = sernr;
      aorddat[i] = orddat;
      aneeddat[i] = needdat;
      adays[i] = days;
      aqty[i] = qty;
      acnt = acnt + 1;
      goto LAddToPlanArray;
    end;  
  end;
  aitem[0] = itemcode;
  aprodplan[0] = sernr;
  aorddat[0] = orddat;
  aneeddat[0] = needdat;
  adays[0] = days;
  aqty[0] = qty;
  acnt = acnt + 1;
LAddToPlanArray:;
  return;
end;

global
procedure ProdPlanProdRn(record RcVc RepSpec)
begin
  record ProdPlanVc ProdPlanr;
  record INVc INr;
  row ProdPlanVc ProdPlanrw;
  Integer i,rwcnt;
  Integer acnt;
  array Date aorddat;
  array Date aneeddat;
  array LongInt adays;
  array LongInt aprodplan;
  array string 20 aitem;
  array val aqty;
  Boolean foundf,testf;
  val qty;
  string 20 previtem;
  string 255 tstr,ckey;
  Integer rw;
  Date loopdate;
  vector Boolean prntdv;
  
  acnt = 0;
  StartReportJob(USetStr(16394));    
  rw = 1;
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTOKF(RepSpec.flags[1],RepSpec.flags[2],tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  Header(rw,USetStr(18202+RepSpec.flags[7]),1);
  EndHeader;
  SetRepCol(2,80);
  SetRepCol(3,220);
  SetRepCol(4,310);
  SetRepCol(5,380);
  SetRepCol(6,150);
  StartFormat(15);
  OutString(0,0,USetStr(16381),false);
  OutString(2,0,USetStr(16383),false);
  OutString(3,0,USetStr(16385),false);
  OutString(4,0,USetStr(16386),false);
  OutString(5,0,USetStr(16387),false);
  OutString(1,0,USetStr(16384),true);
  EndFormat;

  loopdate = RepSpec.sStartDate;
  while (loopdate<=RepSpec.sEndDate) begin
    ResetLoop(ProdPlanr);
    foundf = true;
    ckey = "ProdStartDate:" & DateToString(loopdate,"YYYY-MM-DD");
    ProdPlanr.Closed = 0;
    while (LoopKey(ckey,ProdPlanr,1,foundf)) begin
      if (ProdPlanr.Closed!=0) then begin foundf = false; end;
      if (foundf) then begin
        testf = true;
        if (testf) then begin
          testf = false;
          if (RepSpec.flags[1]==1) then begin
            if (ProdPlanr.OKFlag!=0) then begin
              testf = true;
            end;
          end;
          if (RepSpec.flags[2]==1) then begin
            if (ProdPlanr.OKFlag==0) then begin
              testf = true;
            end;
          end;
        end;
        if (prntdv[ProdPlanr.SerNr]) then begin
          testf = false;
        end;
        if (testf) then begin
          prntdv[ProdPlanr.SerNr] = true;
          rwcnt = MatRowCnt(ProdPlanr);
          for (i=0;i<rwcnt;i=i+1) begin
            testf = true;
            MatRowGet(ProdPlanr,i,ProdPlanrw);
            if (DateInRange(ProdPlanrw.ProdStartDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
              testf = false;
            end;
            if (ProdPlanrw.ProdOrder>0) then begin
              testf = false;
            end;
            if (testf) then begin
              if (nonblank(ProdPlanrw.Qty)) then begin
                qty = ProdPlanrw.Qty;
              end else begin
                qty = ProdPlanrw.SugQty;
              end;
              AddToPlanArray(ProdPlanr.SerNr,ProdPlanrw.ItemCode,ProdPlanrw.ProdStartDate,ProdPlanrw.NeededDate,ProdPlanrw.ProdDays,qty,aprodplan,aitem,aorddat,aneeddat,adays,aqty,acnt);
            end;
          end;
        end;
      end;
    end;
    loopdate = AddDay(loopdate,1);
  end;
  previtem = "";
  for (i=0;i<acnt;i=i+1) begin
    if (previtem!=aitem[i]) then begin
      INr.Code = aitem[i];
      if (ReadFirstMain(INr,1,true)==false) then begin
        RecordNew(INr);
        INr.Code = aitem[i];
      end;
      Gray_Divider(0,1);
      StartFormat(15);
      OutString(0,0,INr.Code,false);
      OutString(2,0,INr.Name,false);
      EndFormat;
      previtem = INr.Code;
    end;
    StartFormat(15);
    OutString(6,"DblProdPlan",aprodplan[i],false);
    OutDate(3,0,aorddat[i],false);
    OutLongInt(4,0,adays[i],false);
    OutDate(5,0,aneeddat[i],false);
    OutVal(1,0,aqty[i],M4UVal,true);
    EndFormat;
  end;
  EndJob;  
  return;
end;  