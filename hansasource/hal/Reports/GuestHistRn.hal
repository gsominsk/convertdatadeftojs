external function Boolean IsEnterprise();

procedure PrintLastGuestsObeservations(string theguest)
begin
  record GuestObserVc GuestObser;
  Boolean firstf;
  Boolean TrHs;
  
  TrHs = true;
  firstf = true;
  GuestObser.Guest = theguest;
  while (LoopKey("Guest",GuestObser,1,TrHs)) begin
    if (GuestObser.Guest!=theguest) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      if (firstf) then begin
        StartFormat(15);
        EndFormat;
        StartFormat(15);
        OutString(0,0,USetStr(10203),false);
        EndFormat;
        firstf = false;
      end;
      if ((GuestObser.Type==0) or (GuestObser.Type==2) or (GuestObser.Type==4)) then begin
        StartFormat(15);
        OutStringID(4,"DblGuestObserVc",GuestObser.Comment,false,GuestObser.SerNr);
        EndFormat;
      end;
      if ((GuestObser.Type==1) or (GuestObser.Type==3) or (GuestObser.Type==6) or (GuestObser.Type==5)) then begin
        StartFormat(15);
        OutStringID(4,"DblGuestObserVc",GuestObser.Comment,false,GuestObser.SerNr);
        OutString(7,0,GuestObser.TransDate,false);
        OutString(250,0,Left(GuestObser.TransTime,5),false);
        EndFormat;
      end; 
    end;   
  end;     
end;

procedure PrintLastGuestsActivities(string theguest)
begin
  record ActVc Actr;
  Boolean TrHs;
  Boolean firstf;

  TrHs = true;
  firstf = true;
  Actr.CUCode = theguest;
  while (LoopKey("CUCode",Actr,1,TrHs)) begin
    if (Actr.CUCode!=theguest) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      if (firstf) then begin
        StartFormat(15);
        EndFormat;
        StartFormat(15);
        OutString(0,0,USetStr(10204),false);
        EndFormat;
        firstf = false;
      end;
      StartFormat(15);
      OutStringID(4,"DblActRec",Actr.Comment,false,Actr.SerNr);
      OutString(7,0,Actr.TransDate,false);
      OutString(250,0,Actr.ActType,false);
      if (Actr.OKFlag==0) then begin 
        OutString(300,0,"",false);
      end else begin
        OutString(300,0,USetStr(10115),false);
      end;
      OutString(350,"DblPersAct",Actr.MainPersons,false);//DblPersAct
      OutString(400,0,Actr.Contact,false);
      EndFormat;
    end;   
  end;     
end;

global
procedure GuestHistRn(record RcVc RepSpec)
begin
  record GuestrsVc Guestrsr;
  record JobResTypeMtrxVc JobResTypeMtrxr;
  Boolean TrHs,TrHsGuest;
  record JobVc Jobr;
  record CUVc Guestr;
  string 20 frguest,toguest;
  string 20 lastguest;
  
  StartReportJob(USetStr(10042));
  EndHeader;
  SetRepCol(2,120);
  SetRepCol(4,20);
  SetRepCol(5,80);
  SetRepCol(7,190);
  SetRepCol(8,330);
  SetRepCol(9,340);
  frguest = FirstInRange(RepSpec.f1,20);
  toguest = LastInRange(RepSpec.f1,20);
  TrHs = true;
  Guestrsr.GuestCode = frguest;
  while (LoopKey("GuestCode",Guestrsr,1,TrHs)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (Guestrsr.GuestCode>toguest) then begin
        TrHs = false; 
      end;
    end;
    if (TrHs==true) then begin
      if (lastguest!=Guestrsr.GuestCode) then begin
        if (nonblank(lastguest)) then begin
          PrintLastGuestsObeservations(lastguest);
          PrintLastGuestsActivities(lastguest);
          StartFormat(15);
          EndFormat;
          Black_Divider(0,1);
        end;
        StartFormat(15);
         OutString(0,0,Guestrsr.GuestCode,false);
         Guestr.Code = Guestrsr.GuestCode;
         if (ReadFirstMain(Guestr,1,true)) then begin
           OutStringID(5,"DblGuestObserv",Guestr.Name,false,Guestr.Code);
         end;
        EndFormat;
        StartFormat(15);
        OutString(4,0,USetStr(10195),false);
        if (IsEnterprise) then begin
          OutString(5,0,USetStr(10201),false);
        end;
        OutString(2,0,USetStr(10199),false);
        OutString(7,0,USetStr(10200),false);
        OutString(8,0,USetStr(10202),true);
        OutString(9,0,USetStr(10198),false);
        EndFormat;
        Gray_Divider(0,1);
      end;
      StartFormat(15);
      OutLongInt(4,"DblJobVc",Guestrsr.JobSerNr,false);
      Jobr.SerNr = Guestrsr.JobSerNr;
      if (ReadFirstMain(Jobr,1,true)) then begin
        if (IsEnterprise) then begin
          OutString(5,0,Jobr.Type,false);
        end;
        OutString(2,0,Jobr.TransDate,false);
        OutString(7,0,Jobr.EndDate,false);
        OutVal(8,0,Jobr.Sum4,M4Val,true);
        OutString(9,0,Jobr.Comment,false);
      end;
      EndFormat;
      lastguest = Guestrsr.GuestCode;
    end;
  end;
  PrintLastGuestsObeservations(lastguest);
  PrintLastGuestsActivities(lastguest);
  EndJob;
  return;
end;
