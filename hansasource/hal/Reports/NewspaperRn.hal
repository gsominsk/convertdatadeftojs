external function LongInt FindReservation(string,Date,Time);
external procedure SplitEclass(string,var Array string);

global
procedure NewspaperRn(record RcVc RepSpec)
begin
  record ResVc Resr;
  record HotelBlock HBr;
  record NewspaperBlock NewspaperRec;
  row NewspaperBlock NewspaperRecw;
  record JobVc Jobr;
  Boolean TsHr,testf;
  Integer rw;
  LongInt ResNro;
  Integer pos,i,rwcnt;
  Array string 20 ac;
  Boolean printedf;
  array LongInt apapers;
  
  BlockLoad(NewspaperRec);
  BlockLoad(HBr);
  rwcnt = MatRowCnt(NewspaperRec);
  for (i=0;i<rwcnt;i=i+1) begin
    apapers[i] = 0;
  end;
  StartReportJob(USetStr(9980));
  rw = 1;
  Header(rw,RepSpec.d1,1);
  rw = rw + 1;
  EndHeader;
  SetRepCol(2,55);
  SetRepCol(4,170);
  SetRepCol(5,300);
  StartFormat(15);
  OutString(0,0,USetStr(9971),false);
  OutString(2,0,USetStr(9979),false);
  OutString(5,0,USetStr(9973),false);
  EndFormat;
  Gray_Divider(0,1);
  TsHr = true;
  while (LoopMain(Resr,1,TsHr)) begin
    testf = true;
    if ((RepSpec.f1 <> Resr.Type) and (not Blank(RepSpec.f1))) then begin
      testf = false;
    end;
    If (testf) then begin
      ResNro = FindReservation(Resr.Code,RepSpec.d1,StringToTime("07:00")); //## If a person is there when houskeeping is run, give them a paper
      if (ResNro!=-1) then begin
        Jobr.SerNr = ResNro;
        if (ReadFirstMain(Jobr,1,true)) then begin
          if (nonblank(Jobr.Newspapers)) then begin
            printedf = false;
            StartFormat(15);
            OutString(0,0,Resr.Code,false);
            SplitEclass(Jobr.Newspapers,ac);
            pos = 0;
            while (nonblank(ac[pos])) begin
              for (i=0;i<rwcnt;i=i+1) begin
                MatRowGet(NewspaperRec,i,NewspaperRecw);
                if (ac[pos]==NewspaperRecw.NewsCode) then begin
                  if (printedf) then begin
                    EndFormat;
                    StartFormat(15);
                  end;
                  if (nonblank(NewspaperRecw.NewsName)) then begin
                    OutString(2,0,NewspaperRecw.NewsName,false);
                  end else begin
                    OutString(2,0,NewspaperRecw.NewsCode,false);
                  end;
                  if (printedf==false) then begin
                    OutString(5,0,Left(Jobr.CUName,25),false);
                  end;
                  apapers[i] = apapers[i] + 1;
                  printedf = true;
                end;
              end;
              pos = pos + 1;
            end;
            EndFormat;
          end;
        end;    
      end;
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
  EndFormat;
  StartFormat(15);
  OutString(0,0,USetStr(9977),false);
  EndFormat;
  Gray_Divider(0,4);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(NewspaperRec,i,NewspaperRecw);
    StartFormat(15);
    OutString(0,0,NewspaperRecw.NewsName,false);
    OutLongInt(4,0,apapers[i],true);
    EndFormat;
  end;
  EndJob;
  return;
end;