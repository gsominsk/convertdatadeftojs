external procedure CurValToOtherCur(Date,string,val,string,var val,roundmode);

global
procedure INLastPPriceRn(record RcVc RepSpec)
BEGIN
  Boolean found,testf;
  record PUVc PUr;
  row PUVc PUrw;
  Integer i,rwcnt;
  LongInt j,nrofpr;
  Array val lastprieces;
  val t,v;
  string 255 tstr;
  record POVc POr;
  
  StartReportJob(USetStr(10425));
  EndHeader;
  if (blank(RepSpec.f1)) then begin
    StartFormat(15);     
     OutString(0,0,USetStr(10426),false);
    EndFormat;   
    goto LINLastPPriceRn;
  end;
  if (blank(RepSpec.f2)) then begin
    StartFormat(15);     
     OutString(0,0,USetStr(10427),false);
    EndFormat;   
    goto LINLastPPriceRn;
  end;
  nrofpr = 0;
  StartFormat(15);     
   OutString(0,0,USetStr(10428),false);
   OutString(70,0,USetStr(10429),false);
   OutString(150,0,USetStr(10430),false);
   OutString(300,0,USetStr(10431),false);
   OutString(420,0,USetStr(10432),true);
   OutString(480,0,USetStr(10433),true);
  EndFormat;   
  
  POr.SerNr = RepSpec.long2;
  ReadFirstMain(POr,1,true);
  Gray_Divider(0,1);
  found = true;
  PUr.VECode = RepSpec.f1;
  found = LoopBackKey("VECode",PUr,1,found);
  if (PUr.PONr!=POr.SerNr) then begin
    ResetLoop(PUr);
  end;
  while (LoopBackKey("VECode",PUr,1,found)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (PUr.VECode<>RepSpec.f1) then begin found = false; end;
    end;
    if (RepSpec.long1!=-1) then begin
      if (nrofpr>=RepSpec.long1) then begin
        found = false;       
      end;
    end;     
    if (found) then begin       
      rwcnt = MatRowCnt(PUr);
      for (i=0;i<rwcnt;i=i+1) begin      
        MatRowGet(PUr,i,PUrw);
        testf = true;
        if (nonblank(RepSpec.f2)) then begin
          if (PUrw.ArtCode<>RepSpec.f2) then begin
            testf = false;
          end;
        end;
        if (testf) then begin
          CurValToOtherCur(PUr.TransDate,PUr.CurncyCode,PUrw.UPrice,POr.CurncyCode,v,DefaultCurRoundOff);
          for (j=0;j<=nrofpr;j=j+1) begin 
            t = lastprieces[j];
            if (t==v) then begin
              testf  = false;
              j = nrofpr;
            end;    
          end;

          if (testf) then begin
            StartFormat(15);     
             OutLongInt(0,"DblPU",PUr.SerNr,false);
             OutString(70,0,PUr.VECode,false);
             tstr = Left(PUr.VEName,30);             
             OutString(150,0,tstr,false);
             OutDate(300,0,PUr.TransDate,false);
             OutVal(420,0,PUrw.Quant,M4Qty,true);
             OutVal(480,"DblPastePriceOnPO",v,M4Val,true);             
            EndFormat;   
            lastprieces[nrofpr] = PUrw.CostPrice;
            nrofpr = nrofpr + 1;
          end;  
        end;
      end;
    end;
  end; 
  if (RepSpec.ArtMode==1) then begin
    StartFormat(25);     
    EndFormat;   
    StartFormat(15);     
     OutString(150,0,USetStr(10434),false);
     OutVal(1,0,RepSpec.vals0,M4Val,true);
    EndFormat;   
  end;    
LINLastPPriceRn:;
  EndJob;
  RETURN;
END;
