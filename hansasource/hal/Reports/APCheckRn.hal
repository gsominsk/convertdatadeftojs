external procedure GetAccTurnover(string,string,Date,Date,record ObjBalVc,string,Integer,Integer,Integer,var val,var val);
external procedure ExtractObj(string,var Integer,var string);
external procedure RunAPRn(record RcVc,var val);
external procedure HTAccount(Integer,var string);
external procedure HT2Per(Date, Date , var string);
external procedure GetAccStartBalance(string,string,Date,record ObjBalVc,Integer,Integer,string,Integer,var val);
external procedure GetAccBalance(string,string,Date,record ObjBalVc,Integer,Integer,Integer,string,Integer,var val);

procedure APCheckOnDate(record RcVc RepSpec,Date td)
begin
  val tot1Sum;
  record AccVc Accr;
  Integer pos;
  string 255 accnumber;
  val Sb,Pb,Eb;
  record ObjBalVc ObjBalr;
  Boolean Objbals,testf;
  record RcVc APRepSpec;

  APRepSpec = RepSpec;
  APRepSpec.ArtMode = 10;
  APRepSpec.d1 = td;
  APRepSpec.FirstAcc = RepSpec.f8;
  RunAPRn(APRepSpec,tot1Sum);

  pos = 0;
  ExtractObj(RepSpec.f8,pos,accnumber);
  while (nonblank(accnumber)) begin
    Accr.AccNumber = accnumber;
    if (ReadFirstMain(Accr,1,true)) then begin
      ObjBalr.AccNumber = Accr.AccNumber;
      ObjBalr.Object = "";
      Objbals = ReadFirstMain(ObjBalr,2,true);

      GetAccStartBalance(Accr.AccNumber,"",td,ObjBalr,RepSpec.SimVerf,RepSpec.basecurncy,RepSpec.CurncyCode,RepSpec.IncDaughter,Sb);
      GetAccTurnover(Accr.AccNumber,"",td,td,ObjBalr,RepSpec.CurncyCode,RepSpec.SimVerf,RepSpec.basecurncy,RepSpec.IncDaughter,Sb,Pb);
      Eb = Eb + Sb + Pb;
    end;
    ExtractObj(RepSpec.f8,pos,accnumber);
  end;
  testf = true;
  if (RepSpec.flags[1]!=0) then begin
    if (Eb==tot1Sum) then begin
      testf = false;
    end;
  end;
  if (testf) then begin
    StartFormat(15);
     OutDate(0,"DblAPRollFwd",td,false);
     OutVal(360,0,Eb,M4Val,true);
     OutVal(420,0,tot1Sum,M4Val,true);
     OutVal(1,0,Eb+tot1Sum,M4Val,true);
    EndFormat;
  end;
  return;
end;

global
procedure APCheckRn(record RcVc RepSpec)
begin
  Integer rw;
  string 255 tstr;
  Date td;
  
  StartReportJob(USetStr(28300));
   rw = 1;
   HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
   Header(rw,tstr,1);
   rw = rw + 1;
   Header(rw,RepSpec.f8,1);
   rw = rw + 1;
  EndHeader;  
    StartFormat(15);
     OutString(0,0,USetStr(28301),false);
     OutString(360,0,USetStr(28302),true);
     OutString(420,0,USetStr(28303),true);
     OutString(1,0,USetStr(28304),true);
    EndFormat;
  Gray_Divider(0,1);
    
  td = RepSpec.sStartDate;
  while (td<=RepSpec.sEndDate) begin
    APCheckOnDate(RepSpec,td);
    td = AddDay(td,1);
  end;
  EndJob;
  return;
end;