// A very similar report exists in JobFollowUpRn.hal (that is from the Special Menu though)
global
procedure FollowUpJRn(Record RcVc RepSpec)
begin
  Record FollowUpVc FollowUpr;
  boolean skip, foundf;
  String 255 tstr;
  Date vDate;
  Val mTotal;
  Record JobVc Jobr;
  Record CUVc CUr;
    
  mTotal = 0;
  StartReportJob(USetStr(12440));
  EndHeader;

  SetRepCol(2,0);
  SetRepCol(3,40);
  SetRepCol(4,90);
  SetRepCol(5,140);
  SetRepCol(8,190);
  SetRepCol(6,275);
  SetRepCol(9,430);
  SetRepCol(7,480);
  
  StartFormat(15);
  OutString(2,0,USetStr(12441),false);
  OutString(3,0,USetStr(12443),false);
  OutString(4,0,USetStr(12444),false);
  OutString(5,0,USetStr(12442),false);
  OutString(8,0,USetStr(12445),false);
  OutString(6,0,USetStr(12446),false);
  OutString(9,0,USetStr(12447),true);
  OutString(7,0,USetStr(12448),true);
  EndFormat;
  Gray_Divider(0,1);

  foundf = true;
  FollowUpr.TransDate = RepSpec.sStartDate;
  while LoopKey("TransDate",FollowUpr,1,foundf) begin
  
    skip = false;
    if (FollowUpr.TransDate > RepSpec.sEndDate) then begin
      foundf = false;
      skip = true;
    end;
    if ((FollowUpr.MainPersons<>RepSpec.f2) and nonblank(RepSpec.f2)) then begin
      skip = true;      
    end;
    if (RepSpec.flags[1]==1 and RepSpec.flags[2]==1) then begin
      //
    end else begin
     if ((RepSpec.flags[1] == 1 AND FollowUpr.OKFlag == 0) OR 
         (RepSpec.flags[2] == 1 AND FollowUpr.OKFlag == 1)) then begin
       skip= true;
     end;
    end;
    
    Jobr.SerNr = FollowUpr.JobNr;
    if readfirstmain(Jobr,1,true) then begin
      if ((Jobr.Source<>RepSpec.f1) and nonblank(RepSpec.f1)) then begin
        skip = true;
      end;
      if nonblank(RepSpec.f2) then begin
        CUr.Code = Jobr.Source;
        if readfirstmain(CUr,1,true) then begin
          if (CUr.PayDeal<>RepSpec.f2) then begin
            skip = true;
          end;
        end;
      end;
    end;
    
    if (not skip) then begin
      StartFormat(15);
      OutLongInt(2,"DblFollowUpVc",FollowUpr.SerNr,false);
      OutString(3,0,DateToString(FollowUpr.TransDate,"DD/MM"),false);
    //  OutString(5,0,FollowUpr.MainPersons,false);
      OutString(4,"DblCUVc",Jobr.Source,false);
      OutString(5,"DblJobVc",Jobr.SerNr,false);  // FollowUpr.NReservationStatus
      OutString(8,0,Jobr.CUName,false);
      OutString(6,0,FollowUpr.Comment,false);
      if (FollowUpr.OKFlag==1) then begin
        OutString(9,0,"Ã",true);
      end else begin
        OutString(9,0," ",true);
      end;
      OutVal(7,0,FollowUpr.Amount,M4Val,true);
      EndFormat;
      mTotal = mTotal + FollowUpr.Amount;
    end;
  end;
  if (mTotal!=0) then begin
    Gray_Divider(0,1);
    StartFormat(15);
    OutString(0,0,USetStr(12449),false);
    OutVal(7,0,mTotal,M4Val,true);
    EndFormat;
  end;
  EndJob;
  
  return;
end;

