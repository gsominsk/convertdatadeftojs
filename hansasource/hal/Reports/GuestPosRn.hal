function Boolean IsResEventSug( LongInt JobNr ) 
begin
  record ResEventTypersVc ResEventTypers;
  Boolean res;
  res= false;
  ResEventTypers.JobNr= JobNr; 
  if ReadFirstKey("JobNr",ResEventTypers,1,true) then begin res= true; end;   
  IsResEventSug= res;
  return;
end;

function Boolean isGuestinEvent( LongInt JobNr, String GuestCode, Date vDate )
begin
  Record ExcursionrsVc Excurs;
  Record ExcursionVc Excur;
  Row ExcursionVc Excurw;
  Integer i,rwcnt;
  Boolean res;
  
  res= false;
  
  Excurs.JobNr= JobNr;
  if ReadFirstMain(Excurs,1,true) then begin
    if (Excurs.FileName=="ExcursionVc") then begin // otherwise is transfer
      if (vDate==Excurs.TransDate) then begin  
        Excur.SerNr= Excurs.EventNr;
        if ReadFirstMain(Excur,1,true) then begin
          rwcnt= MatRowCnt(Excur);
          for(i=0;i<rwcnt;i=i+1) begin   
            MatRowGet(Excur,i,Excurw);
            if (GuestCode==Excurw.GuestCode) then begin
              res= true; 
            end;
          end;
        end;  
      end;
    end;
  end;
  isGuestinEvent = res;
  return;
end;

global
procedure GuestPosRn(record RcVc RepSpec)
begin
  record JobVc Jobr;
  row JobVc Jobrw;
  record CUVc Profile;
  record ResEventTypersVc ResEventTypers;
  Boolean TrHs,Skip;
  Integer rwcnt,i;
  
  StartReportJob("Huespedes Posibles para el Evento" );
  EndHeader;
  
  SetRepCol(2,60);
  SetRepCol(3,150);
  SetRepCol(4,230);
  
  StartFormat(15);
  OutString(0,0,"Reserva",false);
  OutString(2,0,"Huesped",false);
  OutString(3,0,"Checkin",false);
  OutString(4,0,"Checkout",false);
  EndFormat;
  
  Gray_Divider(0,1);
  TrHs = true;
  Jobr.TransDate = RepSpec.d1;  
  while(LoopKey("TransDate",Jobr,1,TrHs)) begin
    
    Skip= false;
    if (Jobr.EndDate<RepSpec.d1) then begin
      Skip= true;
    end;
    if (RepSpec.d1<Jobr.TransDate) then begin
      TrHs= false;
      Skip= true;
    end;
  //  if IsResEventSug(Jobr.SerNr) then begin
   //   Skip= true;
  //  end;
  
    if (not Skip) then begin
      rwcnt= MatRowCnt(Jobr);
      for(i=0;i<rwcnt;i=i+1) begin
        MatRowGet(Jobr,i,Jobrw); 
        if not isGuestinEvent( Jobr.SerNr, Jobrw.GuestCode, RepSpec.d1 ) then begin
          
          Profile.Code= Jobrw.GuestCode;
          if ReadFirstMain(Profile,1,true) then begin
            StartFormat(15);
            OutString(0,"DblJobVc",Jobr.SerNr,false);
            OutString(2,0,Profile.Name,false); 
            OutDate(3,0,Jobr.TransDate,false);
            OutDate(4,0,Jobr.EndDate,false);
            EndFormat;
          end;
        end;
      end;
    end;
    
  end; // while
  EndJob;
  return;
end;
