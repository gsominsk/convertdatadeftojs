procedure SumProjectStats(string code,Integer invd,Integer invable,var val tbp,var val pbp,var val gpp)
BEGIN
  record TBIVVc TBIVr;
  Boolean found;
  
  tbp = 0;
  pbp = 0;
  gpp = 0;
  TBIVr.PRCode = code;
  TBIVr.ArtCode = "";
  found = true;
  while (LoopMain(TBIVr,1,found)) begin
    if (found) then begin
      if (code<>TBIVr.PRCode) then begin found = false; end;
    end;  
    if (found) then begin
      if ((TBIVr.Invoice==-1) and (invable==1)) then begin gpp = TBIVr.GP + gpp; end;
      if ((TBIVr.Invoice<>-1) and (invd==1))    then begin gpp = TBIVr.GP + gpp; end;
      if ((TBIVr.ItemType==0) and (TBIVr.ItemType==1) and (TBIVr.ItemType==5)) then begin
        if ((TBIVr.Invoice==-1) and (invable==1)) then begin pbp = TBIVr.Sum + pbp; end;
        if ((TBIVr.Invoice<>-1) and (invd==1))    then begin pbp = TBIVr.Sum + pbp; end;
      end;
      if (TBIVr.ItemType==3) then begin
        if ((TBIVr.Invoice==-1) and (invable==1)) then begin tbp = TBIVr.Sum + tbp; end;
        if ((TBIVr.Invoice<>-1) and (invd==1))    then begin tbp = TBIVr.Sum + tbp; end;
      end;
    end;
  end;
  RETURN;
END;

global
procedure PRStatRn(record RcVc RepSpec)
BEGIN
  Boolean found;
  record PRVc PRp;
  string 20 frpr,topr;
  string 20 frcu,tocu;
  Boolean testf;
  string 255 ckey;
  Integer keys;
  val tottb,totpb,totgp,s;
  val tb,pb,gp;
  
  StartReportJob(5560);
  EndHeader;
  StartFormat(15);
   OutString(0,0,USetStr(5561),false);
   OutString(240,0,USetStr(5562),true);
   OutString(320,0,USetStr(5563),true);
   OutString(400,0,USetStr(5564),true);
   OutString(480,0,USetStr(5565),true);
  EndFormat;
  Gray_Divider(0,1);
  frpr = FirstInRange(RepSpec.f1,20);
  topr = LastInRange(RepSpec.f1,20);
  frcu = FirstInRange(RepSpec.f3,20);
  tocu = LastInRange(RepSpec.f3,20);

  if (RepSpec.flags[1]==0) then begin
    PRp.Code = frpr;
    ckey = "Code";
    keys = 1;
  end else begin
    PRp.CustCode = frcu;
    PRp.Code = frpr;
    ckey = "CustCode";
    keys = 2;
  end;

  found = true;
  while (LoopKey(ckey,PRp,keys,found)) begin
    testf = false;
    if (found) then begin
      if (RepSpec.flags[1]==0) then begin
        if (nonblank(topr) and (PRp.Code>topr)) then begin found = false; end;
      end else begin
        if (nonblank(tocu) and (PRp.CustCode>tocu)) then begin found = false; end;
      end;
    end;
    if (found) then begin
      testf = true;
      if (RepSpec.flags[1]==0) then begin
        if (nonblank(frcu) and (PRp.CustCode<frcu)) then begin testf = false; end;
        if (nonblank(tocu) and (PRp.CustCode>tocu)) then begin testf = false; end;
      end else begin
        if (nonblank(frpr) and (PRp.Code<frpr)) then begin testf = false; end;
        if (nonblank(topr) and (PRp.Code>topr)) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.FirstAcc) and (RepSpec.FirstAcc<>PRp.PRClass)) then begin testf = false; end;
      if (PRp.Terminated==1) then begin testf = false; end;
    end;
    if (testf) then begin      
        StartFormat(15);
        SumProjectStats(PRp.Code,RepSpec.flags[1],RepSpec.flags[2],tb,pb,gp);
        s = tb + pb;
        OutString(0,0,PRp.Code,false);
        OutString(100,0,PRp.Name,false);
        OutVal(240,0,tb,M4Val,true);
        OutVal(320,0,pb,M4Val,true);
        OutVal(400,0,s,M4Val,true);
        OutVal(480,0,gp,M4Val,true);
        EndFormat;
        tottb = tottb + tb;
        totpb = totpb + pb;
        totgp = totgp + gp;
    end;
  end;
  Gray_Divider(0,1);
  StartFormat(15);
  s = tottb + totpb;
  OutVal(240,0,tottb,M4Val,true);
  OutVal(320,0,totpb,M4Val,true);
  OutVal(400,0,s,M4Val,true);
  OutVal(480,0,totgp,M4Val,true);
  EndFormat;
  EndJob;
  RETURN;
END;
