external procedure HT2Per(Date, Date , var string);
external procedure HTCusts(string, string, var string);
external procedure HTCustClass(string,var string);
external function Boolean SetInSet2(string,string);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external function val GetLCardStartBalance(string,string,date); 
external procedure HTCustCat(string,var string);

procedure PrintLCard(record RcVc RepSpec,var boolean printcard,record PointsHistVc PHr, var val points, var val invoices,var val bal)
begin 
  record LoyaltyCardVc LoyaltyCardr;
  record CUVc CUr;
  record IVVc IVr;
  record IVCashVc IVCashr;
  record RestAccVc RestAccr;
  Integer koef;
  val t;

  if (printcard==false) then begin 
    LoyaltyCardr.SerNr = PHr.LMCardNr;
    if (ReadFirstMain(LoyaltyCardr,1,true)) then begin end;
    bal = GetLCardStartBalance(LoyaltyCardr.SerNr,PHr.Location,RepSpec.sStartDate);
    printcard = true;

    if (RepSpec.ArtMode==0) then begin
      StartFormat(15);
      OutString(0,"DblLCardVc",PHr.LMCardNr,false);
      OutString(80,"DblCUVc",PHr.CustCode,false);
      OutString(120,0,LoyaltyCardr.CustName,false);
      OutVal(260,0,bal,M4Val,true);
    end;
    if (RepSpec.ArtMode==1) then begin
      points = bal;

      StartFormat(15);
      OutString(0,"DblLCardVc",PHr.LMCardNr,false);
      OutString(100,"DblCUVc",PHr.CustCode,false);
      OutString(160,0,LoyaltyCardr.CustName,false);
      EndFormat;
      if (DateInRange(PHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)) then begin 
        Black_Divider(0,1);
        StartFormat(15);
        OutString(0,0,USetStr(19269),false);
        OutString(100,0,USetStr(19270),false);
        OutString(180,0,USetStr(19271),false);
        OutString(260,0,USetStr(19274),false);
        OutString(360,0,USetStr(19272),true);
        OutString(1,0,USetStr(19273),true);
        EndFormat;
      end;  

      Gray_Divider(0,1);
      StartFormat(15);
      OutString(100,0,USetStr(19275),false);
      OutVal(1,0,bal,M4Val,true);
      EndFormat;
    end;  
  end;
  if (DateInRange(PHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==true) then begin
    switch (PHr.FileName) begin
      case "IVVc": 
        IVr.SerNr = PHr.TransNr;
        koef = 1;
        if (PHr.Points<0) then begin
          koef = -1;
        end;
        if (ReadFirstMain(IVr,1,true)) then begin 
          if (IVr.InvType==kInvoiceTypeCredit or IVr.InvType==kInvoiceTypeCreditSpecialSales) then begin 
            koef = -1;
          end;  
          if (IVr.Invalid!=1) then begin 
            points   = points + IVr.Points*koef;
            t = MulRateToBase1(IVr.CurncyCode,IVr.Sum4,IVr.FrRate,IVr.ToRateB1,IVr.ToRateB2,IVr.BaseRate1,IVr.BaseRate2,DefaultCurRoundOff);
            invoices = invoices + t*koef;
            if (RepSpec.ArtMode==1) then begin
              StartFormat(15);
              OutLongIntOvst(0,"DblIVVc",IVr.SerNr,false,IVr.Invalid);
              OutString(100,0,IVr.TransDate,false);
              OutString(180,0,IVr.LCMLevel,false);
              OutString(260,"DblLCardVc",PHr.PointsFrom,false);
              OutVal(360,0,t*koef,M4Val,true);
              OutVal(1,0,PHr.Points,M4Val,true);
              EndFormat;
            end;
          end;  
        end;
      case "IVCashVc":
        IVCashr.SerNr = PHr.TransNr;
        koef = 1;
        if (PHr.Points<0) then begin
          koef = -1;
        end;
        if (ReadFirstMain(IVCashr,1,true)) then begin 
          if (IVCashr.Invalid!=1) then begin 
            points   = points + PHr.Points;
            t = MulRateToBase1(IVCashr.CurncyCode,IVCashr.Sum4,IVCashr.FrRate,IVCashr.ToRateB1,IVCashr.ToRateB2,IVCashr.BaseRate1,IVCashr.BaseRate2,DefaultCurRoundOff);
            invoices = invoices + t*koef;
            if (RepSpec.ArtMode==1) then begin
              StartFormat(15);
              OutLongIntOvst(0,"DblIVCash",IVCashr.SerNr,false,IVCashr.Invalid);
              OutString(100,0,IVCashr.TransDate,false);
              OutString(180,0,IVCashr.LCMLevel,false);
              OutString(260,"DblLCardVc",PHr.PointsFrom,false);
              OutVal(360,0,t*koef,M4Val,true);
              OutVal(1,0,PHr.Points,M4Val,true);
              EndFormat;
            end;
          end;  
        end;  
      case "RestAccVc":
        RestAccr.SerNr = PHr.TransNr;
        koef = 1;
        if (PHr.Points<0) then begin
          koef = -1;
        end;
        if (ReadFirstMain(RestAccr,1,true)) then begin 
          if (RestAccr.Invalid==0) then begin 
            points = points + PHr.Points;
            t = RestAccr.Sum4;
            invoices = invoices + t*koef;
            if (RepSpec.ArtMode==1) then begin
              StartFormat(15);
              OutLongIntOvst(0,"DblRestAccVc",RestAccr.SerNr,false,RestAccr.Invalid);
              OutString(100,0,RestAccr.TransDate,false);
              OutString(180,0,RestAccr.LCMLevel,false);
              OutString(260,"DblLCardVc",PHr.PointsFrom,false);
              OutVal(360,0,t*koef,M4Val,true);
              OutVal(1,0,PHr.Points,M4Val,true);
              EndFormat;
            end;
          end;  
        end;  
      case "LoyaltyCardVc":
        StartFormat(15);
        OutLongIntOvst(0,"DblLCardVc",PHr.PointsFrom,false,0);
        OutString(100,0,PHr.TransDate,false);
        OutString(180,0,"",false);
        OutString(260,"DblLCardVc",PHr.PointsFrom,false);
        OutVal(360,0,PHr.Points,M4Val,true);
        OutVal(1,0,PHr.Points,M4Val,true);
        EndFormat;     
        points   = points + PHr.Points;
    end;
  end;  

  return;
end;  

global
procedure LoyaltyCardRn(record RcVc RepSpec)
begin 
  record PointsHistVc PHr;
  record CUVc CUr;
  boolean TrHs,testf;
  integer rw;
  string 255 tstr;
  Boolean printcard;
  val points,cpoints,tpoints;
  val invoices,cinvoices,tinvoices,startbal;
  val tbal;
  string 20 card,customer;
  
  StartReportJob(USetStr(19260));
  points = 0;
  cpoints = 0;
  tpoints = 0;
  rw = 1;
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  if (nonblank(RepSpec.f1)) then begin 
    tstr = UsetStr(19261) & ":" & RepSpec.f1;  
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTCusts(RepSpec.f2,RepSpec.f2,tstr);
  Header(rw,tstr,1); 
  rw = rw + 1;
  HTCustClass(RepSpec.f3,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  HTCustCat(RepSpec.f4,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  EndHeader;
 
  if (RepSpec.ArtMode==0) then begin
    StartFormat(15);
    OutString(0,0,USetStr(19261),false);
    OutString(80,0,USetStr(19262),false);
    OutString(120,0,USetStr(19263),false);
    OutString(260,0,USetStr(19275),true);
    OutString(320,0,USetStr(19264),true);
    OutString(400,0,USetStr(19273),true);
    OutString(1,0,USetStr(19276),true);
    EndFormat;
  end; 
   if (RepSpec.ArtMode==1) then begin
    StartFormat(15);
    OutString(0,0,USetStr(19261),false);
    OutString(100,0,USetStr(19262),false);
    OutString(160,0,USetStr(19263),false);
    OutString(360,0,USetStr(19264),true);
    OutString(1,0,USetStr(19265),true);
    EndFormat;
  end; 
  Gray_Divider(0,1);

  if (nonblank(RepSpec.f1)) then begin 
    printcard = false;
    PHr.LMCardNr = RepSpec.f1;
    TrHs = true;
    while (LoopKey("CardDate",PHr,1,TrHs)) begin 
      testf = true;
      if (PHr.LMCardNr!=RepSpec.f1) then begin 
        TrHs = false;
        testf = false;
      end;
      if (RepSpec.flags[1]==1) then begin 
        if (DateInRange(PHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
          testf = false;
        end else begin 
          if (PHr.TransDate>RepSpec.sEndDate) then begin 
            testf = false;
          end;  
        end;
      end;  
      if (nonblank(RepSpec.LastAcc)) then begin
        if (PHr.Location!=RepSpec.LastAcc) then begin testf = false; end;
      end;
      if (testf) then begin
        PrintLCard(RepSpec,printcard,PHr,points,invoices,startbal);
      end;
    end;  
    if (printcard==true) then begin 
      tbal = tbal + startbal;
    end;
  end else begin 
    //customer is specified
    if nonblank(RepSpec.f2) then begin 
      PHr.CustCode = RepSpec.f2;
    end;
    TrHs = true;
    while (LoopKey("CustCode",PHr,1,TrHs)) begin 
      testf = true;
      if (nonblank(RepSpec.f2)) then begin 
        if (PHr.CustCode!=RepSpec.f2) then begin 
          TrHs = false;
          testf = false;
        end;
      end;
      if (RepSpec.flags[1]==1) then begin 
        if (DateInRange(PHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
          testf = false;
        end;
      end else begin 
        if (PHr.TransDate>RepSpec.sEndDate) then begin 
          testf = false;
        end;  
      end;
      if (nonblank(RepSpec.LastAcc)) then begin
        if (PHr.Location!=RepSpec.LastAcc) then begin testf = false; end;
      end;
      if (testf) then begin 
        if (nonblank(RepSpec.f3) or nonblank(RepSpec.f4))  then begin 
          CUr.Code = PHr.CustCode;
          if (ReadFirstMain(CUr,1,true)) then begin end;
          if (nonblank(RepSpec.f3)) then begin 
            if (SetInSet2(RepSpec.f3,CUr.Classification)==false) then begin 
              testf = false;
            end;
          end;  
          if (nonblank(RepSpec.f4)) then begin 
            if (CUr.CustCat!=RepSpec.f4) then begin 
              testf = false;
            end;
          end;
        end;
      end;  
      if (testf) then begin 
        if (blank(card) or (PHr.LMCardNr!=card)) then begin
          //cust total
          if (nonblank(card) and printcard==true) then begin 
            if (RepSpec.ArtMode==0) then begin
              OutVal(320,0,invoices,M4Val,true);
              OutVal(400,0,points,M4Val,true);
              OutVal(1,0,points+startbal,M4Val,true);
              EndFormat;
              tbal = tbal + startbal;
            end;
            if (RepSpec.ArtMode==1) then begin
              Gray_Divider(0,1);
              StartFormat(15);
              OutString(100,0,USetStr(19266),false);
              OutVal(360,0,invoices,M4Val,true);
              OutVal(1,0,points,M4Val,true);
              EndFormat;
              StartFormat(15);
              EndFormat;
              Black_Divider(0,1);
            end;  
            cpoints = cpoints + points;
            cinvoices = cinvoices + invoices;
            tpoints = tpoints + points;
            tinvoices = tinvoices + invoices;
            printcard = false;
          end; 
          card = PHr.LMCardNr;
          points   = 0;
          invoices = 0;
        end;
        if (blank(customer) or PHr.CustCode!=customer) then begin
          //cust total
          if (nonblank(customer) and RepSpec.ArtMode==1) then begin 
            StartFormat(15);
            OutString(100,0,USetStr(19267),false);
            OutVal (360,0,cinvoices,M4Val,true);
            OutVal(1,0,cpoints,M4Val,true);
            EndFormat;
            StartFormat(15);
            EndFormat;
          end;  
          customer = PHr.CustCode;
          card = PHr.LMCardNr;
          printcard = false;
          cpoints   = 0;
          cinvoices = 0;
        end;
        PrintLCard(RepSpec,printcard,PHr,points,invoices,startbal);
      end;
    end; 
  end;
  cpoints = cpoints + points;
  cinvoices = cinvoices + invoices;
  tpoints = tpoints + points;
  tinvoices = tinvoices + invoices;
  if (RepSpec.ArtMode==1) then begin
    if (invoices!=0 or points!=0) then begin 
      Gray_Divider(0,1);
      StartFormat(15);
      OutString(100,0,USetStr(19266),false);
      OutVal(360,0,invoices,M4Val,true);
      OutVal(1,0,points,M4Val,true);
      EndFormat;
    end;
    if (cinvoices!=0 or cpoints!=0) then begin 
      Black_Divider(0,1);
      StartFormat(15);
      OutString(100,0,USetStr(19267),false);
      OutVal(360,0,cinvoices,M4Val,true);
      OutVal(1,0,cpoints,M4Val,true);
      EndFormat;
      //StartFormat(15);
      //EndFormat;
    end;  
  end; 
  if (invoices!=0 or points!=0 or startbal!=0) then begin 
    if (RepSpec.ArtMode==0) then begin
      OutVal(320,0,invoices,M4Val,true);
      OutVal(400,0,points,M4Val,true);
      OutVal(1,0,points+startbal,M4Val,true);
    end;
    EndFormat;
  end;  
 
  if (RepSpec.ArtMode==1) then begin
    if (tinvoices!=0 or tpoints!=0) then begin
      Black_Divider(0,1);
      StartFormat(15);
      OutString(100,0,USetStr(19268),false);
      OutVal(360,0,tinvoices,M4Val,true);
      OutVal(1,0,tpoints,M4Val,true);
      EndFormat;
    end;  
  end;   
  if (RepSpec.ArtMode==0) then begin
    if (tinvoices!=0 or tpoints!=0 or tbal!=0) then begin
      Black_Divider(0,1);
      StartFormat(15);
      OutString(100,0,USetStr(19268),false);
      OutVal(260,0,tbal,M4Val,true);
      OutVal(320,0,tinvoices,M4Val,true);
      OutVal(400,0,tpoints,M4Val,true);
      OutVal(1,0,tpoints+tbal,M4Val,true);
      EndFormat;
    end;
  end;  

  EndJob;
  return;
end;
