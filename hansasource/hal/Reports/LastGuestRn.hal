procedure HTRess(string a,string b,var string res)
BEGIN
  record ResVc Resr;

  if (blank(a) and blank(b)) then begin
    res = USetStr(8192);
  end else begin
    res = USetStr(8176);
    res = res & " ";
    res = res & a;
    if (a<>b) then begin
      res = res & " : ";
      res = res & b;
    end else begin
      Resr.Code = a;
      if (ReadFirstMain(Resr,1,true)) then begin
        res = res & " ";
        res = res & Resr.Name;
      end;
    end;
  end;
  RETURN;
END;

global
procedure LastGuestRn(record RcVc RepSpec)
BEGIN
  record CUVc Guestr;
  Boolean Cont;
  record JobVc Jobr;
  record ResVc Resr;
  row JobVc Jobrw;
  Integer i,rwcnt;
  string 255 tstr;
  Integer rw;
  
  StartReportJob(USetStr(10305));
  rw = 1;
  HTRess(RepSpec.f1,RepSpec.f1,tstr);
  Header(rw,tstr,1);
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(9919),false);
  OutString(70,0,USetStr(9932),false);
  OutString(130,0,USetStr(9616),false);
  EndFormat;
  Gray_Divider(0,1);
  if nonblank(RepSpec.f1) then begin
    Resr.Code  = RepSpec.f1;
  end;
  Cont = true;
  while (LoopMain(Resr,1,Cont)) begin
    if ((Resr.Code  <> RepSpec.f1) and (nonblank(RepSpec.f1))) then begin
      Cont = false;
    end;
    if (Cont) then begin
      Jobr.ResCode   = Resr.Code;
      Jobr.TransDate = CurrentDate;
      if (ReadLastKey("ResStartTime",Jobr,2,false)) then begin
        if (RepSpec.flags[1]==1) then begin
          rwcnt = MatRowCnt(Jobr);
          for (i=0;i<rwcnt;i=i+1) begin
            MatRowGet(Jobr,i,Jobrw);
            Guestr.Code = Jobrw.GuestCode;
            if (ReadFirstMain(Guestr,1,true)) then begin
              StartFormat(15);
              OutString(0,0,Jobr.ResCode,false);
              OutString(70,"DblJobVc",Jobr.SerNr,false);
              OutString(130,0,Jobr.CUName,false);
              OutString(230,0,Jobrw.GuestCode,false);
              OutString(340,0,Guestr.Name,false);
              EndFormat; 
            end;  
          end;
        end else begin
          StartFormat(15);
          OutString(0,0,Jobr.ResCode,false);
          OutString(70,"DblJobVc",Jobr.SerNr,false);
          OutString(130,0,Jobr.CUCode,false);
          OutString(230,0,Jobr.CUName,false);
          EndFormat;         
        end; 
      end;
    end;
  end;
  EndJob;  
  RETURN;
END;
