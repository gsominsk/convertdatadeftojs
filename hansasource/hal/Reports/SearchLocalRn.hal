external function Integer FirstLocalToRow(record LocalMailVc,var string); 
external procedure HT2Per(Date, Date , var string);
external procedure FindUserMailboxName(string,var string,var string);

global
procedure SearchLocalMailRn(record RcVc RepSpec)
begin
  record LocalMailVc LocalMailr;
  record LocalMailVc LocalMail2r;
  record RLinkVc RLr;
  record Attach2Vc Attach2r;
  row LocalMailVc LocalMailrw;
  boolean TrHs,testf,testtextf;
  string 255 tstr,ckey,mbrno,tostr,subj;
  integer rw,i,rwcnt,cnt,l,rowtype;
  
  cnt = 0;
  rw = 1;
  StartReportJob(USetStr(11700));
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  if (RepSpec.long1<=0) then begin
    tstr = "User: ";
    tstr = tstr & CurrentUser;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  if (nonblank(RepSpec.f1)) then begin
    if (RepSpec.ArtMode == 0) then begin
      tstr = USetStr(11701) & " " & RepSpec.f1;
    end;
    if (RepSpec.ArtMode == 1) then begin    
      tstr = USetStr(11702) & " " & RepSpec.f1;
    end;
    if (RepSpec.ArtMode == 2) then begin
      tstr = USetStr(11703) & " " & RepSpec.f1;
    end;
    Header(rw,tstr,1);
    rw = rw + 1;  
  end;
  if (nonblank(RepSpec.f2)) then begin
    tstr = USetStr(11704) & " " & RepSpec.f2;
    Header(rw,tstr,1);
  end;
  EndHeader;
  StartFormat(15);
  OutString(0,0,USetStr(11705),false);
  OutString(200,0,USetStr(11706),false);
  OutString(350,0,USetStr(11707),false);
  OutString(430,0,USetStr(11708),false);
  EndFormat;
  Gray_Divider(0,1);
  FindUserMailboxName(CurrentUser,mbrno,tostr); 
  ckey = "UserTime";
  LocalMailr.TransDate = RepSpec.sEndDate;
  TrHs = true;
  while (LoopBackKey(ckey,LocalMailr,1,TrHs)) begin
    if (LocalMailr.TransDate < RepSpec.sStartDate) then begin 
       TrHs = false;
    end;
    if (LocalMailr.TransDate > RepSpec.sEndDate) then begin 
       TrHs = false;
    end;
    if (TrHs) then begin
      testf = false;
      rwcnt = MatRowCnt(LocalMailr);
      if (nonblank(RepSpec.f1)) then begin
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(LocalMailr,i,LocalMailrw);
          if (RepSpec.ArtMode == 0) then begin
            if (LocalMailrw.RowTyp == 0) then begin
              if (RepSpec.f1==LocalMailrw.AddrCode) then begin
                testf = true;
              end;
            end;
          end;
          if (RepSpec.ArtMode == 2) then begin
            if (RepSpec.f1==LocalMailrw.AddrCode) then begin
              testf = true;
            end;
          end;
          if (RepSpec.ArtMode == 1) then begin
            if (LocalMailrw.RowTyp == 1) then begin
              if (RepSpec.f1==LocalMailrw.AddrCode) then begin
                testf = true;
              end;
            end;
          end;
        end;
      end;   
      if (RepSpec.flags[1]>0) then begin
        if (ReadRecordLink(LocalMailr,1,Attach2r,RLr)) then begin
          if (RepSpec.flags[1]==2) then begin
            testf = false;
          end;
        end else begin
          if (RepSpec.flags[1]==1) then begin
            testf = false;
          end;
        end;
      end;
      testtextf = true;
      if (nonblank(RepSpec.f2)) then begin
        testtextf = false;
        RecordNew(LocalMail2r);
        if ((RepSpec.flags[0]==0) or (RepSpec.flags[0]==1)) then begin
          RecordCopy(LocalMail2r,LocalMailr);
        end;
        if ((RepSpec.flags[0]==0) or (RepSpec.flags[0]==2)) then begin
          AddToText(LocalMailr.Header,LocalMail2r);
        end;
        if (StringInText(RepSpec.f2,LocalMail2r)) then begin
          testtextf = true;
        end;
      end;    
      if (testf and testtextf) then begin
        if (rwcnt>0) then begin
          tstr = "";
          MatRowGet(LocalMailr,0,LocalMailrw);
          if (tostr==LocalMailrw.AddrCode) then begin
            rowtype = FirstLocalToRow(LocalMailr,tstr);
            if (rowtype==0) then begin
              tstr = USetStr(11709) & " " & tstr;
            end;
          end else begin
            tstr = LocalMailrw.AddrCode;
          end;
        end;
        subj = LocalMailr.Header;
        if (blank(subj)) then begin
          subj = USetStr(11710);
        end;
        StartFormat(15);
        OutStringID(0,"DblLocalMailVc",subj,false,LocalMailr.SerNr);
        OutString(200,0,tstr,false);
        OutDate(350,0,LocalMailr.TransDate,false);
        OutString(430,0,LocalMailr.TransTime,false);
        EndFormat;
        cnt = cnt + 1;
      end;  
    end;
 
  end;  
  Gray_Divider(0,1);
  StartFormat(15);
   OutString(0,0,USetStr(11711),false);
   OutVal(100,0,cnt,M40Val,true);
  EndFormat;
  EndJob;
return;
end;

