external function Boolean ReadRestOpenHours(string,var record RestOpenHoursVc);
external procedure GetRestBookTime(record RestOpenHoursVc,Date,var Time,var Time);
external procedure HT2Per(Date, Date , var string);
external function LongInt DateDiff(Date,Date);
external function LongInt TimeToMinutes(Time);

global
procedure RestBookingRn(record RcVc RepSpec)
BEGIN
  record RestBookingVc RestBookingr;
  record RestOpenHoursVc ROHr;
  Boolean found;
  Date dat;
  string 200 tstr;
  Integer rw;
  Time frtim,totim;
  LongInt frmin,tomin,mins,i;
  LongInt frmin2,tomin2,t;
  Array LongInt persarr;
  Array LongInt guestarr;
  string 255 Persons,dblclck;
  Boolean firstf,guestarrf;
  
  rw = 1;
  ReadRestOpenHours("",ROHr);
  StartReportJob(USetStr(12480));
  HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  Header(rw,USetStr(12482) & " " & ROHr.Seats,1);
  EndHeader;
  dat = RepSpec.sStartDate;
  while (DateDiff(dat,RepSpec.sEndDate)<=0) begin
    GetRestBookTime(ROHr,dat,frtim,totim);
    frmin = TimeToMinutes(frtim)/60;
    tomin = TimeToMinutes(totim)/60;
    if (tomin<frmin) then begin
      tomin = tomin + 24;
    end;
    mins = tomin - frmin;
    StartFormat(15);
    OutString(0,0,dat,false);
    if (mins<=0) then begin
      OutString(100,0,USetStr(12493),false);
      goto LRestBookingRn;
    end;
    for (i=0;i<=mins;i=i+1) begin
      t = frmin+i;
      if (t>23) then begin
        t = t - 24;
      end;
      OutString(((400/mins)*i)+80,0,t,true);
      persarr[i] = 0;
      guestarr[i] = 0;
    end;
    EndFormat;
    Gray_Divider(0,1);
    firstf = true;
    RestBookingr.BookDate = dat;
    found = true;
    ResetLoop(RestBookingr);
    while (LoopKey("BookTime",RestBookingr,1,found)) begin
      if (RestBookingr.BookDate!=dat) then begin
        found = false;
      end;
      if (found) then begin
        frtim = RestBookingr.BookStart;
        totim = RestBookingr.BookEnd;
        if (totim.minute>29) then begin
          totim.hour = totim.hour + 1;
        end;
        StartFormat(15);
        frmin2 = TimeToMinutes(frtim)/60;
        tomin2 = TimeToMinutes(totim)/60;
        if (tomin2<frmin2) then begin
          tomin2 = tomin2 + 24;
        end;
        tstr = RestBookingr.BookStart;
        if (nonblank(RestBookingr.CUName)) then begin
          tstr = Left(RestBookingr.CUName,15);
        end;
        if (nonblank(RestBookingr.Contact)) then begin
          tstr = Left(RestBookingr.Contact,15);
        end;
/*
        if (nonblank(RestBookingr.ResCode)) then begin
          tstr = Left(USetStr(12483) & " " & RestBookingr.ResCode,15);
        end;
*/
        if (nonblank(RestBookingr.ResCode)) then begin
          if (nonblank(tstr)) then begin
            tstr = tstr & " (" & RestBookingr.ResCode & ")";
          end else begin
            tstr = Left(USetStr(12483) & " " & RestBookingr.ResCode,15);
          end;
        end;
        if (RepSpec.UsedOnly!=0) then begin
          dblclck = "RestBookToRestAcc";
        end else begin
          dblclck = "";
        end;
        guestarrf = true;
        OutStringID(0,dblclck,tstr,false,RestBookingr.SerNr);
        for (i=0;i<=mins;i=i+1) begin
          firstf = true;
          if ((i+frmin>=frmin2) and (i+frmin<tomin2)) then begin
            if (RestBookingr.Persons!=-1) then begin
              Persons = RestBookingr.Persons;
              persarr[i] = persarr[i] + RestBookingr.Persons;
              if (guestarrf) then begin
                guestarr[i] = guestarr[i] + RestBookingr.Persons;
                guestarrf = false;
              end;
            end else begin
              Persons = "";
           end;
            if (firstf) then begin
              OutStringID(((400/mins)*i)+80,"RestBookID",Persons,true,RestBookingr.SerNr);
              firstf = false;
            end else begin
              OutString(((400/mins)*i)+80,0,Persons,true);
            end;
           end;
        end;
        EndFormat;
      end;
    end;
    Gray_Divider(0,1);
    StartFormat(15);
    OutString(0,0,USetStr(12481),false);
    for (i=0;i<=mins;i=i+1) begin
      OutString(((400/mins)*i)+80,0,persarr[i],true);
    end;
    EndFormat;
    StartFormat(15);
    OutString(0,0,USetStr(12484),false);
    for (i=0;i<=mins;i=i+1) begin
      OutString(((400/mins)*i)+80,0,guestarr[i],true);
    end;
    EndFormat;
LRestBookingRn:;
    StartFormat(15);
    EndFormat;
    dat = AddDay(dat,1);
  end;
  EndJob;
  RETURN;
END;
