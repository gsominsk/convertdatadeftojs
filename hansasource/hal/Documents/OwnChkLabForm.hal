external procedure PrintOwnCheck(record RcVc,record OwnCheckVc,string,string);
external procedure GetAccountName(String,var String);  
external function longint GetOriginNr(Integer,LongInt);  
external procedure GetMonthText(string,Date,var string);
external procedure GetCurncyComment(string,var string);
external procedure GetFieldArgument(record DocVc,Integer,var string);
external procedure Val2ToText(string,val,Integer,string,string,var string);
external procedure GetDateMonthInWords(Date,var string);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);

procedure DoPrintOwnCheck(record RcVc RepSpec,string formcode,string langcode,string printer)
begin
  Boolean formisopenf;
  LongInt lbls,lblYstep;
  LongInt fromchq,tochq;
  record OwnCheckVc OwnCheckr;
  Boolean found;
  LongInt ownchklabcnt;
  Integer i,rwcnt;
  record DocVc Docr;
  row DocVc Docrw;
  
  ownchklabcnt = 0;
  fromchq = FirstInRange(RepSpec.f1,20);
  tochq = LastInRange(RepSpec.f1,20);
  if (OpenForm(formcode)==false) then begin 
    goto LDoPrintOwnCheck; 
  end;
  SetPrinter(printer);  
  formisopenf = true;
  switch (RepSpec.ArtMode) begin
    case 0: lblYstep = 63150/297; //PageHeight = 842; /* 297 mm */  75*842/297
    case 1: lblYstep = 1;
  end;

  switch (RepSpec.ArtMode) begin
    case 0: FormLabelGrid(1,1,4,lblYstep); lbls = 4;
    case 1: FormLabelGrid(1,1,1,lblYstep); lbls = 1;
  end;
  Docr.Code = formcode;
  if (ReadFirstMain(Docr,1,true)) then begin end;

  found = true;
  OwnCheckr.SerNr = fromchq;
  while (LoopKey("SerNr",OwnCheckr,1,found)) begin  
    if (nonblank(RepSpec.f1)) then begin
      if (OwnCheckr.SerNr>tochq) then begin
        found = false;
      end;
    end;
    if (found) then begin
      if (formisopenf==false) then begin
        if (OpenForm(formcode)==false) then begin 
          goto LDoPrintOwnCheck; 
        end;
        SetPrinter(printer);  
        formisopenf = true;
        switch (RepSpec.ArtMode) begin
          case 0: FormLabelGrid(1,1,4,lblYstep); lbls = 4;
          case 1: FormLabelGrid(1,1,1,lblYstep); lbls = 1;
        end;
      end;
      PrintOwnCheck(RepSpec,OwnCheckr,formcode,langcode);

      rwcnt = MatRowCnt(Docr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(Docr,i,Docrw);
        if (Docrw.fieldSetNr==0) then begin
          OUTFORMFIELD(Docrw.unitText,Docrw.unitText);
        end;
      end;  

      EndFormLabel;
      ownchklabcnt = ownchklabcnt + 1;
      if (ownchklabcnt>=lbls) then begin
        CloseForm;
        ownchklabcnt = 0;
        formisopenf = false;
      end;      
    end;
  end;
LDoPrintOwnCheck:;  
  if (formisopenf) then begin
    CloseForm;
  end;
  return;
end;

global
procedure DoOwnChkLabForm(record RcVc RepSpec,record OwnCheckVc OwnCheckr)
begin
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;
  Integer i,rwcnt;
    
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoOwnChkLabForm;
  end;
  rwcnt = MatRowCnt(FDr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,OwnCheckr.SerNr,FDrw.PrintGroupCode,
                      "",intdocnr,"OwnCheckVc",formcode)) then
      begin
        goto LBREAK;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      DoPrintOwnCheck(RepSpec,formcode,FDrw.LangCode,FDrw.PrintGroupCode);
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoOwnChkLabForm:;  
  return;
end;

global
procedure OwnChkLabForm(record RcVc RepSpec)
begin
  Boolean tmpf;
  
  tmpf = PrintDocument(RepSpec,"OwnChkLabForm",false);
  return;
end;

