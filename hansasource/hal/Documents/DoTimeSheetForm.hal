external function Boolean FindFormcode(Integer,Integer);
external procedure CommonDocumentFields(record RcVc);
external procedure GetUserName(string,var string);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);

procedure PrintTimeSheet(record TSVc TSr)
BEGIN
  record SysFormatBlock SysFormatRec;
  record LangNrVc LangNrr;
  record PRVc PRr;
  Integer i,rwcnt;
  row TSVc TSrw;
  string 255 tstr;

  BlockLoad(SysFormatRec);  
  GetLangNr("",LangNrr);
  PrintValue("F_TOTINQTY",TSr.SumOther,M4Qty,LangNrr,SysFormatRec,false);
  PrintValue("F_TOTQTY",TSr.Sum,M4Qty,LangNrr,SysFormatRec,false);
  PrintValue("F_SUMMA",TSr.Sum,M4UVal,LangNrr,SysFormatRec,false);
  OUTFORMFIELD("F_NUMMER",TSr.SerNr);
  OUTFORMFIELD("F_COMMENT",TSr.Comment);
  OUTFORMFIELD("F_TRANSDATE",TSr.RegDate);
  OUTFORMFIELD("F_DOCTYPE",USetStr(1831));
  rwcnt = MatRowCnt(TSr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(TSr,i,TSrw);
    OUTFORMFIELD("F_ARTNR",TSrw.ArtCode);
    OUTFORMFIELD("F_TRANSDATUM",TSrw.date);
    OUTFORMFIELD("F_BESKRIVNING",TSrw.Spec);
    GetUserName(TSrw.EMCode,tstr);
    OUTFORMFIELD("F_PERSNAME",tstr);
    if (TSrw.stp==2) then begin
      OUTFORMFIELD("F_PERSON",USetStr(3051));
      OUTFORMFIELD("F_PROJEKTNUMMER",TSrw.EMCode);
    end else begin
      OUTFORMFIELD("F_PERSON",TSrw.EMCode);
      OUTFORMFIELD("F_PROJEKTNUMMER",TSrw.PRCode);
      PrintValue("F_ANTAL",TSrw.Qty,M4UVal,LangNrr,SysFormatRec,false);
    end;
    PRr.Code = TSrw.PRCode;
    if (ReadFirstMain(PRr,1,true)) then begin
      OUTFORMFIELD("F_LEDARE",PRr.Leader);  
      OUTFORMFIELD("F_LEDARE2",PRr.Leader2);  
      OUTFORMFIELD("F_LEDARE3",PRr.Leader3);  
      OUTFORMFIELD("F_LEDARE4",PRr.Leader4);  
      OUTFORMFIELD("F_LEDARE5",PRr.Leader5);  
      OUTFORMFIELD("F_NAMN",PRr.LeaderName);  
      OUTFORMFIELD("F_NAMN2",PRr.Leader2Name);  
      OUTFORMFIELD("F_NAMN3",PRr.Leader3Name);  
      OUTFORMFIELD("F_NAMN4",PRr.Leader4Name);  
      OUTFORMFIELD("F_NAMN5",PRr.Leader5Name);  
    end;
    EndFormRow;
  end;
  RETURN;
END;

global
procedure DoTimeSheetForm(record RcVc RepSpec,record TSVc TSr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;
  Integer i,rwcnt;
    
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoTimeSheetForm;
  end;
  rwcnt = MatRowCnt(FDr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (FindFormcode(TSr.OKFlag,FDrw.Typ)) then begin 
        if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,TSr.SerNr,FDrw.PrintGroupCode,
                        "",intdocnr,"TSVc",formcode)) then
        begin
          goto LBREAK;
        end;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        CommonDocumentFields(RepSpec);
        PrintTimeSheet(TSr);
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoTimeSheetForm:;  
  RETURN;
END;
