external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external procedure CommonDocumentFields(record RcVc);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external function Integer NumberofDocumentPages(string,Integer);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintRouting(LongInt,string,Integer,record DocVc);

global
procedure DoRoutingPrOrdForm(record RcVc RepSpec,record ProdOrderVc ProdOrdr)
begin
  record DocVc Docr;
  record FormDefVc FDr;
  row FormDefVc FDrw;
  record SysFormatBlock SFb;
  record LangNrVc LangNrr;
  Integer i,rwcnt;
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;

  BlockLoad(SFb);  
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto L99;
  end;
  rwcnt = MatRowCnt(FDr);
  GetLangNr("",LangNrr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,ProdOrdr.SerNr,FDrw.PrintGroupCode,
                      ProdOrdr.LangCode,intdocnr,"RoutingVc",formcode)) then

      begin
        goto LBREAK;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        CommonDocumentFields(RepSpec);
        OUTFORMFIELD("F_SERNR",ProdOrdr.SerNr);
        OUTFORMFIELD("F_DUEDATE",ProdOrdr.DueDate);        
        OUTFORMFIELD("F_FAKTDATUM",ProdOrdr.PlannedStartDate);        
        OUTFORMFIELD("F_RECEPY",ProdOrdr.Recipe);
        OUTFORMFIELD("F_RECNAME",ProdOrdr.RecName);
        OUTFORMFIELD("F_LOCATION",ProdOrdr.Location);
        OUTFORMFIELD("F_FIXASSETCODE",ProdOrdr.Machine);
        Docr.Code = formcode;
        ReadFirstMain(Docr,1,true);
        PrintRouting(ProdOrdr.SerNr,ProdOrdr.Routing,1,Docr);
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
L99:;
  return;
end;
