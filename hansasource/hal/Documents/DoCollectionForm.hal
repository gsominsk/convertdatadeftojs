external procedure OutAddress(string,string,string,string,string,string,string);
external function Boolean FindFormcode(Integer,Integer);
external procedure PrintAgreementInformation(record AgreementVc,record CUVc,Boolean,Boolean);
external procedure SetFaxInfo(string,string,string,string);
external procedure SelectUnitText(string,string,var string);
external procedure ItemBC39(string,var string);
external procedure ItemBCEAN(string,var string);
external procedure ItemBCEAN13(string,var string);
external procedure ValToText(val,Integer,string,string,var string);
external procedure GetUserName(string,var string);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external procedure PrintSiteInformation(string,string);
external procedure PrintLocationInformation(string);


procedure FindCustOrderNo(record CollectionVc Collectionr,var string tstr)
begin
  row CollectionVc Collectionrw;
  record RentResVc RentResr;
  integer i, rwcnt;
  rwcnt = MatRowCnt(Collectionr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Collectionr,i,Collectionrw);
    if (Collectionrw.RentResSerNo <> -1) then begin
      RentResr.SerNr = Collectionrw.RentResSerNo;
      if (ReadFirstMain(RentResr,1,true)) then begin
        tstr = RentResr.CustOrdNr;
        goto L99;
      end;
    end;
  end;
  L99:;
  return;
end;

procedure PrintCollection(record CollectionVc Collectionr,string formcode)
BEGIN
  record LangNrVc LangNrr;
  record SysFormatBlock SysFormatRec;
  record AgreementVc Agreementr;
  record CUVc CUr;
  record INVc INr;
  row CollectionVc Collectionrw;
  record RentResVc RentResr;
  Integer i,rwcnt;
  string 255 tstr;

  BlockLoad(SysFormatRec);  
  GetLangNr("",LangNrr);
  CUr.Code = Collectionr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    SetFaxInfo(CUr.Person,CUr.Name,CUr.Fax,USetStr(1820));
  end;
  Agreementr.SerNr = Collectionr.AgreementNr;
  if (ReadFirstMain(Agreementr,1,true)==false) then begin
    RecordClear(Agreementr);
  end;
  OUTFORMFIELD("F_KUNDNR",Collectionr.CustCode);
  OUTFORMFIELD("F_NUMMER",Collectionr.SerNr);
  OutAddress("F_LEVADRESS",Collectionr.Addr0,Collectionr.Addr1,Collectionr.Addr2,Collectionr.Addr3,Collectionr.DelAddr3,Collectionr.DelAddr4);
  OUTFORMFIELD("F_DELADDRESSADDR",Collectionr.Addr0);
  OUTFORMFIELD("F_DELADDRESSADDR1",Collectionr.Addr1);
  OUTFORMFIELD("F_DELADDRESSADDR2",Collectionr.Addr2);
  OUTFORMFIELD("F_DELADDRESSADDR3",Collectionr.Addr3);
  OUTFORMFIELD("F_DELADDRESSADDR4",Collectionr.DelAddr3);
  OUTFORMFIELD("F_ERREFERENS",Collectionr.ClientContact);
  OUTFORMFIELD("F_KOMMENTAR",Collectionr.Comment);
  if (FIELDINFORM("F_RESNR")) then begin
    for (i=0;i<1;i=i+1) begin
      MatRowGet(Collectionr,i,Collectionrw);
      OUTFORMFIELD("F_RESNR",Collectionrw.RentResSerNo);
    end;
  end;
  if (FIELDINFORM("F_KUNDORDNR")) then begin
    FindCustOrderNo(Collectionr,tstr);
    OUTFORMFIELD("F_KUNDORDNR",tstr);
  end;
  PrintAgreementInformation(Agreementr,CUr,true,false);
  OUTFORMFIELD("F_DUEDATE",Collectionr.CollectionDate);
  OUTFORMFIELD("F_TRANSDATE",Collectionr.TransDate);
  if (FIELDINFORM("F_ENDDATE")) then begin
   for (i=0;i<1;i=i+1) begin
     MatRowGet(Collectionr,i,Collectionrw);
     RentResr.SerNr = Collectionrw.RentResSerNo;
     if (ReadFirstMain(RentResr,1,true)) then begin
       OUTFORMFIELD("F_ENDDATE",RentResr.EndDate);
     end;
    end;
  end;
  rwcnt = MatRowCnt(Collectionr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Collectionr,i,Collectionrw);
    INr.Code = Collectionrw.ArtCode;
    if (ReadFirstMain(INr,1,true)) then begin
    end;
    OUTFORMFIELD("F_ARTNR",Collectionrw.ArtCode);
    OUTFORMFIELD("F_ARTNR2",Collectionrw.ArtCode);
    if (FIELDINFORM("F_ARTNRBC39")) then begin
      ItemBC39(Collectionrw.ArtCode,tstr);
      OUTFORMFIELD("F_ARTNRBC39",tstr);
    end;  
    if (FIELDINFORM("F_ARTNRBCEAN")) then begin
      ItemBCEAN(Collectionrw.ArtCode,tstr);
      OUTFORMFIELD("F_ARTNRBCEAN",tstr);
    end;  
    if (FIELDINFORM("F_ARTNRBCEAN13")) then begin
      ItemBCEAN13(Collectionrw.ArtCode,tstr);      
      OUTFORMFIELD("F_ARTNRBCEAN13",tstr);
    end;  
    OUTFORMFIELD("F_ARTGRUPP",INr.Group);
/*
printed from PrintAgreementInformation    
    Agreementr.SerNr = Collectionr.AgreementNr;
    if (ReadFirstMain(Agreementr,1,true)) then begin
      PrintSiteInformation(Agreementr.Site);
    end; 
*/    
    PrintLocationInformation(Collectionr.Location);
    OUTFORMFIELD("F_AGREEMENT",Collectionr.AgreementNr);
    PrintValue("F_ANTAL",Collectionrw.Quant,M4UVal,LangNrr,SysFormatRec,false);
    SelectUnitText("",INr.Unittext,tstr);    
    OUTFORMFIELD("F_ENHET",tstr);
    OUTFORMFIELD("F_SERIALNR",Collectionrw.SerialNr);
    OUTFORMFIELD("F_SPECIFIKATION",Collectionrw.Spec);
    EndFormRow;
  end;  
  RETURN;
END;

global
procedure DoCollectionForm(record RcVc RepSpec,record CollectionVc Collectionr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Boolean printf,langf;
  Integer intdocnr;
  string 30 formcode,langcode;
  Integer i,rwcnt;
  string 255 tstr;
    
  langcode = Collectionr.LangCode;
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoCollectionForm;
  end;
  rwcnt = MatRowCnt(FDr);
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(FDr,i,FDrw);
    if (FDrw.LangCode==langcode) then begin
      i = -1;
      langf = true;
    end;
  end;
  if (langf==false) then begin langcode = ""; end;
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (FindFormcode(Collectionr.OKFlag,FDrw.Typ)) then begin 
        if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,Collectionr.SerNr,FDrw.PrintGroupCode,
                        langcode,intdocnr,"CollectionVc",formcode)) then
        begin
          goto LBREAKCollection;
        end;
      end;
    end;
LBREAKCollection:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        PrintCollection(Collectionr,formcode);
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoCollectionForm:;  
  RETURN;
END;

