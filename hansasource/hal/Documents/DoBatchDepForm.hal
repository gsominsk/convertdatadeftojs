external procedure PrintCurrencyName(string,string);
external procedure PrintCurrencyCode(string,string);
external procedure GetAccountName(String,var String);  
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external procedure CommonDocumentFields(record RcVc);
external function Boolean GetPM(string,var string,var string);

procedure PrintBatchDep(record RcVc RepSpec,record ChqDepVc ChqDepp,string formcode,string langcode)
BEGIN
  record CUVC CUr;
  row ChqDepVc ChqDeprw;
  Integer i,rwcnt;
  string 255 tstr,t2;
  record CheckVc Checkr;

  OUTFORMFIELD("F_SERIALNR",ChqDepp.SerNr);
  OUTFORMFIELD("F_BATCHDEPDATUM",ChqDepp.TransDate);
  OUTFORMFIELD("F_COMMENT",ChqDepp.Comment);
  OUTFORMFIELD("F_PAYMODE",ChqDepp.PayMode);
  if (FIELDINFORM("F_PAYMODE2")) then begin
    if (GetPM(ChqDepp.PayMode,t2,tstr)==false) then begin
    end;    
    OUTFORMFIELD("F_PAYMODE2",tstr);
  end;  
  OUTFORMFIELD("F_CASHVALUE",ChqDepp.Cash);
  OUTFORMFIELD("F_PAYMODECASH",ChqDepp.PayModeCash);
  if (FIELDINFORM("F_PAYMODECASH2")) then begin
    if (GetPM(ChqDepp.PayModeCash,t2,tstr)==false) then begin
    end;    
    OUTFORMFIELD("F_PAYMODECASH2",tstr);
  end;   
  OUTFORMFIELD("F_TOTSUM",ChqDepp.Sum);
  PrintCurrencyCode("F_VALUTA",ChqDepp.CurncyCode);
  if (FIELDINFORM("F_VALUTANAMN")) then begin
    PrintCurrencyName("F_VALUTANAMN",ChqDepp.CurncyCode);
  end;   
  OUTFORMFIELD("F_DOCTYPE",USetStr(23468));
  rwcnt = MatRowCnt(ChqDepp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ChqDepp,i,ChqDeprw);
    Checkr.SerNr = ChqDeprw.ChqNr;
    if (ReadFirstMain(Checkr,1,true)) then begin 
      OUTFORMFIELD("F_CHECKNR",Checkr.CheckNr);
      OUTFORMFIELD("F_KUNDNR",Checkr.CUCode);
      CUr.Code = Checkr.CUCode;
      if (ReadFirstMain(CUr,1,true)) then begin      
        OUTFORMFIELD("F_KUNDNAMN",CUr.Name);
      end;
      OUTFORMFIELD("F_BANKNAME",Checkr.BankCode);
      OUTFORMFIELD("F_BANKNAME2",Checkr.BankName);
      OUTFORMFIELD("F_CHEQUETOTAL",Checkr.Amount);
      OUTFORMFIELD("F_EFFECTDATE",Checkr.EffectDate);
      OUTFORMFIELD("F_DEPOSITDATE",Checkr.ClearingDate);
      EndFormRow;     
    end;
  end;
  RETURN;
END;

global
procedure DoBatchDepForm(record RcVc RepSpec,record ChqDepVc BatchDepp)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;
  Integer i,rwcnt;
    
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoBatchDepForm;
  end;

  rwcnt = MatRowCnt(FDr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,BatchDepp.SerNr,FDrw.PrintGroupCode,
                      "",intdocnr,"ChqDepVc",formcode)) then
      begin
        goto LBREAK;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        CommonDocumentFields(RepSpec);
        PrintBatchDep(RepSpec,BatchDepp,formcode,"");
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoBatchDepForm:;  
  RETURN;
END;
