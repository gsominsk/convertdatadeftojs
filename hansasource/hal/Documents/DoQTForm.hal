external outer procedure HMPrintQTBlankItem(row QTVc,record SysFormatBlock,record LangNrVc,var string);
external procedure PrintCurrencyCode(string,string);
external function string 255 GetOffSerNrIVJournalID(LongInt,string,string);
external procedure PrintCountryCode(string,string);
external procedure PrintBankData(string);
external procedure FindSalesExVat(record TaxMatrixVc,string,val,Integer,Integer,var val);
external function string 255 SAFTHashControl(string);
external function roundmode SetRoundModeD(Integer);
external function roundmode DefaultRoundMode();
external procedure PrintSignature(string);
external procedure PrintPersonsCustomerDetails(string);
external procedure PrintValToTextBaseCur(val,Integer,string,Integer,var string);
external procedure PrintUserDefinedCustomerFields(string,string,string,string,string,Date,Date,Date,val,val,val);
external procedure PrintUserDefinedItemFields(string,string,string,string,string,Date,Date,Date,val,val,val);
external procedure OutAddress(string,string,string,string,string,string,string);
external function string 255 GetVarietyComment(string,string);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure PrintValueInclZero(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function val MulRateToBase2(var string,val,val,val,val,val,val,roundmode);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external procedure QTSetFax(record CUVc,record QTVc);
external procedure GetFieldArgument(record DocVc,Integer,var string);
external procedure SelectUnitText(string,string,var string);
external procedure GetShipModeText(string,string,var string);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external function Integer NumberofDocumentPages(string,Integer);
external procedure MulVATIV(string,val,var val,var val,Integer,Integer);
external procedure GetLangNr(string,var record LangNrVc);
external procedure ItemBC39(string,var string);
external procedure ItemBCEAN(string,var string);
external procedure ItemBCEAN13(string,var string);
external procedure GetPayDealText(string,string,var string);
external procedure GetUserEmail(string,var string);
external procedure GetShipDealText(string,string,var string);
external procedure GetUserName(string,var string);
external procedure GetPRName(string,var string);
external procedure GetItemCommodityCode(string,var string);
external procedure GetAlternativeItemCode(string,var string);
external procedure CalcSum(val,val,val,val,var val,Integer);
external procedure BuildKSerno(LongInt,string,var string);
external procedure CommonDocumentFields(record RcVc);
external procedure Mul2VAT(string,val,var val,var val,Integer);
external function Boolean GetVAT2(string,var val,var Integer,Integer);
external function Boolean GetVATincl(string,var val,var Integer);
external procedure ValToText(val,Integer,string,string,var string);
external procedure PrintNote(record NotepadVc);
external procedure GetVATdouble(string,var val,var val,Integer);
external procedure SetRowStyle(record Docvc,string,Integer,string,val);
external procedure PrintBranchData(string);
external function string 255 GetJwl_CustomerName(string);

function
Integer CountQuotationRows(record QTVc QTr)
BEGIN
  row QTVc QTrw;
  Integer i,rwcnt;
  rwcnt = MatRowCnt(QTr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    if (QTrw.stp==10) then begin
      rwcnt = i;
      goto LCountQuotationRows;
    end;
  end;
LCountQuotationRows:;
 CountQuotationRows = rwcnt;
  RETURN;
END;

procedure QTPrintNote(record QTVc QTr,string argumentstr)
BEGIN
  record NotepadVc Noter;
  record RLinkVc RLr;
  Integer notenr;

  notenr = 1;
  while (ReadRecordLink(QTr,notenr,Noter,RLr)) begin
    if ((argumentstr==RLr.Comment) or (blank(argumentstr))) then begin
      PrintNote(Noter);
      goto LQTPrintNote;
    end;
    notenr = notenr + 1;
  end;
LQTPrintNote:;  
  RETURN;
END;

procedure CalcSubtotWithoutDisc(record QTVc QTr,var val res)
BEGIN
  row QTVc QTrw;  
  Integer i,rwcnt;
  
  res = 0;
  rwcnt = MatRowCnt(QTr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    if (QTrw.stp==1) then begin
      res = res + QTrw.Quant*QTrw.Price;
    end;  
  end;    
  RETURN;        
END;

procedure CalcQTTotVolume(record QTVc QTr,var val res)
BEGIN
  record INVc INr;
  row QTVc QTrw;  
  Integer i,rwcnt;

  res = blankval;
  rwcnt = MatRowCnt(QTr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    if (QTrw.stp==1) or (QTrw.stp==kInvoiceRowTypeStructuredItemComponent) then begin
      INr.Code = QTrw.ArtCode;
      if (ReadFirstMain(INr,1,true)) then begin
      end;
      res = res + QTrw.Quant*INr.Volume;
    end;  
  end;  
  RETURN;        
END;

procedure CalcQTTotWeight(record QTVc QTr,var val res)
BEGIN
  record INVc INr;
  row QTVc QTrw;
  Integer i,rwcnt;

  res = blankval;
  rwcnt = MatRowCnt(QTr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    if (QTrw.stp==1) or (QTrw.stp==kInvoiceRowTypeStructuredItemComponent) then begin
      INr.Code = QTrw.ArtCode;
      if (ReadFirstMain(INr,1,true)) then begin
      end;
      res = res + QTrw.Quant*INr.Weight;
    end;  
  end;  
  RETURN;        
END;

procedure QTVat(Integer stp,Integer incvat,Integer exportflag,string vatcode,val rowsump,
                var val vatprcp,var val vatvalp,var val vatexclprc,var val vatinclprc)
BEGIN
  Integer rn;
  
  vatprcp = blankval;
  vatvalp = blankval;
  vatexclprc = blankval;
  vatinclprc = blankval;
  if (stp==1) then begin
    switch (incvat) begin
      case 0:
        if ((exportflag==0) or (exportflag==3) or (exportflag==4)) then begin
          Mul2VAT(vatcode,rowsump,vatvalp,vatprcp,incvat);
          if (GetVAT2(vatcode,vatexclprc,rn,0)) then begin
          end;
          if (GetVATincl(vatcode,vatinclprc,rn)) then begin
          end;
        end;
      otherwise 
        Mul2VAT(vatcode,rowsump,vatvalp,vatprcp,incvat);
        if (GetVAT2(vatcode,vatexclprc,rn,0)) then begin
        end;
        if (GetVATincl(vatcode,vatinclprc,rn)) then begin
        end;
    end;
  end;
  RETURN;
END;

procedure PrintQuotationRows(record RcVc RepSpec,record DocVc Docr,record QTVc QTr,record LangNrVc LangNrr,record SysFormatBlock SFb,Integer rwcnt,var val totdiscount)
BEGIN
  Integer i,rownr;
  row QTVc QTrw;
  string 255 tstr;
  val t,t1,t2,v;
  record INVc INr;
  val vatprc,vatval,vatexclprc,vatinclprc;
  record GeneralOptionBlock GenOptRec;
  record MainStockBlock MSb;
  record RegionVc Regionr;
  record TaxMatrixVc TMr;
  record RoundBlock Roundb;
  record INVc dummyINr;

  RecordClear(dummyINr);
  BlockLoad(GenOptRec);
  BlockLoad(MSb);
  BlockLoad(Roundb);
  RecordClear(dummyINr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    SetRowStyle(Docr,"QTVc",QTrw.stp,QTrw.ArtCode,QTrw.Sum);
    HMPrintQTBlankItem(QTrw,SFb,LangNrr,tstr);
    if (tstr=="SKIP") then begin goto LNextQuotationRow; end;
    
    switch (QTrw.stp) begin
      case kInvoiceRowTypeStructuredItemComponent:
        goto LkInvoiceRowTypeNormal;
      case kInvoiceRowTypeNormal:
  LkInvoiceRowTypeNormal:;    
        if (ReadFirstItem(QTrw.ArtCode,INr,true,false)) then begin
        end;
        OUTFORMFIELD("F_CUSTARTNR",QTrw.CustArtCode);
        OUTFORMFIELD("F_ARTNR",QTrw.ArtCode);
        OUTFORMFIELD("F_ARTNR2",QTrw.ArtCode);
        OUTFORMFIELD("F_ARTCODEVARIETIES",GetVarietyComment(QTrw.ArtCode,INr.Code));
        OUTFORMFIELD("F_ARTCODENOVARIETIES",INr.Code);
        OUTFORMFIELD("F_CPSCODE",INr.CPSCode);

        v = QTrw.vRebate;
        GetFieldArgument(Docr,F_RABATT,tstr);
        if (nonblank(tstr)) then begin
          if (blank(QTrw.ArtCode)) then begin
            PrintValue("F_RABATT",Round(v,SetRoundModeD(StringToInt(tstr))),M4Qty,LangNrr,SFb,false);
          end else begin
            PrintValueInclZero("F_RABATT",Round(v,SetRoundModeD(StringToInt(tstr))),M4Qty,LangNrr,SFb,false);
          end;
        end else begin
          if (blank(QTrw.ArtCode)) then begin
            PrintValue("F_RABATT",v,M41Val,LangNrr,SFb,false);
          end else begin
            PrintValueInclZero("F_RABATT",v,M41Val,LangNrr,SFb,false);
          end;
        end;

        if (nonblank(QTrw.RvrsVATCode)) then begin
          OUTFORMFIELD("F_REVERSEVATCODE",QTrw.RvrsVATCode);
          if (HasLocalization("GBR,LIT")) then begin
            if (nonblank(QTr.RvrsVAT) and nonblank(QTr.Sum3)) then begin
              OUTFORMFIELD("F_ROWREVERSEVATTEXT",USetStr(39370));
            end;
          end;
        end;
        OUTFORMFIELD("F_MOMSKOD",QTrw.VATCode);
        OUTFORMFIELD("F_UNITFACTQUANT",QTrw.UnitFactQuant);
        OUTFORMFIELD("F_UNITCODE",QTrw.UnitCode);
        tstr = ValToString(QTrw.UnitFactPrice,M423Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        OUTFORMFIELD("F_UNITFACTPRICE",tstr);
        GetItemCommodityCode(QTrw.ArtCode,tstr);
        OUTFORMFIELD("F_COMMODITYCODE",tstr);
        GetAlternativeItemCode(QTrw.ArtCode,tstr);
        OUTFORMFIELD("F_ALTERNATIVECODE",tstr);
        OUTFORMFIELD("F_ROWWIDTH",QTrw.UnitXval);
        OUTFORMFIELD("F_ROWHEIGHT",QTrw.UnitYval);
        OUTFORMFIELD("F_ROWDEPTH",QTrw.UnitZval);
        if (FIELDINFORM("F_ARTNRBC39")) then begin
          ItemBC39(QTrw.ArtCode,tstr);
          OUTFORMFIELD("F_ARTNRBC39",tstr);
        end;  
        if (FIELDINFORM("F_ARTNRBCEAN")) then begin
          ItemBCEAN(QTrw.ArtCode,tstr);
          OUTFORMFIELD("F_ARTNRBCEAN",tstr);
        end;  
        if (FIELDINFORM("F_ARTNRBCEAN13")) then begin
          ItemBCEAN13(QTrw.ArtCode,tstr);      
          OUTFORMFIELD("F_ARTNRBCEAN13",tstr);
        end;  
        OUTFORMFIELD("F_SPECIFIKATION",QTrw.Spec);
        tstr = ValToString(QTrw.Sum,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        OUTFORMFIELD("F_BELOPP",tstr);
        tstr = ValToString(QTrw.Quant,M4UVal,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        OUTFORMFIELD("F_ANTAL",tstr);
        if (MSb.UnitConvCalc==0) then begin
          tstr = ValToString(QTrw.Quant*INr.UnitCoefficient,M4UVal,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        end else begin
          tstr = ValToString(QTrw.Quant/INr.UnitCoefficient,M4UVal,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        end;
        OUTFORMFIELD("F_ANTAL2",tstr);
        tstr = ValToString(QTrw.Price,M423Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        OUTFORMFIELD("F_APRIS",tstr);
        if (QTrw.PriceFactor==blankval) then begin
          tstr = "";
        end else begin
          tstr = QTrw.PriceFactor;
        end;
        OUTFORMFIELD("F_PRICEFACTOR",tstr);
        if (QTrw.PriceFactor!=blankval) then begin
          t = QTrw.Price/QTrw.PriceFactor;
        end else begin
          t = QTrw.Price;
        end;
        tstr = ValToString(t,M423Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
        OUTFORMFIELD("F_UNITPRICE",tstr);      
        if (FIELDINFORM("F_ENHET")) then begin
          SelectUnitText(QTr.LangCode,INr.Unittext,tstr);
          OUTFORMFIELD("F_ENHET",tstr);
        end;
        OUTFORMFIELD("F_VOLUME",INr.Volume);      
        OUTFORMFIELD("F_WIDTH",INr.Width);      
        OUTFORMFIELD("F_HEIGHT",INr.Height);      
        OUTFORMFIELD("F_DEPTH",INr.Depth);      
        OUTFORMFIELD("F_WEIGHT",INr.Weight);      
        OUTFORMFIELD("F_ROWWEIGHT",QTrw.Quant*INr.Weight);      
        OUTFORMFIELD("F_ROWVOLUME",QTrw.Quant*INr.Volume);  
        tstr = -QTrw.vRebate;
        if (nonblank(tstr)) then begin
          tstr = tstr & "%";
        end;
        OUTFORMFIELD("F_RABATTSPEC",tstr);      
        OUTFORMFIELD("F_AMOUNTROUNDED",Round(QTrw.Sum,SetRoundModeD(0)));      
        t = QTrw.Sum*QTr.DiscPerc;
        t = t/100;
        OUTFORMFIELD("F_DIFFROWVALUE",t);      
        if (nonblank(QTrw.ArtCode)) then begin
          rownr = rownr + 1;
          OUTFORMFIELD("F_ROWNR",rownr);      
        end;  
        CalcSum(1,QTrw.Price,QTrw.PriceFactor,QTrw.vRebate,t,GenOptRec.UseDiscount);
        tstr = ValToString(t,M423Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);      
        if (t==0) then begin
          t = blankval;
        end;
        OUTFORMFIELD("F_ROWPRICEREBATE",t);         
        t = MulRateToBase1(QTr.CurncyCode,QTrw.Price,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
        if (t==0) then begin
          t = blankval;
        end;
        OUTFORMFIELD("F_PRICEBASECURNCY1",t);      
        t = MulRateToBase2(QTr.CurncyCode,QTrw.Price,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
        if (t==0) then begin
          t = blankval;
        end;
        OUTFORMFIELD("F_PRICEBASECURNCY2",t);
        t = MulRateToBase1(QTr.CurncyCode,QTrw.Sum,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
        if (t==0) then begin
          t = blankval;
        end;
        OUTFORMFIELD("F_ROWPRICEBASECURNCY1",t);
        t = MulRateToBase2(QTr.CurncyCode,QTrw.Sum,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
        if (t==0) then begin
          t = blankval;
        end;
        OUTFORMFIELD("F_ROWPRICEBASECURNCY2",t);
        BuildKSerno(QTr.SerNr,QTrw.ArtCode,tstr);
        OUTFORMFIELD("F_KFAKTURANR",tstr);
        QTVat(QTrw.stp,QTr.InclVAT,QTr.ExportFlag,QTrw.VATCode,QTrw.Sum,vatprc,vatval,vatexclprc,vatinclprc);
        if (HasLocalization("POL")) then begin
          tstr = "";
          if (nonblank(QTrw.Sum)) then begin
            if (nonblank(vatinclprc)) then begin
              if ((vatinclprc==0) and (nonblank(vatexclprc))) then begin
                tstr = USetStr(8111);
              end;
            end;
            if ((vatinclprc==0) and (nonblank(vatinclprc))) then begin
              if (blank(vatexclprc)) then begin
                tstr = USetStr(8110);
              end;
            end;
          end;
          if (blank(tstr)) then begin
            tstr = ValToString(vatexclprc,M4UVal,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
          end;
          if (blank(tstr)) then begin
            tstr = USetStr(1244);
          end;
          OUTFORMFIELD("F_ROWVATEXCLPRC",tstr);    
        end else begin
          if (nonblank(QTrw.Sum)) then begin
            tstr = ValToString(vatexclprc,M4UVal,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
            if (blank(tstr)) then begin
              tstr = USetStr(1244);
            end else begin
              tstr = tstr & "%";
            end;
            OUTFORMFIELD("F_ROWVATEXCLPRC",tstr);
          end;
        end;


        if (FIELDINFORM("F_BASE1ROWVATVAL")) then begin
          t = MulRateToBase1(QTr.CurncyCode,vatval,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
          if (t==0) then begin
            t = blankval;
          end;
          OUTFORMFIELD("F_BASE1ROWVATVAL",t);    
        end;
        if (FIELDINFORM("F_BASE2ROWVATVAL")) then begin
          t = MulRateToBase2(QTr.CurncyCode,vatval,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
          if (t==0) then begin
            t = blankval;
          end;
          OUTFORMFIELD("F_BASE2ROWVATVAL",t);    
        end;
        if (FIELDINFORM("F_DIFFTOTVALUE")) then begin
          if (QTrw.Sum!=blankval) then begin
            t = vatexclprc*QTr.DiscPerc;
            t = t/100;
            if (t==0) then begin
              t = blankval;
            end;
            tstr = t;
            if (blank(tstr)) then begin
              tstr = USetStr(1244);
            end;  
            OUTFORMFIELD("F_DIFFTOTVALUE",tstr);
          end;
        end;
        if (QTrw.Sum!=blankval) then begin
          MulVATIV(QTrw.VATCode,QTrw.Sum,t,t1,QTr.InclVAT,QTr.NoTAXonVAT);
          OUTFORMFIELD("F_ROWTAXVAL",t1);    
          OUTFORMFIELD("F_ROWVATVAL",t);
          t1 = t + QTrw.Sum + t1;          
          OUTFORMFIELD("F_ROWSUMTOTAL",t1);    
          t = t + QTrw.Sum;          
          OUTFORMFIELD("F_ROWSUMWITHVAT",t);    
          GetVATdouble(QTrw.VATCode,t,t1,QTr.InclVAT);
          PrintValue("F_ROWVATPRC",t,M4Val,LangNrr,SFb,false);
        end;
        OUTFORMFIELD("F_TIMECLASS",QTrw.TimeClass);

        if (FIELDINFORM("F_ROWTOT")) then begin
          v = QTrw.Sum;
          if (QTr.InclVAT==0) then begin
            v = v + vatval;
          end;
          if ((nonblank(vatval)) or (nonblank(QTrw.Sum))) then begin
            GetFieldArgument(Docr,F_ROWTOT,tstr);
            if (nonblank(tstr)) then begin
              if (blank(QTrw.ArtCode)) then begin
                PrintValue("F_ROWTOT",Round(v,SetRoundModeD(StringToInt(tstr))),M45Val,LangNrr,SFb,false);
              end else begin
                PrintValueInclZero("F_ROWTOT",Round(v,SetRoundModeD(StringToInt(tstr))),M45Val,LangNrr,SFb,false);
              end;
            end else begin
              if (blank(QTrw.ArtCode)) then begin
                PrintValue("F_ROWTOT",v,M4Val,LangNrr,SFb,false);
              end else begin
                PrintValueInclZero("F_ROWTOT",v,M4Val,LangNrr,SFb,false);
              end;
            end;
          end;
        end;
        
        if (FIELDINFORM("F_ROWBASE")) then begin
          v = QTrw.Sum;
          if (QTr.InclVAT>0) then begin
            if (v!=0) then begin
              v = v - vatval;
            end;
          end;
          GetFieldArgument(Docr,F_ROWBASE,tstr);
          if (v==0) then begin v = blankval; end;          
          if (nonblank(tstr)) then begin
            if (QTrw.Sum==0) then begin v = blankval; end;          
            PrintValueInclZero("F_ROWBASE",Round(v,SetRoundModeD(StringToInt(tstr))),M45Val,LangNrr,SFb,false);
          end else begin
            if (QTrw.Sum==0) then begin v = blankval; end;          
            PrintValueInclZero("F_ROWBASE",v,M4Val,LangNrr,SFb,false);
          end;
        end;
        if (FIELDINFORM("F_BASE1ROWBASE")) then begin
          v = QTrw.Sum;
          if (QTr.InclVAT>0) then begin
            if (v!=0) then begin
              v = v - vatval;
            end;
          end;
          GetFieldArgument(Docr,F_BASE1ROWBASE,tstr);
          if (nonblank(tstr)) then begin
            t = Round(v,SetRoundModeD(StringToInt(tstr)));
            t = MulRateToBase1(QTr.CurncyCode,t,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,SetRoundModeD(StringToInt(tstr)));
            if (blank(QTrw.Sum)) then begin
              if (t==0) then begin t = blankval; end;
            end;
            PrintValueInclZero("F_BASE1ROWBASE",t,M4Val,LangNrr,SFb,false);
          end else begin
            t = MulRateToBase1(QTr.CurncyCode,v,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
            if (blank(QTrw.Sum)) then begin
              if (t==0) then begin t = blankval; end;          
            end;
            PrintValueInclZero("F_BASE1ROWBASE",t,M4Val,LangNrr,SFb,false);
          end;
        end;
        if (FIELDINFORM("F_BASE2ROWBASE")) then begin
          v = QTrw.Sum;
          if (QTr.InclVAT>0) then begin
            if (v!=0) then begin
              v = v - vatval;
            end;
          end;
          GetFieldArgument(Docr,F_BASE2ROWBASE,tstr);
          if (nonblank(tstr)) then begin
            t = Round(v,SetRoundModeD(StringToInt(tstr)));
            t = MulRateToBase2(QTr.CurncyCode,t,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,SetRoundModeD(StringToInt(tstr)));
            if (t==0) then begin t = blankval; end;          
            PrintValueInclZero("F_BASE2ROWBASE",t,M4Val,LangNrr,SFb,false);
          end else begin
            t = MulRateToBase2(QTr.CurncyCode,v,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
            if (t==0) then begin t = blankval; end;          
            PrintValueInclZero("F_BASE2ROWBASE",t,M4Val,LangNrr,SFb,false);
          end;
        end;
        
        UnpackRowFieldMatrix(QTrw,"TaxMatrix",TMr);
        if (FIELDINFORM("F_APRISEXCLVAT")) then begin
          FindSalesExVat(TMr,QTrw.VATCode,QTrw.Price,QTr.InclVAT,QTr.NoTAXonVAT,t);
          GetFieldArgument(Docr,F_APRISEXCLVAT,tstr);
          if (nonblank(tstr)) then begin
            t = Round(t,SetRoundModeD(StringToInt(tstr)));
            PrintValueInclZero("F_APRISEXCLVAT",t,M423Val,LangNrr,SFb,false);
          end else begin
            if (Roundb.Discount==0) then begin
              t = Round(t,SetRoundModeD(3));
            end else begin
              t = Round(t,DefaultRoundMode);
            end;
            PrintValueInclZero("F_APRISEXCLVAT",t,M423Val,LangNrr,SFb,false);
          end;  
        end;
        if (FIELDINFORM("F_BASPRIS")) then begin
          if (QTr.InclVAT==0) then begin
            MulVATIV(QTrw.VATCode,QTrw.Price,t,t2,0,0);
            t = t + QTrw.Price;
          end else begin
            t = QTrw.Price*QTrw.vRebate;
            t = t/100;
            v = QTrw.Price - t;
            t = vatval/QTrw.Quant;
            t = v - t;
          end;
          GetFieldArgument(Docr,F_BASPRIS,tstr);
          if (nonblank(tstr)) then begin
            PrintValue("F_BASPRIS",Round(t,SetRoundModeD(StringToInt(tstr))),M423Val,LangNrr,SFb,false);
          end else begin
            GetFieldArgument(Docr,F_BASPRIS,tstr);
            if (nonblank(tstr)) then begin
              PrintValue("F_BASPRIS",Round(t,SetRoundModeD(StringToInt(tstr))),M423Val,LangNrr,SFb,false);
            end else begin
              PrintValue("F_BASPRIS",t,M423Val,LangNrr,SFb,false);
            end;
          end;
        end;
        totdiscount = totdiscount + (QTrw.Quant*QTrw.Price - QTrw.Sum);        
        PrintUserDefinedItemFields(INr.UserStr1,INr.UserStr2,INr.UserStr3,INr.UserStr4,INr.UserStr5,INr.UserDate1,INr.UserDate2,INr.UserDate3,INr.UserVal1,INr.UserVal2,INr.UserVal3);
        OutFormImage(INr);
        EndFormRow;
      case kInvoiceRowTypeSubtotal:
        PrintValueInclZero("F_BELOPP",QTrw.Sum,M4Val,LangNrr,SFb,false);
        PrintValueInclZero("F_SUBTOTALAMOUNT",QTrw.Sum,M4Val,LangNrr,SFb,false);
        OUTFORMFIELD("F_SPECIFIKATION",QTrw.Spec);    
        OutFormImage(dummyINr);
        EndFormRow;
      case kInvoiceRowTypeHidden:
        goto LPrintQuotationRows;
      case kInvoiceRowTypeHeader:
        OUTFORMFIELD("F_SPECIFIKATION",QTrw.Spec);    
        OutFormImage(dummyINr);
        EndFormRow;
      case kInvoiceRowTypePerceptionTax:
        OUTFORMFIELD("F_ROWNAME",USetStr(8108));
        OUTFORMFIELD("F_REGIONROW",QTrw.Region);
        Regionr.Code = QTrw.Region;
        ReadFirstMain(Regionr,1,true);
        OUTFORMFIELD("F_REGIONNAMEROW",Regionr.Name);
        OUTFORMFIELD("F_SPECIFIKATION",QTrw.Spec); 
        OUTFORMFIELD("F_SALESACC",QTrw.SalesAcc);
        PrintValue("F_TAX2PRC",QTrw.TAX2Prc,M4Val,LangNrr,SFb,false);          
        PrintValue("F_BELOPP",QTrw.Sum,M4Val,LangNrr,SFb,false);        
        EndFormRow;        
    end;
LNextQuotationRow:;
  end;  
LPrintQuotationRows:;  
  RETURN;
END;

procedure PrintQT(record RcVc RepSpec,record SysFormatBlock SFb,string formcode,record QTVc QTr,record LangNrVc LangNrr)
begin
  record DocVc Docr;
  record PRVc PRr;
  record LocationVc Locr;
  Integer i,rwcnt,rwcnt2;
  record CUVc CUr;
  record IIBlock IIRec;
  string 255 tstr;
  val t;
  val totdiscount;

  Docr.Code = formcode;
  ReadFirstMain(Docr,1,true);
  CommonDocumentFields(RepSpec);          
  rwcnt2 = CountQuotationRows(QTr);
  NumberofDocumentPages(formcode,rwcnt2);
  CUr.Code = QTr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
  end;        
  PrintUserDefinedCustomerFields(CUr.UserStr1,CUr.UserStr2,CUr.UserStr3,CUr.UserStr4,CUr.UserStr5,CUr.UserDate1,CUr.UserDate2,CUr.UserDate3,CUr.UserVal1,CUr.UserVal2,CUr.UserVal3);
  PrintSignature(QTr.Sign);
  OUTFORMFIELD("F_SAFTHASHCONTROL",SAFTHashControl(QTr.Hash));
  if (HasLocalization("PRT")) then begin
    OUTFORMFIELD("F_OFFICIALSERNO",GetOffSerNrIVJournalID(QTr.SerNr,"ProformaQTVc",QTr.ProformaOfficialSerNr) & " " & QTr.ProformaOfficialSerNrSerie & "/" & Right(QTr.ProformaOfficialSerNr,len(QTr.ProformaOfficialSerNr)-len(QTr.ProformaOfficialSerNrSerie)));
  end else begin
    OUTFORMFIELD("F_OFFICIALSERNO",QTr.ProformaOfficialSerNr);
  end;

  OUTFORMFIELD("F_REFERENSNUMMER",QTr.RefStr);
  OUTFORMFIELD("F_KUNDNAMN",QTr.Addr0);        
  OUTFORMFIELD("F_DISCSUM",QTr.DiscSum);        
  OUTFORMFIELD("F_DISCPERC",QTr.DiscPerc);        
  OUTFORMFIELD("F_RECIPIENTGLN",QTr.RecipientGLN);
  OUTFORMFIELD("F_GOODSRECIPIENTGLN",QTr.DelRecipientGLN);
  OUTFORMFIELD("F_VATNR",QTr.VATNr);        
  OUTFORMFIELD("F_COMMENT",QTr.Comment);        
  OUTFORMFIELD("F_KOMMENTAR",QTr.Comment);
  OUTFORMFIELD("F_KOMMENTAR",QTr.Comment2);
  OUTFORMFIELD("F_KOMMENTAR",QTr.Comment3);
  OUTFORMFIELD("F_KOMMENTAR",QTr.Comment4);
  OUTFORMFIELD("F_SERVICEORDER",QTr.SVOSerNr);
  PrintBankData(QTr.BankCode);
  OUTFORMFIELD("F_OFFERTDATUM",QTr.QTDate);        
  OUTFORMFIELD("F_OFFERTDATUMHIJRI",ConvertDate(kCalendarGregorian,QTr.QTDate,kCalendarHijri));
  OUTFORMFIELD("F_NUMMER",QTr.SerNr);        
  tstr = ValToString(QTr.Sum1,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_SUMMA",tstr);      
  tstr = ValToString(QTr.Sum3,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_MOMS",tstr);      
  tstr = ValToString(QTr.Sum4,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  PrintValue("F_FRAKT",QTr.FrPrice,M4Val,LangNrr,SFb,false);
  PrintValue("F_SUMFREIGHT",QTr.Sum1-QTr.FrPrice,M4Val,LangNrr,SFb,false);
  PrintValue("F_TOPAYEXCLFREIGHT",QTr.Sum1-QTr.FrPrice+QTr.Sum3,M4Val,LangNrr,SFb,false);
  OUTFORMFIELD("F_ATTBETALA",tstr);       
  if (FIELDINFORM("F_ATTBETALATEXT")) then begin
    ValToText(QTr.Sum4,M4Val,QTr.CurncyCode,QTr.LangCode,tstr);
    OUTFORMFIELD("F_ATTBETALATEXT",tstr);        
  end;
  if (FIELDINFORM("F_AMOUNTINTEXTBASECUR1")) then begin
    t = MulRateToBase1(QTr.CurncyCode,QTr.Sum4,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
    PrintValToTextBaseCur(t,M4Val,QTr.LangCode,1,tstr);
    OUTFORMFIELD("F_AMOUNTINTEXTBASECUR1",tstr);  
  end;  
  if (FIELDINFORM("F_AMOUNTINTEXT")) then begin
    ValToText(QTr.Sum4,M4Val,QTr.CurncyCode,QTr.LangCode,tstr);
    OUTFORMFIELD("F_AMOUNTINTEXT",tstr);        
  end;
  if (FIELDINFORM("F_BASE1SUMMA")) then begin
    t = MulRateToBase1(QTr.CurncyCode,QTr.Sum1,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
    tstr = ValToString(t,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_BASE1SUMMA",tstr);
  end;
  if (FIELDINFORM("F_BASE2SUMMA")) then begin
    t = MulRateToBase2(QTr.CurncyCode,QTr.Sum1,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
    tstr = ValToString(t,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_BASE2SUMMA",tstr);
  end;
  if (FIELDINFORM("F_BASE1BETALA")) then begin
    t = MulRateToBase1(QTr.CurncyCode,QTr.Sum4,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
    tstr = ValToString(t,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_BASE1BETALA",tstr);
  end;
  if (FIELDINFORM("F_BASE2BETALA")) then begin
    t = MulRateToBase2(QTr.CurncyCode,QTr.Sum4,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
    tstr = ValToString(t,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_BASE2BETALA",tstr);
  end;
  PrintValue("F_REVERSEVATAMOUNT",QTr.RvrsVAT,M4Val,LangNrr,SFb,false);
  if (blank(QTr.RvrsVAT)==false) then begin
    if (HasLocalization("POL")) then begin
      OUTFORMFIELD("F_REVERSEVATTEXT",USetStr(39371));
    end;
    if (HasLocalization("LIT")) then begin
      if (blank(QTr.Sum3)==false) then begin
        OUTFORMFIELD("F_REVERSEVATTEXT",USetStr(39371));
      end;
    end;
    if (HasLocalization("GBR")) then begin
      if (blank(QTr.Sum3)) then begin
        OUTFORMFIELD("F_REVERSEVATTEXT",USetStr(39372));
      end else begin
        OUTFORMFIELD("F_REVERSEVATTEXT",USetStr(39374));
      end;
    end;
  end;

  OUTFORMFIELD("F_KUNDNR",QTr.CustCode);        
  OUTFORMFIELD("F_DEPARTMENT",CUr.Department); 
  PrintCountryCode("F_CUCOUNTRYCODE",CUr.CountryCode)
  GetUserEmail(QTr.SalesMan,tstr);       
  OUTFORMFIELD("F_EMAIL",tstr);        
  OUTFORMFIELD("F_CUSTOMEREMAIL",CUr.eMail);     
  OUTFORMFIELD("F_AMOUNTWITHOUTDISCSUM",QTr.Sum4-QTr.DiscSum);        
  OUTFORMFIELD("F_AVSNAME",QTr.ShipAddr0);        
  OUTFORMFIELD("F_AVSADRESS1",QTr.ShipAddr1);        
  OUTFORMFIELD("F_AVSADRESS2",QTr.ShipAddr2);        
  OUTFORMFIELD("F_AVSADRESS3",QTr.ShipAddr3);        
  OUTFORMFIELD("F_AVSADRESS4",QTr.DelAddr3);        
  OUTFORMFIELD("F_AVSADRESS5",QTr.DelAddr4); 
  if (FIELDINFORM("F_VILLKOR")) then begin
    GetPayDealText(QTr.PayDeal,QTr.LangCode,tstr);
    OUTFORMFIELD("F_VILLKOR",tstr);
  end;                   
  OUTFORMFIELD("F_VARREFERENS",QTr.OurContact);        
  OUTFORMFIELD("F_ERREFERENS",QTr.CustContact);        
  OUTFORMFIELD("F_PLANLEVDATUM",QTr.PlanShip);        
  OUTFORMFIELD("F_LEVSATT",QTr.ShipMode); 
  if (FIELDINFORM("F_LEVSATTTEXT")) then begin
    GetShipModeText(QTr.ShipMode,QTr.LangCode,tstr);
    OUTFORMFIELD("F_LEVSATTTEXT",tstr);
  end;
  if (QTr.DaysToDelivery>0) then begin
    OUTFORMFIELD("F_DAYSTODELIERY",QTr.DaysToDelivery);
  end;
  OUTFORMFIELD("F_SALJARE",QTr.SalesMan);        
  PrintCurrencyCode("F_VALUTA",QTr.CurncyCode);
  OUTFORMFIELD("F_SHIPTERM",QTr.ShipDeal);        
  if (FIELDINFORM("F_SHIPTERMTEXT")) then begin
    GetShipDealText(QTr.ShipDeal,QTr.LangCode,tstr);
    OUTFORMFIELD("F_SHIPTERMTEXT",tstr);
  end;  
  OUTFORMFIELD("F_GILTIGTDATUM",QTr.ValidUntilDate);      
  OUTFORMFIELD("F_GILTIGTDATUMHIJRI",ConvertDate(kCalendarGregorian,QTr.ValidUntilDate,kCalendarHijri));
  if (FIELDINFORM("F_SALJARNAMN")) then begin
    GetUserName(QTr.SalesMan,tstr);
    OUTFORMFIELD("F_SALJARNAMN",tstr);
  end;  
  PrintPersonsCustomerDetails(QTr.SalesMan);
  PrintBranchData(QTr.BranchID);
  OUTFORMFIELD("F_ADRESSATT",QTr.Addr0);
  OUTFORMFIELD("F_ADRESSATT",QTr.CustContact);
  OUTFORMFIELD("F_ADRESSATT",QTr.Addr1);
  OUTFORMFIELD("F_ADRESSATT",QTr.Addr2);
  OUTFORMFIELD("F_ADRESSATT",QTr.Addr3);
  OUTFORMFIELD("F_ADRESSATT",QTr.InvAddr3);
  OUTFORMFIELD("F_ADRESSATT",QTr.InvAddr4);
  OutAddress("F_ADRESS",QTr.Addr0,QTr.Addr1,QTr.Addr2,QTr.Addr3,QTr.InvAddr3,QTr.InvAddr4);
  OutAddress("F_LEVADRESS",QTr.ShipAddr0,QTr.ShipAddr1,QTr.ShipAddr2,QTr.ShipAddr3,QTr.DelAddr3,QTr.DelAddr4);
  OUTFORMFIELD("F_ADRESSNAMN",QTr.Addr0);
  OUTFORMFIELD("F_ADDR1",QTr.CustContact);
  OUTFORMFIELD("F_ADDR2",QTr.Addr1);
  OUTFORMFIELD("F_ADDR3",QTr.Addr2);
  OUTFORMFIELD("F_ADDR4",QTr.Addr3);
  OUTFORMFIELD("F_INVADDR3",QTr.InvAddr3);
  OUTFORMFIELD("F_INVADDR4",QTr.InvAddr4);
  if (blank(QTr.Fax)) then begin
    tstr = CUr.Fax;
  end else begin
    tstr = QTr.Fax;
  end;  
  OUTFORMFIELD("F_KUNDFAX",tstr);
  OUTFORMFIELD("F_MOBILE",CUr.Mobile);
  if (blank(QTr.Phone)) then begin
    tstr = CUr.Phone;
  end else begin
    tstr = QTr.Phone;
  end;  
  OUTFORMFIELD("F_KUNDTEL",tstr);
  if (FIELDINFORM("F_RANTASATS")) then begin
    OUTFORMFIELD("F_RANTASATS",IIRec.IntRate);
  end;
  OUTFORMFIELD("F_NETTO",Round(QTr.Sum1 - QTr.Sum3,DefaultRoundMode));
  OUTFORMFIELD("F_NETTOROUNDED",Round(QTr.Sum1 - QTr.Sum3,SetRoundModeD(0)));
  OUTFORMFIELD("F_PROJEKTNUMMER",QTr.PRCode);
  GetPRName(QTr.PRCode,tstr);
  OUTFORMFIELD("F_PROJECTNAME",tstr);

  if (FIELDINFORM("F_TOTVOLUME")) then begin
    CalcQTTotVolume(QTr,t);
    OUTFORMFIELD("F_TOTVOLUME",t);
  end;
  if (FIELDINFORM("F_TOTWEIGHT")) then begin
    CalcQTTotWeight(QTr,t);
    OUTFORMFIELD("F_TOTWEIGHT",t);
  end;      
  if (FIELDINFORM("F_SUBTOTALWITHOUTDISCOUNT")) or (FIELDINFORM("F_TOTALDISCOUNT")) then begin
    CalcSubtotWithoutDisc(QTr,t);
    tstr = ValToString(t,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_SUBTOTALWITHOUTDISCOUNT",tstr);
    tstr = ValToString(t-QTr.Sum1,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
    OUTFORMFIELD("F_TOTALDISCOUNT",tstr);
  end;
  tstr = ValToString(QTr.SumTime,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_SUMTIME",tstr);
  tstr = ValToString(QTr.SumMaterial,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_SUMMATERIAL",tstr);
  tstr = ValToString(QTr.SumStocked,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_SUMSTOCKED",tstr);
  tstr = ValToString(QTr.SumOther,M4Val,SFb.thousSep,SFb.decimalPt,LangNrr.CutDecimals);
  OUTFORMFIELD("F_SUMOTHER",tstr);
  if (FIELDINFORM("F_NOTE")) then begin
    GetFieldArgument(Docr,F_NOTE,tstr);
    QTPrintNote(QTr,tstr);
  end;
  PrintQuotationRows(RepSpec,Docr,QTr,LangNrr,SFb,rwcnt2,totdiscount);
  OUTFORMFIELD("F_CLASS",QTr.QuoteClass);      
  OUTFORMFIELD("F_TAXSUM",QTr.TAX1Sum);          
  if (QTr.InclVAT==0) then begin
    t = QTr.Sum3+QTr.Sum1;
  end else begin
    t = QTr.Sum1;
  end;
  OUTFORMFIELD("F_SUBTOTALWITHVAT",t);          
  QTSetFax(CUr,QTr);
  if (FIELDINFORM("F_PROJECTTEXT")) then begin
    if (nonblank(QTr.PRCode)) then begin
      PRr.Code = QTr.PRCode;
      if (ReadFirstMain(PRr,1,true)) then begin
        OUTFORMFIELD("F_PROJECTTEXT",PRr.Desc0);
        OUTFORMFIELD("F_PROJECTTEXT",PRr.Desc1);
        OUTFORMFIELD("F_PROJECTTEXT",PRr.Desc2);
      end;
    end;
  end;
  PrintValue("F_DISCSUMTOT",totdiscount,M4Val,LangNrr,SFb,false);
  t = MulRateToBase1(QTr.CurncyCode,totdiscount,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
  PrintValue("F_DISCSUMTOTBC1",t,M4Val,LangNrr,SFb,false);
  t = MulRateToBase2(QTr.CurncyCode,totdiscount,QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,DefaultCurRoundOff);
  PrintValue("F_DISCSUMTOTBC2",t,M4Val,LangNrr,SFb,false);
  Locr.Code = QTr.Location;
  if (ReadFirstMain(Locr,1,true)) then begin
  end;
  OUTFORMFIELD("F_LOCATION",QTr.Location);          
  OUTFORMFIELD("F_LOCATIONNAME",Locr.Name);
  OUTFORMFIELD("F_LOCCONTACT",Locr.Contact);
  OUTFORMFIELD("F_LOCPHONE",Locr.Phone);
  OUTFORMFIELD("F_LOCFAX",Locr.Fax);
  OUTFORMFIELD("F_LOCEMAIL",Locr.Email);
  OUTFORMFIELD("F_LOCADDRESS",Locr.Addr0);
  OUTFORMFIELD("F_LOCADDRESS",Locr.Addr1);
  OUTFORMFIELD("F_LOCADDRESS",Locr.Addr2);
  OUTFORMFIELD("F_LOCADDRESS",Locr.Addr3);
  OUTFORMFIELD("F_LOCADDRESS",Locr.Addr4);


  if (FIELDINFORM("F_TEXT")) then begin
    if (nonblank(QTr.PRCode)) then begin
      PRr.Code = QTr.PRCode;
      if (ReadFirstMain(PRr,1,true)) then begin
        OUTFORMFIELD("F_TEXT",PRr.Text0);
        OUTFORMFIELD("F_TEXT",PRr.Text1);
        OUTFORMFIELD("F_TEXT",PRr.Text2);
        OUTFORMFIELD("F_TEXT",PRr.Text3);
        OUTFORMFIELD("F_TEXT",PRr.Text4);
        OUTFORMFIELD("F_TEXT",PRr.Text5);
        OUTFORMFIELD("F_TEXT",PRr.Text6);
        OUTFORMFIELD("F_TEXT",PRr.Text7);
        OUTFORMFIELD("F_TEXT",PRr.Text8);
        OUTFORMFIELD("F_TEXT",PRr.Text9);
      end;
    end;
  end;
  tstr = GetJwl_CustomerName(QTr.CustCode);
  OUTFORMFIELD("F_KUNDNAMNJWL",tstr);
  OutFormImage(QTr);
  return;
end;

global
procedure DoQTForm(record RcVc RepSpec,record QTVc QTr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Integer i,rwcnt,rwcnt2;
  record LangNrVc LangNrr;
  record CUVc CUr;
  string 255 tstr;
  record IIBlock IIRec;
  val t;
  record SysFormatBlock SFb;
  record DocVc Docr;
  record PRVc PRr;
  record LocationVc Locr;
  Boolean printf,langf;
  Integer intdocnr;
  string 30 formcode,langcode;
    
  BlockLoad(SFb);  
  BlockLoad(IIRec);
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto L99;
  end;
  rwcnt = MatRowCnt(FDr);
  langcode = QTr.LangCode;
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(FDr,i,FDrw);
    if (FDrw.LangCode==langcode) then begin
      i = -1;
      langf = true;
    end;
  end;
  if (langf==false) then begin langcode = ""; end;
  GetLangNr(QTr.LangCode,LangNrr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,QTr.SerNr,FDrw.PrintGroupCode,
                      langcode,intdocnr,"QTVc",formcode)) then
      begin
        goto LBREAK;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        OUTFORMFIELD("F_DOCTYPE",USetStr(23454));
        PrintQT(RepSpec,SFb,formcode,QTr,LangNrr);
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
/*//fixdoc          
    case F_SUMFORPAGE:begin
        Integer i,rwcnt;
        QTRowType QT2rw;
        m4val t2,v;
        
        rwcnt = MatRowCnt(QTVc,1,ivrp);
        SetM4ValBlank(&t);
        for (i = glDoc2p->DocRowTop;i<(glDoc2p->DocRowTop+glDoc2p->DocRowMax);i++) begin
          if (i<rwcnt) then begin
            MatRowGet(QTVc,1,ivrp,i,&QT2rw);
            if (ivrp->InclVAT==0) then
              AddM4Val(&t,&QT2rw.Sum,&t);
            else begin
              MulVAT(QT2rw.VATCode,&QT2rw.Sum,&t2,1);              
              SubM4Val(&QT2rw.Sum,&t2,&v);
              AddM4Val(&t,&v,&t);
            end;  
          end;  
        end;
        M4ToStr(&t,M4Val,res);
      end;    
      break;  
  */
L99:;
  RETURN;
END;

global
procedure DoProformaQTForm(record RcVc RepSpec,record QTVc QTr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Integer i,rwcnt,rwcnt2;
  record LangNrVc LangNrr;
  record CUVc CUr;
  string 255 tstr;
  record IIBlock IIRec;
  val t;
  record SysFormatBlock SFb;
  record DocVc Docr;
  record PRVc PRr;
  record LocationVc Locr;
  Boolean printf,langf;
  Integer intdocnr;
  string 30 formcode,langcode;
    
  BlockLoad(SFb);  
  BlockLoad(IIRec);
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto L99;
  end;
  rwcnt = MatRowCnt(FDr);
  langcode = QTr.LangCode;
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(FDr,i,FDrw);
    if (FDrw.LangCode==langcode) then begin
      i = -1;
      langf = true;
    end;
  end;
  if (langf==false) then begin langcode = ""; end;
  GetLangNr(QTr.LangCode,LangNrr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,QTr.SerNr,FDrw.PrintGroupCode,
                      langcode,intdocnr,"QTVc",formcode)) then
      begin
        goto LBREAK;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        OUTFORMFIELD("F_DOCTYPE",USetStr(23461));
        PrintQT(RepSpec,SFb,formcode,QTr,LangNrr);
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
L99:;
  RETURN;
END;
