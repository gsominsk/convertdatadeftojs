external function LongInt DateDiff(Date,Date);
external function Integer RemLevelOneUpQueued(LongInt,Integer,Boolean,Boolean);

global
procedure RemForm(record RcVc RepSpec)
BEGIN
  record ARVc ARr;
  Boolean TrHs;
  string 255 ccfr,ccto;
  Boolean testf;
  record CUVc CUr;
  LongInt latedays;
  record IVVc IVr;
  LongInt afr,ato;
  Boolean ivfound;
  Date curdate;
  Integer i;
  string 20 ckey;
  Boolean custkeyf;

  StartPrintDialogGroup;
  curdate = CurrentDate;
  afr = FirstInRange(RepSpec.f1,20);
  ato = LastInRange(RepSpec.f1,20);
  ccfr = FirstInRange(RepSpec.f2,20);
  ccto = LastInRange(RepSpec.f2,20);
  ARr.InvoiceNr = afr;
  ckey = "InvoiceNr";
  if (blank(RepSpec.f1)) then begin
    if (nonblank(RepSpec.f2)) then begin
      ckey = "CustCode";
      ARr.CustCode = ccfr;
      custkeyf = true; 
    end;
  end;
  TrHs = true;
  while (LoopKey(ckey,ARr,1,TrHs)) begin
    if (TrHs) then begin
      if (custkeyf) then begin
        if (ARr.CustCode>ccto) then begin
          TrHs = false;
        end;
      end else begin
        if (ato!=-1) then begin
          if (ARr.InvoiceNr>ato) then begin
            TrHs = false;
          end;
        end;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f2)) then begin
        if (ARr.CustCode<ccfr) then begin testf = false; end;                
        if (ARr.CustCode>ccto) then begin testf = false; end;
      end;
      if (testf) then begin
        IVr.SerNr = ARr.InvoiceNr;
        ivfound = ReadFirstMain(IVr,1,true);
      end;
      if (testf) then begin
        CUr.Code = ARr.CustCode;
        if (ReadFirstMain(CUr,1,true)) then begin
          if (nonblank(RepSpec.f3)) then begin
            if (RepSpec.f3!=CUr.CustCat) then begin
              testf = false;
            end;
          end;
          if (CUr.RemndrFlag==0) then begin testf = false; end;
          if (CUr.CUType==0) then begin testf = false; end;
        end;
      end;
      if (testf) then begin
        latedays = DateDiff(ARr.DueDate,curdate);
        if (latedays<0) then begin
          if (RepSpec.long1!=-1) then begin
            latedays = -latedays;
            if (latedays<RepSpec.long1) then begin testf = false; end;
          end;
        end else begin
          testf = false;
        end;
      end;
      if (testf) then begin
        if (IVr.Invalid!=0) then begin testf = false; end;
        if (IVr.NoRemndrFlag!=0) then begin testf = false; end;
        if (RepSpec.long2!=-1) then begin
          if (ivfound) then begin
            if (IVr.LastRemndr==-1) then begin IVr.LastRemndr = 0; end;
            if (IVr.LastRemndr!=RepSpec.long2) then begin testf = false; end;
          end;
        end;
      end;
      if (testf) then begin
        if (ARr.RVal<0) then begin
          testf = false;
        end;
      end;
      if (nonblank(RepSpec.Language)) then begin
        if (RepSpec.flags[0]==0) then begin
          if (IVr.LangCode!=RepSpec.Language) then begin testf = false; end;
        end;
      end;
      if (testf) then begin
        if (RepSpec.flags[1]!=0) then begin
          i = RemLevelOneUpQueued(ARr.InvoiceNr,0,true,false);
        end;
        testf = PrintDocument(ARr,"RemForm",false);
      end;
    end;
  end;
  EndPrintDialogGroup;
  RETURN;
END;  
