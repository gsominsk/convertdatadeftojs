external function Boolean IsCancelState(string);
external function boolean CheckInSelect(Record RcVc,Record JobVc,var boolean);

global
procedure PrintJobGuests(Record JobVc Jobr)
begin
  Integer i,rwcnt;
  Boolean tmpf,previewf;
  record CUVc Guestr;
  row JobVc jobrw;
  
  previewf = false;
  rwcnt = MatRowCnt(Jobr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Jobr,i,Jobrw);
    Guestr.Code = Jobrw.GuestCode;
    if readfirstmain(Guestr,1,true) then begin
      tmpf = PrintDocument(Guestr,"GuestDocForm",previewf);    
    end;
  end;  
  return;  
end;

global
procedure GuestDocForm(record RcVc RepSpec)
BEGIN
  record CUVc Guestr;
  String 20 afr,ato;
  Boolean tmpf,previewf,TrHs;
  record JobVc Jobr,MotherJobr;
  string 255 keystr;

  StartPrintDialogGroup;
  if nonblankdate(RepSpec.d1) then begin
    TrHs = true;
    Jobr.TransDate = RepSpec.d1;
    while LoopKey("TransDate",Jobr,1,TrHs) begin
      if CheckInSelect(RepSpec,Jobr,TrHs) then begin
        PrintJobGuests(Jobr);
      end; 
    end; 
    goto LGuestDocForm;
  end;
  
  if nonblank(RepSpec.f1) then begin
    previewf = false;
    afr = FirstInRange(RepSpec.f1,10);
    ato = LastInRange(RepSpec.f1,10);
    Guestr.Code = afr;
    TrHs = true;
    while (LoopKey("GuestActCode",Guestr,1,TrHs)) begin
      if (TrHs) then begin
        if (ato!="") then begin
          if (Guestr.Code>ato) then begin
            TrHs = false;
          end;
        end; 
      end;
      if (TrHs) then begin
        tmpf = PrintDocument(Guestr,"GuestDocForm",previewf);    
      end;
    end;
    goto LGuestDocForm;
  end;
  
  if (RepSpec.long1<>-1) then begin
  
    MotherJobr.SerNr = RepSpec.long1;
    if readfirstmain(MotherJobr,1,true) then begin
      PrintJobGuests(MotherJobr);  
      keystr = "Mother:";
      keystr = keystr & MotherJobr.SerNr;
      TrHs = true;
      while (LoopKey(keystr,Jobr,1,TrHs)) begin
        if (Jobr.Mother!=MotherJobr.SerNr) then begin
          TrHs = false;
        end;  
        if (TrHs) then begin
          if (not IsCancelState(Jobr.ResStatus)) then begin
            PrintJobGuests(Jobr);
          end;  
        end;
      end;
    end; 
  end;  
  
LGuestDocForm:;
  EndPrintDialogGroup;
  RETURN;
END;
