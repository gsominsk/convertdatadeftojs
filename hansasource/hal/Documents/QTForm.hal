external function Integer QTTestApprovalStatus(record QTVc);
external function string 255 UpdateOfficialSerNrSerie(Integer,Integer,Integer,string,boolean);
remote updating procedure UpdateOffSerNr(LongInt,string,Integer,string,Boolean);
external procedure FindNextProformaQTVcOfficialSerialNr(var record QTVc);
external procedure GetLegalInvNrRow(string,var row LegalInvNrBlock);
external updating procedure RecordActionQT_Print(var record QTVc,string,Boolean);
external function Boolean ValidateOfficialSerialNrChronology(string,string,LongInt,Date,var Date);
external procedure AddPortugueseSAFTHashToProformaQT(var record QTVc);

global
updating procedure QTForm(record RcVc RepSpec)
BEGIN
  record QTVc QTr;
  Boolean TrHs;
  LongInt afr,ato;
  Integer err;
  Boolean testf;
  
  StartPrintDialogGroup;
  if (blank(RepSpec.f1)) then begin
    goto LQTForm; 
  end;
  afr = FirstInRange(RepSpec.f1,10);
  ato = LastInRange(RepSpec.f1,10);
  QTr.SerNr = afr;
  TrHs = true;
  while (LoopMain(QTr,1,TrHs)) begin
    if (TrHs) then begin
      if (ato!=-1) then begin
        if (QTr.SerNr>ato) then begin
          TrHs = false;
        end;
      end;     
    end;
    if (TrHs) then begin
      testf = true;
      err = QTTestApprovalStatus(QTr);
      if (err!=0) then begin
        MessageBox(err,": " & QTr.SerNr);
        testf = false;
      end;
      if (testf) then begin
        RecordActionQT_Print(QTr,"",false);
      end;
//      RecidAction(rlQTLPrint,QTVc,recid,NIL);
    end;
  end;
LQTForm:;
  EndPrintDialogGroup;
  RETURN;
END;

global
updating procedure ProformaQTForm(record RcVc RepSpec)
begin
  record QTVc oldQTr;
  record QTVc QTr;
  Boolean TrHs;
  LongInt afr,ato;
  Integer err;
  Boolean testf;
  record LegalInvNrBlock LINrb;
  row LegalInvNrBlock LINrbrw;
  Date td;
  
  StartPrintDialogGroup;
  if (blank(RepSpec.f1)) then begin
    goto LProformaQTForm; 
  end;
  afr = FirstInRange(RepSpec.f1,10);
  ato = LastInRange(RepSpec.f1,10);
  QTr.SerNr = afr;
  TrHs = true;
  while (LoopMain(QTr,1,TrHs)) begin
    if (TrHs) then begin
      if (ato!=-1) then begin
        if (QTr.SerNr>ato) then begin
          TrHs = false;
        end;
      end;     
    end;
    if (TrHs) then begin
      testf = true;
      err = QTTestApprovalStatus(QTr);
      if (err!=0) then begin
        MessageBox(err,": " & QTr.SerNr);
        testf = false;
      end;
      if (testf) then begin
        if (blank(QTr.ProformaOfficialSerNr)) then begin
          RecordCopy(oldQTr,QTr);
          BlockLoad(LINrb);  
          if (nonblank(QTr.ProformaOfficialSerNr)) then begin
            GetLegalInvNrRow(QTr.ProformaOfficialSerNr,LINrbrw);
            switch (LINrbrw.SelectionType) begin
              case kLegalInvNrSelectionTypeManual:
                if (blank(LINrbrw.Serie)) then begin
                  QTr.ProformaOfficialSerNr = "";
                end;
              case kLegalInvNrSelectionTypeAtOK:
                if (true) then begin
                  QTr.ProformaOfficialSerNr = "";
                end;
            end;
          end;
          FindNextProformaQTVcOfficialSerialNr(QTr);
          if (ValidateOfficialSerialNrChronology("QTVc",QTr.ProformaOfficialSerNr,QTr.SerNr,QTr.QTDate,td)==false) then begin
            RecordCheckError(26201," " & td,-1,"QTDate");
            goto LProformaQTForm;
          end;
          if (nonblank(QTr.ProformaOfficialSerNr)) then begin
            if (HasLocalization("PRT")) then begin 
              if (blank(QTr.ProformaOfficialSerNrSerie)) then begin 
                QTr.ProformaOfficialSerNrSerie = UpdateOfficialSerNrSerie(Rs_update,1,0,QTr.ProformaOfficialSerNr,true);
              end;
            end;              
            AddPortugueseSAFTHashToProformaQT(QTr);
            if (RecordUpdate(oldQTr,QTr,false)==0) then begin
              UpdateOffSerNr(QTr.SerNr,"ProformaQTVc",0,QTr.ProformaOfficialSerNr,false);
            end;
          end;
        end;
        if (nonblank(QTr.ProformaOfficialSerNr)) or (HasLocalization("PRT")==false) then begin
          if (PrintDocument(QTr,"ProformaQTForm",false)) then begin
          end;   
        end;
      end;
    end;
  end;
LProformaQTForm:;
  EndPrintDialogGroup;
  return;
end;