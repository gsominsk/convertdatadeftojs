external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValueInclZero(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function Boolean FindFormcode(Integer,Integer);
external procedure CommonDocumentFields(record RcVc);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external procedure Val2ToText(string,val,Integer,string,string,var string);
external procedure GetFieldArgument(record DocVc,Integer,var string);

global
procedure DoGCForm(record RcVc RepSpec,record GCVc GCr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Integer i,rwcnt;
  string 255 tstr,fldarg;
  Boolean printf;
  Integer intdocnr;
  string 30 formcode;
  record DocVc Docr;
  record SysFormatBlock SFb;
  record LangNrVc LangNrr;
  record GCSVc GCSr;
    
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoGCForm;
  end;
  BlockLoad(SFb);
  GetLangNr("",LangNrr);
  rwcnt = MatRowCnt(FDr);
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (FindFormcode(GCr.OKFlag,FDrw.Typ)) then begin 
        if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,-1,FDrw.PrintGroupCode,
                        "",intdocnr,"GCVc",formcode)) then
        begin
          goto LBREAK;
        end;
      end;
    end;
LBREAK:;
    if (nonblank(formcode)) then begin  
      if (OpenForm(formcode)) then begin
        Docr.Code = formcode;
        if (ReadFirstMain(Docr,1,true)) then begin
        end;
        CommonDocumentFields(RepSpec);
        OUTFORMFIELD("F_SERNR",GCr.SerNr);
        OUTFORMFIELD("F_TRANSDATUM",GCr.TransDate);
        OUTFORMFIELD("F_AMOUNT",GCr.Amount);
        OUTFORMFIELD("F_SPECIFIKATION",GCr.Comment);
        OUTFORMFIELD("F_ENDDATE",GCr.ExpiryDate);
        OUTFORMFIELD("F_BARCODE",GCr.BarCode);
        GCSr.SerNr = GCr.SerNr;
        ReadFIrstMain(GCSr,1,true);
        PrintValueInclZero("F_SALDO",GCSr.Balance,M4Val,LangNrr,SFb,false);
        if (FIELDINFORM("F_AMOUNTINTEXT")) then begin
          GetFieldArgument(Docr,F_AMOUNTINTEXT,fldarg);
          Val2ToText(fldarg,GCr.Amount,M4Val,"","",tstr);
          OUTFORMFIELD("F_AMOUNTINTEXT",tstr);
        end;
        CloseForm;        
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoGCForm:;  
  RETURN;  
END;     
 