external procedure GetShipDealText(string,string,var string);
external procedure GetShipModeText(string,string,var string);
external procedure OutAddress(string,string,string,string,string,string,string);
external function Boolean FindFormcode(Integer,Integer);
external function Integer NumberofDocumentPages(string,Integer);
external procedure CommonDocumentFields(record RcVc);
external procedure PrintAgreementInformation(record AgreementVc,record CUVc,Boolean,Boolean);
external procedure SetFaxInfo(string,string,string,string);
external procedure SelectUnitText(string,string,var string);
external procedure ItemBC39(string,var string);
external procedure ItemBCEAN(string,var string);
external procedure ItemBCEAN13(string,var string);
external procedure ValToText(val,Integer,string,string,var string);
external procedure GetUserName(string,var string);
external procedure GetLangNr(string,var record LangNrVc);
external procedure PrintValue(string,val,Integer,record LangNrVc,record SysFormatBlock,Boolean);
external function Boolean Getformcode(Integer,Integer,string,string,string,LongInt,LongInt,string,string,Integer,string,var string);
external procedure PrintSiteInformation(string,string);
external procedure PrintLocationInformation(string);

procedure PrintCustOrderNo(record DispatchVc Dispatchr) 
begin
  row DispatchVc Dispatchrw;
  record RentResVc RentResr;
  boolean TrHs,testf;
  integer rwcnt,i;
  
  rwcnt = MatRowCnt(Dispatchr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Dispatchr,i,Dispatchrw);
    if (nonblank(Dispatchrw.RentResSerNo)) then begin
      RentResr.Code = Dispatchrw.RentResSerNo;
      if (ReadFirstMain(RentResr,1,true)) then begin
        OUTFORMFIELD("F_KUNDORDNR",RentResr.CustOrdNr);
        goto LPrintCustOrderNo;
      end;
    end;
  end;
LPrintCustOrderNo:;    
  return;
end;

procedure PrintDispatch(record DispatchVc Dispatchr,string formcode)
BEGIN
  record LangNrVc LangNrr;
  record SysFormatBlock SysFormatRec;
  record AgreementVc Agreementr;
  record CUVc CUr;
  record INVc INr;
  row DispatchVc Dispatchrw;
  Integer i,rwcnt;
  string 255 tstr;
  val totweight;

  NumberofDocumentPages(formcode,MatRowCnt(Dispatchr));
  BlockLoad(SysFormatRec);  
  GetLangNr("",LangNrr);
  CUr.Code = Dispatchr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    SetFaxInfo(CUr.Person,CUr.Name,CUr.Fax,USetStr(1820));
  end;
  Agreementr.SerNr = Dispatchr.AgreementNr;
  if (ReadFirstMain(Agreementr,1,true)==false) then begin
    RecordClear(Agreementr);
  end;
  OUTFORMFIELD("F_KUNDNR",Dispatchr.CustCode);
  OUTFORMFIELD("F_NUMMER",Dispatchr.SerNr);
  OutAddress("F_LEVADRESS",Dispatchr.Addr0,Dispatchr.Addr1,Dispatchr.Addr2,Dispatchr.Addr3,Dispatchr.DelAddr3,Dispatchr.DelAddr4);
  OUTFORMFIELD("F_DELADDRESSADDR",Dispatchr.Addr0);
  OUTFORMFIELD("F_DELADDRESSADDR1",Dispatchr.Addr1);
  OUTFORMFIELD("F_DELADDRESSADDR2",Dispatchr.Addr2);
  OUTFORMFIELD("F_DELADDRESSADDR3",Dispatchr.Addr3);
  OUTFORMFIELD("F_DELADDRESSADDR4",Dispatchr.DelAddr3);
  OUTFORMFIELD("F_ERREFERENS",Dispatchr.ClientContact);
  OUTFORMFIELD("F_DUEDATE",Dispatchr.DeliveryDate);
  OUTFORMFIELD("F_TRANSDATE",Dispatchr.TransDate);
  OUTFORMFIELD("F_COMMENT",Dispatchr.Comment);
  PrintSiteInformation(Dispatchr.Site,Dispatchr.CustCode);
  PrintLocationInformation(Dispatchr.Location);
  OUTFORMFIELD("F_AGREEMENT",Dispatchr.AgreementNr);
  OUTFORMFIELD("F_INVLEVSATT",Dispatchr.ShipMode);
  OUTFORMFIELD("F_LEVSATT",Dispatchr.ShipMode);
  if (FIELDINFORM("F_LEVSATTTEXT") or FIELDINFORM("F_INVLEVSATTTEXT")) then begin
    GetShipModeText(Dispatchr.ShipMode,Dispatchr.LangCode,tstr);
    OUTFORMFIELD("F_LEVSATTTEXT",tstr);
    OUTFORMFIELD("F_INVLEVSATTTEXT",tstr);
  end;
  OUTFORMFIELD("F_SHIPTERM",Dispatchr.ShipDeal);
  OUTFORMFIELD("F_INVLEVVILLK",Dispatchr.ShipDeal);
  if (FIELDINFORM("F_SHIPTERMTEXT") or FIELDINFORM("F_INVLEVVILLKTEXT")) then begin
    GetShipDealText(Dispatchr.ShipDeal,Dispatchr.LangCode,tstr);
    OUTFORMFIELD("F_SHIPTERMTEXT",tstr);
    OUTFORMFIELD("F_INVLEVVILLKTEXT",tstr);
  end;  
  PrintCustOrderNo(Dispatchr);
  PrintAgreementInformation(Agreementr,CUr,false,false);
  rwcnt = MatRowCnt(Dispatchr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Dispatchr,i,Dispatchrw);
    INr.Code = Dispatchrw.ArtCode;
    if (ReadFirstMain(INr,1,true)) then begin
      totweight = totweight + INr.Weight * Dispatchrw.Quant;
    end;
    OUTFORMFIELD("F_ARTNR",Dispatchrw.ArtCode);
    OUTFORMFIELD("F_ARTNR2",Dispatchrw.ArtCode);
    if (FIELDINFORM("F_ARTNRBC39")) then begin
      ItemBC39(Dispatchrw.ArtCode,tstr);
      OUTFORMFIELD("F_ARTNRBC39",tstr);
    end;  
    if (FIELDINFORM("F_ARTNRBCEAN")) then begin
      ItemBCEAN(Dispatchrw.ArtCode,tstr);
      OUTFORMFIELD("F_ARTNRBCEAN",tstr);
    end;  
    if (FIELDINFORM("F_ARTNRBCEAN13")) then begin
      ItemBCEAN13(Dispatchrw.ArtCode,tstr);      
      OUTFORMFIELD("F_ARTNRBCEAN13",tstr);
    end;  
    OUTFORMFIELD("F_ARTGRUPP",INr.Group);
    PrintValue("F_ANTAL",Dispatchrw.Quant,M4UVal,LangNrr,SysFormatRec,false);
    SelectUnitText("",INr.Unittext,tstr);    
    OUTFORMFIELD("F_ENHET",tstr);
    OUTFORMFIELD("F_SERIALNR",Dispatchrw.SerialNr);
    OUTFORMFIELD("F_SPECIFIKATION",Dispatchrw.Spec);
    OUTFORMFIELD("F_SPECIFIKATION",Dispatchrw.Spec);
    EndFormRow;
  end;  
  PrintValue("F_TOTWEIGHT",totweight,M4UVal,LangNrr,SysFormatRec,false);
  RETURN;
END;

global
procedure DoDispatchForm(record RcVc RepSpec,record DispatchVc Dispatchr)
BEGIN
  record FormDefVc FDr;
  row FormDefVc FDrw;
  Boolean printf,langf;
  Integer intdocnr;
  string 30 formcode,langcode;
  Integer i,rwcnt;
  string 255 tstr;
    
  langcode = Dispatchr.LangCode;
  FDr.repname = RepSpec.repname;
  FDr.shortname = RepSpec.shortname;
  if (ReadFirstMain(FDr,1,true)==false) then begin
    MessageBox(1624, " " & USetStr(1623));
    goto LDoDispatchForm;
  end;
  rwcnt = MatRowCnt(FDr);
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(FDr,i,FDrw);
    if (FDrw.LangCode==langcode) then begin
      i = -1;
      langf = true;
    end;
  end;
  if (langf==false) then begin langcode = ""; end;
  intdocnr = 1;
  printf = true;
  while (printf) begin  
    formcode = "";
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(FDr,i,FDrw);
      if (FindFormcode(Dispatchr.OKFlag,FDrw.Typ)) then begin 
        if (Getformcode(i,FDrw.intdocnr,FDrw.FPCode,FDrw.UserGroup,FDrw.LangCode,FDrw.SerNr,Dispatchr.SerNr,FDrw.PrintGroupCode,
                        langcode,intdocnr,"DispatchVc",formcode)) then
        begin
          goto LBREAKDispatch;  
        end;
      end;
    end;
LBREAKDispatch:;
    if (nonblank(formcode)) then begin
      if (OpenForm(formcode)) then begin
        PrintDispatch(Dispatchr,formcode);
        CommonDocumentFields(RepSpec);    
        CloseForm;
      end else begin
        printf = false;
        MessageBox(1546,formcode);
      end;
    end else begin
      printf = false;
      if (intdocnr==1) then begin
        MessageBox(1624, " " & USetStr(1623));
      end;
    end;
    intdocnr = intdocnr + 1;
  end;
LDoDispatchForm:;  
  RETURN;
END;

