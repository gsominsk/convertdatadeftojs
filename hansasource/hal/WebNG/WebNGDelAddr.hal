//tools
external function string 255 FormatLink(string,string,boolean,boolean);
external procedure GetListOfCompaniesForCountry(string,var array integer,var array string);
external function string 10 ToolWebNGGetCountry();
external procedure FillArrayWithCountry(var array string,var array string,var integer);
external procedure SimpleElementComboBox(string,string,string,string,array string,array string,integer,string,boolean,boolean);
external function string 255 FormatSimpleLink(string,boolean);
external procedure SessionAddToArray(string,string);
external function Boolean SessionGetNextFromArray(string,var string,var Integer);
external procedure SessionClearArray(string);
external procedure WebOutSubmit(string,string,string,string);
//translation
external function string 255 ToolWebNGTranslateText(integer);
external function string 255 GetTranslatedNameDelAddrField(record DelAddrFieldVc);
//webng
external procedure RedirectToURL(string);
external procedure RedirectToStartPage();
//myaccount
external function string 255 GetLinkToMyAccount;
external procedure MyAccountShowErrors(string);
//shop
external function boolean GetShoppingBasket(var record WebNGShopBasketVc,integer);
external procedure SumUpShopBasket(var record WebNGShopBasketVc);
//std
external function string 255 AddStr(string,string,string);
external function string 255 ConvertXml(string);
external function string 255 GetNextValue(var string);
external procedure DelAddrVc_PasteCustCode(var record DelAddrVc);
external procedure DelAddrVc_PasteDelCountry(var record DelAddrVc);
//old code:
external function Boolean GetNextDelCode(var string);

function string 100 GetDelAddrFieldName(Integer FieldType)
begin
  string 100 res;
  
  switch (FieldType) begin
    case kDelAddrFieldTypeDelCode: res = "delcode";
    case kDelAddrFieldTypeComment: res = "comment";
    case kDelAddrFieldTypeName: res = "name";
    case kDelAddrFieldTypeDelAddr0: res = "deladdr0";
    case kDelAddrFieldTypeDelAddr1: res = "deladdr1";
    case kDelAddrFieldTypeDelAddr2: res = "deladdr2";
    case kDelAddrFieldTypeDelAddr3: res = "deladdr3";
    case kDelAddrFieldTypeDelAddr4: res = "deladdr4";
    case kDelAddrFieldTypeDelCountry: res = "country";
    case kDelAddrFieldTypeContact: res = "contact";
    case kDelAddrFieldTypePhone: res = "telephone";
    case kDelAddrFieldTypeFax: res = "fax";
    case kDelAddrFieldTypeEmail: res = "email";
    otherwise res = "unknown";
  end;
  
  GetDelAddrFieldName = res;
  return;
end;

function string 255 GetDelAddrFieldValue(record DelAddrVc DelAddrr,record DelAddrFieldVc DelAddrFieldr)
begin
  string 255 res,tstr;
  
  switch (DelAddrFieldr.FieldType) begin
    case kDelAddrFieldTypeDelCode: res = DelAddrr.DelCode;
    case kDelAddrFieldTypeComment: res = DelAddrr.Comment;
    case kDelAddrFieldTypeName: res = DelAddrr.Name;
    case kDelAddrFieldTypeDelAddr0: res = DelAddrr.DelAddr0;
    case kDelAddrFieldTypeDelAddr1: res = DelAddrr.DelAddr1;
    case kDelAddrFieldTypeDelAddr2: res = DelAddrr.DelAddr2;
    case kDelAddrFieldTypeDelAddr3: res = DelAddrr.DelAddr3;
    case kDelAddrFieldTypeDelAddr4: res = DelAddrr.DelAddr4;
    case kDelAddrFieldTypeDelCountry: res = DelAddrr.DelCountry;
    case kDelAddrFieldTypeContact: res = DelAddrr.Contact;
    case kDelAddrFieldTypePhone: res = DelAddrr.Phone;
    case kDelAddrFieldTypeFax: res = DelAddrr.Fax;
    case kDelAddrFieldTypeEmail: res = DelAddrr.Email;
  end;
  
  GetDelAddrFieldValue = res;
  return;
end;

function Boolean LoadDelAddr(string DelCode,string CustCode)
begin
  Boolean res;
  record DelAddrVc DelAddrr;
  record DelAddrFieldVc DelAddrFieldr;
  string 255 fieldname;
  
  if (CustCode!=CurrentCust) then begin
    goto LExit;
  end;
  res = true;
  if (blank(GetSessionString("deladdr_dontload"))) then begin
    DelAddrr.DelCode = DelCode;
    DelAddrr.CustCode = CustCode;
    if (ReadFirstMain(DelAddrr,2,true)) then begin
      while (LoopKey("Sorting",DelAddrFieldr,1,true)) begin
        fieldname = GetDelAddrFieldName(DelAddrFieldr.FieldType);
        PutSessionString("deladdr_" & fieldname,GetDelAddrFieldValue(DelAddrr,DelAddrFieldr));
      end;
    end else begin
      res = false;
    end;
  end else begin
    PutSessionString("deladdr_dontload","");
  end;
  
LExit:;
  LoadDelAddr = res;
  return;
end;

global
procedure ShowDelAddrList(string custcode)
begin
  record DelAddrVc DelAddrr;
  Boolean TrHs,firstline;
  string 255 tstr,link;
  
  WebOutTagOpen("<div id=""deladdrlist"">");
  DelAddrr.CustCode = custcode;
  TrHs = true;
  firstline = true;
  while (LoopKey("CustCode",DelAddrr,1,TrHs)) begin
    if (DelAddrr.CustCode!=custcode) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      if (firstline) then begin
        WebOutTagOpen("<ul>");
        firstline = false;
      end;
      tstr = DelAddrr.DelAddr0;
      link = FormatSimpleLink(GetLinkToMyAccount & "deliveryaddress/" & ConvertXml(DelAddrr.DelCode & "," & DelAddrr.CustCode),true);
      WebOutTagOpen("<li>");
      WebOutStringFormatNL("<a href=""" & link & """>" & ConvertXml(tstr) & "</a>");
      WebOutTagClose("</li>");
    end;
  end;
  if (!firstline) then begin
    WebOutTagClose("</ul>");
  end;
  link = FormatSimpleLink(GetLinkToMyAccount & "deliveryaddress/new",true);
  WebOutStringFormatNL("<div id=""button""><a href=""" & link & """>" & ToolWebNGTranslateText(25526) & "</a></div>");
  WebOutTagClose("</div>");
  
  return;
end;

procedure ShowDropDown(Integer FieldType,string fieldname,string value)
begin
  array string 255 texts,codes;
  integer arrlen;

  switch (FieldType) begin
    case kDelAddrFieldTypeDelCountry: FillArrayWithCountry(texts,codes,arrlen);
  end;
  SimpleElementComboBox("","",fieldname,"",texts,codes,arrlen,value,true,true);
  
  return;
end;

global
procedure ShowDelAddrEdit(string Param,string LinkToSuccess,boolean UpdateBasketDelAddr)
begin
  record DelAddrFieldVc DelAddrFieldr;
  array integer numberlist;
  array string 244 namelist;
  Integer i,hmcompany,nr;
  Boolean firstline;
  string 255 DelCode,CustCode,mParam,fieldname,value,tstr;
  
  if (Param=="new") then begin
    DelCode = "";
    CustCode = CurrentCust;
    PutSessionString("deladdr_dontload","true");
  end else begin
    mParam = Param;
    DelCode = GetNextValue(mParam);
    CustCode = GetNextValue(mParam);
  end;
  if (!LoadDelAddr(DelCode,CustCode)) then begin
    goto LExit;
  end;
  
  WebOutTagOpen("<div id=""deladdr_edit"">");
  WebOutStringFormatNL("<h2>Delivery Address</h2>");

  MyAccountShowErrors("deladdr_error");

  WebOutTagOpen("<form method=""post"" action=""" & FormatLink("/WebUpdatingAction.hal","?action=deladdr_save",true,true) & """>");
  WebOutStringFormatNL("<input type=""hidden"" id=""path"" name=""path"" value=""" & WebGetArg("path") & """>");
  WebOutStringFormatNL("<input type=""hidden"" id=""linktosuccess"" name=""linktosuccess"" value=""" & LinkToSuccess & """>");
  if (UpdateBasketDelAddr) then begin
    WebOutStringFormatNL("<input type=""hidden"" id=""updatebasketdeladdr"" name=""updatebasketdeladdr"" value=""true"">");
  end;
  WebOutTagOpen("<ul>");
  
  numberlist[0] = 0;
  GetListOfCompaniesForCountry(ToolWebNGGetCountry,numberlist,namelist);
  if (numberlist[0]>1) then begin
    firstline = true;
    for (i=1;i<=numberlist[0];i=i+1) begin
      if (firstline) then begin
        WebOutTagOpen("<li>");
        WebOutStringFormat("<label><span class=""required"">*</span>" & ToolWebNGTranslateText(25901) & "</label>"); //Select which company you want to register as a customer in:
        WebOutTagOpen("<select name=""hmcompany"">");
        firstline = false;
      end;
      if (numberlist[i] == hmcompany) then begin
        WebOutTagOpen("<option selected value=""" & numberlist[i] & """>");
      end else begin
        WebOutTagOpen("<option value=""" & numberlist[i] & """>");
      end;
      WebOutStringFormat(namelist[i]);
      WebOutTagClose("</option>");
    end;
    if (!firstline) then begin
      WebOutTagClose("</select>");
      WebOutTagClose("</label>");
    end;
  end else begin
    if (numberlist[0]==1) then begin
      WebOutStringFormatNL("<input type=""hidden"" id=""hmcompany"" name=""hmcompany"" value=""" & numberlist[1] & """>");
    end;
  end;
  
  if (Param=="new") then begin
    WebOutStringFormatNL("<input type=""hidden"" id=""new"" name=""new"" value=""new"">");
  end;
  WebOutStringFormatNL("<input type=""hidden"" id=""olddelcode"" name=""olddelcode"" value=""" & DelCode & """>");
  WebOutStringFormatNL("<input type=""hidden"" id=""oldcustcode"" name=""oldcustcode"" value=""" & CustCode & """>");
  
  while (LoopKey("Sorting",DelAddrFieldr,1,true)) begin
    fieldname = GetDelAddrFieldName(DelAddrFieldr.FieldType);
    value = GetSessionString("deladdr_" & fieldname);
    PutSessionString("deladdr_" & fieldname,"");
    
    WebOutTagOpen("<li>");
    WebOutStringFormat("<label for=""" & fieldname & """>" & GetTranslatedNameDelAddrField(DelAddrFieldr));
    if (DelAddrFieldr.Required!=0) then begin
      WebOutStringFormat("<em>*</em>");
    end;
    WebOutStringFormatNL("</label>");
    switch (DelAddrFieldr.FieldType) begin
      case kDelAddrFieldTypeDelCountry:
        ShowDropDown(DelAddrFieldr.FieldType,fieldname,value);
    otherwise
      WebOutStringFormatNL("<input type=""text"" class=""textfield"" id=""" & fieldname & """ name=""" & fieldname & """ value=""" & value & """  /> ");
    end;
    WebOutTagClose("</li>");
  end;
  WebOutTagOpen("<li>");
  WebOutSubmit("","button","Submit",ToolWebNGTranslateText(20067));
  WebOutTagClose("</li>");
  WebOutTagClose("</ul>");
  WebOutTagClose("</form>");
  WebOutTagClose("</div>");
  
LExit:;
  return;
end;

function Boolean ValidateDelAddrForm(string CustCode,Boolean new)
begin
  Boolean res;
  record DelAddrVc DelAddrr;
  record DelAddrFieldVc DelAddrFieldr;
  string 255 fieldname,value;
  
  res = true;
  while (LoopKey("Sorting",DelAddrFieldr,1,true)) begin
    fieldname = GetDelAddrFieldName(DelAddrFieldr.FieldType);
    value = WebGetArg(fieldname);
    if (DelAddrFieldr.Required!=0 and blank(value)) then begin
      SessionAddToArray("deladdr_error",GetTranslatedNameDelAddrField(DelAddrFieldr) & " " & ToolWebNGTranslateText(25044));
      res = false;
    end;
    if (DelAddrFieldr.FieldType==kDelAddrFieldTypeDelCode and new) then begin
      DelAddrr.DelCode = value;
      DelAddrr.CustCode = CustCode;
      if (ReadFirstMain(DelAddrr,2,true)) then begin
        SessionAddToArray("deladdr_error",ToolWebNGTranslateText(25048));
        res = false;
      end;
    end;
  end;
  
  if (!res) then begin
    ResetLoop(DelAddrFieldr);
    RecordNew(DelAddrFieldr);
    while (LoopKey("Sorting",DelAddrFieldr,1,true)) begin
      fieldname = GetDelAddrFieldName(DelAddrFieldr.FieldType);
      value = WebGetArg(fieldname);
      PutSessionString("deladdr_" & fieldname,value);
    end;
    PutSessionString("deladdr_dontload","true");
  end;
  
  ValidateDelAddrForm = res;
  return;
end;

global
updating function StoreDelAddrForm()
begin
  record DelAddrVc DelAddrr,OldDelAddrr;
  record WebNGShopBasketVc oldWSBr,WSBr;
  Boolean RecordExists,new;
  record DelAddrFieldVc DelAddrFieldr;
  string 255 DelCode,CustCode,fieldname,value,link,tstr;
  LongInt err;
  
  DelCode = WebGetArg("olddelcode");
  CustCode = WebGetArg("oldcustcode");
  new = nonblank(WebGetArg("new"));
  if (ValidateDelAddrForm(CustCode,new)) then begin
    DelAddrr.DelCode = DelCode;
    DelAddrr.CustCode = CustCode;
    if (ReadFirstMain(DelAddrr,2,true)) then begin
      RecordExists = true;
      RecordCopy(OldDelAddrr,DelAddrr);
    end else begin
      RecordNew(DelAddrr);
      DelAddrr.CustCode = CustCode;
      DelAddrVc_PasteCustCode(DelAddrr);
    end;
    while (LoopKey("Sorting",DelAddrFieldr,1,true)) begin
      fieldname = GetDelAddrFieldName(DelAddrFieldr.FieldType);
      value = WebGetArg(fieldname);
      switch (DelAddrFieldr.FieldType) begin
        case kDelAddrFieldTypeDelCode: DelAddrr.DelCode = value;
        case kDelAddrFieldTypeComment: DelAddrr.Comment = value;
        case kDelAddrFieldTypeName: DelAddrr.Name = value;
        case kDelAddrFieldTypeDelAddr0: DelAddrr.DelAddr0 = value;
        case kDelAddrFieldTypeDelAddr1: DelAddrr.DelAddr1 = value;
        case kDelAddrFieldTypeDelAddr2: DelAddrr.DelAddr2 = value;
        case kDelAddrFieldTypeDelAddr3: DelAddrr.DelAddr3 = value;
        case kDelAddrFieldTypeDelAddr4: DelAddrr.DelAddr4 = value;
        case kDelAddrFieldTypeDelCountry:
          DelAddrr.DelCountry = value;
          DelAddrVc_PasteDelCountry(DelAddrr);
        case kDelAddrFieldTypeContact: DelAddrr.Contact = value;
        case kDelAddrFieldTypePhone: DelAddrr.Phone = value;
        case kDelAddrFieldTypeFax: DelAddrr.Fax = value;
        case kDelAddrFieldTypeEmail: DelAddrr.Email = value;
      end;
    end;
    if (blank(DelAddrr.DelCode)) then begin
      GetNextDelCode(tstr);
      DelAddrr.DelCode = tstr;
    end;
    if (RecordExists) then begin
      err = RecordUpdate(OldDelAddrr,DelAddrr,true);
    end else begin
      RecordStore(DelAddrr,false);
    end;
    if (nonblank(WebGetArg("updatebasketdeladdr"))) then begin
      if (GetShoppingBasket(WSBr,0)) then begin
        RecordCopy(oldWSBr,WSBr);
        WSBr.DelAddrCode = DelAddrr.DelCode;
        SumUpShopBasket(WSBr);
        RecordUpdate(oldWSBr,WSBr,false);
      end;
    end;
    
    link = FormatSimpleLink(WebGetArg("linktosuccess"),true);
    RedirectToURL(FormatSimpleLink(link,true));
  end else begin
    link = FormatSimpleLink(WebGetArg("path"),true);
    RedirectToURL(FormatSimpleLink(link,true));
  end;
  
  return;
end;
