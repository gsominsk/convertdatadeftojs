//tools
external function string 255 FormatLink(string,string,boolean,boolean);
external function string 255 GetTranslatedNameCountry(record WebNGCountryVc);
external procedure WebOutSubmit(string,string,string,string);
external function boolean CheckRecaptcha(area);
external function boolean IsRecaptchaEnabled(integer);
external function string 255 RecaptchaSiteKey();
//translation
external function string 255 ToolWebNGTranslateText(integer);
//webng
external procedure RedirectToRequestedPage();
//std hal
external procedure AddTextLineToMail(string,record MailVc);
external procedure AddNLToMail(record MailVc);
external function string 255 GetWebmailBoxForCountry(string);

global
procedure OldCodeWebHBSDivContactLine(string thetext,string thename,string preval,Boolean passwordline)
begin  
  string 255 type;
  string 255 mpreval;
  
  WebOutTagOpen("<tr>");
  if (nonblank(thename)) then begin
    WebOutTagOpen("<td width=""28%"" class=""feedback-txt"">");
    WebOutStringFormat(thetext);
    WebOutTagClose("</td>");
    WebOutTagOpen("<td width=""72%"">");
    if (passwordline) then begin
      type = "password";
      mpreval = "";
    end else begin
      type = "text";
      mpreval = left(preval,60);
    end;
    WebOutStringFormatNL("<input value=""" & mpreval & """ type=""" & type & """ maxlength=60 name=""" & thename & """ size=""31"" class=""feedback"">");
    WebOutTagClose("</td>");
  end else begin
    WebOutTagOpen("<td colspan=2 class=""feedback-comment"">");
    WebOutStringFormat(thetext);
    WebOutTagClose("</td>");
  end;
  WebOutTagClose("</tr>");
  return;
end;

global
procedure OldCodeWebHBSDivRadioButton(string webname,string varname,Integer ival,Boolean checked)
begin
  string 255 checkstr;
  
  if (checked) then begin
    checkstr = " checked";
  end else begin
    checkstr = "";
  end;
  
  WebOutTagOpen("<div id=""" & varname & """ >");
  WebOutStringFormat("<input type=""radio"" name=""" & varname & """ value=""" & ival & """" & checkstr & ">");
  WebOutStringFormat(webname);
  WebOutTagClose("</div>");
  return;
end;

global
procedure OldCodeContactTextBox(string thetext,string thename,record NotepadVc Noter)
begin
  WebOutTagOpen("<tr>");
  if (nonblank(thename)) then begin
    WebOutTagOpen("<td width=""28%"" class=""feedback-txt"">");
    WebOutStringFormat(thetext);
    WebOutTagClose("</td>");
    WebOutTagOpen("<td width=""72%"">");
    WebOutTagOpen("<textarea name=""" & thename & """ maxlength=255 cols=""30"" rows=""5"" class=""feedback"">");
    WebOutText(Noter,false,"");
    WebOutTagClose("</textarea>");
    WebOutTagClose("</td>");
  end else begin
    WebOutTagOpen("<td colspan=2 class=""feedback-comment"">");
    WebOutStringFormat(thetext);
    WebOutTagClose("</td>");
  end;
  WebOutTagClose("</tr>");

  return;
end;

global
procedure OldCodeCountryDropDown(string thetext,string thename,string thevalue,boolean showall)
begin
  record WebNGCountryVc WCr;
  
  WebOutTagOpen("<tr valign=""top"">");
  WebOutTagOpen("<td width=""28%"" class=""feedback-txt"">");
  WebOutStringFormat(thetext);
  WebOutTagClose("</td>");
  WebOutTagOpen("<td width=""72%"">");
  WebOutTagOpen("<select name=""" & thename & """ size=""1"">");
  WebOutTagOpen("<option value="""">");
  WebOutStringFormat(ToolWebNGTranslateText(20055));
  WebOutTagClose("</option>");

  while (LoopMain(WCr,0,true)) begin
    if (WCr.ShowOnWeb==1 or showall) then begin
      if (WCr.Code==thevalue) then begin
        WebOutTagOpen("<option selected value=""" & WCr.Code & """>");
      end else begin
        WebOutTagOpen("<option value=""" & WCr.Code & """>");
      end;
      WebOutStringFormat(GetTranslatedNameCountry(WCr));
      WebOutTagClose("</option>");
    end;
  end;
  WebOutTagClose("</select>");
  WebOutTagClose("</td>");
  WebOutTagClose("</tr>");
  
  return;
end;

procedure ClearContactUsSessionVariables()
begin
  integer i,cnt;
  PutSessionString("contactus_complete","");

  PutSessionString("contactus_companyname","");
  PutSessionString("contactus_personname","");
  PutSessionString("contactus_address1","");
  PutSessionString("contactus_address2","");
  PutSessionString("contactus_address3","");
  PutSessionString("contactus_address4","");
  PutSessionString("contactus_address5","");
  PutSessionString("contactus_email","");
  PutSessionString("contactus_url","");
  PutSessionString("contactus_phone","");
  PutSessionString("contactus_fax","");
  PutSessionString("contactus_interestflag","");
  PutSessionString("contactus_country","");
  PutSessionString("contactus_errorflags","");

  cnt = StringToInt(GetSessionString("contactus_messagecnt"));
  for (i=1;i<=cnt;i=i+1) begin
    PutSessionString("contactus_message" & i,"");
  end;
  PutSessionString("contactus_messagecnt","");

  return;
end;

global
procedure ShowContactUsElement(record WebNGElementVc WEr)
begin
  Boolean res;
  string 200 tstr;
  LongInt interestf;
  string 255 llink;
  integer i,cnt;
  string 255 companyname;
  string 255 personname;
  string 255 address1;
  string 255 address2;
  string 255 address3;
  string 255 address4;
  string 255 address5;
  string 255 email;
  string 255 url;
  string 255 phone;
  string 255 fax;
  string 255 messagecnt;
  string 255 interestflag;
  string 255 country;
  record NotepadVc Noter;
  integer errorflags;
  
  messagecnt  = GetSessionString("contactus_messagecnt");

  if (GetSessionString("contactus_complete")=="1") then begin
    ClearContactUsSessionVariables;
    
    WebOutString("<p class=""heading3"">");
    WebOutString(ToolWebNGTranslateText(25432));////Thank you. Your message has been submitted
    WebOutString("</p>");
  end else begin
    companyname = GetSessionString("contactus_companyname");
    personname  = GetSessionString("contactus_personname");
    address1    = GetSessionString("contactus_address1");
    address2    = GetSessionString("contactus_address2");
    address3    = GetSessionString("contactus_address3");
    address4    = GetSessionString("contactus_address4");
    address5    = GetSessionString("contactus_address5");
    email       = GetSessionString("contactus_email");
    url         = GetSessionString("contactus_url");
    phone       = GetSessionString("contactus_phone");
    fax         = GetSessionString("contactus_fax");
    cnt = StringToInt(messagecnt);
    for (i=1;i<=cnt;i=i+1) begin
      AddToText(GetSessionString("contactus_message" & i),Noter);
    end;
    interestflag= GetSessionString("contactus_interestflag");
    country     = GetSessionString("contactus_country");

    errorflags  = StringToInt(GetSessionString("contactus_errorflags"));
    
    ClearContactUsSessionVariables;
        
    WebOutTagOpen("<form method=""post"" action=""" & FormatLink("/WebUpdatingAction.hal","?action=save_contactus",true,true) & """>");
    WebOutTagOpen("<table width=""100%"" border=""0"" cellpadding=""1"" cellspacing=""0"">");
    WebOutStringFormatNL("<input type=""hidden"" id=""path"" name=""path"" value=""" & WebGetArg("path") & """>");

    if (errorflags!=0) then begin
      WebOutTagOpen("<tr>");
      WebOutTagOpen("<td colspan=""2"" class=""error"">");
      WebOutStringFormat(ToolWebNGTranslateText(25002));
      WebOutTagOpen("<ul>");
      if (BitAnd(errorflags,1)==1) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(20059) & "</li>");
      end;
      if (BitAnd(errorflags,2)==2) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(20058) & "</li>");
      end;
      if (BitAnd(errorflags,32)!=0) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(20060) & "</li>");
      end;
      if (BitAnd(errorflags,4)==4) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(20062) & "</li>");
      end;
      if (BitAnd(errorflags,8)==8) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(20065) & "</li>");
      end;   
      if (BitAnd(errorflags,64)==64) then begin
        WebOutStringFormatNL("<li>" & ToolWebNGTranslateText(30081) & "</li>");
      end;         
      WebOutTagClose("<ul>");
      WebOutTagClose("</td>");
      WebOutTagClose("</tr>");
    end;

    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20059) & " (*)","companyname",companyname,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20058) & " (*)","personname",personname,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20069),"address1",address1,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20070),"address2",address2,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20071),"address3",address3,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20072),"address4",address4,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20073),"address5",address5,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20060) & " (*)","email",email,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20061),"url",url,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20062) & " (*)","phone",phone,false);
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20063),"fax",fax,false);
    OldCodeContactTextBox(ToolWebNGTranslateText(20064),"message",Noter); //TODO
    OldCodeCountryDropDown(ToolWebNGTranslateText(20065) & "  (*)","country",country,false); //TODO
    OldCodeWebHBSDivContactLine(ToolWebNGTranslateText(20066),"","",false);
    if (IsRecaptchaEnabled(kElementTypeContactUsForm)) then begin
      WebOutTagOpen("<tr>");
      WebOutTagOpen("<td>");
      WebOutStringFormat("&nbsp;");      
      WebOutTagClose("</td>");
      WebOutTagOpen("<td>");
      WebOutStringFormatNL("<div class=""g-recaptcha"" data-sitekey="""& RecaptchaSiteKey &"""></div>");    
      WebOutTagClose("</td>");
      WebOutTagOpen("</tr>");
    end;
    WebOutTagOpen("<tr valign=""top"">");
    WebOutTagOpen("<td width=""28%"">");
    WebOutStringFormat("&nbsp;");
    WebOutTagClose("</td>");
    WebOutTagOpen("<td width=""72%"">");
    WebOutSubmit("","button","Submit",ToolWebNGTranslateText(20067));
    WebOutStringFormatNL("<input type=""reset"" name=""Reset"" value=""" & ToolWebNGTranslateText(20068) & """>");
    WebOutTagClose("</td>");
    WebOutTagClose("</tr>");
    WebOutTagClose("</table>");
    WebOutTagClose("</form>");
  end;
  return;
end;

procedure AddTextToMail(record NotepadVc Noter,var record MailVc Mailr)
begin
  integer i,cnt;
  string 255 tstr;
  
  i = 0;
  cnt = SizeTextCnt(Noter);
  while (i<cnt) begin
    tstr = StringFromText(Noter,i,255);
    i = i + 255;
    AddToText(tstr,Mailr);
  end;
  
  return;
end;

updating procedure StoreContactUsMail(string companyname,string personname,string address1,string address2,string address3,string address4,string address5,
                                      string email,string url,string phone,string fax,integer interestflag,string country,record NotepadVc Noter)
begin
  record MailSettingsBlock MSr;
  record ConfVc Confr;
  record MailVc Mail;
  row MailVc Mailrw;
  Boolean mailstored;
  string 255 tstr;
  string 255 toconf;

  toconf = "Contact Us";
  
  if (nonblank(toconf)) then begin
    BlockLoad(MSr);
    RecordNew(Mail);
    Mail.SendFlag = 1;
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = ToolWebNGTranslateText(25433);  //Download Registration

    MatRowPut(Mail,0,Mailrw);
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = GetWebmailBoxForCountry(country);
    if (blank(Mailrw.AddrCode)) then begin
      Mailrw.AddrCode = MSr.Postmaster;
    end;
    MatRowPut(Mail,1,Mailrw);
    Mail.Header = "Please contact: " & companyname;
    AddTextLineToMail(personname,Mail);
    AddTextLineToMail(companyname,Mail);
    AddTextLineToMail(address1,Mail);
    AddTextLineToMail(address2,Mail);
    AddTextLineToMail(address3,Mail);
    AddTextLineToMail(address4,Mail);
    AddTextLineToMail(address5,Mail);
    AddTextLineToMail(email,Mail);
    AddTextLineToMail(url,Mail);
    AddTextLineToMail(phone,Mail);
    AddTextLineToMail(fax,Mail);
    AddTextLineToMail("",Mail);
    AddTextLineToMail("",Mail);
    
    AddTextToMail(Noter,Mail);
    AddNLToMail(Mail);
    AddNLToMail(Mail);

    switch (interestFlag) begin
      case 0:
        /*Confr.AddrName = Confr.AddrName & " FO";
        if (ReadFirstKey("AddrName",Confr,1,true)) then begin
          Mailrw.RowTyp = 0;
          Mailrw.AddrCode = Confr.AddrName;
          MatRowPut(Mail,1,Mailrw);
        end;
        ToolWebGetTheText(20129,tstr);*/
        tstr = ToolWebNGTranslateText(20129);
      case 1: tstr = ToolWebNGTranslateText(20130); 
      case 2: tstr = ToolWebNGTranslateText(20131); 
      case 3: tstr = ToolWebNGTranslateText(20132); 
      case 4: tstr = ToolWebNGTranslateText(20133); 
    end;
    AddTextLineToMail("Interested in: " & tstr,Mail);
    mailstored = RecordInsert(Mail,false);
  end;
  
  return;
end;

global
updating procedure StoreContactUsForm()
begin
  integer i,cnt,pos,errorflags;
  string 255 companyname;
  string 255 personname;
  string 255 address1;
  string 255 address2;
  string 255 address3;
  string 255 address4;
  string 255 address5;
  string 255 email;
  string 255 url;
  string 255 phone;
  string 255 fax;
  string 255 messagecnt;
  string 255 interestflag;
  string 255 country;
  record NotepadVc Noter;
  string 255 tstr;
  area WebPostArea;
  
  //read from webargs
  companyname = WebGetArg("companyname");
  personname  = WebGetArg("personname");
  address1    = WebGetArg("address1");
  address2    = WebGetArg("address2");
  address3    = WebGetArg("address3");
  address4    = WebGetArg("address4");
  address5    = WebGetArg("address5");
  email       = WebGetArg("email");
  url         = WebGetArg("url");
  phone       = WebGetArg("phone");
  fax         = WebGetArg("fax");
  interestflag= WebGetArg("interestflag");
  country     = WebGetArg("country");
  WebGetText("message",Noter);
  
  //validate
  errorflags = 0;
  if (blank(companyname)) then begin
    errorflags = errorflags + 1;
  end;
  if (blank(personname)) then begin
    errorflags = errorflags + 2;
  end;
  if (blank(phone)) then begin
    errorflags = errorflags + 4;
  end;
  if (blank(country)) then begin
    errorflags = errorflags + 8;
  end;
  if (blank(interestflag)) then begin
    errorflags = errorflags + 16;
  end;
  if (blank(email)) then begin
    errorflags = errorflags + 32;
  end;
  if (errorflags==0 and IsRecaptchaEnabled(kElementTypeContactUsForm)) then begin
    WebGetPostData(WebPostArea);
    if (CheckRecaptcha(WebPostArea)==false) then begin 
      errorflags = errorflags + 64;
    end;      
  end;
  if (errorflags==0) then begin
    StoreContactUsMail(companyname,personname,address1,address2,address3,address4,address5,email,url,phone,fax,StringToInt(interestflag),country,Noter);
    PutSessionString("contactus_complete","1");
  end else begin
    PutSessionString("contactus_errorflags",errorflags);
    PutSessionString("contactus_companyname",companyname);
    PutSessionString("contactus_personname",personname);
    PutSessionString("contactus_address1",address1);
    PutSessionString("contactus_address2",address2);
    PutSessionString("contactus_address3",address3);
    PutSessionString("contactus_address4",address4);
    PutSessionString("contactus_address5",address5);
    PutSessionString("contactus_email",email);
    PutSessionString("contactus_url",url);
    PutSessionString("contactus_phone",phone);
    PutSessionString("contactus_fax",fax);
    PutSessionString("contactus_interestflag",interestflag);
    PutSessionString("contactus_country",country);
    
    pos=0;
    i = 0;
    cnt = SizeTextCnt(Noter);
    while (pos<cnt) begin
      tstr = StringFromText(Noter,i,255);
      if (len(tstr)!=0) then begin
        i = i+1;
        PutSessionString("contactus_message" & i,tstr);
      end;
      pos = pos + 255;
    end;
    PutSessionString("contactus_messagecnt","" & i);
  end;
  
  RedirectToRequestedPage;
  
  return;
end;
