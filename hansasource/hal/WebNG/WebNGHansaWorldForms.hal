//tools
external function string 255 removenextnode(var string);
external function string 255 GetNextValue(var string);
external function string 255 CleanPath(string);
external function string 10 ToolWebNGGetCountry();
external procedure UpdateWebCountry(string);
external function boolean LoginState();
external function string 255 GetLinkToStruct(LongInt,boolean);
external function string 255 FormatLink(string,string,boolean,boolean);
external function string 255 FormatSimpleLinkWithArg(string,string,boolean);
external function string 10 CurrentWebLang();
external procedure RequireSessionState();
//elements
external procedure ShowWebElement(string);
//pages
external procedure ShowErrorPage(string);
external procedure ShowWebAppPageStart(record WebNGPageVc,string);
external procedure ShowWebAppPageEnd(record WebNGPageVc);
//translation
external function string 255 ToolWebNGTranslateText(integer);
//std
external procedure AddTextLineToMail(string,record MailVc);
external function Boolean InterNetAddrTest(string);
external function Boolean GetNextCustNr(var string);

global
updating procedure HWFormsStoreMail(record MailVc Mailr)
begin
  Boolean mailstored;

  Mailr.SerNr = NextSerNr("MailVc",Mailr.TransDate,-1,false,"");
  mailstored = RecordStore(Mailr,false);
  
  return;
end;

function boolean TestEmailAddress(string email)
begin
  boolean res;
  integer p;
  string 255 tstr;
  
  res = false;
  p = InString(email,"@");
  if (p>1) then begin
    tstr = mid(email,p,255);
    if (InString(tstr,"@")==0) then begin
      if (InString(tstr,".")>1) then begin
        if (right(tstr,1)!=".") then begin
          res = true;
        end;
      end;
    end;
  end;
  TestEmailAddress = res;
  return;
end;

function string 255 TranslatedFieldName(integer code,string default)
begin
  record WebTextVc WTr;
  row WebTextVc WTrw;
  integer i,cnt;
  string 255 langcode;
  string 255 res;
  
  WTr.Code = code;
  if (ReadFirstMain(WTr,1,true)) then begin
    res = WTr.Comment;

    cnt = MatRowCnt(WTr);
    langcode = CurrentWebLang;
    for (i=0;i<cnt;i=i+1) begin
      MatRowGet(WTr,i,WTrw);
      if (WTrw.LangCode==langcode) then begin
        res = WTrw.Comment;
        i = cnt;
      end;
    end;
  end else begin
    res = default;
  end;
  
  TranslatedFieldName = res;
  return;
end;

function string 255 GetWebmailConference(string country)
begin
  record WebNGCountryVc WCr;
  string 255 res;

  res = "";
  WCr.Code = country;
  if (ReadFirstMain(WCr,1,true)) then begin
    res = WCr.WebMailConference;
  end;
  
  GetWebmailConference = res;
  return;
end;

function string 200 FormValue(string varname)
begin
  if (nonblank(WebGetArg(varname))) then begin
    FormValue = " value=""" & WebGetArg(varname) & """";
  end;
  return;
end;

procedure ShowHWFormThanksPage(string page,string element)
begin
  record WebNGPageVc WPr;
  
  WPr.Code = page;
  if (ReadFirstMain(WPr,1,true)) then begin end;
  
  ShowWebAppPageStart(WPr,"");

  ShowWebElement(element);

  ShowWebAppPageEnd(WPr);
  return;
end;
 
procedure HWForm_ACN(string page,string elementbefore,string elementafter,integer err)
begin
  boolean res;
  string 60 tstr;
  record WebNGPageVc WPr;
  integer totalwidth,col1width,col2width;
  
  totalwidth = 500;
  col1width = 150;
  col2width = 300;
  
  WPr.Code = page;
  if (ReadFirstMain(WPr,1,true)) then begin end;
  
  ShowWebAppPageStart(WPr,"");
  ShowWebElement(elementbefore);
  
  //start
  tstr = "<span class=""error"">" & TranslatedFieldName(32001,"(This field is required)") & "</span>";
  
  WebOutString("<form method=""post"" action=""");
  WebOutString(""); //to myself
  WebOutString(""">");
  
  WebOutString("<input type=""hidden"" name=""postback"" value=""1"">");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");
  WebOutString("<tr>");
  WebOutString("<td colspan=""2""><span style=""font-weight: bold"">" & TranslatedFieldName(32002,"Your Information") & "</span></td>");
  WebOutString("</tr>");
  
  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32003,"Company Name") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companyname""" & FormValue("companyname") & ">");
  if (BitAnd(err,1)==1) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32004,"Your Name") & ":</td>");
  WebOutString("<td><input type=""text"" name=""yourname""" & FormValue("yourname") & ">");
  if (BitAnd(err,2)==2) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("</table>");

  WebOutString("<br>");
  
  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");

  WebOutString("<tr>");
  WebOutString("<td colspan=""2""><span style=""font-weight: bold"">" & TranslatedFieldName(32005,"Lead Information") & "</span></td>");
  WebOutString("</tr>");
  
  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32003,"Company Name") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""leadcompanyname""" & FormValue("leadcompanyname") & ">");
  if (BitAnd(err,4)==4) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32006,"City/State") & ":</td>");
  WebOutString("<td><input type=""text"" name=""leadcitystate""" & FormValue("leadcitystate") & ">");
  if (BitAnd(err,8)==8) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32007,"Industry") & ":</td>");
  WebOutString("<td><input type=""text"" name=""leadindustry""" & FormValue("leadindustry") & ">");
  if (BitAnd(err,16)==16) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("</table>");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");
  WebOutString("<tr>");
  WebOutString("<td colspan=""2"" align=""left"">");
  WebOutString("<input type=""submit"" name=""send"" value=""" & TranslatedFieldName(32000,"Submit") & """>");
  WebOutString("</td>");
  WebOutString("</tr>");
  WebOutString("</table>");

  WebOutString("</form>");
  //End

  ShowWebElement(elementafter);  
  ShowWebAppPageEnd(WPr);
  return;
end;

procedure HWForm_ACN_PostBack(string page,string elementbefore,string elementafter,string thankselement)
begin
  string 100 destAddr,destAddr2,mailHeader;
  record MailVc Mailr;
  row MailVc Mailrw;
  string 255 tstr;
  boolean res;
  integer err;
   integer i,cnt;
  array string 20 a;
  array string 35 b;
  
  destAddr = "WWW Apple";
  mailHeader = "(Web) Lead Registration from ACN";

  err = 0;
  if (blank(WebGetArg("companyname"))) then begin
    err = err+1;
  end;  
  if (blank(WebGetArg("yourname"))) then begin
    err = err+2;
  end;  
  if (blank(WebGetArg("leadcompanyname"))) then begin
    err = err+4;
  end;
  if (blank(WebGetArg("leadcitystate"))) then begin
    err = err+8;
  end;
  if (blank(WebGetArg("leadindustry"))) then begin
    err = err+16;
  end;
  
  if (err!=0) then begin
    HWForm_ACN(page,elementbefore,elementafter,err);
  end else begin
    RecordNew(Mailr);
    
    Mailr.SendFlag = 1;

    Mailr.Header = mailHeader;
    
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = "Postmaster";
    MatRowPut(Mailr,0,Mailrw);
    
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = destAddr;
    MatRowPut(Mailr,1,Mailrw);
    
    if (nonblank(destAddr2)) then begin
      Mailrw.RowTyp = 0;
      Mailrw.AddrCode = destAddr2;
      MatRowPut(Mailr,2,Mailrw);
    end;

    a[0] = "companyname";
    a[1] = "yourname";
    a[2] = "leadcompanyname";
    a[3] = "leadcitystate";
    a[4] = "leadindustry";
    
    b[0] = "Company Name";
    b[1] = "Your Name";
    b[2] = "Lead Company Name";
    b[3] = "Lead City/State";
    b[4] = "Lead Industry";
    cnt = 5;

    for (i=0;i<cnt;i=i+1) begin
      AddTextLineToMail(b[i] & " = " & WebGetArg(a[i]),Mailr);
    end;

    queued.HWFormsStoreMail(Mailr);

    ShowHWFormThanksPage(page,thankselement);
  end;
  
  return;
end;

global
procedure HWForm_TestLanding_Postback(string page,string element,string thankselement)
begin
  record WebNGPageVc WPr;
  record MailVc Mailr;
  row MailVc Mailrw;
  string 255 email,country;
  string 100 destAddr,destAddr2,mailHeader;

  WPr.Code = page;
  if (ReadFirstMain(WPr,1,true)) then begin end;

  email = WebGetArg("email");
  if (blank(email)) then begin
    ShowWebAppPageStart(WPr,"");
    ShowWebElement(element);
    ShowWebAppPageEnd(WPr);
  end else begin
    if (InterNetAddrTest(email)==false) then begin
      ShowWebAppPageStart(WPr,"");
      WebOutString("<h1>" & TranslatedFieldName(32008,"Invalid email address") & "</h1>");
      WebOutString("<a href="""">" & TranslatedFieldName(32009,"Please try again") & "</a>");
      ShowWebAppPageEnd(WPr);
    end else begin
      destAddr = "Webmail Sweden";
      country = WebGetArg("country");
      if (nonblank(country)) then begin
        switch (country) begin
          case "norway": destAddr = "Webmail Norway";
          case "finland": destAddr = "Webmail Finland";
        end;
      end else begin
        country = "not selected";
      end;
      destAddr2 = "Annika Khan Pettersson";
      mailHeader = "(Web) More Info Requested for HW Standard ERP";  

      RecordNew(Mailr);
      
      Mailr.SendFlag = 1;

      Mailr.Header = mailHeader;
      
      Mailrw.RowTyp = 1;
      Mailrw.AddrCode = "Postmaster";
      MatRowPut(Mailr,0,Mailrw);
      
      Mailrw.RowTyp = 0;
      Mailrw.AddrCode = destAddr;
      MatRowPut(Mailr,1,Mailrw);
      
      if (nonblank(destAddr2)) then begin
        Mailrw.RowTyp = 0;
        Mailrw.AddrCode = destAddr2;
        MatRowPut(Mailr,2,Mailrw);
      end;

      AddTextLineToMail("Someone requested more information about HansaWorld Standard ERP using our Test Landing page",Mailr);
      AddTextLineToMail("Email address = " & email,Mailr);
      AddTextLineToMail("Country = " & country,Mailr);

      queued.HWFormsStoreMail(Mailr);

      ShowHWFormThanksPage(page,thankselement);
    end;
  end;
  
  return;
end;

global
updating procedure HWFormsStoreMailAndContact(record MailVc Mailr,record CUVc CUr,string country)
begin
  Boolean mailstored,contactstored;
  boolean res;
  integer oldcompany;
  string 255 tstr;
  record WebNGCompCountryVc WCCr;

  Mailr.SerNr = NextSerNr("MailVc",Mailr.TransDate,-1,false,"");
  mailstored = RecordStore(Mailr,false);

  contactstored = false;
  WCCr.Country = country;
  if (ReadFirstKey("Country",WCCr,1,true)) then begin
    
    if (CurrentCompany!=WCCr.CompanyNr) then begin
      oldcompany = CurrentCompany;
      res = SetCompany(WCCr.CompanyNr,false);

      if (res) then begin
        if (GetNextCustNr(tstr)) then begin end;
        CUr.Code = tstr;

        contactstored = RecordStore(CUr,false);
      end;
    end;
  end;
  
  if (mailstored and contactstored) then begin
    CreateRecordLink(Mailr,oldcompany,CUr,CurrentCompany);
  end;
  
  if (res) then begin
    ResetCompany(oldcompany);
  end;
  
  return;
end;

global
procedure HWForm_RegisterAsPartner(string page,string elementbefore,string elementafter,string thankselement,integer err)
begin
  record WebNGPageVc WPr;
  string 255 tstr,tstr2;
  integer totalwidth,col1width,col2width;
  
  totalwidth = 500;
  col1width = 150;
  col2width = 300;
  
  WPr.Code = page;
  if (ReadFirstMain(WPr,1,true)) then begin end;

  ShowWebAppPageStart(WPr,"");
  ShowWebElement(elementbefore);

  //start
  tstr  = "<span class=""error"">" & TranslatedFieldName(32001,"(This field is required)") & "</span>";
  tstr2 = "<span class=""error"">" & TranslatedFieldName(32008,"Invalid email address") & "</span>";
  
  WebOutString("<form method=""post"" action=""");
  WebOutString(""); //to myself
  WebOutString(""">");
  
  WebOutString("<input type=""hidden"" name=""postback"" value=""1"">");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32003,"Company Name") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companyname""" & FormValue("companyname") & ">");
  if (BitAnd(err,1)==1) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32010,"Company Registration") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companyregnr""" & FormValue("companyregnr") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32011,"Company VAT Number") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companyvatnr""" & FormValue("companyvatnr") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32012,"Contact Person") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""contactname""" & FormValue("contactname") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32013,"Company Telephone") & ":</td>");
  WebOutString("<td><input type=""text"" name=""companyphone""" & FormValue("companyphone") & ">");
  if (BitAnd(err,2)==2) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32014,"Company Website") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companywebsite""" & FormValue("companywebsite") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32015,"Contact Person E-Mail") & ":</td>");
  WebOutString("<td><input type=""email"" name=""contactpersonemail""" & FormValue("contactpersonemail") & ">");
  if (BitAnd(err,4)==4) then begin
    WebOutString(tstr);
  end;
  if (BitAnd(err,8)==8) then begin
    WebOutString(tstr2);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32016,"Contact Person Mobile") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""contactpersonmobile""" & FormValue("contactpersonmobile") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32017,"Type of Agreement interested in") & ":</td>");
  WebOutString("<td width=""" & col2width & "px"">");//<input type=""text"" name=""typeofagreement""" & FormValue("typeofagreement") & ">");
  tstr = FormValue("typeofagreement");
  WebOutString("<select name=""typeofagreement"" style=""width:" & col2width & "px;"">");

  if (tstr=="") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""""" & tstr2 &">" & "" & "</option>");
  
  if (tstr=="distribution partner") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""distribution partner""" & tstr2 &">" & TranslatedFieldName(32025,"Distribution Partner") & "</option>");
  
  if (tstr=="private network sales") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""private network sales""" & tstr2 &">" & TranslatedFieldName(32026,"Private Network Sales Partner") & "</option>");
  
  if (tstr=="harware partner") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""harware partner""" & tstr2 &">" & TranslatedFieldName(32027,"Hardware Partner") & "</option>");
  
  if (tstr=="value added reseller") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""value added reseller""" & tstr2 &">" & TranslatedFieldName(32028,"Value Added Reseller") & "</option>");

  if (tstr=="open dist partner") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""open dist partner""" & tstr2 &">" & TranslatedFieldName(32029,"Open Distribution Partner") & "</option>");
  
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td colspan=""2""><a href=""/partners"" target=""_blank"">" & TranslatedFieldName(32024,"Click here to read more about our Partner Agreements") & "</a></span></td>");
  WebOutString("</tr>");

  WebOutString("</table>");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");
  WebOutString("<tr>");
  WebOutString("<td colspan=""2"" align=""left"">");
  WebOutString("<input type=""submit"" name=""send"" value=""" & TranslatedFieldName(32000,"Submit") & """>");
  WebOutString("</td>");
  WebOutString("</tr>");
  WebOutString("</table>");

  WebOutString("</form>");
  //End

  ShowWebElement(elementafter);  
  ShowWebAppPageEnd(WPr);

  return;
end;

procedure BuildPartnerContactRecord(var record CUVc CUr,string companyname,string companyregnr,string companyvatnr,string contactname,string companyphone,string companywebsite,string contactpersonemail,string contactpersonmobile,string typeofagreement)
begin
  string 255 tstr;
  
  RecordNew(CUr);
  
  CUr.Name = companyname;
  CUr.RegNr1 = companyregnr;
  CUr.VATNr = companyvatnr;
  CUr.Person = contactname;
  CUr.Phone = companyphone;
  CUr.wwwAddr = companywebsite;
  CUr.eMail = contactpersonemail;
  CUr.Mobile = contactpersonmobile;
  CUr.SalesMan = "WEB";
  CUr.CUType = 1;
  CUr.VEType = 0;
  CUr.AllowLogin = 0;

  CUr.CustCat = "59"; //potential partner
  
  CUr.Comment2 = typeofagreement;

  return;
end;

procedure HWForm_RegisterAsPartner_PostBack(string page,string elementbefore,string elementafter,string thankselement)
begin
  integer err;
  string 255 destAddr,destAddr2,mailHeader;
  record CUVc CUr;
  record MailVc Mailr;
  row MailVc Mailrw;
  
  err = 0;
  if (blank(WebGetArg("companyname"))) then begin
    err = err + 1;
  end;
  if (blank(WebGetArg("companyphone"))) then begin
    err = err + 2;
  end;
  if (blank(WebGetArg("contactpersonemail"))) then begin
    err = err + 4;
  end else begin
    if (TestEmailAddress(WebGetArg("contactpersonemail"))==false) then begin
      err = err + 8;
    end;
  end;
  
  if (err!=0) then begin
    HWForm_RegisterAsPartner(page,elementbefore,elementafter,thankselement,err);
  end else begin
    destAddr = GetWebmailConference(ToolWebNGGetCountry);
    if (blank(destAddr)) then begin
      destAddr = "Brendan Peo";
    end;
    mailHeader = "(Web) A new interested partner registered on our website";

    RecordNew(Mailr);
    
    Mailr.SendFlag = 1;

    Mailr.Header = mailHeader;
    
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = "Postmaster";
    MatRowPut(Mailr,0,Mailrw);
    
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = destAddr;
    MatRowPut(Mailr,1,Mailrw);
    
    if (nonblank(destAddr2)) then begin
      Mailrw.RowTyp = 0;
      Mailrw.AddrCode = destAddr2;
      MatRowPut(Mailr,2,Mailrw);
    end;

    AddTextLineToMail("Someone registered as a partner on our web site",Mailr);
    AddTextLineToMail("Company name = "          & WebGetArg("companyname"),Mailr);
    AddTextLineToMail("Company registration = "  & WebGetArg("companyregnr"),Mailr);
    AddTextLineToMail("Company VAT number = "    & WebGetArg("companyvatnr"),Mailr);
    AddTextLineToMail("Contact person name = "   & WebGetArg("contactname"),Mailr);
    AddTextLineToMail("Company phone number = "  & WebGetArg("companyphone"),Mailr);
    AddTextLineToMail("Company website = "       & WebGetArg("companywebsite"),Mailr);
    AddTextLineToMail("Contact person email = "  & WebGetArg("contactpersonemail"),Mailr);
    AddTextLineToMail("Contact person mobile = " & WebGetArg("contactpersonmobile"),Mailr);
    AddTextLineToMail("Type of partner agreement = " & WebGetArg("typeofagreement"),Mailr);

    BuildPartnerContactRecord(CUr,WebGetArg("companyname"),WebGetArg("companyregnr"),WebGetArg("companyvatnr"),WebGetArg("contactname"),WebGetArg("companyphone"),
      WebGetArg("companywebsite"),WebGetArg("contactpersonemail"),WebGetArg("contactpersonmobile"),WebGetArg("typeofagreement"));
    queued.HWFormsStoreMailAndContact(Mailr,CUr,ToolWebNGGetCountry);

    ShowHWFormThanksPage(page,thankselement);
  end;
  return;
end;

global
procedure HWForm_RegisterALead(string page,string elementbefore,string elementafter,string thankselement,integer err)
begin
  record WebNGPageVc WPr;
  string 255 tstr,tstr2;
  integer totalwidth,col1width,col2width;
  record CClassVc CClassr;
  boolean foundf;
  integer i;
  
  totalwidth = 500;
  col1width = 150;
  col2width = 300;
  
  WPr.Code = page;
  if (ReadFirstMain(WPr,1,true)) then begin end;

  ShowWebAppPageStart(WPr,"");
  ShowWebElement(elementbefore);

  //start
  tstr  = "<span class=""error"">" & TranslatedFieldName(32001,"(This field is required)") & "</span>";
  tstr2 = "<span class=""error"">" & TranslatedFieldName(32008,"Invalid email address") & "</span>";
  
  WebOutString("<form method=""post"" action=""");
  WebOutString(""); //to myself
  WebOutString(""">");
  
  WebOutString("<input type=""hidden"" name=""postback"" value=""1"">");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32018,"Partner number (if applicable)") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""partnerno""" & FormValue("partnerno") & ">");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td colspan=""2"">&nbsp;</span></td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td colspan=""2""><span style=""font-weight: bold"">" & TranslatedFieldName(32023,"Information about the lead") & "</span></td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32003,"Company Name") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""companyname""" & FormValue("companyname") & ">");
  if (BitAnd(err,1)==1) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32012,"Contact Person") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""contactname""" & FormValue("contactname") & ">");
  if (BitAnd(err,2)==2) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">*" & TranslatedFieldName(32019,"Contact Number") & ":</td>");
  WebOutString("<td width=""" & col2width & "px""><input type=""text"" name=""contactphone""" & FormValue("contactphone") & ">");
  if (BitAnd(err,4)==4) then begin
    WebOutString(tstr);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td>*" & TranslatedFieldName(32020,"Contact E-Mail") & ":</td>");
  WebOutString("<td><input type=""email"" name=""contactemail""" & FormValue("contactemail") & ">");
  if (BitAnd(err,8)==8) then begin
    WebOutString(tstr);
  end;
  if (BitAnd(err,16)==16) then begin
    WebOutString(tstr2);
  end;
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32007,"Industry") & ":</td>");
  WebOutString("<td width=""" & col2width & "px"">"); //<input type=""text"" name=""industry""" & FormValue("industry") & ">");
  tstr = FormValue("industry");
  WebOutString("<select name=""industry"" style=""width:" & col2width & "px;"">");
  foundf = true;
  CClassr.CType = "102";
  while (LoopKey("CType",CClassr,1,foundf)) begin
    if (CClassr.CType!="102") then begin
      foundf = false;
    end;
    if (foundf) then begin
      WebOutString("<option");
      WebOutString(" value=" & CClassr.Code);
      if (CClassr.Code==tstr) then begin
        WebOutString(" selected");
      end;
      WebOutString(">");
      for (i=0;i<len(CClassr.Code)-2;i=i+1) begin
        WebOutString("&nbsp;");
      end;
      WebOutString(CClassr.Comment);
      WebOutString("</option>");
    end;
  end;
  WebOutString("</select>");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32021,"Number of employees") & ":</td>");
  WebOutString("<td width=""" & col2width & "px"">");
  tstr = FormValue("employeecount");
  WebOutString("<select name=""employeecount"">");
  if (tstr=="10") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""10""" & tstr2 &">1 - 9</option>");
  if (tstr=="50") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""50""" & tstr2 &">10 - 49</option>");
  if (tstr=="99") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""99""" & tstr2 &">50 - 99</option>");
  if (tstr=="100") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""100""" & tstr2 &">100+</option>");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("<tr>");
  WebOutString("<td width=""" & col1width & "px"">" & TranslatedFieldName(32022,"Turnover (million EUR)") & ":</td>");
  WebOutString("<td width=""" & col2width & "px"">");
  tstr = FormValue("turnover");
  WebOutString("<select name=""turnover"">");
  if (tstr=="1") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""1""" & tstr2 &">0 - 1</option>");
  if (tstr=="5") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""5""" & tstr2 &">1 - 5</option>");
  if (tstr=="10") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""10""" & tstr2 &">5 - 10</option>");
  if (tstr=="20") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""20""" & tstr2 &">10 - 20</option>");
  if (tstr=="50") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""50""" & tstr2 &">20 - 50</option>");
  if (tstr=="100") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""100""" & tstr2 &">50 - 100</option>");
  if (tstr=="100+") then begin tstr2 = " selected"; end else begin tstr2 = ""; end;
  WebOutString("<option value=""100+""" & tstr2 &">100+</option>");
  WebOutString("</td>");
  WebOutString("</tr>");

  WebOutString("</table>");

  WebOutString("<table width=""" & totalwidth & "px"" border=""0"">");
  WebOutString("<tr>");
  WebOutString("<td colspan=""2"" align=""left"">");
  WebOutString("<input type=""submit"" name=""send"" value=""" & TranslatedFieldName(32000,"Submit") & """>");
  WebOutString("</td>");
  WebOutString("</tr>");
  WebOutString("</table>");

  WebOutString("</form>");
  //End

  ShowWebElement(elementafter);  
  ShowWebAppPageEnd(WPr);

  return;
end;

procedure BuildLeadContactRecord(record CUVc CUr,string partnerno,string companyname,string contactname,string contactphone,string contactemail,string industry,string noofemployees,string turnover)
begin
  string 255 tstr;
  
  RecordNew(CUr);
  
  CUr.Name = companyname;
  CUr.Person = contactname;
  CUr.Phone = contactphone;
  CUr.eMail = contactemail;
  if (nonblank(partnerno)) then begin
    CUr.Comment2 = "Partner number: " & partnerno;
  end;
  CUr.CustCat = "41"; //potential customer
  CUr.SalesMan = "WEB";
  CUr.CUType = 1;
  CUr.VEType = 0;
  CUr.AllowLogin = 0;
  
  switch (noofemployees) begin
    case "10" : tstr = "140";
    case "50" : tstr = "141";
    case "99" : tstr = "142";
    case "100" : tstr = "143";
  end;
  
  if (nonblank(CUr.Classification)) then begin
    CUr.Classification = CUr.Classification & ",";
  end;
  CUr.Classification = CUr.Classification & tstr;

  switch (turnover) begin
    case "1" : tstr = "145";
    case "5" : tstr = "146";
    case "10" : tstr = "147";
    case "20" : tstr = "148";
    case "50" : tstr = "149";
    case "100" : tstr = "150";
    case "100+" : tstr = "151";
  end;
  
  if (nonblank(CUr.Classification)) then begin
    CUr.Classification = CUr.Classification & ",";
  end;
  CUr.Classification = CUr.Classification & tstr;

  if (nonblank(industry)) then begin
    if (nonblank(CUr.Classification)) then begin
      CUr.Classification = CUr.Classification & ",";
    end;
    CUr.Classification = CUr.Classification & industry;
  end;
  
  return;
end;

procedure HWForm_RegisterALead_PostBack(string page,string elementbefore,string elementafter,string thankselement)
begin
  integer err;
  string 255 destAddr,destAddr2,mailHeader,tstr;
  record CUVc CUr;
  record MailVc Mailr;
  row MailVc Mailrw;
  
  err = 0;
  if (blank(WebGetArg("companyname"))) then begin
    err = err + 1;
  end;
  if (blank(WebGetArg("contactname"))) then begin
    err = err + 2;
  end;
  if (blank(WebGetArg("contactphone"))) then begin
    err = err + 4;
  end;
  if (blank(WebGetArg("contactemail"))) then begin
    err = err + 8;
  end else begin
    if (TestEmailAddress(WebGetArg("contactemail"))==false) then begin
      err = err + 16;
    end;
  end;
  
  if (err!=0) then begin
    HWForm_RegisterALead(page,elementbefore,elementafter,thankselement,err);
  end else begin
    destAddr = GetWebmailConference(ToolWebNGGetCountry);
    if (blank(destAddr)) then begin
      destAddr = "Brendan Peo";
    end;
    mailHeader = "(Web) A new lead was registered on our web site";

    RecordNew(Mailr);
    
    Mailr.SendFlag = 1;

    Mailr.Header = mailHeader;
    
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = "Postmaster";
    MatRowPut(Mailr,0,Mailrw);
    
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = destAddr;
    MatRowPut(Mailr,1,Mailrw);
    
    if (nonblank(destAddr2)) then begin
      Mailrw.RowTyp = 0;
      Mailrw.AddrCode = destAddr2;
      MatRowPut(Mailr,2,Mailrw);
    end;

    AddTextLineToMail("Someone registered as a new lead on our web site",Mailr);
    AddTextLineToMail("Partner number = "        & WebGetArg("partnerno"),Mailr);
    AddTextLineToMail("Company name = "          & WebGetArg("companyname"),Mailr);
    AddTextLineToMail("Contact person name = "   & WebGetArg("contactname"),Mailr);
    AddTextLineToMail("Company phone number = "  & WebGetArg("contactphone"),Mailr);
    AddTextLineToMail("Contact person email = "  & WebGetArg("contactemail"),Mailr);
    AddTextLineToMail("Contact person mobile = " & WebGetArg("industry"),Mailr);
    switch (WebGetArg("employeecount")) begin
      case "10" : tstr = "1 - 9";
      case "50" : tstr = "10 - 50";
      case "99" : tstr = "50 - 99";
      case "100" : tstr = "100+";
      otherwise
        tstr = "";
    end;
    AddTextLineToMail("Number of employees = "   & tstr,Mailr);
    switch (WebGetArg("turnover")) begin
      case "1" : tstr = "0 - 1 million EUR";
      case "5" : tstr = "1 - 5 million EUR";
      case "10" : tstr = "5 - 10 million EUR";
      case "20" : tstr = "10 - 20 million EUR";
      case "50" : tstr = "20 - 50 million EUR";
      case "100" : tstr = "50 - 100 million EUR";
      case "100+" : tstr = "over 100 million EUR";
      otherwise
        tstr = "";
    end;
    AddTextLineToMail("Turnover = "              & tstr,Mailr);

    BuildLeadContactRecord(CUr,WebGetArg("partnerno"),WebGetArg("companyname"),WebGetArg("contactname"),WebGetArg("contactphone"),WebGetArg("contactemail"),
      WebGetArg("industry"),WebGetArg("employeecount"),WebGetArg("turnover"));
    queued.HWFormsStoreMailAndContact(Mailr,CUr,ToolWebNGGetCountry);

    ShowHWFormThanksPage(page,thankselement);
  end;
  return;
end;

global
procedure HansaWorldFormsApp(record WebNGStructVc WSr,string path,string elements)
begin
  string 255 node,mpath;
  string 255 melements,thankselement,elementbefore,elementafter;
  
  mpath = path;
  node = removenextnode(mpath);

  melements = elements;
  thankselement = GetNextValue(melements);
  elementbefore = GetNextValue(melements);
  elementafter = GetNextValue(melements);
  
  if (WSr.Name == "acn") then begin //temp fix.
    WSr.Name = "acn_leads";
  end;
  switch (WSr.Name) begin
    case "acn_leads" :
      if (WebGetArg("postback")=="1") then begin
        HWForm_ACN_PostBack(WSr.WebPage,elementbefore,elementafter,thankselement);
      end else begin
        HWForm_ACN(WSr.WebPage,elementbefore,elementafter,0);
      end;
    case "complete_accounting_system":
      HWForm_TestLanding_Postback(WSr.WebPage,elementbefore,thankselement);
    case "test_landing" :
      HWForm_TestLanding_Postback(WSr.WebPage,elementbefore,thankselement);
    case "register_partner":
      if (WebGetArg("postback")=="1") then begin
        HWForm_RegisterAsPartner_PostBack(WSr.WebPage,elementbefore,elementafter,thankselement);
      end else begin
        HWForm_RegisterAsPartner(WSr.WebPage,elementbefore,elementafter,thankselement,0);
      end;
    case "register_lead":
      if (WebGetArg("postback")=="1") then begin
        HWForm_RegisterALead_PostBack(WSr.WebPage,elementbefore,elementafter,thankselement);
      end else begin
        HWForm_RegisterALead(WSr.WebPage,elementbefore,elementafter,thankselement,0);
      end;

    otherwise
      ShowErrorPage("Invalid form");
  end;
  
  return;
end;