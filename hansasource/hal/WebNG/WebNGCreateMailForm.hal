//tools
external function string 255 FormatLink(string,string,boolean,boolean);
external procedure ShowInputWithLabel(string,string,string,string,string,boolean,boolean);
external procedure SessionAddToArray(string,string);
external function Boolean SessionGetNextFromArray(string,var string,var Integer);
external procedure SessionClearArray(string);
external procedure MyAccountShowErrors(string);
external procedure WebOutSubmit(string,string,string,string);
//translation
external function string 255 ToolWebNGTranslateText(integer);
//webng
external procedure RedirectToRequestedPage();

global
procedure ShowCreateMailForm(record WebNGElementVc WEr)
begin
  record NotepadVc Noter;
  string 255 tstr;
  Integer nr;
  
  if (blank(GetSessionString("createmail_complete"))) then begin
    MyAccountShowErrors("createmail_error");
    WebOutTagOpen("<form method=""post"" action=""" & FormatLink("/WebUpdatingAction.hal","?action=createmail_save",true,true) & """>");
    WebOutStringFormatNL("<input type=""hidden"" id=""conference"" name=""conference"" value=""" & WEr.HALFunc & """>");
    WebOutTagOpen("<ul>");
    WebOutTagOpen("<li>");
    ShowInputWithLabel(ToolWebNGTranslateText(25207),"text","subject","subject",WebGetArg("subject"),true,false);
    WebOutTagClose("</li>");
    WebOutTagOpen("<li>");
    WebOutStringFormatNL("<label for=""body"">" & ToolWebNGTranslateText(25208) & "<em>*</em></label>");
    WebOutTagOpen("<textarea name=""body"" class=""body"">"); // maxlength=255 cols=""30"" rows=""5""
    while (SessionGetNextFromArray("createmail_body",tstr,nr)) begin
      AddToText(tstr,Noter);
    end;
    SessionClearArray("createmail_body");
    WebOutText(Noter,false,"");
    WebOutTagClose("</textarea>");
    WebOutTagClose("</li>");
    WebOutTagOpen("<li>");
    WebOutSubmit("","button","Submit",ToolWebNGTranslateText(20067));
    WebOutTagClose("</li>");
    WebOutTagClose("</ul>");
    WebOutTagClose("</form>");
  end else begin
    WebOutStringFormatNL(ToolWebNGTranslateText(25428));
    PutSessionString("createmail_complete","");
  end;
  
  return;
end;

global
updating procedure StoreCreateMail()
begin
  Integer i,cnt,pos;
  string 255 conference,subject;
  record NotepadVc Noter;
  string 255 tstr;
  Boolean valid;
  record MailVc Mailr;
  row MailVc Mailrw;
  record CUVc CUr;
  
  // read from webargs
  conference = WebGetArg("conference");
  subject = WebGetArg("subject");
  WebGetText("body",Noter);
  
  // validate
  valid = true;
  if (blank(subject)) then begin
    SessionAddToArray("createmail_error",ToolWebNGTranslateText(25207) & " " & ToolWebNGTranslateText(25044));
    valid = false;
  end;
  
  // store
  if (valid) then begin
    CUr.Code = CurrentCust;
    if (nonblank(CUr.Code)) then begin
      ReadFirstMain(CUr,1,true);
      tstr = CUr.Name;
    end else begin
      tstr = ToolWebNGTranslateText(25336);
    end;
    RecordNew(Mailr);
    Mailrw.RowTyp = kMailRowTypeFrom;
    Mailrw.AddrCode = tstr;
    MatRowPut(Mailr,0,Mailrw);
    
    Mailrw.RowTyp = kMailRowTypeTo;
    Mailrw.AddrCode = conference;
    MatRowPut(Mailr,1,Mailrw);
    
    Mailr.Header = subject;
    Mailr.SendFlag = 1;
    
    i = 0;
    cnt = SizeTextCnt(Noter);
    while (i<cnt) begin
      tstr = StringFromText(Noter,i,255);
      i = i + 255;
      AddToText(tstr,Mailr);
    end;
    
    Mailr.SerNr = NextSerNr("MailVc",CurrentDate,-1,false,"");
    RecordStore(Mailr,false);
    
    PutSessionString("createmail_complete","true");
  end else begin
    PutSessionString("createmail_subject",subject);
    pos = 0;
    i = 0;
    cnt = SizeTextCnt(Noter);
    while (pos<cnt) begin
      tstr = StringFromText(Noter,i,255);
      if (len(tstr)!=0) then begin
        SessionAddToArray("createmail_body",tstr);
      end;
      pos = pos + 255;
    end;
  end;
  
  RedirectToRequestedPage;
  
  return;
end;
