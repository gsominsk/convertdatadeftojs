//tools
external function string 255 removenextnode(var string);
external function string 255 FormatSimpleLink(string,Boolean);
external function string 255 FormatSimpleLinkWithArg(string,string,Boolean);
external procedure ToolWebOutContentTextFormat(record WebNGContentVc,Boolean);
external function string 10 CurrentWebLang();
external function string 255 GetLinkToStruct(Longint,Boolean);
external function string 255 FormatLink(string,string,Boolean,Boolean);
external function Boolean LoginState();
external procedure RedirectToLoginPage();
external function string 255 GetNextValue(var string);
//pages
external procedure ShowWebAppPageStart(record WebNGPageVc,string);
external procedure ShowWebAppPageEnd(record WebNGPageVc);
//elements
external procedure ShowWebElementTemplate(string,Integer);
//translation
external function string 255 ToolWebNGTranslateText(Integer);
external function string 255 GetTranslatedNameLanguage(record WebNGLanguageVc);
//std
external function string 255 UrlEncodeCS(string);
external procedure ShowRedirectHtml(string,Boolean);
external function Boolean FileNameIsImage(string);
external procedure ShowSimpleWebElement(record WebNGElementVc);
external updating procedure StoreMailView(Longint,record WebNGForumRecVc);
external procedure AddToSortedArray(string,string,var array string,var array string,var Integer);
external procedure SimpleElementComboBox(string,string,string,string,array string,array string,Integer,string,Boolean,Boolean);
external function string 10 ToolWebNGGetCountry();
//std
external function string 255 ConvertXml(string);
external function string 255 UrlEncodeCS(string);
external function string 255 GetLoginPageStructLink();
external function Boolean FindStringInString(string,string);
external procedure ShowWebElement(string);
external updating procedure GenForumSupportSub(record ConfVc,Longint,var Longint,var Integer,var Integer);
external updating procedure SendCustomerLetterWithDetails(record RCVc,string,string,string);
external function Boolean GetNextCustNr(var string);

procedure AddMailToArray(var array record MailVc aMailr,record MailVc Mailr,Boolean sortingf,Longint sernr)
begin
  Integer pos,i;
  record MailVc tMailr;

  pos = aMailr.length;
  if (sortingf) then begin
    for (i=aMailr.length-1;i>=0;i=i-1) begin//don't want to mess the first mail in any case
      tMailr = aMailr[i];
      if (tMailr.TransDate<Mailr.TransDate) then begin
        pos = i + 1;
        i = -1;
      end else begin
        if (tMailr.TransDate==Mailr.TransDate) then begin
          if (tMailr.TransTime<Mailr.TransTime) then begin
            pos = i + 1;
            i = -1;
          end;
        end;
      end;
    end;
  end;
  
  for (i=aMailr.length-1;i>=pos;i=i-1) begin
    tMailr = aMailr[i];
    aMailr[i+1] = tMailr;
  end;
  
  Mailr.Version = sernr;
  aMailr[pos] = Mailr;

  return;
end;

global
procedure FillWithForumAnswers(var array record MailVc aMailr,record MailVc Mailr,Boolean sortingf,Longint sernr)
begin
  record MailVc tMailr;
  Integer i;
  record RLinkVc RLr;
  
  i = 1;
  while (ReadRecordLink(Mailr,i,tMailr,RLr)) begin
    AddMailToArray(aMailr,tMailr,sortingf,sernr);
    FillWithForumAnswers(aMailr,tMailr,sortingf,tMailr.SerNr);
    i = i + 1;
  end;

  return;
end;

global
function Boolean GetForumRecord(string code,var record WebNGForumRecVc WFr)
begin
  Boolean res;

  WFr.Code = code;
  WFr.Language = CurrentWebLang;
  res = ReadFirstMain(WFr,2,true);
  if (res==false) then begin
    WFr.Code = code;
    res = ReadFirstMain(WFr,1,true);
  end;
  
  GetForumRecord = res;
  return;
end;

global
function Longint GetConfSerNr(string conf)
begin
  Longint res;
  record ConfVc Confr;

  res = -1;
  Confr.AddrName = conf;
  if (ReadFirstKey("AddrName",Confr,1,true)) then begin
    res = Confr.SerNr;
  end;

  GetConfSerNr = res;
  return;
end;

function Longint GetViewCount(Longint sernr,record WebNGForumRecVc WFr)
begin
  Longint res;
  record MailReadVc MRr;
  Boolean TrHs;
  Longint conf;

  res = 0;
  conf = getConfSerNr(WFr.UserMailBox);
  ResetLoop(MRr);
  MRr.mailSerNr = sernr;
  MRr.mailBoxNr = conf;
  MRr.accode = 0;
  TrHs = true;
  while (LoopKey("userCode",MRr,3,TrHs)) begin
    if (MRr.accode!=0 or MRr.mailSerNr!=sernr or MRr.mailBoxNr!=conf) then begin
      TrHs = false;
    end else begin
      res = res + 1;
    end;
  end;

  GetViewCount = res;
  return;
end;

function Longint FindAnswerInAttach(record MailVc Mailr,Longint answer)
begin
  Longint res;
  record MailVc Mail2r;
  record RLinkVc RLr;
  Integer i;

  res = -1;
  i = 1;
  while (ReadRecordLink(Mailr,i,Mail2r,RLr)) begin
    if (Mail2r.SerNr == answer) then begin
      res = Mail2r.SerNr;
      goto LFindAnswerInAttach;
    end else begin
      res = FindAnswerInAttach(Mail2r,answer);
    end;
    i = i + 1;
  end;

LFindAnswerInAttach:;
  FindAnswerInAttach = res;
  return;
end;


function Longint GetQuestionMail2(record MailVc Mailr)
begin
  Longint res;
  record RLinkVc RLr;
  record MailVc nMailr;

  res = Mailr.SerNr;
  if (ReadRecordToLink(Mailr,1,nMailr,RLr)) then begin
    res = GetQuestionMail2(nMailr);
  end;

  GetQuestionMail2 = res;
  return;
end;

function Longint GetQuestionMail(Longint answer,Longint confser) 
begin
  Longint res;
  record MailVc Mailr,Mail2r;
  record ConfVc Confr,Conf2r;
  record RLinkVc RLr;
  Integer i;
  Boolean testf,TrHs;
  string 255 ckey;
  Longint tmp;

  res = answer;
  TrHs = true;
  Confr.SerNr = confser;
  if (ReadFirstMain(Confr,1,true)) then begin
    if (Confr.Class==kConfClassFolder) then begin
      ckey = "SubConfSortorder:" & Confr.SerNr;
      Conf2r.Sortorder = "";
      ResetLoop(Conf2r);
      while (LoopKey(ckey,Conf2r,1,TrHs)) begin
        tmp = GetQuestionMail(answer,Conf2r.SerNr);
        if (tmp>-1 and tmp!=answer) then begin
          res = tmp;
          goto LGetQuestionMail;
        end;
      end;
    end else begin
      ResetLoop(Mailr);
      Mailr.SerNr = -1;
      while (LoopKey("UserSer:" & Confr.SerNr,Mailr,1,TrHs)) begin
        i = 1;
        while (ReadRecordLink(Mailr,i,Mail2r,RLr)) begin
          if (Mail2r.SerNr==answer) then begin
            res = Mailr.SerNr;
            goto LGetQuestionMail;
          end else begin
            tmp = FindAnswerInAttach(Mail2r,answer);
            if (tmp>-1) then begin
              res = Mailr.SerNr;
              goto LGetQuestionMail;
            end;
          end;
          i = i + 1;
        end;
      end;
    end;
  end;
  
LGetQuestionMail:;        
  GetQuestionMail = res;
  return;
end;

procedure GetConfTextLang(record ConfVc Confr,var record MailVc Mail2r,string langcode)
begin
  record NotepadVc Noter;
  record RLinkVc RLr;
  Integer cnt;
  string 255 tstr;

  cnt = 1;
  RecordNew(Mail2r);
  while (ReadRecordLink(Confr,cnt,Noter,RLr)) begin
    cnt = cnt + 1;
    if (langcode==Noter.LangCode or blank(Noter.LangCode)) then begin // WebLanguage(1)
      tstr = StringFromText(Noter,0,255);
      if (nonblank(RLr.Comment)) then begin
        Mail2r.Header = RLr.Comment;
      end else begin
        Mail2r.Header = Confr.Comment;
      end;
      goto LWebGetConfTextLang;
    end;
  end;
  Mail2r.Header = Confr.Comment;
  
LWebGetConfTextLang:;
  AddToText(tstr,Mail2r);
  return;
end;

procedure OutputConfTextLang(record ConfVc Confr,string langcode)
begin
  record NotepadVc Noter;
  record RLinkVc RLr;
  Integer cnt;
  string 255 tstr;

  cnt = 1;
  while (ReadRecordLink(Confr,cnt,Noter,RLr)) begin
    cnt = cnt + 1;
    if (langcode==Noter.LangCode) then begin // WebLanguage(1)
      WebOutText(Noter,1,"");
      goto LWebGetConfTextLang;
    end;
  end;
LWebGetConfTextLang:;
  return;
end;

function Boolean CanCommentConf(record ConfVc Confr)
begin
  row ConfVc Confrw;
  Integer rwcnt,i;
  Boolean res;
  
  res = true;
  rwcnt = MatRowCnt(Confr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Confr,i,Confrw);
    if (Confrw.Group=="NOADD") then begin
      res = false;
    end;
  end;

  CanCommentConf = res;
  return;
end;

procedure GetMailTextLang(record MailVc Mailr,record MailVc Mail2r,string langcode,Boolean alllanguages)
begin
  record NotepadVc Noter;
  record RLinkVc RLr;
  Integer cnt;
  string 255 tstr;

  cnt = 1;
  RecordNew(Mail2r);
  while (ReadRecordLink(Mailr,cnt,Noter,RLr)) begin
    cnt = cnt + 1;
    if (langcode==Noter.LangCode) then begin // WebLanguage(1)
      tstr = StringFromText(Noter,0,255);
      Mail2r.Header = RLr.Comment;
      goto LGetMailTextLang;
    end;
  end;
  tstr = StringFromText(Mailr,0,255);
  if (Mailr.LangCode==langcode or blank(Mailr.LangCode) or alllanguages or blank(Mail2r.Header)) then begin
    Mail2r.Header = Mailr.Header;
  end;
LGetMailTextLang:;
  AddToText(tstr,Mail2r);
  return;
end;

global 
function Boolean MailIsAnAnswer(record MailVc Mailr,string conf) 
begin
  record ConfVc Confrepr;
  record ConfVc Confr;
  row MailVc Mailrw;
  Integer rwcnt,i;
  Boolean res;
  
  res = false;
  Confrepr.AddrName = conf;
  if (ReadFirstKey("AddrName",Confrepr,1,true)) then begin
    rwcnt = MatRowCnt(Mailr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Mailr,i,Mailrw);
      if (Mailrw.AddrCode == Confrepr.AddrName) then begin
        res = true;
      end;
    end;
  end;

  MailIsAnAnswer = res;
  return;
end;

global
function Boolean GetForumCreatorCU(string tuuid,var record CUVc CUr)
begin
  Boolean res;

  if (InString(tuuid,"@")>0) then begin
    CUr.eMail = tuuid;
    if (ReadFirstKey("eMail",CUr,1,true)) begin
      res = true;
    end;
  end else begin
    CUr.UUID = StringToUUID(tuuid);
    if (ReadFirstKey("UUID",CUr,1,true)) begin
      res = true;
    end;
  end;

  GetForumCreatorCU = res;
  return;
end;

function string 255 GetCurrentUserID()
begin
  record CUVc CUr;
  string 255 res;
  
  CUr.Code = CurrentCust;
  if (ReadFirstMain(CUr,1,true)) begin
    res = CUr.UUID;
  end;

  GetCurrentUserID = res;
  return;
end;

global
function string 100 GetForumCreator(string tuuid)
begin
  string 100 res,tstr2;
  record CUVc CUr,tCUr;
  record ContactRelVc ContactRelr;
  record MyAccountBlock MAbl;

  BlockLoad(MAbl);
  res = "";//ToolWebNGTranslateText(31037);
  //TODO::Implement nicknames
  if (InString(tuuid,"@")>0) then begin
    CUr.eMail = tuuid;
    if (ReadFirstKey("eMail",CUr,1,true)) begin
      res = CUr.Name;
    end;
  end else begin
    CUr.UUID = StringToUUID(tuuid);
    if (ReadFirstKey("UUID",CUr,1,true)) begin
      res = CUr.Name;
    end;
  end;

  GetForumCreator = res;
  return;
end;

global
procedure DisplayCUPic(record CUVc CUr,string src)
begin
  record Attach2Vc Attachr;
  record RLinkVc RLr;
  Integer i;
  string 255 res;

  res = src;
  i = 1;
  while (ReadRecordLink(CUr,i,Attachr,RLr)) begin
    if (FileNameIsImage(Attachr.FileName)) then begin
      res = "/WebOutImg.hal?sernr=" & Attachr.SerNr;
      goto LDisplayCUPic;
    end;
  end;
LDisplayCUPic:;
  if (nonblank(res)) then begin
    WebOutStringFormatNL("<div class='prof_pic_wrap'><img src=""" & res & """></div>"); 
  end;
  return;
end;

procedure DisplayProfile(record CUVc CUr)
begin

  WebOutStringFormatNL("<div class=""prof_pic"">");
  DisplayCUPic(CUr,"");//MAbl.
  WebOutStringFormatNL("</div>");

  return;
end;

procedure FindAnswerMail(array record MailVc aMailr,var record MailVc Mailr)
begin
  record MailVc tMailr;
  Integer i;
  transaction string 255 gforum_correcttag;
  
  for (i=0;i<aMailr.length;i=i+1) begin
    tMailr = aMailr[i];
    if (SetInSet(gforum_correcttag,tMailr.Tags)) then begin
      Mailr = aMailr[i];
      i = aMailr.length;
    end;
  end;

  return;
end;

function string 255 ConvertForumDate(Date td,Time t)
begin
  string 255 res;
  
  if (nonblankdate(td)) then begin
    res = Left(t,5) & " " & GetDay(td) & " " & ToolWebNGTranslateText(31000 + GetMonth(td)) & " " & GetYear(td);
  end else begin
    res = "-";
  end;
  ConvertForumDate = res;
  return;
end;

procedure DisplayForumFullAuthor(record MailVc Mailr)
begin
  row MailVc Mailrw;
  string 100 author;

  MatRowGet(Mailr,0,Mailrw);
  author = GetForumCreator(Mailrw.AddrCode);
  WebOutString("<div class=""forum_list_date"">" & ToolWebNGTranslateText(31031) & "<span class='fr_list_auth'>" & author & "</span>" & ConvertForumDate(Mailr.TransDate,Mailr.TransTime) & "</div>");

  return;
end;

procedure ShowSmallMail(record MailVc Mailr,Integer type)
begin
  string 255 cls,addstr;
  
  switch (type) begin
    case 0: cls = "forum_rec_answer"; addstr = ToolWebNGTranslateText(31032);
    case 1: cls = "forum_rec_useful"; addstr = ToolWebNGTranslateText(31033);
  end;
  
  WebOutString("<div class='" & cls & "'>");
  WebOutString("<div class='add_mail_small'>");
  WebOutString("<span class='text_start'>" & addstr & "</span>");
  if (type==1) then begin
    WebOutString("<a href='#post" & Mailr.SerNr & "'>");
  end;
  DisplayForumFullAuthor(Mailr);
  if (type==1) then begin
    WebOutString("</a>");
  end;
  WebOutString("</div>");
  if (type==0) then begin
    WebOutString("<div class='add_mail_small_text'>");
    WebOutText(Mailr,1,"");
    WebOutString("</div>");
  end else begin
    
  end;
  WebOutString("</div>");

  return;
end;

procedure ShowUsefulAnswers(array record MailVc aMailr)
begin
  transaction string 255 gforum_usefultag;
  record MailVc Mailr;
  Integer i;
  
  for (i=0;i<aMailr.length;i=i+1) begin
    Mailr = aMailr[i];
    if (SetInSet(gforum_usefultag,Mailr.Tags)) then begin
      ShowSmallMail(Mailr,1);
    end;
  end;
  
  return;
end;

procedure ShowQuotedMessage(record MailVc Mailr)
begin
  string 255 tstr;
  
  tstr = StringFromText(Mailr,0,255);
  WebOutString("<div class='forum_quote'>");
  WebOutString("<div class='forum_quote_auth'>");
  DisplayForumFullAuthor(Mailr);
  WebOutString("</div>");
  WebOutString("<div class='forum_quote_text'>" & tstr & "</div>");
  WebOutString("</div>");

  return;
end;

procedure ShowBookmarkStatus(Longint sernr,Integer type)
begin
  record WebNGForumFavoritesVc FFr;
  string 255 cls;

  if (nonblank(CurrentCust)) then begin
    FFr.SerNr = sernr;
    FFr.RecType = type;
    FFr.CustCode = CurrentCust;
    if (ReadFirstMain(FFr,3,true)) then begin
      cls = "sub_done";
    end else begin
      cls = "sub_empty";
    end;
    WebOutString("<div class='bookmark_icon " & cls & "'><a href='/WebUpdatingAction.hal?action=forum.bookmark&t=" & sernr & "&type=" & type & "'></a></div>");
  end;
  return;
end;

procedure DisplayForumFullMail(record WebNGStructVc WSr,record MailVc Mailr,string command,string node,string langcode,Boolean reportf,record WebNGForumRecVc WFr, var Boolean evenf,Boolean withmainf,Boolean answerf,Boolean custquestf,Boolean isansweredf,Boolean employeef,array record MailVc aMailr,record ConfVc Confr)
begin
  record MailVc Mail2r,tMailr,mainMailr,confMailr;
  string 255 llink;
  row MailVc Mailrw;
  Integer cnt2,acnt;
  string 20 eventxt;
  string 255 origauthor;
  record CUVc CUr;
  string 255 extracls,link;
  transaction string 255 gforum_answeredtag,gforum_correcttag,gforum_usefultag;
  Boolean correctf,usefulf;
  record WebNGForumTopicVc FTr;
  
  if (evenf) then begin
    evenf = false;
    eventxt = " forumEven";
  end else begin
    evenf = true;
    if (WSr.Name !="faq") then begin
      eventxt = "forumOdd";
    end;
  end;
  
  if (SetInSet(gforum_correcttag,Mailr.Tags)) then begin
    extracls = " forum_correct"; 
    correctf = true;
  end;
  if (SetInSet(gforum_usefultag,Mailr.Tags)) then begin
    extracls = extracls & " forum_useful";    
    usefulf = true;
  end;
  
  MatRowGet(Mailr,0,Mailrw);
  origauthor = GetForumCreator(Mailrw.AddrCode);
  GetMailTextLang(Mailr,Mail2r,langcode,true);
  Mail2r.TransDate = Mailr.TransDate;
  link = GetLinkToStruct(WSr.SerNr,true) & "forumbrowse/" & Confr.SerNr;
  if (!answerf) then begin
    GetConfTextLang(Confr,confMailr,CurrentWebLang);
    WebOutString("<div class='forum_main_title'><a href='" & link & "'>" & confMailr.Header & "</a></div>");
  end;
  WebOutString("<div class=""forum_item");
  if (!answerf) then begin
    WebOutString(" main_rec");
  end;
  WebOutString(extracls);
  WebOutString(""">")
  WebOutString("<div class=""f_inner_wrap " & eventxt & """>");
  if (correctf) then begin
    WebOutString("<div class='f_rec_status_intro si_correct'>" & ToolWebNGTranslateText(31032) & "</div>");
  end;
  if (usefulf) then begin
    WebOutString("<div class='f_rec_status_intro si_useful'>" & ToolWebNGTranslateText(31033) & "</div>");
  end;
  
  if (!answerf) then begin
    WebOutString("<div class='forum_question_tophead'><div class='forum_question_title'>" & Mailr.Header & "</div>");
    ShowBookmarkStatus(Mailr.SerNr,0);
    WebOutString("</div>");
  end;
  WebOutString("<div class='profile_section' id='post" & Mailr.SerNr & "'>");
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class='profile_right'>");
    GetForumCreatorCU(Mailrw.AddrCode,CUr);
    DisplayProfile(CUr);
    WebOutString("</div>");
  end;
  WebOutString("</div>");
  WebOutString("<div class='forum_rec_body'>");
  WebOutString("<div class='profile_left'>");
  if (!answerf) then begin
    //WebOutString("<div class='forum_main_subject'>" & Mailr.Header & "</div>");
  end;
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class='forum_auth_name'>" & origauthor & "</div>");
    WebOutString("<div class=""forum_date"">" & Mailr.TransDate & "</div>");
  end;
  WebOutString("</div>");
  WebOutString("<div class='forum_full_text'>");
  if (answerf) then begin
    mainMailr = aMailr[0];
    if (Mailr.Version!=mainMailr.SerNr) then begin
      tMailr.SerNr = Mailr.Version;
      if (ReadFirstMain(tMailr,1,true)) then begin
        ShowQuotedMessage(tMailr);
      end;
    end;
  end;
  WebOutText(Mailr,1,"");
  if (!answerf) then begin
    if (SetInSet(gforum_answeredtag,Mailr.Tags)) then begin
      FindAnswerMail(aMailr,tMailr);
      if (tMailr.SerNr>-1) then begin
        ShowSmallMail(tMailr,0);
      end;
    end;
    ShowUsefulAnswers(aMailr);
  end;
  WebOutString("</div>");
  
  
  WebOutString("</div>");
  WebOutTagClose("</div>");
  if ((answerf and LoginState) or employeef) then begin
    WebOutString("<div class='f_rec_actions'>");
    if (!isansweredf and (custquestf or employeef) and answerf) then begin
      WebOutString("<div class='mark_as_correct'><a href='/WebUpdatingAction.hal?action=forum.addanswertag&t=" & Mailr.SerNr & "&tag=correct&wsnr=" & WSr.SerNr & "&path=" & WebGetArg("path") & "'>" & ToolWebNGTranslateText(25536) & "</a></div>");
    end else begin
      if (correctf and employeef and answerf) then begin
        WebOutString("<div class='unmark_as_correct'><a href='/WebUpdatingAction.hal?action=forum.removeanswertag&t=" & Mailr.SerNr & "&wsnr=" & WSr.SerNr & "&path=" & WebGetArg("path") & "'>Mark as not correct</a></div>");
      end;
    end;
    if (employeef) then begin
      WebOutString("<div class='remove_mail'><a href='/WebUpdatingAction.hal?action=forum.removemail&t=" & Mailr.SerNr & "&wsnr=" & WSr.SerNr & "&path=" & WebGetArg("path") & "'>Remove Mail</a></div>");
      FTr.SerNr = Mailr.SerNr;
      ReadFirstMain(FTr,1,true);
      if (FTr.Hidden==1) then begin
        WebOutString("<div class='make_public_mail'><a href='/WebUpdatingAction.hal?action=forum.makepublic&t=" & Mailr.SerNr & "&path=" & WebGetArg("path") & "'>Make Public</a></div>");
      end;
    end;


    if ((custquestf or employeef) and SetInSet(gforum_usefultag,Mailr.Tags)==false and SetInSet(gforum_correcttag,Mailr.Tags)==false and answerf) then begin
      WebOutString("<div class='mark_as_useful'><a href='/WebUpdatingAction.hal?action=forum.addanswertag&t=" & Mailr.SerNr & "&tag=useful&wsnr=" & WSr.SerNr & "&path=" & WebGetArg("path") & "'>" & ToolWebNGTranslateText(25537) & "</a></div>");
    end;
  
    WebOutString("<div class='forum_action_pane'>");
    WebOutString("<div class=""forum_action_but reply""><a href=""");
    WebOutString(FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "replymessage/" & node,true));
    WebOutString(""">");
    WebOutString(ToolWebNGTranslateText(25501));
    WebOutString("</a></div></div>");

    WebOutString("</div>");
  end else begin
    //Show some other info
  end;
  WebOutTagClose("</div>");

  return;
end;

function Boolean IsConfInConf(record ConfVc Confr,record ConfVc InConfr,Longint toplevel)
begin
  Boolean res;
  record ConfVc ConfMother;
  
  res = false;
  if (Confr.SerNr==InConfr.SerNr) then begin
    res = true;
  end else begin
    if (Confr.SerNr!=toplevel) then begin
      ConfMother.SerNr = Confr.MotherConf;
      if (ReadFirstMain(ConfMother,1,true)) then begin
        res = IsConfInConf(ConfMother,InConfr,toplevel);
      end;
    end;
  end;
  IsConfInConf = res;
  return;
end;

function Boolean CheckMailValidity(record WebNGForumRecVc WFr,record MailVc Mailr)
begin
  record ConfVc ConfRootr;
  record ConfVc ConfReplyr;
  record ConfVc Confr;
  row MailVc Mailrw;
  Integer rwcnt,i;
  Boolean res;
  
  res = false;
  ConfRootr.AddrName = WFr.ForumRootConf;
  if (ReadFirstKey("AddrName",ConfRootr,1,true)) then begin
    ConfReplyr.AddrName = WFr.ForumReplyConf;
    if (ReadFirstKey("AddrName",ConfReplyr,1,true)) then begin
      rwcnt = MatRowCnt(Mailr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(Mailr,i,Mailrw);
        if (Mailrw.AddrStatus==0) then begin
          Confr.AddrName = Mailrw.AddrCode;
          if (ReadFirstKey("AddrName",Confr,1,true)) then begin
            if (IsConfInConf(Confr,ConfReplyr,ConfRootr.SerNr)) then begin
              res = true;
              i = rwcnt;
            end;
            if (IsConfInConf(Confr,ConfRootr,ConfRootr.SerNr)) then begin
              res = true;
              i = rwcnt;
            end;
          end;
        end;
      end;
    end;
  end;
  CheckMailValidity = res;
  return;
end;

function string 255 ShowForumInfo(Integer tint,Date td,Integer type)
begin
  string 255 res;

  switch (type) begin
    case 0: 
      res = tint;
      if (tint<=0) then begin
        res = "-";
      end;
    case 1:
      res = DateToString(td,"YYYY-MM-DD");
      if (blankdate(td)) then begin
        res = "-";
      end;
  end;
  
  ShowForumInfo = res;
  return;
end;

procedure GetMailHeaderAndIntro(record MailVc Mailr,string langcode,record WebNGForumRecVc WFr,var string title,var string intro)
begin
  record MailVc Mail2r;
  
  GetMailTextLang(Mailr,Mail2r,langcode,false);
  if (blank(Mail2r.Header) and Mailr.LangCode==langcode) then begin
    Mail2r.Header = Mailr.Header;
  end;
  title = left(Mail2r.Header,WFr.HeaderTxtSize);
  if (WFr.HeaderTxtSize < len(Mail2r.Header)) then begin
    title = title & "..";
  end;
  if (WFr.ExtraTxtSize>0 and WFr.ListBodyFlag==1) then begin
    intro = StringFromText(Mailr,0,WFr.ExtraTxtSize);
    intro = intro & "...";
  end;
  
  return;
end;

procedure DisplayForumLink(record WebNGStructVc WSr,record WebNGForumRecVc WFr,record Mailvc Mailr,string langcode,Integer replies,record ConfVc Confr,Boolean alllanguages,string extraarg,record WebNGForumRecVc WFr,string link,array record MailVc aMailr)
begin
  record MailVc lastMailr;
  row MailVc Mailrw;
  string 255 llink,title,intro;
  string 20 replstr;
  string 255 tstr,tstr2;
  Longint views;
  record MyAccountBlock MAbl;
  record CUVc CUr;
  string 60 author;
  transaction string 255 gforum_answeredtag;

  BlockLoad(MAbl);
  if (replies>0) then begin
    replstr = replies;
  end else begin
    replstr = "-";
  end;
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class=""forum_link_line" & extraarg & """>");
  end else begin
    WebOutString("<div class=""forum_link_line" & extraarg & " faq_line"">");  
  end;
  GetMailHeaderAndIntro(Mailr,langcode,WFr,title,intro);
  if (aMailr.length>0) then begin
    lastMailr = aMailr[aMailr.length-1];
  end;
  if (WFr.AllowReply==1) then begin
    WebOutString("<a class=""link"" href='" & link & "'>");
  end;
  WebOutString("<div class='f_link_stat'>");
  if (SetInSet(gforum_answeredtag,Mailr.Tags)) then begin
    WebOutString("<div class='f_link_stat_done'></div>");
  end;
  WebOutString("</div>");
  WebOutString("<div class='forum_link_col short_text'><div class='forum_list_head'>" & title & "</div>");
  if (WFr.AllowReply==1) then begin
    WebOutString("</a>");
  end;
  if (WFr.AllowReply==1) then begin
    if (WFr.ExtraTxtSize>0 and WFr.ListBodyFlag==1) then begin
    
      WebOutString("<div class='forum_list_intro'>" & intro & "</div>");
    end;
    DisplayForumFullAuthor(Mailr);
  end else begin
    WebOutString("<div class='faq_extended'>");
    WebOutText(Mailr,0,"");
    WebOutString("</div>");
  end;
  WebOutString("</div>");
  if (WFr.AllowReply==1) then begin
    MatRowGet(lastMailr,0,Mailrw);
    WebOutString("<div class='forum_link_col views'>" & ShowForumInfo(GetViewCount(Mailr.SerNr,WFr),"",0) & "</div>");
    WebOutString("<div class='forum_link_col replies'>" & ShowForumInfo(aMailr.length,"",0) & "</div>");
    WebOutString("<div class='forum_link_col lastreply'>" & ConvertForumDate(lastMailr.TransDate,lastMailr.TransTime) & "<br>" & GetForumCreator(Mailrw.AddrCode) & "</div>");
  end;
  WebOutString("<div class='clear'></div>");
  WebOutTagClose("</div>");
LDisplayForumLink:;
  return;
end;

procedure DisplayForumError()
begin
  Integer err;

  err = StringToInt(GetSessionString("forum_msg_err"));
  if (err>0) then begin
    if (BitAnd(err,2)==2) then begin
      WebOutString("<div class='error_msg'>" & ToolWebNGTranslateText(31035) & "</div>");
    end;
  end;
  PutSessionString("forum_msg_err",0);
  
  return;
end;

procedure WebOutConfOption(record ConfVc Confr,longint csernr,boolean allf)
begin
  record ConfVc Conf2r;
  
  if Confr.Class==kConfClassFolder then begin
    Conf2r.Sortorder = "";
    while (LoopKey("SubConfSortorder:" & Confr.SerNr,Conf2r,1,true)) begin
      WebOutConfOption(Conf2r,csernr,allf);
    end;
  end else begin
    if CanCommentConf(Confr) then begin
      if Confr.SerNr == csernr then begin
        WebOutStringFormatNL("<option value=""" & Confr.SerNr & """ selected>" & Confr.Comment & "</option>");
      end else begin
        WebOutStringFormatNL("<option value=""" & Confr.SerNr & """>" & Confr.Comment & "</option>");
      end;
    end;
  end;

return;
end;

procedure DisplayForumNewMess(record WebNGStructVc WSr,string confname,string path,string node,string langcode,Boolean addtolink,Boolean addfromlink,Boolean newmessagef,string str,Boolean showrepf,Boolean fullrepf)
begin
  record CUVc CUr;
  record MailVc Mailr;
  array record MailVc aMailr;
  row MailVc Mailrw; //cust+
  string 200 tstr,name,link; //cust+
  string 200 hdr;
  string 200 command;
  string 10 confnode;
  string 100 mpath;
  string 100 fullnode;
  string 100 mail;
  Longint rootmail;
  record WebNGForumRecVc WFr;
  Boolean testf;
  record ConfVc Confr,tConfr;
  Boolean evenf,res;

  GetForumRecord(WSr.HALFunc,WFr);
  mpath = path;
  confnode = removenextnode(mpath);
  confnode = removenextnode(mpath);
  mail = removenextnode(mpath);
  
  if (newmessagef) then begin
    fullnode = confnode;
  end else begin
    testf = true;
    rootmail = GetQuestionMail(stringtoLongint(mail),GetConfSerNr(WFr.ForumRootConf));
    fullnode = confnode & "/" & rootmail;
  end;
  
  DisplayForumError;

  if (WebLoginStatus==3) then begin
    if (newmessagef) then begin
      command = "forumbrowse";
    end else begin
      command = "viewmessage";
    end;
    CUr.Code = CurrentCust;
    if (ReadFirstMain(CUr,1,true)) then begin
    end;
    Confr.SerNr = GetConfSerNr(WFr.ForumRootConf);
    if (ReadFirstMain(Confr,1,true)) then begin
    end;
    if (newmessagef==false) then begin
      hdr = "(no subject)";
      Mailr.SerNr = StringToLongint(mail);
      if (ReadFirstMain(Mailr,1,true)) then begin
        if (showrepf) then begin
          DisplayForumFullMail(WSr,Mailr,"","",CurrentWebLang,WFr.ReportFlag,WFr,evenf,true,false,false,false,false,aMailr,tConfr);
        end;
        if (nonblank(Mailr.Header)) then begin
          hdr = "Re: " & Mailr.Header;
        end;
      end;
    end;
    if (newmessagef) then begin
      WebOutTagOpen("<form method=""POST"" class=""addnewmess"" action=""" & FormatLink("/WebAction.hal","action=sendmessage",true,true) & """>");
      WebOutString("<div class='reply_wrap'>");
      WebOutTagOpen("<div class=""new_mess"">");
      WebOutStringFormatNL("<input type=""hidden"" id=""path"" id='forum_newmsg_form' name=""path"" value=""" & FormatLink(GetLinkToStruct(WSr.SerNr,true) & "forumbrowse/" & confnode,"",true,true) & """>");
      WebOutStringFormatNL("<input type=""hidden"" name=""forum_key"" value=""" & fullnode & """>");
    end else begin
      WebOutTagOpen("<form method=""POST"" class=""addawnser"" action=""" & FormatLink("/WebAction.hal","action=sendmessage",true,true) & """>");
      WebOutString("<div class='reply_wrap'>");
      WebOutString("<div class='reply_text'>" & ToolWebNGTranslateText(31041) & "</div>");
      WebOutStringFormatNL("<input type=""hidden"" id=""path"" name=""path"" value=""" & FormatLink(GetLinkToStruct(WSr.SerNr,true) & "viewmessage/" & confnode & "/" & rootmail,"",true,true) & """>");
    end;
    WebOutStringFormatNL("<input type=""hidden"" name=""oldpath"" value=""" & WebGetArg("path") & """>");
    WebOutStringFormatNL("<input type=""hidden"" id=""forum"" name=""forum"" value=""" & WFr.Code & """>");
    WebOutStringFormatNL("<input type=""hidden"" id=""repmail"" name=""repmail"" value=""" & Mailr.SerNr & """>");
    WebOutStringFormatNL("<input type=""hidden"" id="""" name=""rootmail"" value=""" & rootmail & """>");
    link = GetLinkToStruct(WSr.SerNr,true);
    if (newmessagef) then begin

      tConfr.SerNr = GetConfSerNr(WFr.ForumRootConf);
      if readfirstmain(tConfr,1,true) then begin
        WebOutTagOpen("<div class='new_mess_line'>");
        WebOutStringFormatNL("<select name=""theme"" class=""new_select"">");
        WebOutConfOption(tConfr,StringToLongInt(node),false);
        WebOutStringFormatNL("</select>");
        WebOutTagClose("</div>");
      end;

      WebOutTagOpen("<div class='new_mess_line subject_line'>");
      WebOutTagOpen("<div class='subj_label'>");
      if (Mailr.SerNr > -1) then begin
        if (MailIsAnAnswer(Mailr,WFr.ForumReplyConf)) then begin
          WebOutStringFormatNL(ToolWebNGTranslateText(25505));
        end else begin
          WebOutStringFormatNL(str);
        end;
        WebOutTagClose("</div>");
        WebOutTagOpen("<div class='subj_text'>");
        WebOutTagOpen("<input type=""hidden"" id=""subject_field"" name=""subject_field"" size=""40"" value=""" & hdr & """>");
        WebOutTagClose("</div>");
        WebOutTagClose("</div>");
      end else begin
        WebOutTagOpen(ToolWebNGTranslateText(22241));
        WebOutTagClose("</div>");
        WebOutTagOpen("<div class='subj_text'>");
        WebOutTagOpen("<input type=""text"" id=""subject_field"" name=""subject_field"" size=""40"" value=""" & hdr & """>");
        WebOutTagClose("</div>");
        WebOutTagClose("</div>");
      end;
      WebOutTagOpen("<div class='new_mess_line'>");
      WebOutTagOpen("<div colspan=""2"" valign=""top"" align=""left"">");
      WebOutTagOpen("<textarea class=""answerarea"" rows=""20"" name=""mail_body"" cols=""60"">");
      WebOutTagClose("</textarea>");
      WebOutTagClose("</div>");
      WebOutTagClose("</div>");
      WebOutString("</div>");
      WebOutString("<div class='new_mess_line'>");
      WebOutString("<input class=""regbutton"" type=""submit"" value=""" & ToolWebNGTranslateText(20053) & """ name=""b1"">");
      //WebOutString("<input class=""regbutton"" type=""button"" value=""" & ToolWebNGTranslateText(25527) & """ onclick=""location.href='" & link & "';""/>");
      WebOutTagClose("</div>");
      WebOutTagClose("</div>");
    end else begin
      WebOutTagOpen("<input type=""hidden"" id=""subject_field"" name=""subject_field"" size=""40"" value=""" & hdr & """>");
      WebOutTagOpen("<textarea class=""answerarea"" rows=""20"" name=""mail_body"" cols=""60"">");
      WebOutTagClose("</textarea>");
      WebOutString("</div>");
      WebOutString("<input class=""regbutton"" type=""submit"" value=""" & ToolWebNGTranslateText(20053) & """ name=""b1"">");
      if (fullrepf) then begin
        WebOutString("<input class=""regbutton"" type=""button"" value=""" & ToolWebNGTranslateText(25502) & """ onclick=""location.href='" & link & "';""/>");
      end;
    end;
    WebOutTagClose("</form>");
  end else begin
    WebOutStringFormatNL("<p class=""error"">" & ToolWebNGTranslateText(22236) & "</p>"); //You must be logged in to be able to write messages
  end;
  return;
end;

procedure DisplayWebInstructions(record ConfVc Confr,Integer epos)
begin
  record WebNGElementVc WebNGElementr;
  record RLinkVc RLr;
  Integer cnt;
  
  cnt = 1;
  while (ReadRecordLink(Confr,cnt,WebNGElementr,RLr)) begin
    ShowWebElementTemplate(WebNGElementr.Code,epos);
    cnt = cnt + 1;
  end;  
  return;
end;

procedure PrintForumBreadcrumb(record WebNGStructVc WSr,record ConfVc Confr,string rootconf)
begin
  record ConfVc ConfMother,ConfRootr;
  record MailVc Mailr;
  array string 255 confnames;
  array Longint confsernr;
  string 255 invalidconfname;
  Integer i,arrlen;
  
  invalidconfname = "??";
  ConfRootr.AddrName = rootconf;
  if (ReadFirstKey("AddrName",ConfRootr,1,true)) then begin
    GetConfTextLang(Confr,Mailr,CurrentWebLang);
    confnames[0] = Mailr.Header;
    confsernr[0] = Confr.SerNr;
    arrlen = 1;
    
    if (Confr.SerNr!=ConfRootr.SerNr) then begin
      ConfMother.SerNr = Confr.MotherConf;
      while (ConfMother.SerNr!=-1) begin
        if (ReadFirstMain(ConfMother,1,true)) then begin
          GetConfTextLang(ConfMother,Mailr,CurrentWebLang);
          confnames[arrlen] = Mailr.Header;
          confsernr[arrlen] = ConfMother.SerNr;
          arrlen = arrlen + 1;
          if (ConfMother.SerNr!=ConfRootr.SerNr) then begin
            ConfMother.SerNr = ConfMother.MotherConf;
          end else begin
            ConfMother.SerNr = -1;
          end;
        end else begin
          ConfMother.SerNr = -1;
        end;
      end;
    end;
      
    WebOutTagOpen("<div id=""forumbreadcrumb"">");
    for (i=arrlen-1;i>=0;i=i-1) begin
      WebOutString("<a href=""");
      WebOutString(FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "forumbrowse" & "/" & confsernr[i] & "/",true));
      WebOutString(""">");
      WebOutStringFormat(confnames[i]);
      WebOutStringFormatNL("</a>");
      if (i!=0) then begin
        WebOutStringFormat("<span class='arrow'></span>");
      end;
    end;
    WebOutTagClose("</div>");
  end;
  return;
end;

procedure DisplayForumSearchForm(record ConfVc Confr,Longint maxhits,Boolean alllanguages,record WebNGStructVc WSr)
begin

  WebOutTagOpen("<form id=""forumsearchform"" name=""forumsearchform"" method=""GET"" action=""" & FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "search/" & Confr.SerNr,WebSecureMode)  & """>");
  
  if (alllanguages) then begin
    WebOutStringFormat("<input type=""hidden"" name=""showall"" value=""true""> ");
  end;
  WebOutStringFormat("<input type=""text"" placeholder='" & ToolWebNGTranslateText(25420) & "' name=""search"" id=""forumsearchform_search"" value="""">");
  WebOutStringFormat(" ");
  WebOutStringFormat("<input type=""hidden"" name=""maxhits"" id=""forumsearchform_maxhits"" size=4 value=""" & maxhits & """>");
  WebOutStringFormat(" ");
  WebOutStringFormat("<input type=""submit"" id=""forumsearchform_submit"" value=""" & ToolWebNGTranslateText(20143) & """>");
  WebOutTagClose("</form>");

  return;
end;

global
procedure ShowForumSearchWidget(record WebNGElementVc WEr)
begin
  record WebNGForumRecVc WFr;
  record WebNGStructVc WSr;
  record ConfVc Confr;

  GetForumRecord(WEr.HALFunc,WFr);
  Confr.AddrName = WFr.ForumRootConf;
  if (ReadFirstKey("AddrName",Confr,1,true)) then begin
    WSr.SerNr = WEr.LinkToStruct;
    DisplayForumSearchForm(Confr,99,true,WSr);
  end;

  return;
end;

procedure ShowNewMessageLink(Longint linkto,record ConfVc Confr)
begin
  string 255 confnr,path;
  Integer pos;

  confnr = Confr.SerNr;
  path = WebGetArg("path");
  pos = InString(path,"forumbrowse/");
  if (pos>0) then begin
    confnr = right(path,len(path)-pos-11);
  end;

  WebOutString("<div class=""add_link""><a id=""newmessagelink"" href=""");
  WebOutString(FormatSimpleLink(GetLinkToStruct(linkto,true) & "newmessage/" & confnr & "/",true));
  WebOutString(""">");
  WebOutString(ToolWebNGTranslateText(25500)); //New Message
  WebOutString("</a></div>");  
    
  return;
end;

procedure ShowExtraNav(Longint linkto)
begin

  WebOutString("<div class=""right_for_nav"">");
  WebOutString("<a id=""open_qt"" href='" & (FormatSimpleLink(GetLinkToStruct(linkto,true) & "openquestions",true)) & "'>" & ToolWebNGTranslateText(31038) & "</a>");
  WebOutString("<a id=""my_qt"" href='" & (FormatSimpleLink(GetLinkToStruct(linkto,true) & "myquestions",true)) & "'>" & ToolWebNGTranslateText(31039) & "</a>");
  
  WebOutString("</div>");  
    
  return;
end;

procedure DisplayForumConfHeader(record ConfVc Confr,Longint maxhits,Boolean alllanguages,record WebNGStructVc WSr,Integer showheadf,record WebNGForumRecVc WFr)
begin
  record MailVc tMailr;
  string 255 link;

//0 - show only cat name
//1 - show search and headers 
//2 - show search and headers

  link = GetLinkToStruct(WSr.SerNr,true);// & "forumbrowse" & "/" & Confr.SerNr //& "forumbrowse/" & Confr.SerNr
  GetConfTextLang(Confr,tMailr,CurrentWebLang);
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class='forum_main_title'><a href='" & link & "'>" & tMailr.Header & "</a></div>");
  end;
  WebOutString("<div class='forum_list_wrap'>");
  WebOutString("<div class='fr_list_col_head headtype" & showheadf & "'>");
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class='forum_link_col short_text'>&nbsp;</div>");
    WebOutString("<div class='forum_link_col views'>" & ToolWebNGTranslateText(22238) & "</div>");
    WebOutString("<div class='forum_link_col replies'>" & ToolWebNGTranslateText(22233) & "</div>");
    WebOutString("<div class='forum_link_col lastreply'>" & ToolWebNGTranslateText(22237) & "</div>");
  end else begin
    WebOutString("<div class='forum_link_col short_text'>" & tMailr.Header & "</div>");
  end;
  WebOutString("</div>");

  return;
end;

procedure DisplayForumMainHeader(record ConfVc Confr,Longint maxhits,Boolean alllanguages,record WebNGStructVc WSr,Integer showheadf,record WebNGForumRecVc WFr)
begin
  record MailVc tMailr;
  string 255 link;

  GetConfTextLang(Confr,tMailr,CurrentWebLang);
  link = GetLinkToStruct(WSr.SerNr,true);// & "forumbrowse" & "/" & Confr.SerNr
  WebOutString("<div class='forum_main_title'><a href='" & link & "'>" & tMailr.Header & "</a></div>");
  if (WFr.AllowReply==1) then begin
    WebOutString("<div class='forum_list_wrap'>");
    WebOutString("<div class='fr_list_col_head headtype" & showheadf & "'>");
    WebOutString("<div class='forum_link_col short_text'>&nbsp;</div>");
    WebOutString("<div class='forum_link_col topics'>" & ToolWebNGTranslateText(22242) & "</div>");
    WebOutString("<div class='forum_link_col replies'>" & ToolWebNGTranslateText(22233) & "</div>");
    WebOutString("<div class='forum_link_col lastreply'>" & ToolWebNGTranslateText(22237) & "</div>");
    ShowBookmarkStatus(Confr.SerNr,1);
    WebOutString("<div class='clear'></div>");
    WebOutString("</div>");
  end;
  return;
end;

procedure DisplayForumCatDetails(record ConfVc Confr,record WebNGStructVc WSr)
begin
  record WebNGForumCatVc FCr;
  string 255 link;
  record MailVc Mailr,tMailr;
  row MailVc Mailrw;

  FCr.SerNr = Confr.SerNr;
  if (ReadFirstMain(FCr,1,true)) then begin
    Mailr.SerNr = FCr.LastReplyMail;
    ReadFirstMain(Mailr,1,true);
    MatRowGet(Mailr,0,Mailrw);
    GetConfTextLang(Confr,tMailr,CurrentWebLang);
    link = FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "forumbrowse" & "/" & Confr.SerNr,WebSecureMode);
    WebOutString("<div class='forum_link_col short_text'><div class='short_title'><a href='" & link & "'>" & tMailr.Header & "</a></div><div class='short_intro'><a href='" & link & "'></div>");
    WebOutText(tMailr,0,"");
    WebOutString("</a></div>");
    WebOutString("<div class='forum_link_col topics'>" & ShowForumInfo(FCr.TopicCount,"",0) & "</div>");
    WebOutString("<div class='forum_link_col replies'>" & ShowForumInfo(FCr.PostCount,"",0) & "</div>");
    WebOutString("<div class='forum_link_col lastreply'>" & ConvertForumDate(Mailr.TransDate,Mailr.TransTime) & "<br>" & GetForumCreator(Mailrw.AddrCode) & "</div>");
    WebOutString("<div class='clear'></div>");

  end;

  return;
end;

procedure DisplayForumSearchResultFromMail(record WebNGStructVc WSr,record ConfVc Confr,record MailVc Mailr,var Longint cnt,Boolean alllanguages)
begin
  record MailVc Mail2r,rMailr,lastMailr,tMailr;
  row MailVc Mailrw;
  Longint rootmail;
  array Longint amails;
  string 255 tstr;
  Boolean testf,res;
  record WebNGForumRecVc WFr;
  Longint views;
  string 100 replstr;
  Integer replies;
  Longint lastmail;
  string 255 tstr2;
  record MyAccountBlock MAbl;
  record CUVc CUr;
  array record MailVc aMailr;
  string 20 cls;
  
  BlockLoad(MAbl);
  
  GetForumRecord(WSr.HALFunc,WFr);
  testf = true;
  rootmail = GetQuestionMail2(Mailr);
  rMailr.SerNr = rootmail;
  ReadFirstMain(rMailr,1,true);

  GetMailTextLang(Mailr,Mail2r,CurrentWebLang,alllanguages);
  if (blank(Mail2r.Header) and Mailr.LangCode==CurrentWebLang) then begin
    Mail2r.Header = Mailr.Header;
  end;
  if (blank(Mail2r.Header) and alllanguages) then begin
    Mail2r.Header = Mailr.Header;
  end;
  if (nonblank(Mail2r.Header)) then begin
    tMailr.SerNr = rootmail;
    if (ReadFirstMain(tMailr,1,true)) then begin
      FillWithForumAnswers(aMailr,tMailr,true,rootmail);
      cls = "";
      if (mod(cnt,2)==0) then begin
        cls = " evenline";
      end;
      DisplayForumLink(WSr,WFr,tMailr,CurrentWebLang,aMailr.length-1,Confr,alllanguages,cls,WFr,
      FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "viewmessage/" & Confr.SerNr & "/" & rootmail,true) & "#post" & rootmail,aMailr);
      cnt = cnt + 1;
    end;
  end;
  
  return;
end;

procedure DisplayForumSearchResultFromAttachedMail(record WebNGStructVc WSr,record ConfVc Confr,record MailVc Mailr,var Longint cnt,Boolean alllanguages,string searchstr)
begin
  Integer i;
  record RLinkVc RLr;
  record MailVc Mail2r;

  i = 1;
  while (ReadRecordLink(Mailr,i,Mail2r,RLr)) begin
    i = i + 1;
    AddToText(Mail2r.Header,Mail2r);
    if (searchstr=="*" or StringInText(searchstr,Mail2r)) then begin
      DisplayForumSearchResultFromMail(WSr,Confr,Mail2r,cnt,alllanguages);
    end;
    DisplayForumSearchResultFromAttachedMail(WSr,Confr,Mail2r,cnt,alllanguages,searchstr);
  end;

  return;
end;

procedure DisplayForumSearchResultsList(record WebNGStructVc WSr,record ConfVc Confr,string searchstr,Longint maxhits,var Longint cnt,Boolean alllanguages,Integer page)
begin
  record MailVc Mailr,Mail2r;
  record RLinkVc RLr;
  string 200 ckey;
  Boolean foundf,testf;
  Longint maxmail;
  Integer rlcnt;


//TODO-KSZ::Implement pagination for search results
  ckey = "UserSortorder:" & Confr.SerNr;
  Mailr.Sortorder = "";
  foundf = true;
  while (LoopKey(ckey,Mailr,1,foundf)) begin
    testf = foundf;
    if (cnt>=maxhits) then begin
      testf = false;
      foundf = false;
    end;
    if (testf) then begin
      AddToText(Mailr.Header,Mailr);
      if (searchstr=="*" or StringInText(searchstr,Mailr)) then begin
        DisplayForumSearchResultFromMail(WSr,Confr,Mailr,cnt,alllanguages);
      end else begin
        DisplayForumSearchResultFromAttachedMail(WSr,Confr,Mailr,cnt,alllanguages,searchstr);
      end;
    end;
    if (cnt>maxhits) then begin
      foundf = false;
    end;
  end;
  return;
end;

procedure DisplayForumSearchResultsScan(record WebNGStructVc WSr,record ConfVc Confr,string searchstr,Longint maxhits,var Longint cnt,Boolean alllanguages,Integer page)
begin
  record ConfVc Conf2r;
  string 200 ckey;
  Boolean foundf;
  
  if (Confr.Class==kConfClassFolder) then begin
    foundf = true;
    if (cnt>maxhits) then begin
      foundf = false;
    end;
    ckey = "SubConfSortorder:" & Confr.SerNr;
    Conf2r.Sortorder = "";
    while (LoopKey(ckey,Conf2r,1,foundf)) begin
      if (foundf) then begin
        DisplayForumSearchResultsScan(WSr,Conf2r,searchstr,maxhits,cnt,alllanguages,page);
      end;
    end;
  end else begin
    DisplayForumSearchResultsList(WSr,Confr,searchstr,maxhits,cnt,alllanguages,page);
  end;
  
  return;
end;

procedure DisplayForumSearch(record WebNGStructVc WSr,record ConfVc Confr,string rootconf,string searchstr,Longint maxhits,Boolean alllanguages,Integer page)
begin
  Longint m_maxhits,cnt;
  record MailVc Mailr;
  record WebNGForumRecVc WFr;
  
  if (maxhits<0) then begin
    m_maxhits = 99;
  end else begin
    m_maxhits = maxhits;
  end;
  
  GetForumRecord(WSr.HALFunc,WFr);
  Confr.Comment = ToolWebNGTranslateText(31030);
  DisplayForumConfHeader(Confr,maxhits,alllanguages,WSr,1,WFr);

  DisplayForumSearchResultsScan(WSr,Confr,searchstr,maxhits,cnt,alllanguages,page);
  if (cnt!=0) then begin
  end else begin
    WebOutStringFormat("<p id=""forumsearchresults_noresults"">" & ToolWebNGTranslateText(25041) & "</p>"); //No results
  end;
  WebOutTagClose("</div>");
  
  return;
end;

global webpublic
procedure WebSmallSearch()
begin
  string 255 searchstr;

  searchstr = WebGetArg("search");
  if (len(searchstr) < 3) then begin
    ShowRedirectHTML(FormatSimpleLink(WebGetArg("path"),true),false);
  end else begin
    ShowRedirectHTML(FormatSimpleLink(GetLinkToStruct(WebGetArg("struct"),true) & "search/" & WebGetArg("conf"),true) & "?search=" & searchstr & "&maxhits=" & WebGetArg("maxhits"),false);
  end;
  return;
end;

global
function Boolean ConfIsValidChildOfRoot(record ConfVc Confr,string rootconf)
begin
  record ConfVc ConfRootr;
  Boolean res;
  
  res = false;
  ConfRootr.AddrName = rootconf;
  if (ReadFirstKey("AddrName",ConfRootr,1,true)) then begin
    res = IsConfInConf(Confr,ConfRootr,ConfRootr.SerNr);
  end;
  ConfIsValidChildOfRoot = res;
  return;
end;

function Boolean MailboxInMail(record MailVc Mailr,string mailbox)
begin
  row MailVc Mailrw;
  Boolean res;
  
  MatRowGet(Mailr,0,Mailrw);
  res = (Mailrw.AddrCode==mailbox);

  MailboxInMail = res;
  return;
end;

procedure DisplayPagination(Longint size,Longint perpage,Longint page,string base,string type,Longint sernr)
begin
  Integer i,pagecnt;
  string 255 basestr;
  
  switch (type) begin
    case "forumbrowse": 
      basestr = base & type & "/" & sernr;
    otherwise
      basestr = base & type;
  end;
  pagecnt = size/perpage+1;
  if (pagecnt>1) then begin
    WebOutString("<div class='pagination'>");
    for (i=0;i<pagecnt;i=i+1) begin
      if ((i+1)==page or (i==0 and page<1)) then begin
        WebOutString("<a class='pageitem active_page' href='" & basestr & "/" & i+1 & "'>" & i+1 & "</a>");
      end else begin
        WebOutString("<a class='pageitem' href='" & basestr & "/" & i+1 & "'>" & i+1 & "</a>");
      end;
    end;
    WebOutString("</div>");
  end;
  return;
end;

function Boolean MailCanBeViewed(record MailVc Mailr,record CUVc Empr)
begin
  Boolean res;
  record WebNGForumTopicVc FTr;
  row MailVc Mailrw;
  record CUVc CUr;

  res = true;
  FTr.SerNr = Mailr.SerNr;
  if (ReadFirstMain(FTr,1,true)) then begin
    if (FTr.Hidden==1) then begin
      MatRowGet(Mailr,0,Mailrw);
      GetForumCreatorCU(Mailrw.AddrCode,CUr);
      if (Empr.EmployeeType!=1 and CurrentCust!=Cur.Code) then begin
        res = false;
      end;
    end;
  end;

  MailCanBeViewed = res;
  return;
end;

procedure DisplayForumBrowse(record WebNGStructVc WSr,record ConfVc Confr,string rootconf,record WebNGForumRecVc WFr,Boolean alllanguages,Boolean fulllist,record WebNGForumRecVc WFr,string filter,Boolean forcesinglepagef,Boolean mailboxf,Integer page,string type)
begin
  record ConfVc Conf2r;
  record MailVc Mailr,tMailr;
  record MailVc Mail2r;
  record RLinkVc RLr;
  Integer cnt,cnt2,cnt3;
  string 60 ckey;
  Boolean foundf,testf;
  string 60 confcode,confname,custuuid;
  array Longint amails,tmpmails;
  Boolean addf,even,movef;
  string 20 eventxt;
  Integer pos,keylen;
  string 20 sort;
  string 255 conflink;
  record WebNGElementVc WEr;
  array record MailVc aMailr,aMail2r;
  string 20 cls;
  record WebNGForumTopicVc FTr;
  Integer perpage;
  Longint npos,lpos,i,showcnt;
  record CUVc CUr;
  row MailVc Mailrw;
  Boolean isemployeef,openf,myqf;
  
  CUr.Code = CurrentCust;
  if (ReadFirstMain(CUr,1,true)) then begin
    isemployeef = Cur.EmployeeType==1;
    custuuid = CUr.UUID;
  end;

  perpage = 25;
  addf = true;
  if (CanCommentConf(Confr)==false) then begin
    addf = false;
  end;
  if (ConfIsValidChildOfRoot(Confr,rootconf)) then begin
    if (fulllist) then begin
      DisplayWebInstructions(Confr,0);
      WEr.Code = WFr.BannerElement;
      if (readfirstmain(WEr,1,true)) then begin
        ShowSimpleWebElement(WEr);
      end;
    end;
    if (Confr.Class==kConfClassFolder and blank(filter)) then begin//if list of conferences
      DisplayForumMainHeader(Confr,99,alllanguages,WSr,2,WFr);
      foundf = true;
      ckey = "SubConfSortorder:" & Confr.SerNr;
      Conf2r.Sortorder = "";
      while (LoopKey(ckey,Conf2r,1,foundf)) begin
        testf = foundf;
        if (testf) then begin
          WebOutStringFormatNL("<div class=""category isfaq" & (WFr.AllowReply==0) & """>");
          if (WFr.AllowReply==1) then begin
            DisplayForumCatDetails(Conf2r,WSr);
          end else begin
            DisplayForumBrowse(WSr,Conf2r,rootconf,WFr,alllanguages,false,WFr,filter,forcesinglepagef,mailboxf,0,type);
          end;
          WebOutStringFormatNL("</div>");
        end;
      end;
    end else begin
      switch (type) begin
        case "openquestions": openf = true;
        case "myquestions": myqf = true;
      end;
      GetConfTextLang(Confr,Mailr,CurrentWebLang);
      DisplayForumConfHeader(Confr,99,alllanguages,WSr,1,WFr);
      foundf = true;
      keylen = 1;
      if (myqf) then begin
        ckey = "MotherConf:" & Confr.SerNr;
      end else begin
        ckey = "LastReplyDate:" & Confr.SerNr;
      end;
      if (openf) then begin
        ckey = "AnsweredFlag:" & Confr.SerNr;
        keylen = 2;
        FTr.AnsweredFlag = 0;
      end;
      FTr.LastReplyDate = AddDay(CurrentDate,1);
      even = false;
      if (page==0) then begin
        npos = 0;
      end else begin
        npos = (page-1)*perpage;
      end;
      while (LoopBackKey(ckey,FTr,keylen,foundf)) begin
          if (keylen==2 and FTr.AnsweredFlag!=0) then begin
            foundf = false;
          end else begin
            testf = true;
            Mailr.SerNr = FTr.SerNr;
            ReadFirstMain(Mailr,1,true);
            if (MailCanBeViewed(Mailr,CUr)==false) then begin
              testf = false;
            end;
            if (myqf) then begin
              testf = false;
              for (i=0;i<MatRowCnt(Mailr);i=i+1) begin
                MatRowGet(Mailr,i,Mailrw);
                if (Mailrw.AddrCode==custuuid) then begin
                  testf = true;
                  i = MatRowCnt(Mailr);
                end;
              end;
            end;
            if (testf) then begin
              aMailr[aMailr.length] = Mailr;
            end;
          end;
      end; 
      i = npos;
      showcnt = 0;
      while (i<aMailr.length) begin//showcnt<25 and 
        Mailr = aMailr[i];
        ClearArray(aMail2r);
        testf = true;
        FillWithForumAnswers(aMail2r,Mailr,true,Mailr.SerNr);
        if (openf and isemployeef and aMail2r.length>0) then begin
          tMailr = aMail2r[aMail2r.length-1];
          MatRowGet(tMailr,0,Mailrw);
          CUr.UUID = StringToUUID(Mailrw.AddrCode);
          if (ReadFirstKey("UUID",CUr,1,true)) then begin
            if (CUr.EmployeeType==1) then begin
              testf = false;
            end;
          end;
        end;
        if (testf and (showcnt<25 or WFr.AllowReply==0)) then begin
          cls = "";
          if (mod(i,2)==0) then begin
            cls = " evenline";
          end;

          DisplayForumLink(WSr,WFr,Mailr,CurrentWebLang,aMailr.length-1,Confr,alllanguages,cls,WFr,
                           FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "viewmessage/" & Confr.SerNr & "/" & Mailr.SerNr,true),aMail2r);
          showcnt = showcnt + 1;
        end;
        i = i + 1;
      end;
      if (WFr.AllowReply==1) then begin
        DisplayPagination(i,perpage,page,GetLinkToStruct(WSr.SerNr,true),type,Confr.SerNr);
      end;
    end;
    WebOutString("</div>");
  end else begin
    WebOutString(ToolWebNGTranslateText(25042)); //Invalid specification
  end;

  return;
end;

function Boolean CurrentCustomerQuestion(record MailVc Mailr,var Boolean isansweredf,Boolean isemployeef)
begin
  row MailVc Mailrw;
  record CUVc CUr;
  Boolean res;
  transaction string 255 gforum_answeredtag;
  
  res = false;
  isansweredf = false;
  MatRowGet(Mailr,0,Mailrw);
  if (GetForumCreatorCU(Mailrw.AddrCode,CUr)) then begin
    if (CUr.Code==CurrentCust) then begin
      res = true;
    end;
  end;
  isansweredf = false;
  if (SetInSet(gforum_answeredtag,Mailr.Tags)) then begin
    isansweredf = true;
  end;

  CurrentCustomerQuestion = res;
  return;
end;

procedure DisplayForumMessage(record WebNGStructVc WSr,record WebNGForumRecVc WFr,string node,record ConfVc Confr,Boolean withmainf)
begin
  record MailVc Mailr,tMailr;
  row MailVc Mailrw;
  Integer acnt,i;
  string 100 mail;
  record WebNGElementVc WEr;
  Boolean evenf;
  array record MailVc aMailr;
  Boolean custquestf,isansweredf;
  record CUVc Empr;

  Mailr.SerNr = StringToLongint(node);
  if (ReadFirstMain(Mailr,1,true)) then begin   
    Empr.Code = CurrentCust;
    ReadFirstMain(Empr,1,true);
    if (CheckMailValidity(WFr,Mailr)) then begin
      if (MailCanBeViewed(Mailr,Empr)) then begin
        queued.StoreMailView(Mailr.SerNr,WFr);
        if (withmainf) then begin
          WEr.Code = WFr.BannerElement;
          if (readfirstmain(WEr,1,true)) then begin
            ShowSimpleWebElement(WEr);
          end;
          DisplayWebInstructions(Confr,0);
        end;
        aMailr[aMailr.length] = Mailr;
        if (WFr.AllowReply) then begin
          FillWithForumAnswers(aMailr,Mailr,true,Mailr.SerNr);
        end;    
        if (aMailr.length>0) then begin
          custquestf = CurrentCustomerQuestion(Mailr,isansweredf,(Empr.EmployeeType==1));
          for (i=0;i<aMailr.length;i=i+1) begin
            tMailr = aMailr[i];
            if (CanCommentConf(Confr)) then begin
              DisplayForumFullMail(WSr,tMailr,"viewmessage",Confr.SerNr & "/" & tMailr.SerNr,CurrentWebLang,WFr.ReportFlag,WFr,evenf,withmainf,i!=0,custquestf,isansweredf,(Empr.EmployeeType==1),aMailr,Confr);
            end else begin
              DisplayForumFullMail(WSr,tMailr,"","",CurrentWebLang,WFr.ReportFlag,WFr,evenf,withmainf,i!=0,custquestf,isansweredf,false,aMailr,Confr);
            end;
          end;
          if (WFr.AllowReply) then begin
            if (LoginState) then begin
              DisplayForumNewMess(WSr,WFr.ForumReplyConf,"replymessage/" & Confr.SerNr & "/" & Mailr.SerNr,Confr.SerNr & "/" & Mailr.SerNr,CurrentWebLang,false,true,false,ToolWebNGTranslateText(22226),false,false); //Replies can take a while to appear in the list of messages
            end else begin
              WebOutString("<div class='forum_action_pane'>");
              WebOutString("<div class=""forum_action_but reply""><a href=""");
              WebOutString(FormatSimpleLink(GetLoginPageStructLink,true));
              WebOutString(""">");
              WebOutString(ToolWebNGTranslateText(31034));
              WebOutString("</a></div></div>");            
            end;
          end;
          if (withmainf) then begin
            WebOutStringFormatNL("<a class=""link nav-back"" href=""" & FormatSimpleLink(GetLinkToStruct(WSr.SerNr,true) & "forumbrowse/" & Confr.SerNr,true) & """>" & ToolWebNGTranslateText(22230) & "</a>");
          end;
        end;
        if (withmainf) then begin
          DisplayWebInstructions(Confr,2);
        end;
      end;
    end;
  end;
  return;
end;

global
updating procedure StoreMailView(Longint sernr,record WebNGForumRecVc WFr)
begin
  record MailReadVc MRr,tMRr;
  
  if (nonblank(CurrentCust)) then begin
    RecordNew(MRr);
    MRr.mailSerNr = sernr;
    MRr.accode = 0;
    MRr.fDate = CurrentDate;
    MRr.fTime = CurrentTime;
    MRr.mailBoxNr = getConfSerNr(WFr.UserMailBox);
    RecordCopy(tMRr,MRr);
    if (ReadFirstMain(tMRr,0,true)==false) then begin
      if (RecordStore(MRr,false)) then begin
      end;
    end;
  end;
  
return;
end;

global
procedure ShowForumBreadCrumbs(record WebNGElementVc WEr)
begin  
  record WebNGForumRecVc WFr;
  record ConfVc Confr,tConfr;
  string 255 node,node2,mpath,theconf,path;
  record WebNGStructVc WSr;
  Longint sernr;
  
  GetForumRecord(WEr.HALFunc,WFr);
  WSr.SerNr = WEr.LinkToStruct;
  ReadFirstMain(WSr,1,true);
  path = WebGetArg("path");
  node = removenextnode(path);
  node = removenextnode(path);
  while (nonblank(node)) begin
    sernr = StringToLongint(node);
    if (sernr>-1) then begin
      tConfr.SerNr = sernr;
      if (ReadFirstMain(tConfr,1,true)) then begin
        RecordCopy(Confr,tConfr);
      end;
    end;
    node = removenextnode(path);
  end;
    
  PrintForumBreadcrumb(WSr,Confr,WFr.ForumRootConf);
  
  return;
end;

global
function Boolean ShowForumNewMessageElement(record WebNGElementVc WEr)
begin
  record WebNGStructVc WSr;
  record ConfVc Confr;
  string 255 node;
  
  node = GetSessionString("activeforum");
  Confr.SerNr = StringToLongint(node);
  if (ReadFirstMain(Confr,1,true)) then begin 
    WSr.Name = "forum";
    WSr.Mother = -1;
    if (ReadFirstKey("Mother",WSr,2,true)) then begin end;
    DisplayForumNewMess(WSr,Confr.AddrName,"",node,CurrentWebLang,false,false,true,ToolWebNGTranslateText(22228),false,true); //If you write your message in English, more people can reply to it
  end;
  return;
end;

procedure SetForumSiteTitle(string node,string mpath)
begin
  record MailVc Mailr,Mail2r;
  string 255 tmp,sernr;
  transaction string 255 gExtraSiteTitle;

  if (node=="viewmessage") then begin
    tmp = removenextnode(mpath);
    sernr = removenextnode(mpath);
    if (blank(sernr)) then begin
      sernr = tmp;
    end;
    Mailr.SerNr = StringToLongint(sernr);
    if (ReadFirstMain(Mailr,1,true)) then begin
      GetMailTextLang(Mailr,Mail2r,CurrentWebLang,false);
      gExtraSiteTitle = " - " & Mail2r.Header;
    end;
  end;

  return;
end;

procedure ShowWidgetItem(Longint origsernr,record MailVc origMailr,record WebNGForumRecVc WFr,record WebNGForumTopicVc FTr,Longint linkto)
begin
  row MailVc Mailrw;
  string 255 author,title,intro,link,tstr;
  record MailVc Mailr;
  Longint pos;

  GetMailTextLang(origMailr,Mailr,WFr.Language,false);
  MatRowGet(origMailr,0,Mailrw);

  GetMailHeaderAndIntro(Mailr,WFr.Language,WFr,title,intro);
  WebOutString("<div class='forum_wg_item'>");
  author = GetForumCreator(Mailrw.AddrCode);
  GetNextSubString(FTr.RootConf,pos,",",tstr);
  link = FormatSimpleLink(GetLinkToStruct(linkto,true) & "viewmessage/" & tstr & "/" & origsernr,true);
  WebOutString("<div class='forum_wg_title'><a href='" & link & "'>" & title & "</a></div><div class='forum_wg_author'>" & author & "</div><div class='forum_wg_intro'>" & intro & "</div><div class='forum_wg_date'>" & ConvertForumDate(origMailr.TransDate,origMailr.TransTime) & "</div>");

  WebOutString("</div>");
  
  return;
end;

procedure ShowLatestPostWidget(record WebNGForumRecVc WFr,record ConfVc Confr,Longint linkto)
begin
  record WebNGForumTopicVc FTr;
  Boolean TrHs,testf;
  Integer cnt;
  record MailVc Mailr;

  WebOutString("<div class='forum_widget_frame'>");
  WebOutString("<div class='forum_widget_title'>" & ToolWebNGTranslateText(31042) & "</div>");
  FTr.LastReplyDate = AddDay(CurrentDate,1);
  TrHs = true;
  while (LoopBackKey("MotherConf:" & Confr.SerNr,FTr,1,TrHs)) begin
    if (cnt>1) then begin
      TrHs = false;
    end else begin
      Mailr.SerNr = FTr.SerNr;
      if (FTr.LastReplyMail>0) then begin
        Mailr.SerNr = FTr.LastReplyMail;
      end;
      if (ReadFirstMain(Mailr,1,true)) then begin
        ShowWidgetItem(FTr.SerNr,Mailr,WFr,FTr,linkto);
        cnt = cnt + 1;
      end;
    end;
  end;
  WebOutString("</div>");

  return;
end;

global
procedure ShowForumButtonWidget(record WebNGElementVc WEr)
begin
  record WebNGForumRecVc WFr;
  record ConfVc Confr;
  Boolean res;

  res = GetForumRecord(WEr.HALFunc,WFr);
  Confr.AddrName = WFr.ForumRootConf;
  if (ReadFirstKey("AddrName",Confr,1,true)) then begin
    ShowNewMessageLink(WEr.LinkToStruct,Confr);
    ShowExtraNav(WEr.LinkToStruct);
  end;
  
  return;
end;

global
procedure ShowForumPostWidget(record WebNGElementVc WEr)
begin
  record WebNGForumRecVc WFr;
  record ConfVc Confr;
  Boolean res;

  res = GetForumRecord(WEr.HALFunc,WFr);
  Confr.AddrName = WFr.ForumRootConf;
  if (ReadFirstKey("AddrName",Confr,1,true)) then begin
    ShowLatestPostWidget(WFr,Confr,WEr.LinkToStruct);
  end;
  
  return;
end;

global
procedure ForumApp(record WebNGStructVc WSr,string path)
begin
  record WebNGPageVc WPr;
  record WebNGForumRecVc WFr;
  record ConfVc Confr;
  string 255 node,node2,mpath,theconf;
  Boolean foundf,res;
  string 255 showall;
  record WebNGForumTagsBlock FTb;  
  transaction string 255 gforum_newtag,gforum_answeredtag,gforum_correcttag,gforum_usefultag;
  Integer page;
  
  res = GetForumRecord(WSr.HALFunc,WFr);
  if (res) then begin
    BlockLoad(FTb);
    gforum_newtag = FTb.NewTag;
    gforum_answeredtag = FTb.AnsweredTag;
    gforum_correcttag = FTb.CorrectTag;
    gforum_usefultag = FTb.UsefulTag;
    mpath = WSr.HALFunc;
    while (nonblank(mpath)) begin
      node = GetNextValue(mpath);
      switch (node) begin
        case "readonly": WSr.HALFunc = "readonly";
        case "showall" : showall = "true";
      end;
    end;
    WPr.Code = WSr.WebPage;
    if (nonblank(WPr.Code)) then begin
      if (ReadFirstMain(WPr,1,true)) then begin end;
    end;
    mpath = path;
    node = removenextnode(mpath);
    if (blank(node)) then begin
      node = "forumbrowse";
    end;

    if (node=="newmessage" or node=="reportmessage" or node=="replymessage" or node=="myquestions") then begin
      if (WSr.HALFunc=="readonly") then begin
        node = "forumbrowse";
        mpath = GetSessionString("activeforum");
      end else begin
        if (LoginState==false) then begin
          RedirectToLoginPage;
          goto LForumApp;
        end;
      end;
    end;
    if (blank(showall)) then begin
      showall = WebGetArg("showall");
    end;
    SetForumSiteTitle(node,mpath);
    ShowWebAppPageStart(WPr,"");
    WebOutString("<div class='forum_main_body'>");
    switch (node) begin
      case "forumbrowse":
        node = removenextnode(mpath);
        if (blank(node)) then begin
          Confr.AddrName = WFr.ForumRootConf;
          foundf = ReadFirstKey("AddrName",Confr,1,true);
        end else begin
          Confr.SerNr = node;
          foundf = ReadFirstMain(Confr,1,true);
        end;
        page = StringToInt(removenextnode(mpath));

        if (foundf) then begin
          DisplayForumBrowse(WSr,Confr,WFr.ForumRootConf,WFr,showall=="true",true,WFr,"",false,false,page,"forumbrowse");
        end;
        PutSessionString("activeforum","" & Confr.SerNr);
      case "viewmessage":
        node = removenextnode(mpath);
        node2 = removenextnode(mpath);
        if (blank(node2)) then begin
          node2 = node;
          node = GetSessionString("activeforum");
        end;
        Confr.SerNr = StringToLongint(node);
        if (ReadFirstMain(Confr,1,true)) then begin
          DisplayForumMessage(WSr,WFr,node2,Confr,true);
        end;
      case "newmessage":
        node = removenextnode(mpath);
        if (blank(node)) then begin
          node = GetSessionString("activeforum");
        end;
        Confr.SerNr = StringToLongint(node);
        if (ReadFirstMain(Confr,1,true)) then begin 
          DisplayForumNewMess(WSr,Confr.AddrName,path,node,CurrentWebLang,false,false,true,ToolWebNGTranslateText(22228),false,true);
        end;
      case "reportmessage":
        node = removenextnode(mpath);
        DisplayForumNewMess(WSr,WFr.ForumReportConf,path,mpath,CurrentWebLang,true,false,false,ToolWebNGTranslateText(22227),true,true);
      case "replymessage":
        node = removenextnode(mpath);
       DisplayForumNewMess(WSr,WFr.ForumReplyConf,path,mpath,CurrentWebLang,false,true,false,ToolWebNGTranslateText(22226),true,true);
      case "search":
        node = removenextnode(mpath);
        Confr.SerNr = StringToLongint(node);
        if (ReadFirstMain(Confr,1,true)) then begin
        page = StringToLongint(removenextnode(mpath));
          DisplayForumSearch(WSr,Confr,WFr.ForumRootConf,WebGetArg("search"),StringToLongint(WebGetArg("maxhits")),WebGetArg("showall")=="true",page);
        end;
      case "myquestions":
        Confr.SerNr = GetConfSerNr(WFr.ForumRootConf);
        ReadFirstMain(Confr,1,true);
        page = StringToInt(removenextnode(mpath));
        //DisplayForumMyQuestions(WSr,Confr,WFr.ForumRootConf);
        DisplayForumBrowse(WSr,Confr,WFr.ForumRootConf,WFr,true,true,WFr,GetCurrentUserID,true,true,page,"myquestions");
      case "openquestions":
        Confr.SerNr = GetConfSerNr(WFr.ForumRootConf);
        ReadFirstMain(Confr,1,true);
        WFr.ListCnt = 20;
        page = StringToInt(removenextnode(mpath));
        DisplayForumBrowse(WSr,Confr,WFr.ForumRootConf,WFr,true,true,WFr,gforum_newtag,true,false,page,"openquestions");
    end;
    WebOutString("</div>");
    ShowWebAppPageEnd(WPr);
  end;
LForumApp:;
  return;
end;

updating procedure ChangeMailTags(Longint mailnr,string tag,Longint wsnr)
begin
  record MailVc Mailr,oldMailr;
  record WebNGForumTagsBlock FTb; 
  record WebNGStructVc WSr;
  record WebNGForumRecVc WFr;
  Longint rootmail;
  record WebNGForumTopicVc FTr,oldFTr;
  
  BlockLoad(FTb);
  //todo::do extra check against customer and mail
  Mailr.SerNr = mailnr;
  if (ReadFirstMain(Mailr,1,true)) then begin
    RecordCopy(oldMailr,Mailr);
    switch (tag) begin
      case "useful":
        Mailr.Tags = FTb.UsefulTag;
        RecordUpdate(oldMailr,Mailr,true);
      case "correct":
        Mailr.Tags = FTb.CorrectTag;
        if (RecordUpdate(oldMailr,Mailr,true)==0) then begin
          WSr.SerNr = wsnr;
          if (ReadFirstMain(WSr,1,true)) then begin
           rootmail = GetQuestionMail2(Mailr);
            if (rootmail!=Mailr.SerNr) then begin
              Mailr.SerNr = rootmail;
              if (ReadFirstMain(Mailr,1,true)) then begin
                RecordCopy(oldMailr,Mailr);
                Mailr.Tags = FTb.AnsweredTag;
                RecordUpdate(oldMailr,Mailr,true);
                FTr.SerNr = Mailr.SerNr;
                if (ReadFirstMain(FTr,1,true)) then begin
                  RecordCopy(oldFTr,FTr);
                  FTr.AnsweredFlag = 1;
                  RecordUpdate(oldFTr,FTr,true);
                end;
              end;
            end;
          end;
        end;
    end;
  end;
  ShowRedirectHtml(WebGetArg("path"),WebSecureMode);
    
  return;
end;

updating procedure RemoveMailTags(Longint mailnr,string tag,Longint wsnr)
begin
  record MailVc Mailr,oldMailr;
  record WebNGForumTagsBlock FTb; 
  record WebNGStructVc WSr;
  record WebNGForumRecVc WFr;
  Longint rootmail;
  record WebNGForumTopicVc FTr,oldFTr;
  
  BlockLoad(FTb);
  //todo::do extra check against customer and mail
  Mailr.SerNr = mailnr;
  if (ReadFirstMain(Mailr,1,true)) then begin
    RecordCopy(oldMailr,Mailr);
    Mailr.Tags = "";
    if (RecordUpdate(oldMailr,Mailr,true)==0) then begin
      WSr.SerNr = wsnr;
      if (ReadFirstMain(WSr,1,true)) then begin
        GetForumRecord(WSr.HALFunc,WFr);
        rootmail = GetQuestionMail2(Mailr);
        if (rootmail!=Mailr.SerNr) then begin
          Mailr.SerNr = rootmail;
          if (ReadFirstMain(Mailr,1,true)) then begin
            RecordCopy(oldMailr,Mailr);
            Mailr.Tags = FTb.NewTag;
            RecordUpdate(oldMailr,Mailr,true);
            FTr.SerNr = Mailr.SerNr;
            if (ReadFirstMain(FTr,1,true)) then begin
              RecordCopy(oldFTr,FTr);
              FTr.AnsweredFlag = 0;
              RecordUpdate(oldFTr,FTr,true);
            end;
          end;
        end;
      end;
    end;
  end;
  ShowRedirectHtml(WebGetArg("path"),WebSecureMode);
    
  return;
end;

global
updating procedure RefreshForumSupport(Longint confsernr,Longint rootconf)
begin
  record WebNGForumCatVc FCr;
  Integer topiccnt,postcnt;
  Longint lastmailnumber;
  record ConfVc Confr;

  Confr.SerNr = rootconf;//this is actually the subconf
  if (ReadFirstMain(Confr,1,true)) then begin
    GenForumSupportSub(Confr,confsernr,lastmailnumber,topiccnt,postcnt);
    RecordNew(FCr);
    FCr.SerNr = Confr.SerNr;
    FCr.TopicCount = topiccnt;
    FCr.PostCount = postcnt;
    FCr.LastReplyMail = lastmailnumber;
    if (RecordInsert(FCr,true)) then begin end;
  end;

  return;
end;


function Boolean GetMainForumCat(record WebNGForumRecVc WFr,record MailVc Mailr,var record WebNGForumTopicVc FTr)
begin
  Boolean res;
  Longint sernr;

  sernr = GetQuestionMail2(Mailr);
  FTr.SerNr = sernr;
  if (ReadFirstMain(FTr,1,true)) then begin
    res = true;
  end;

  GetMainForumCat = res;
  return;
end;

updating procedure RemoveForumMail(Longint mailnr,Longint wsnr)
begin
  record MailVc Mailr,oldMailr,nMailr;
  record WebNGForumTagsBlock FTb;
  record RLinkVc RLr;
  record WebNGStructVc WSr;
  record WebNGForumRecVc WFr;
  Longint rootmail,confsernr,rootconf,lastconf;
  record WebNGForumTopicVc FTr,oldFTr;
  record CUVc CUr;
  record ConfVc Confr;
  Integer i,rwcnt;
  row MailVc Mailrw;
  string 255 path;

  path = WebGetArg("path");
  CUr.Code = CurrentCust;
  ReadFirstMain(CUr,1,true);
  if (CUr.EmployeeType==1) then begin
    Mailr.SerNr = mailnr;
    if (ReadFirstMain(Mailr,1,true)) then begin
      //check that this user can do this action.
      WSr.SerNr = wsnr;
      if (ReadFirstMain(WSr,1,true)) then begin
        GetForumRecord(WSr.HALFunc,WFr);
        if (ReadRecordToLink(Mailr,1,nMailr,RLr)) then begin
          RecordRemove(RLr);
          if (GetMainForumCat(WFr,nMailr,FTr)) then begin
            RefreshForumSupport(FTr.MotherConf,FTr.RootConf);
          end;
        end else begin
          RecordCopy(oldMailr,Mailr);
          rwcnt = MatRowCnt(Mailr);
          for (i=1;i<rwcnt;i=i+1) begin
            MatRowGet(Mailr,i,Mailrw);
            Confr.AddrName = Mailrw.AddrCode;
            if (ReadFirstKey("AddrName",Confr,1,true)) then begin
              lastconf = Confr.SerNr;
              if (ConfIsValidChildOfRoot(Confr,WFr.ForumRootConf)) then begin
                MatRowDelete(Mailr,i);
                i = rwcnt;
              end;
            end;
          end;
          Mailr.SendFlag = 0;
          RecordUpdate(oldMailr,Mailr,false);
          FTr.SerNr = Mailr.SerNr;
          if (ReadFirstMain(FTr,1,true)) then begin
            RefreshForumSupport(FTr.MotherConf,FTr.RootConf);
            RecordRemove(FTr);
          end;
          path = GetLinkToStruct(WSr.SerNr,true) & "forumbrowse" & "/" & lastconf;
        end;
      end;
    end;
  end;
  ShowRedirectHtml(path,WebSecureMode);
    
  return;
end;

global
updating procedure SubscribeForForumMail2(Longint mailnr,Integer type,string custcode,Boolean ignoref)
begin
  record WebNGForumFavoritesVc FFr,FF2r;
  record CUVc CUr;

  if (type>-1) then begin
    CUr.Code = custcode;
    if (nonblank(custcode) and ReadFirstMain(CUr,1,true)) then begin
      RecordNew(FFr);
      FFr.SerNr = mailnr;
      FFr.CustCode = custcode;
      FFr.RecType = type;
      RecordCopy(FF2r,FFr);
      if (ReadFirstMain(FF2r,3,true)) then begin
        if (!ignoref) then begin
          RecordRemove(FF2r);
        end;
      end else begin
        if (RecordInsert(FFr,true)) then begin
        end;
      end;
    end;
  end;

  return;
end;

global
updating procedure SubscribeForForumMail(Longint mailnr,Integer type,Boolean ignoref)
begin
  SubscribeForForumMail2(mailnr,type,CurrentCust,ignoref);
  return;
end;

global
updating procedure MakeForumMailPublic(Longint sernr)
begin
  record WebNGForumTopicVc FTr,oldFTr;
  
  FTr.SerNr = sernr;
  if (ReadFirstMain(FTr,1,true)) then begin
    if (FTr.Hidden==1) then begin
      RecordCopy(oldFTr,FTr);
      FTr.Hidden = 0;
      RecordUpdate(oldFTr,FTr,true);
    end;
  end;
  ShowRedirectHtml(WebGetArg("path"),WebSecureMode);

  return;
end;

global
updating function Boolean ForumAction(string action,Longint wsnr)
begin
  Boolean res;  

  switch (action) begin
    case "addanswertag": ChangeMailTags(StringToLongint(WebGetArg("t")),WebGetArg("tag"),wsnr); res = true;
    case "removeanswertag": RemoveMailTags(StringToLongint(WebGetArg("t")),WebGetArg("tag"),wsnr); res = true;
    case "removemail": RemoveForumMail(StringToLongint(WebGetArg("t")),wsnr); res = true;
    case "bookmark": qupdating.SubscribeForForumMail(StringToLongint(WebGetArg("t")),StringToLongint(WebGetArg("type")),false); res = true;
    case "makepublic": MakeForumMailPublic(StringToLongint(WebGetArg("t"))); res = true;
  end;

  ForumAction = res;
  return;
end;

global
updating procedure StoreTheMessageMail_Forum(record MailVc Mailr,LongInt fromlink,LongInt tolink,string rootconf,string tomail,Longint subconf,var Longint mailnr)
begin
  record MailVc Mail2r,rMailr;
  record WebNGForumTopicVc FTr,oldFTr;

  Mailr.SerNr = NextSerNr("MailVc",Mailr.TransDate,-1,false,"");
  if (RecordInsert(Mailr,false)) then begin
    mailnr = Mailr.SerNr;
  end;
  if (tolink>0) then begin
    Mail2r.SerNr = tolink;
    if (ReadFirstMain(Mail2r,1,true)) then begin
      CreateRecordLink(Mail2r,CurrentCompany,Mailr,CurrentCompany);
    end;
    //KSZ::TODO this is an answer >> we find the webngforumtopic and update with last status
    rMailr.SerNr = GetQuestionMail2(Mailr);
    if (rMailr.SerNr>-1) then begin
      if (ReadFirstMain(rMailr,1,true)) then begin
        RefreshForumSupport(GetConfSerNr(rootconf),subconf);
      end;
    end;
  end else begin
    RefreshForumSupport(GetConfSerNr(rootconf),subconf);
  end;

  return;
end;

global
updating procedure SendParsedCustomerLetterForForum(record WebNGForumRecVc WFr,string type,record MailVc nMailr,string path)
begin
  Area a;
  record LetVc Letr,tLetr;
  String 255 langcode;
  boolean testf,found,foundletf;
  row MailVc tMailrw;
  string 255 mailbox;
  record CUVc CUr;
  record RCVc RepSpec;
  
  MatRowGet(nMailr,0,tMailrw);
  mailbox = tMailrw.AddrCode;
  CUr.UUID = StringToUUID(tMailrw.AddrCode);
  if (ReadFirstKey("UUID",CUr,1,true)) then begin
    if (nonblank(CUr.eMail)) then begin
      RepSpec.f4 = "http://" & WebHost & path;
      RepSpec.f1 = CUr.Code;
      SendCustomerLetterWithDetails(RepSpec,type,WFr.Language,CUr.eMail);
    end;
  end;

  return;
end;

procedure SendForumSubscribedMailsType(record WebNGForumRecVc WFr,Longint sernr,Integer type,string path,string custcode,var vector Boolean vCust)
begin
  record WebNGForumFavoritesVc FFr;
  Boolean TrHs;
  record RCVc RepSpec;
  record CUVc CUr;

  FFr.SerNr = sernr;
  FFr.RecType = type;
  TrHs = true;
  while (LoopKey("RecType",FFr,2,TrHs)) begin
    if (FFr.RecType!=type or FFr.SerNr!=sernr) then begin
      TrHs = false;
    end else begin
      if (custcode!=FFr.CustCode and !vCust[FFr.CustCode]) then begin
        CUr.Code = FFr.CustCode;
        if (ReadFirstMain(CUr,1,true)) then begin
          RepSpec.f4 = "http://" & WebHost & path;
          RepSpec.f1 = CUr.Code;
          qupdating.SendCustomerLetterWithDetails(RepSpec,"forum-subscribed",WFr.Language,CUr.eMail);
          vCust[FFr.CustCode] = true;
        end;
      end;
    end;
  end;

  return;
end;

global
procedure SendForumSubscribedMails(record WebNGForumRecVc WFr,Longint sernr,string path,string custcode)
begin
  record WebNGForumTopicVc FTr;
  vector Boolean vCust;

  if (sernr>0) then begin
    FTr.SerNr = sernr;
    if (ReadFirstMain(FTr,1,true)) then begin
      SendForumSubscribedMailsType(WFr,FTr.SerNr,0,path,custcode,vCust);
      SendForumSubscribedMailsType(WFr,FTr.MotherConf,1,path,custcode,vCust);
      SendForumSubscribedMailsType(WFr,FTr.RootConf,1,path,custcode,vCust);
    end;
  end;

  return;
end;

global
procedure SendForumMessage()
begin
  record MailVc Mailr,tMailr,rMailr;
  row MailVc Mailrw;
  record CUVc CUr;
  record WebNGForumRecVc WFr;
  string 255 frmail,tomail,link,subject;
  Boolean res;
  record ConfVc Confr,tConfr;
  Longint rootmail,subconf;
  record WebNGForumTagsBlock FTb;
  Integer err,i,rwcnt;
  record WebNGForumTopicVc FTr;
  Longint newmailnr;
  
  rootmail = StringToLongInt(WebGetArg("rootmail"));
  CUr.Code = CurrentCust;
  if (ReadFirstMain(CUr,1,true)) then begin
    frmail = CUr.UUID;
  end;

  GetForumRecord(WebGetArg("forum"),WFr);
  if (rootmail>0) then begin
    tomail = WFr.ForumReplyConf;  
  end else begin
    Confr.SerNr = StringToLongint(WebGetArg("forum_key"));
    if (ReadFirstMain(Confr,1,true)) then begin
      tomail = Confr.AddrName;
    end;
    if (nonblank(WebGetArg("theme"))) then begin
      Confr.SerNr = StringToLongInt(WebGetArg("theme"));
      if (ReadFirstMain(Confr,1,true)) then begin
        if (ConfIsValidChildOfRoot(Confr,WFr.ForumRootConf)) then begin
          tomail = Confr.AddrName;
        end;
      end;
    end;
    if (blank(tomail)) then begin
      tomail = WFr.ForumReplyConf;
    end;
  end;
  subject = WebGetArg("subject_field");
  if (blank(subject)) then begin
    err = err + 2;
  end;
  if (blank(frmail) or blank(tomail)) then begin
    err = err + 4;
  end;

  if (err==0) then begin
    RecordNew(Mailr);
    Mailr.HtmlTranslation = 1;
    Mailr.SendFlag = 1;
    Mailrw.RowTyp = 1;
    Mailrw.AddrCode = frmail;
    MatRowPut(Mailr,0,Mailrw);
    Mailrw.RowTyp = 0;
    Mailrw.AddrCode = tomail;
    MatRowPut(Mailr,1,Mailrw);
    Mailr.Header = subject;
    WebGetText("mail_body",Mailr); 
    if (rootmail>0) then begin
      link = FormatSimpleLink(WebGetArg("path"),true);
      FTr.SerNr = rootmail;
      if (ReadFirstMain(FTr,1,true)) then begin
        subconf = FTr.RootConf;;
      end;
    end else begin
      BlockLoad(FTb);
      Mailr.Tags = FTb.NewTag;
      link = FormatSimpleLink(WebGetArg("path"),true);// & "forumbrowse/" & Confr.SerNr,true);
      subconf = Confr.SerNr;
    end;
    qupdating.StoreTheMessageMail_Forum(Mailr,rootmail,StringToLongInt(WebGetArg("repmail")),WFr.ForumRootConf,tomail,subconf,newmailnr);
    if (WFr.NewCustMail==1) then begin
      qupdating.SendParsedCustomerLetterForForum(WFr,"forum-new-mail",Mailr,WebGetArg("path"));
    end;
    if (rootmail>0) then begin
      threadasync.SendForumSubscribedMails(WFr,rootmail,WebGetArg("path"),CUr.Code);
      queued.SubscribeForForumMail(rootmail,0,true);
    end else begin
      threadasync.SendForumSubscribedMails(WFr,newmailnr,WebGetArg("path"),CUr.Code);
      queued.SubscribeForForumMail(newmailnr,0,true);
    end;
    ShowRedirectHtml(link,WebSecureMode);
  end else begin
    PutSessionString("forum_msg_err",err);
    ShowRedirectHtml(WebGetArg("oldpath"),WebSecureMode);
  end;
  return;
end;


//task for checking forum inbox conference

global
updating procedure ScanForumInboxConf(record WebNGForumRecVc WFr,string inconf,string toconf)
begin
  record ConfVc Confr;
  Longint sernr;
  string 255 ckey,tstr,from;
  record MailVc Mailr,oldMailr;
  row MailVc Mailrw;
  Integer lcnt,ncnt,i;
  record NotePadVc Noter;
  Boolean startcopyf;
  record CUVc CUr;
  record WebNGForumTopicVc FTr,oldFTr;

  sernr = GetConfSerNr(inconf);
  if (sernr>0) then begin
    ckey = "UserTime:" & sernr;
    while (LoopKey(ckey,Mailr,1,true)) begin
      RecordCopy(oldMailr,Mailr);
      if (Mailr.HtmlFlag) then begin
        Mailr.HtmlFlag = 0;
      end;
      lcnt = LineTextCnt(Mailr);
      for (i=0;i<lcnt;i=i+1) begin
        tstr = LineTextGet(Mailr,i);
        if (left(tstr,6)==">From:") then begin
          from = right(tstr,len(tstr)-7);
        end;
        if (left(tstr,6)==">Date:") then begin
          i = i + 1;
          startcopyf = true;
        end else begin
          if (startcopyf) then begin
            LineTextPut(Noter,ncnt,tstr);
            ncnt = ncnt + 1;
          end;
        end;
      end;
      LineTextPut(Mailr,0,"");
      
      CUr.eMail = from;
      if (ReadFirstKey("eMail",CUr,1,true)) then begin
      
      end else begin
        RecordNew(CUr);
        CUr.eMail = from;
        CUr.AllowLogin = 1; 
        CUr.CUType = 1;
        GetNextCustNr(tstr);
        CUr.Code = tstr;
        RecordStore(CUr,false);
        ReadFirstMain(CUr,1,true);
      end;
      
      MatRowGet(Mailr,0,Mailrw);
      Mailrw.AddrCode = CUr.UUID;
      MatRowPut(Mailr,0,Mailrw);
      MatRowGet(Mailr,1,Mailrw);
      Mailrw.AddrCode = toconf;
      MatRowPut(Mailr,1,Mailrw);
      for (i=0;i<ncnt;i=i+1) begin
        tstr = LineTextGet(Noter,i);
        LineTextPut(Mailr,i+1,tstr);
      end;
      if (RecordUpdate(oldMailr,Mailr,true)==0) then begin
        RefreshForumSupport(GetConfSerNr(WFr.ForumRootConf),GetConfSerNr(toconf));
        FTr.SerNr = Mailr.SerNr;
        if (ReadFirstMain(FTr,1,true)) then begin
          RecordCopy(oldFTr,FTr);
          FTr.Hidden = 1;
          RecordUpdate(oldFTr,FTr,true);
        end;
      end;
    end;
  end;

  return;
end;

global
procedure CheckForumInbox(string args)
begin
  record WebNGForumRecVc WFr;

  while (LoopMain(WFr,1,true)) begin
    if (nonblank(WFr.ForumInbox) and nonblank(WFr.ForumInboxMove)) then begin
      qupdating.ScanForumInboxConf(WFr,WFr.ForumInbox,WFr.ForumInboxMove);
    end;
  end;

  return;
end;

global
procedure InitForumInboxTask()
begin

  NewPeriodicTask2("CheckForumInbox","CheckForumInbox","CheckForumInbox","",120,"");

  return;
end;


