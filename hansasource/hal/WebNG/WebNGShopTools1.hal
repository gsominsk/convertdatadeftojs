//translation
external function string 255 ToolWebNGTranslateText(integer);
external function string 200 GetWebLanguage();
//shop
external procedure RedirectToShopPage(string);
external function boolean GetShoppingBasket(var record WebNGShopBasketVc,integer);
external procedure SumUpShopBasket(var record WebNGShopBasketVc);
//tools
external procedure GetFullCurncyRate(var string,Date,var val,var val,var val,var val,var val);
external function boolean LoginState();
//elements
external procedure ShowWebElement(string);
//webng
external procedure RedirectToLoginPage();

global
procedure AddWebCCPaymentLineIV(var record IVVc IVr,val amount,Integer rowstp,string CurncyCode,
                                string pmcode,string authcode,string transid,string cardnumber)
begin
  row IVVc IVrw;
  string 255 tstr;
  Integer rownr;
  val fr,to1,to2,br1,br2;

  rownr = MatRowCnt(IVr);
  if (rownr>0) then begin //blank row after DoOK issue
    MatRowGet(IVr,rownr-1,IVrw);
    if (blank(IVrw.ArtCode) and blank(IVrw.Spec) and IVrw.Quant==blankval and IVrw.Price==blankval) and (IVrw.stp==kInvoiceRowTypeNormal) then begin
      rownr = rownr-1;
    end;
  end;  
  if (rowstp==0) then begin
    ClearRow(IVr,IVrw,kInvoiceRowTypeCreditCardPayment); 
  end else begin
    ClearRow(IVr,IVrw,rowstp);
  end;
  tstr = CurncyCode;
  GetFullCurncyRate(tstr,IVr.TransDate,fr,to1,to2,br1,br2);
  IVrw.CurncyCode = tstr;
  IVrw.Sum = amount;
  IVrw.FrRate = fr;
  IVrw.ToRateB1 = to1; 
  IVrw.ToRateB2 = to2;
  IVrw.BaseRate1 = br1;
  IVrw.BaseRate2 = br2;    
  IVrw.PayMode = pmcode;
  IVrw.AuthorizationCode = authcode;
  IVrw.CCTransID = transid;
  IVrw.CreditCard = "****" & right(cardnumber,4);
  MatRowPut(IVr,rownr,IVrw);
  return;
end;

global
function boolean WebShowDeliveryMode()
begin
  boolean res;
  record CUVc CUr;
  record CCatVc CCatr;
  record WebDisplayVc WebDisplayr;
  record WebNGShopCompBlock WSCb;
  string 5 code;
  
  res = false;
  CUr.Code = CurrentCust;
  if (nonblank(CUr.Code)) then begin
    ReadFirstMain(CUr,1,true);
    if (nonblank(CUr.WebDisplays)) then begin
      code = CUr.WebDisplays;
    end else begin
      if (nonblank(CUr.CustCat)) then begin
        CCatr.Code = CUr.CustCat;
        ReadFirstMain(CCatr,1,true);
        code = CCatr.WebDisplays;
      end;
    end;
  end;
  if (nonblank(code)) then begin
    WebDisplayr.Code = code;
    if (ReadFirstMain(WebDisplayr,1,true)) then begin 
      res = WebDisplayr.ShowDelModes==1;
    end;
  end else begin
    BlockLoad(WSCb);
    res = WSCb.ShowDelModes==1;
  end;
  WebShowDeliveryMode = res;
  return;
end;

global
updating procedure UpdateWebShopBasketDelMode(string DMCode)
begin
  record WebNGShopBasketVc oldWSBr,WSBr;
  
  if (WebShowDeliveryMode) then begin
    if (GetShoppingBasket(WSBr,0)) then begin
      if (WSBr.DelMode!=DMCode) then begin 
        RecordCopy(oldWSBr,WSBr);
        WSBr.DelMode = DMCode;
        SumUpShopBasket(WSBr);
        if (RecordUpdate(oldWSBr,WSBr,false)) then begin end;      
      end;
    end;
  end;
LUpdateWebShopBasketDelMode:;
  return;
end;

global
function string 255 GetShipModeLangStr(record DMVc DMr,string langcode)
begin
  string 255 res;
  Integer i,rwcnt;
  row DMVc DMrw;
  Integer hcnt;
  Boolean hitf;
  
  res = DMr.Comment; //default

  if (nonblank(langcode)) then begin
    rwcnt = MatRowCnt(DMr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(DMr,i,DMrw);
      if (langcode==DMrw.LangCode and nonblank(DMrw.Text)) then begin
        res = DMr.Comment;
        i = rwcnt;
      end;
    end;
  end;
  if (blank(res)) then begin 
    res = ToolWebNGTranslateText(32434); // Delivery Mode Text blank. Please update Delivery Modes
  end;
  GetShipModeLangStr = res;   
  return;
end;

procedure OutCheckedDelMode(string delmodecode_arg, string DMCode)
begin
  WebOutStringFormat(" checked");
  if (blank(delmodecode_arg)) then begin 
    PutSessionString("delmodecode",DMCode);
  end;
  queued.UpdateWebShopBasketDelMode(DMCode);
  return;
end;

global
procedure WebCheckOutDeliveryModes()
begin
  record DMVc DMr;
  boolean TrHs,checkf;
  integer idcnt;
  string 255 delmodecode_arg;
  
  if (WebShowDeliveryMode==false) then begin 
    goto LWebCheckOutDeliveryModes;
  end;
  
  delmodecode_arg = GetSessionString("delmodecode");
  
  WebOutTagOpen("<div class=""checkout_select_delmode"">");

  WebOutStringFormat("<span style=""font-weight: bold"">" & ToolWebNGTranslateText(32431) & "</span>" & "<br>"); //Please select Delivery Mode

  WebOutTagOpen("<form method=""GET"" action=""/WebUpdatingAction.hal"">");
  WebOutStringFormatNL("<input type=""hidden"" name=""action"" value=""updatedeliverymode"">");

  idcnt = 1;
  TrHs = true;
  DMr.WebSort = "";
  DMr.ShowOnWeb=1;
  while (LoopKey("WebSort",DMr,2,TrHs)) begin
    if (DMr.ShowOnWeb==1 and nonblank(DMr.FreightCode)) then begin
      WebOutStringFormat("<input type=""radio"" id=""delmode_" & idcnt  &""" name=""delmode"" onChange=""submit(this.form)"" value=""" & DMr.Code & """");
      if (nonblank(delmodecode_arg) and checkf==false) then begin 
        if (delmodecode_arg==DMr.Code) then begin 
          OutCheckedDelMode(delmodecode_arg,DMr.Code);
          checkf = true;
        end;
      end else begin
        if (idcnt==1 and checkf==false) then begin 
          OutCheckedDelMode(delmodecode_arg,DMr.Code);
          checkf = true;
        end;
      end;
      WebOutStringFormatNL("><label for=""delmode_" & idcnt  &""">" & GetShipModeLangStr(DMr,GetWebLanguage) & "</label>");
      WebOutStringFormatNL("<br>");  
      idcnt = idcnt + 1;
    end;
  end;
  if (idcnt==1) then begin 
    WebOutStringFormat("<span style=""font-weight: bold"">" & ToolWebNGTranslateText(32432) & "</span>" & "<br>"); //Delivery Modes with Web Freight need to be setup!
  end;
  WebOutTagClose("</form>");
  WebOutTagClose("</div>");  //checkout_select_delmode
LWebCheckOutDeliveryModes:;  
  return;
end;

global
function string 255 WebGetDeliveryModeFreight(string curfreightcode)
begin
  string 255 res,delmodecode_arg;
  record DMVc DMr;
  boolean TrHs;

  if (WebShowDeliveryMode) then begin   
    delmodecode_arg = GetSessionString("delmodecode");
    if (nonblank(delmodecode_arg)) then begin 
      DMr.Code = delmodecode_arg;
      if (ReadFirstMain(DMr,1,true)) then begin 
        if (nonblank(DMr.FreightCode)) then begin 
          res = DMr.FreightCode;        
        end;
      end;
    end else begin
      TrHs = true;
      DMr.WebSort=""; 
      DMr.ShowOnWeb=1;    
      while (LoopKey("WebSort",DMr,2,TrHs)) begin
        if (nonblank(DMr.FreightCode) and DMr.ShowOnWeb==1) then begin 
          res = DMr.FreightCode;
          TrHs = false;
        end;
      end;  
    end;
  end;
  if (blank(res)) then begin 
    res = curfreightcode;
  end;  
  WebGetDeliveryModeFreight = res;
  return;
end;

global
function string 255 WebNGGetSelectedDeliveryMode(string CurrentShipMode)
begin
  string 255 res,delmodecode_arg;

  if (WebShowDeliveryMode) then begin   
    res = GetSessionString("delmodecode");
  end;
  
  if (blank(res)) then begin 
    res = CurrentShipMode;
  end;    
  WebNGGetSelectedDeliveryMode = res;
  return;
end;

global
updating procedure ShopUpdateDeliveryMode()
begin
  record WebNGShopBasketVc oldWSBr,WSBr;
  
  if (LoginState==false) then begin
    RedirectToLoginPage;
    goto LShopUpdateDeliveryMode;
  end;
  
  if (WebShowDeliveryMode) then begin
    if (GetShoppingBasket(WSBr,0)) then begin
      RecordCopy(oldWSBr,WSBr);
      WSBr.DelMode = WebNGGetSelectedDeliveryMode("");
      SumUpShopBasket(WSBr);
      if (RecordUpdate(oldWSBr,WSBr,false)) then begin end;
    end;
  end;
  RedirectToShopPage("checkout/confirm");
LShopUpdateDeliveryMode:;
  return;
end;