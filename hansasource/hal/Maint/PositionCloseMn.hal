external procedure CheckFlush(var Integer,Integer);

global
updating procedure PositionCloseMn(record RcVc RepSpec)
BEGIN
  Boolean TrHs,testf;
  record PosVc Posr;
  record PosVc oldPosr;
  string 20 frpos,topos,ckey;
  Integer keys,cnt;
  
  frpos = FirstInRange(RepSpec.f1,20);
  topos = LastInRange(RepSpec.f1,20);

  ckey = "MainKey";
  Posr.Code = frpos;
  keys = 1;
  if (nonblank(RepSpec.f2)) and (nonblank(RepSpec.f3)) then begin
    ckey = "LocArea";
    Posr.Code = "";
    Posr.Location = RepSpec.f3;
    Posr.LocArea = RepSpec.f2;
    keys = 2;
  end;
  Gray_Divider(0,1);
  TrHs = true;
  while (LoopKey(ckey,Posr,keys,TrHs)) begin
    if (nonblank(RepSpec.f2)) and (nonblank(RepSpec.f3)) then begin
      if (Posr.Location!=RepSpec.f3) then begin TrHs = false; end;
      if (Posr.LocArea!=RepSpec.f2) then begin TrHs = false; end;
    end else begin
      if (Posr.Code<frpos) then begin TrHs = false; end;
      if (Posr.Code>topos) then begin TrHs = false; end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f1)) then begin
        if (Posr.Code<frpos) then begin testf = false; end;
        if (Posr.Code>topos) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.f2)) then begin
        if (Posr.LocArea!=RepSpec.f2) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.f3)) then begin
        if (Posr.Location!=RepSpec.f3) then begin testf = false; end;
      end;
      if (testf) then begin
        RecordCopy(oldPosr,Posr);
        if (RepSpec.flags[0]==0) then begin
          if (Posr.Closed==0) then begin
            Posr.Closed = 1;          
            if (RecordUpdate(oldPosr,Posr,false)==0) then begin end;
            CheckFlush(cnt,20);
          end;
        end else begin
          if (Posr.Closed!=0) then begin
            Posr.Closed = 0;          
            if (RecordUpdate(oldPosr,Posr,false)==0) then begin end;
            CheckFlush(cnt,20);
          end;
        end;
      end;
    end;
  end;
  RETURN;
END;