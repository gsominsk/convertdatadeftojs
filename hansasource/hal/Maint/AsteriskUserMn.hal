external procedure MoveAsteriskFileInPlace(string,string,string);

procedure AddOneUser(record AsteriskUser2Vc AsteriskUserr,var area outarea)
begin
  string 255 tstr;

  AddTextToArea("; " & AsteriskUserr.Name & chr(10),outarea);
  tstr = "[" & AsteriskUserr.UserName & "]";
  if (nonblank(AsteriskUserr.Group)) then begin
    tstr = tstr & "(" & AsteriskUserr.Group & ")";
  end;
  tstr = tstr & chr(10);
  AddTextToArea(tstr,outarea);
  AddTextToArea("secret=" & AsteriskUserr.Password & chr(10),outarea);
  AddTextToArea("callerid=""" & AsteriskUserr.CallerIdName & """ <" & AsteriskUserr.CallerIdNumber & ">" & chr(10),outarea);
  AddTextToArea("" & chr(10),outarea);
  return;
end;

procedure AddOneHint(record AsteriskUser2Vc AsteriskUserr,var area outarea)
begin
  AddTextToArea("exten => " & AsteriskUserr.UserName & ",hint,SIP/" & AsteriskUserr.UserName & chr(10),outarea);
  return;
end;

global
procedure AsteriskUserMn(record RcVc RepSpec)
begin
  record AsteriskUser2Vc AsteriskUserr;
  Integer i,rwcnt;
  Boolean found,testf,createdf;
  area siparea,hintarea;
  string 255 fname;
  Time t;


  AddTextToArea(";sip-hansaworld.conf generated by HansaWorld" & chr(10),siparea);
  AddTextToArea(";hints-hansaworld.conf generated by HansaWorld" & chr(10),hintarea);

  found = true;
  AsteriskUserr.PBXConnection = RepSpec.f1;
  while (LoopKey("PBXConnection",AsteriskUserr,1,found)) begin
    if (found) then begin
      testf = false;
      if (AsteriskUserr.Closedf==0) then begin
        if (RepSpec.f1=="") then begin
          testf = true;
        end;
        if (AsteriskUserr.PBXConnection==RepSpec.f1) then begin
          testf = true;
        end;
      end;
      if (testf) then begin
        createdf = true;
        AddOneUser(AsteriskUserr,siparea);
        AddOneHint(AsteriskUserr,hintarea);
      end;
    end;
  end;

  if (createdf) then begin
    fname = CreateTempFile("sip-hansaworld",".conf");
    AddTextToArea("" & chr(10),siparea);
    WriteAreaToFile(siparea,fname,0);
    MoveAsteriskFileInPlace(RepSpec.f1,fname,"sip-hansaworld.conf");
    fname = CreateTempFile("hints-hansaworld.conf",".conf");
    AddTextToArea("" & chr(10),hintarea);
    WriteAreaToFile(hintarea,fname,0);
    MoveAsteriskFileInPlace(RepSpec.f1,fname,"hints-hansaworld.conf");
  end;
  t = CurrentTime;
  t.second = t.second + 10;
  NewTimedTask("ReloadAsterisk","Reload Asterisk with delay","ReloadAsterisk",fname & ":" & RepSpec.f1 & ":chan_sip.so",CurrentDate,t,CurrentTime,"");
  NewTimedTask("ReloadAsterisk","Reload Asterisk with delay","ReloadAsterisk",fname & ":" & RepSpec.f1 & ":pbx_config.so",CurrentDate,t,CurrentTime,"");

  return;
end;
