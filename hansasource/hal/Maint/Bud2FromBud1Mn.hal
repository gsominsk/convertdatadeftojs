external procedure Bud2Sumup(var record Bud2Vc);

global
updating procedure Bud2FromBud1Mn(record RcVc RepSpec)
begin
  record Bud1Vc Bud1r;
  row Bud1Vc Bud1rw;
  record Bud2Vc Bud2r;
  record Bud2Vc oldBud2r;
  Boolean found,testf,readf;
  string 255 fracc,toacc;
  Integer i1,i1rwcnt;
  
  fracc = FirstInRange(RepSpec.f1,20);
  toacc = LastInRange(RepSpec.f1,20);
  found = true;
  while (LoopKey("AccObjKey",Bud1r,1,found)) begin
    if (found) then begin
      testf = true;
      if (DateInRange(Bud1r.StartDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
        testf = false;
      end;
      if (nonblank(RepSpec.f1)) then begin
        if (Bud1r.AccNumber>fracc) then begin testf = false; end;
        if (Bud1r.AccNumber<toacc) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.ObjStr)) then begin
        if (SetInSet(RepSpec.ObjStr,Bud1r.Objects)==false) then begin testf = false; end;
      end;
      if (nonblank(RepSpec.ObjStr)) then begin
        if (SetInSet(RepSpec.FirstAcc,Bud1r.Class)==false) then begin testf = false; end;
      end;
      if (testf) then begin
        Bud2r.AccNumber = Bud1r.AccNumber;
        Bud2r.Objects = Bud1r.Objects;
        Bud2r.Class = Bud1r.Class;
        Bud2r.StartDate = Bud1r.StartDate;
        readf = ReadFirstMain(Bud2r,4,true);
        if (Bud2r.OKFlag!=0) then begin testf = false; end;
        if (RepSpec.flags[0]==0) then begin
          if (readf) then begin testf = false; end;
        end;
      end;
      if (testf) then begin
        if (readf) then begin
          RecordCopy(oldBud2r,Bud2r);
          while (MatRowCnt(Bud2r)>0) begin
            MatRowDelete(Bud2r,0);
          end;
        end else begin
          RecordNew(Bud2r);
          Bud2r.AccNumber = Bud1r.AccNumber;
          Bud2r.Comment = Bud1r.Comment;
          Bud2r.Objects = Bud1r.Objects;
          Bud2r.Class = Bud1r.Class;
          Bud2r.Bud1Proc = 0;
          Bud2r.StartDate = Bud1r.StartDate;
          Bud2r.EndDate = Bud1r.EndDate;
        end;
        i1rwcnt = MatRowCnt(Bud1r);
        for (i1=0;i1<i1rwcnt;i1=i1+1) begin
          MatRowGet(Bud1r,i1,Bud1rw);
          MatRowPut(Bud2r,MatRowCnt(Bud2r),Bud1rw);
        end;
        Bud2Sumup(Bud2r);
        if (readf) then begin
          RecordUpdate(oldBud2r,Bud2r,true);
        end else begin
          RecordStore(Bud2r,false);
        end;
      end;
    end;
  end;
  return;
end;
