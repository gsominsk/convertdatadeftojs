external procedure CheckFlush(var Integer,Integer);
external function LongInt INVcRecordRemoveTest(var record INVc,record INVc,LongInt,LongInt);

global
updating procedure DelNotUsedINMn(record RcVc RepSpec)
BEGIN
  record INVc INr;
  record INVc fakeINr;
  record ItemHistVc IHr;
  record TBIVVc TBIVr;
  string 255 frin,toin;
  Boolean TrHs,testf;
  Integer flushcnt;

  frin = FirstInRange(RepSpec.f1,20);   
  toin = LastInRange(RepSpec.f1,20);   
  INr.Code = frin;
  TrHs = true;
  while (LoopMain(INr,1,TrHs)) begin
    if ((nonblank(RepSpec.f1)) and (INr.Code>toin)) then begin TrHs = false; end;
    if (TrHs) then begin
        testf = true;
        if ((nonblank(RepSpec.f2)) and (INr.Group!=RepSpec.f2)) then begin testf = false; end;
        IHr.ArtCode = INr.Code;
        if (ReadFirstKey("ArtCode",IHr,1,true)) then begin
          testf = false;
        end;       
        TBIVr.ArtCode = INr.Code;
        if (ReadFirstKey("ArtCode",TBIVr,1,true)) then begin
          testf = false;
        end;       
        if (testf) then begin
          if (INVcRecordRemoveTest(INr,fakeINr,0,0)!=0) then begin
            RecordDelete(INr);
            StepBack(INr);
            CheckFlush(flushcnt,10);
          end;
        end;
    end;
    UserTrace(INr.Code,M4Code);                              
  end;
  RETURN;
END;
