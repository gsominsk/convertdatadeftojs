updating function
Boolean UpdateAllAttachLinks(record Attach2Vc Attachr,LongInt lastno)
begin
  record Attach2Vc Attach2r;
  record RLinkVc RLr;
  record RLinkVc RL2r;
  Boolean foundf;
  string 255 tstr;
  Boolean res;

  res = false;
  tstr = BuildRecordIdStr(Attachr,CurrentCompany);
  RecordCopy(Attach2r,Attachr);
  Attachr.SerNr = lastno;
  if (RecordUpdate(Attach2r,Attachr,false)==0) then begin
    if (RenameFile("Attach\\" & Attach2r.SerNr,"Attach\\" & Attachr.SerNr)==0) then begin
    end;
LUpdateAllAttachLinks:;
    foundf = true;
    ResetLoop(RLr);
    RLr.ToRecidStr = tstr;
    while (LoopKey("ToRecKey",RLr,1,foundf)) begin
      if (RLr.ToRecidStr!=tstr) then begin
        foundf = false;
      end;
      if (foundf) then begin
        RecordCopy(RL2r,RLr);
        RLr.ToRecidStr = BuildRecordIdStr(Attachr,CurrentCompany);
        if (RecordUpdate(RL2r,RLr,false)==0) then begin
          goto LUpdateAllAttachLinks;
        end;
      end;
    end;
    res = true;
  end;
  DBFlush;
  DBHold;
  UpdateAllAttachLinks = res;
  return;
end;

updating function
Boolean UpdateAllNotepadLinks(record NotepadVc Notepadr,LongInt lastno)
begin
  record NotepadVc Notepad2r;
  record RLinkVc RLr;
  record RLinkVc RL2r;
  Boolean foundf;
  string 255 tstr;
  Boolean res;

  res = false;
  tstr = BuildRecordIdStr(Notepadr,CurrentCompany);
  RecordCopy(Notepad2r,Notepadr);
  Notepadr.SerNr = lastno;
  if (RecordUpdate(Notepad2r,Notepadr,false)==0) then begin
LUpdateAllNotepadLinks:;
    foundf = true;
    ResetLoop(RLr);
    RLr.ToRecidStr = tstr;
    while (LoopKey("ToRecKey",RLr,1,foundf)) begin
      if (RLr.ToRecidStr!=tstr) then begin
        foundf = false;
      end;
      if (foundf) then begin
        RecordCopy(RL2r,RLr);
        RLr.ToRecidStr = BuildRecordIdStr(Notepadr,CurrentCompany);
        if (RecordUpdate(RL2r,RLr,false)==0) then begin
          goto LUpdateAllNotepadLinks;
        end;
      end;
    end;
    res = true;
  end;
  DBFlush;
  DBHold;
  UpdateAllNotepadLinks = res;
  return;
end;

global
updating procedure RenoSysRecMn(record RcVc RepSpec)
BEGIN
  record Attach2Vc Attachr;
  record NotepadVc Noter;
  Boolean foundf;
  LongInt firstno,lastno,curno;
  Boolean backwards;

  firstno = RepSpec.long1;
  if (firstno>-1) then begin
  switch (RepSpec.flags[1]) begin
    case 0: // Attachements
      backwards = true;
      Attachr.SerNr = -1;
      ResetLoop(Attachr);
      if (LoopMain(Attachr,1,true)) then begin
        if (Attachr.SerNr>firstno) then begin
          backwards = false;
        end;
      end;
      curno = 999999999;
      ResetLoop(Attachr);
      if (backwards) then begin
        lastno = CountRecords("Attach2Vc") + firstno - 1;
        Attachr.SerNr = curno;
        foundf = true;
        while (LoopBackKey("SerNr",Attachr,1,foundf)) begin
          curno = Attachr.SerNr;
          if (UpdateAllAttachLinks(Attachr,lastno)) then begin
            lastno = lastno - 1;
          end;
          Attachr.SerNr = curno;
        end;
      end else begin
        lastno = firstno;
        Attachr.SerNr = -1;
        foundf = true;
        while (LoopKey("SerNr",Attachr,1,foundf)) begin
          curno = Attachr.SerNr;
          if (UpdateAllAttachLinks(Attachr,lastno)) then begin
            lastno = lastno + 1;
          end;
          Attachr.SerNr = curno;
        end;
      end;
    case 1: // Notes
      backwards = true;
      Noter.SerNr = -1;
      ResetLoop(Noter);
      if (LoopMain(Noter,1,true)) then begin
        if (Noter.SerNr>firstno) then begin
          backwards = false;
        end;
      end;
      curno = 999999999;
      ResetLoop(Noter);
      if (backwards) then begin
        lastno = CountRecords("NotepadVc") + firstno - 1;
        Noter.SerNr = curno;
        foundf = true;
        while (LoopBackKey("SerNr",Noter,1,foundf)) begin
          curno = Noter.SerNr;
          if (UpdateAllNotepadLinks(Noter,lastno)) then begin
            lastno = lastno - 1;
          end;
          Noter.SerNr = curno;
        end;
      end else begin
        lastno = firstno;
        Noter.SerNr = -1;
        foundf = true;
        while (LoopKey("SerNr",Noter,1,foundf)) begin
          curno = Noter.SerNr;
          if (UpdateAllNotepadLinks(Noter,lastno)) then begin
            lastno = lastno + 1;
          end;
          Noter.SerNr = curno;
        end;
      end;
  end;
  end;
  
  RETURN;
END;

