global
updating procedure ChMaiboxMailFmtMn(record RcVc RepSpec)
begin
  Boolean TrHs,updf,testf;
  string 60 frconf,toconf;
  record ConfVc oldConfr;
  record ConfVc Confr;
  row ConfVc Confrw;
  Integer i,rwcnt;
  
  frconf = FirstInRange(RepSpec.f1,30);
  toconf = LastInRange(RepSpec.f1,30);
  TrHs = true;
  Confr.AddrName = frconf;
  while (LoopKey("AddrName",Confr,1,TrHs)) begin
    if (nonblank(RepSpec.f1)) then begin
      if (Confr.AddrName>toconf) then begin
        TrHs = false;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f2)) then begin
        if (Confr.Class!=SetFromString(35,RepSpec.f2)) then begin
          testf = false;
        end;
      end;
      if (testf) then begin
        if (nonblank(RepSpec.AccStr)) then begin
          testf = false;
          rwcnt = MatRowCnt(Confr);
          for (i=0;i<rwcnt;i=i+1) begin
            MatRowGet(Confr,i,Confrw);
            if (Confrw.Group==RepSpec.AccStr) then begin
              testf = true;
              i = rwcnt;
            end;
          end;
        end;
      end;

      if (testf) then begin
        RecordCopy(oldConfr,Confr);
        updf = false;
        switch (RepSpec.flags[1]) begin
          case 0:
            if (Confr.DefaultHtmlFlag!=0) then begin
              Confr.DefaultHtmlFlag = 0;
              updf = true;
            end;
          case 1:
            if (Confr.DefaultHtmlFlag==0) then begin
              Confr.DefaultHtmlFlag = 1;
              updf = true;
            end;
        end;
        if (updf) then begin
          RecordUpdate(oldConfr,Confr,true);
        end;
      end;
    end;
  end;  
  return;
end;