external procedure FindFiscalYear(Date,var Date,var Date);
external procedure CheckFlush(var Integer,Integer);

global
updating procedure LockNLMn(record RcVc RepSpec)
begin
  record TRVc TRr;
  record TRVc oldTRr;
  record DBLockBlock DBLb;
  Boolean found,fstestf;
  Integer cnt;
  LongInt startseqnr;
  Date startdate,starftfsd,startfed;  
  Date fsd,fed;
  Date d1fsd,d1fed;
  
  if (nonblankdate(RepSpec.d1)) then begin
    BlockLoad(DBLb);
    if (RepSpec.d1<=DBLb.TRLock) then begin
      goto LLockNLMn;
    end;
    FindFiscalYear(RepSpec.d1,d1fsd,d1fed);  
    startdate = d1fsd;
    TRr.SeqNr = 2147483647;
    TRr.TransDate = RepSpec.d1;
    if (ReadLastKey("SeqNrTransDate",TRr,2,false)) then begin
      if (TRr.TransDate<=RepSpec.d1) then begin
        startseqnr = TRr.SeqNr + 1;
        startdate = TRr.TransDate;
        FindFiscalYear(TRr.TransDate,starftfsd,startfed);  
        if (d1fsd!=starftfsd) then begin
          startdate = d1fsd;
        end;
      end;
    end;
    if (startseqnr<=0) then begin startseqnr = 1; end;
    TRr.TransDate = startdate;
    found = true;
    while (LoopKey("TransDate",TRr,1,found)) begin
      if (TRr.TransDate>RepSpec.d1) then begin found = false; end;
      if (found) then begin
        RecordCopy(oldTRr,TRr);        
        if (!fstestf) then begin
          FindFiscalYear(TRr.TransDate,fsd,fed);  
          if (fsd!=starftfsd) or (fed!=startfed) then begin
            startseqnr = 1;
          end;
          fstestf = true;
        end;
        if (TRr.SeqNr<=0) then begin
          TRr.SeqNr = startseqnr;
          startseqnr = startseqnr + 1;
          if (RecordUpdate(oldTRr,TRr,false)) then begin end;
          CheckFlush(cnt,10);
        end;
      end;
    end;
    DBLb.TRLock = RepSpec.d1;
    DBLb.OtherLock = RepSpec.d1;
    DBLb.SLLock = RepSpec.d1;
    DBLb.PLLock = RepSpec.d1;
    BlockStore(DBLb);
  end else begin
    MessageBox(9610,"");
  end;
LLockNLMn:;  
  return;
end;