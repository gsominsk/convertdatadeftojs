external procedure CheckFlush(var Integer,Integer);
external procedure ORSumup(var record ORVc);
external procedure PasteEDIOrdInOrd(var record EDIORVc,var record ORVc);

global
updating procedure GenORFromEDIOR(record RcVc RepSpec)
begin
  record ORVc ORr;
  record EDIORVc EORr;
  record EDIORVc oldEORr;
  record CUVc CUr;
  Boolean foundf,testf;
  string 200 frcc,tocc;
  LongInt fror,toor;
  LongInt newnr;
  record SRBlock SRRec;
  Integer flushcnt;

  BlockLoad(SRRec);
  fror = FirstInRange(RepSpec.f1,20);
  toor = LastInRange(RepSpec.f1,20);
  frcc = FirstInRange(RepSpec.f2,20);
  tocc = LastInRange(RepSpec.f2,20);
  EORr.OrdDate = RepSpec.sStartDate;
  foundf = true;
  while (LoopKey("OrdDate",EORr,1,foundf)) begin
    if (foundf) then begin
      if (EORr.OrdDate<RepSpec.sStartDate) then begin foundf = false; end;
      if (EORr.OrdDate>RepSpec.sEndDate) then begin foundf = false; end;
    end;
    testf = true;
    if (foundf==false) then begin
      testf = false;
    end;
    if (testf) then begin
      if (nonblank(frcc)) then begin
        if (frcc>EORr.CustCode) then begin testf = false; end;
        if (tocc<EORr.CustCode) then begin testf = false; end;
      end;
      if (fror>-1) then begin
        if (fror<EORr.SerNr) then begin testf = false; end;
        if (toor>EORr.SerNr) then begin testf = false; end;
      end;
      if (EORr.ORSerNr>-1) then begin testf = false; end;
      if (EORr.VersionNr!=1) then begin testf = false; end;
    end;
    if (testf) then begin
      CUr.Code = EORr.CustCode;
      if (ReadFirstMain(CUr,1,true)) then begin
        if (CUr.EDIFlag==0) then begin
          testf = false;
        end;
      end;
    end;
    if (testf) then begin
      RecordNew(ORr);
      if (ORr.SerNr==-1) then begin
        newnr = SRRec.LastOrdNr;
        ORr.SerNr = NextSerNr("ORVc",ORr.OrdDate,SRRec.LastOrdNr,false,ORr.LangCode);
        if (ORr.SerNr==-1) then begin
          goto LGenerateORFromEDIOR;
        end;
      end;
      RecordCopy(oldEORr,EORr);
      PasteEDIOrdInOrd(EORr,ORr);
      if ((MatRowCnt(ORr)>0) and (ORr.SerNr!=-1)) then begin
        if (RecordUpdate(oldEORr,EORr,false)==0) then begin end;
        ORSumup(ORr);
        if (RecordInsert(ORr,false)) then begin
        end;
        CheckFlush(flushcnt,10);
      end;
    end;
  end;
LGenerateORFromEDIOR:;
  return;
end;

