external procedure SetORFlags(record ORVc);

global
updating procedure RecalcORMn(record RcVc RepSpec)
begin
  record ORVc oldORr;
  record ORVc ORr;
  record SHVc SHr;
  record DropSHVc DropSHr;
  record RetVc Retr;
  record IVVc IVr;
  row ORVc ORrw;
  row SHVc SHrw;
  row DropSHVc DropSHrw;
  row RetVc Retrw;
  row IVVc IVrw;
  LongInt fror,toor;
  Integer rwcnt,i,orrwcnt;
  Boolean found,shfound,testf;
  record XSrsVc XSrsr;
  
  if (RepSpec.flags[2]==0) then begin
    if (blank(RepSpec.f1)) then begin
      MessageBox(2278,"");
      goto LRecalcORMn;
    end;
  end;
  fror = FirstInRange(RepSpec.f1,10);
  toor = LastInRange(RepSpec.f1,10);
  ORr.SerNr = fror;
  found = true;
  while (LoopKey("SerNr",ORr,1,found)) begin
    if (found) then begin
      if (nonblank(RepSpec.f1)) then begin
        if (ORr.SerNr>toor) then begin
          found = false;
        end;
      end;
    end;
    testf = true;
    if (found==false) then begin
      testf = false;
    end;
    if (ORr.Closed<>0) then begin
      if (RepSpec.ArtMode==0) then begin
        testf = false;
      end;
    end;
    if (testf) then begin
      RecordCopy(oldORr,ORr);
      orrwcnt = MatRowCnt(ORr);
      for (i=0;i<orrwcnt;i=i+1) begin
        MatRowGet(ORr,i,ORrw);
        if (RepSpec.flags[0]<>0) then begin
          ORrw.Shipd1 = BlankVal;
          ORrw.Shipd2 = BlankVal;
        end;
        if (RepSpec.flags[1]<>0) then begin
          ORrw.Invd = BlankVal;
        end;
        MatRowPut(ORr,i,ORrw);
      end;
      if (RepSpec.flags[0]<>0) then begin
/* find all shipments */
        SHr.OrderNr = ORr.SerNr;
        shfound = true;
        ResetLoop(SHr);
        while (LoopKey("OrderKey",SHr,1,shfound)) begin
          if (shfound) then begin
            if (SHr.OrderNr!=ORr.SerNr) then begin
              shfound = false;
            end;
          end;
          if (shfound) then begin
            rwcnt = MatRowCnt(SHr);
            for (i=0;i<rwcnt;i=i+1) begin
              MatRowGet(SHr,i,SHrw);
              if ((SHrw.OrdRow>=0) and (SHrw.OrdRow<orrwcnt)) then begin
                MatRowGet(ORr,SHrw.OrdRow,ORrw);
                ORrw.Shipd1 = ORrw.Shipd1 + SHrw.Ship;
                if (SHr.OKFlag<>0) then begin
                  ORrw.Shipd2 = ORrw.Shipd2 + SHrw.Ship;
                end;
                MatRowPut(ORr,SHrw.OrdRow,ORrw);
              end;
            end;
          end;
        end;
/* find all drop shipments */
        DropSHr.OrderNr = ORr.SerNr;
        shfound = true;
        ResetLoop(DropSHr);
        while (LoopKey("OrderNr",DropSHr,1,shfound)) begin
          if (shfound) then begin
            if (DropSHr.OrderNr!=ORr.SerNr) then begin
              shfound = false;
            end;
          end;
          if (shfound) then begin
            rwcnt = MatRowCnt(DropSHr);
            for (i=0;i<rwcnt;i=i+1) begin
              MatRowGet(DropSHr,i,DropSHrw);
              if ((DropSHrw.OrdRow>=0) and (DropSHrw.OrdRow<orrwcnt)) then begin
                MatRowGet(ORr,DropSHrw.OrdRow,ORrw);
                ORrw.Shipd1 = ORrw.Shipd1 + DropSHrw.Ship;
                if (DropSHr.OKFlag<>0) then begin
                  ORrw.Shipd2 = ORrw.Shipd2 + DropSHrw.Ship;
                end;
                MatRowPut(ORr,DropSHrw.OrdRow,ORrw);
              end;
            end;
          end;
        end;
/* find all Returned Goods */
        Retr.OrdNr = ORr.SerNr;
        shfound = true;
        ResetLoop(Retr);
        while (LoopKey("OrdNr",Retr,1,shfound)) begin
          if (shfound) then begin
            if (Retr.OrdNr!=ORr.SerNr) then begin
              shfound = false;
            end;
          end;
         if (shfound) then begin
           rwcnt = MatRowCnt(Retr);
           for (i=0;i<rwcnt;i=i+1) begin
             MatRowGet(Retr,i,Retrw);
             if ((Retrw.OrdRow>=0) and (Retrw.OrdRow<orrwcnt)) then begin
               MatRowGet(ORr,Retrw.OrdRow,ORrw);
               if (Retr.OKFlag<>0) then begin
                 ORrw.Shipd1 = ORrw.Shipd1 - Retrw.Quant;
                 ORrw.Shipd2 = ORrw.Shipd2 - Retrw.Quant;
               end;
               MatRowPut(ORr,Retrw.OrdRow,ORrw);
             end;
           end;
         end;
       end;
      end;
      if (RepSpec.flags[1]<>0) then begin
/* find all invoices */
        IVr.OrderNr = ORr.SerNr;
        shfound = true;
        ResetLoop(IVr);
        while (LoopKey("OrderNr",IVr,1,shfound)) begin
          if (shfound) then begin
            if (IVr.OrderNr!=ORr.SerNr) then begin
              shfound = false;
            end;
          end;
          if (shfound) then begin
            if (IVr.Invalid==0) then begin
              rwcnt = MatRowCnt(IVr);
              for (i=0;i<rwcnt;i=i+1) begin
                MatRowGet(IVr,i,IVrw);
                if ((IVrw.OrdRow>=0) and (IVrw.OrdRow<orrwcnt)) then begin
                  if (IVrw.stp==kInvoiceRowTypeNormal) or (IVrw.stp==kInvoiceRowTypeStructuredItemComponent) or (IVrw.stp==kInvoiceRowTypeCorrection) then begin
                    MatRowGet(ORr,IVrw.OrdRow,ORrw);
                    if (IVr.InvType==kInvoiceTypeCredit or IVr.InvType==kInvoiceTypeCreditSpecialSales) then begin
                      switch (IVrw.stp) begin
                        case kInvoiceRowTypeCorrection:
                          ORrw.Invd = ORrw.Invd + IVrw.Quant;
                        otherwise
                          ORrw.Invd = ORrw.Invd - IVrw.Quant;
                      end;                 
                    end else begin
                      ORrw.Invd = ORrw.Invd + IVrw.Quant;
                    end;  
                    MatRowPut(ORr,IVrw.OrdRow,ORrw);
                  end;
                end;
              end;
            end;  
          end;
        end;
        XSrsr.TransType = 0;
        XSrsr.FromFileName = "ORVc";
        XSrsr.FromSerNr = ORr.SerNr;
        shfound = true;
        ResetLoop(XSrsr);
        while (LoopKey("From",XSrsr,3,shfound)) begin
          if (XSrsr.TransType!=0) then begin shfound = false; end;
          if (XSrsr.FromFileName!="ORVc") then begin shfound = false; end;
          if (XSrsr.FromSerNr!=ORr.SerNr) then begin shfound = false; end;
          if (shfound) then begin
            if (XSrsr.FromRow>=0) then begin
              MatRowGet(ORr,XSrsr.FromRow,ORrw);
              ORrw.Invd = ORrw.Invd + XSrsr.FromQty;
              MatRowPut(ORr,XSrsr.FromRow,ORrw);
            end;            
          end;
        end;
        
      end;
      SetORFlags(ORr);
      if (RecordUpdate(oldORr,ORr,false)==0) then begin
      end;
      MaintTrace(ORr.SerNr);
    end;
  end;
LRecalcORMn:;  
  return;
end;

