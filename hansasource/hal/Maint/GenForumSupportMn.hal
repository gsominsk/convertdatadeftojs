external procedure FillWithForumAnswers(var array record MailVc,record MailVc,Boolean,Longint);
external function Longint GetConfSerNr(string);
external updating procedure SubscribeForForumMail2(Longint,Integer,string,Boolean);


global
updating procedure GenForumSupportSub(record ConfVc Confr,Longint motherconf,var Longint lastmailnumber,var Integer topiccnt,var Integer postcnt)
begin
  string 255 ckey;
  record WebNGForumTopicVc FTr,oldFTr;
  Boolean foundf,testf;
  array record MailVc aMail2r;
  record MailVc Mailr,lastMailr;
  Longint confsernr;
  record WebNGForumTagsBlock WTb;
  record ConfVc Conf2r;
  record WebNGForumCatVc FCr;

  BlockLoad(WTb);
  if (Confr.Class==kConfClassFolder) then begin//if list of conferences
    foundf = true;
    ckey = "SubConfSortorder:" & Confr.SerNr;
    Conf2r.Sortorder = "";
    while (LoopKey(ckey,Conf2r,1,foundf)) begin
      testf = foundf;
      if (testf) then begin
        topiccnt = 0;
        postcnt = 0;
        lastmailnumber = -1;
        GenForumSupportSub(Conf2r,Confr.SerNr,lastmailnumber,topiccnt,postcnt);
        RecordNew(FCr);
        FCr.SerNr = Conf2r.SerNr;
        FCr.TopicCount = topiccnt;
        FCr.PostCount = postcnt;
        FCr.LastReplyMail = lastmailnumber;
        if (RecordInsert(FCr,true)) then begin end;
      end;
    end;
  end else begin
    foundf = true;
    ResetLoop(Mailr);
    RecordClear(Mailr);
    ckey = "UserTime:" & Confr.SerNr;
    while (LoopKey(ckey,Mailr,1,foundf)) begin
      ClearArray(aMail2r);
      FillWithForumAnswers(aMail2r,Mailr,true,Mailr.SerNr);
      topiccnt = topiccnt + 1;
      postcnt = postcnt + aMail2r.length;
      if (aMail2r.length>0) then begin
        lastMailr = aMail2r[aMail2r.length-1];
        if (lastmailnumber<lastMailr.SerNr) then begin
          lastmailnumber = lastMailr.SerNr;
        end;
      end else begin
        RecordCopy(lastMailr,Mailr);
        if (lastmailnumber<lastMailr.SerNr) then begin
          lastmailnumber = lastMailr.SerNr;
        end;
      end;
      FTr.SerNr = Mailr.SerNr;
      if (ReadFirstMain(FTr,1,true)) then begin
        if (SetInSet(Confr.SerNr,FTr.RootConf)==false) then begin
          RecordCopy(oldFTr,FTr);
          if (nonblank(FTr.RootConf)) then begin
            FTr.RootConf = FTr.RootConf & ",";
          end;
          FTr.RootConf = FTr.RootConf & Confr.SerNr;
          RecordUpdate(oldFTr,FTr,true);
        end;
      end else begin
        FTr.SerNr = Mailr.SerNr;
        FTr.LastReplyMail = lastMailr.SerNr;
        FTr.RootConf = Confr.SerNr;
        FTr.LastReplyDate = lastMailr.TransDate;
        FTr.LastReplyTime = lastMailr.TransTime;
        FTr.MotherConf = motherconf;
        if (SetInSet(WTb.AnsweredTag,Mailr.Tags)) then begin
          FTr.AnsweredFlag = 1;
        end;
        RecordStore(FTr,true);
      end;
    end;
  end;
  return;
end;

global
updating procedure GenForumSupportMn(record RCVc RepSpec)
begin
  record WebNGForumRecVc FRr;
  record ConfVc Confr;
  record WebNGForumTopicVc FTr,oldFTr;
  Integer topiccnt,postcnt;
  Longint lastmailnumber;

  FTr.SerNr = 999999999;
  while (LoopBackKey("SerNr",FTr,1,true)) begin
    RecordRemove(FTr);
  end;

  while (LoopMain(FRr,1,true)) begin
    Confr.AddrName = FRr.ForumRootConf;
    if (ReadFirstKey("AddrName",Confr,1,true)) then begin
      GenForumSupportSub(Confr,-1,lastmailnumber,topiccnt,postcnt);
    end;
  end;

  return;
end;

updating procedure GenForumSubscriptionsSub(record RCVc RepSpec,record ConfVc Confr)
begin
  string 255 ckey;
  Boolean foundf,testf;
  record CUVc CUr;
  record ConfVc Conf2r;
  row MailVc Mailrw;
  record MailVc Mailr


  if (Confr.Class==kConfClassFolder) then begin//if list of conferences
    foundf = true;
    ckey = "SubConfSortorder:" & Confr.SerNr;
    Conf2r.Sortorder = "";
    while (LoopKey(ckey,Conf2r,1,foundf)) begin
      testf = foundf;
      if (testf) then begin
        GenForumSubscriptionsSub(RepSpec,Conf2r);
      end;
    end;
  end else begin
    foundf = true;
    ckey = "UserTime:" & Confr.SerNr;
    Mailr.TransDate = RepSpec.sStartDate;
    while (LoopKey(ckey,Mailr,1,foundf)) begin
      if (Mailr.TransDate>RepSpec.sEndDate) then begin
        foundf = false;
      end else begin
        MatRowGet(Mailr,0,Mailrw);
        CUr.UUID = StringToUUID(Mailrw.AddrCode);
        if (ReadFirstKey("UUID",CUr,1,true)) then begin
          SubscribeForForumMail2(Mailr.SerNr,0,CUr.Code,true);
        end;
      end;
    end;
  end;

  return;
end;

global
updating procedure GenForumSubscriptionMn(record RCVc RepSpec)
begin
  record ConfVc Confr;
  record WebNGForumRecVc FRr;

  while (LoopMain(FRr,1,true)) begin
    Confr.AddrName = FRr.ForumRootConf;
    if (ReadFirstKey("AddrName",Confr,1,true)) then begin
      GenForumSubscriptionsSub(RepSpec,Confr);
    end;
  end;

  return;
end;
