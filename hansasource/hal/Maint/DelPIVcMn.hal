external procedure CheckFlush(var integer,integer);

global
updating procedure DelPIVcMn(record RcVc RepSpec)
begin
  record PIVc PIr;
  record INVc INr;
  boolean found, testf;
  integer cnt;
  
  found = true;
  PIr.VECode = RepSpec.f2;
  while (LoopKey("Vendor",PIr,1,found)) begin
    if (found) then begin
      if (nonblank(RepSpec.f2)) then begin
        if (PIr.VECode <> RepSpec.f2) then begin
          found = false;
        end;
      end;
    end;
    if (found) then begin
      testf = true;
      if (nonblank(RepSpec.f4)) then begin
        if (PIr.ItemCode <> RepSpec.f4) then begin
          testf = false;
        end;
      end;
      if (nonblank(RepSpec.f3)) or (RepSpec.flags[1] == 1) then begin 
        INr.Code = PIr.ItemCode;
        if (ReadFirstMain(INr,1,true)) then begin end;
        if (nonblank(RepSpec.f3)) then begin
          if (INr.Group <> RepSpec.f3) then begin
            testf = false;
          end;
        end;
        if (RepSpec.flags[1] == 1) then begin
          if (INr.Terminated == 0) then begin
            testf = false;
          end;
        end;
      end;
      if (testf) then begin
        RecordDelete(PIr);
        StepBack(PIr);
        MaintTrace(PIr.ItemCode); 
        CheckFlush(cnt,25);
      end;
    end;
  end; 
  return;
 end;
