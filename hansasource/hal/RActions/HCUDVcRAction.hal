global
function Boolean FindHCUD(string custcode,Date td,string resstatus,var record HCUDVc resHCUDr)
begin
  record HCUDVc HCUDr;
  Boolean found,res;
  record HotelBlock Hotelb;

  BlockLoad(Hotelb);
  if (resstatus==Hotelb.ChInSt) or (resstatus==Hotelb.ChOutSt) or (resstatus==Hotelb.BlockStatus) then begin
    goto LFindHCUD;
  end;
  RecordNew(resHCUDr);
  found = true;
  HCUDr.CUCode = custcode;
  while (LoopBackKey("ActCUCode",HCUDr,1,found)) begin
    if (HCUDr.CUCode!=custcode) then begin found = false; end;
    if (found) then begin
      if (nonblankdate(HCUDr.EndDate)) then begin
        if (DateInRange(td,HCUDr.StartDate,HCUDr.EndDate)) then begin
          res = true;
          RecordCopy(resHCUDr,HCUDr);
          goto LFindHCUD;
        end;
      end else begin
        if (td>=HCUDr.StartDate) then begin
          res = true;
          RecordCopy(resHCUDr,HCUDr);
          goto LFindHCUD;
        end;
      end;
    end;
  end;
LFindHCUD:;  
  FindHCUD = res;
  return;
end;

global
function LongInt HCUDVcRecordInIndex(record HCUDVc HCUDr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (HCUDr.Closed!=0) then begin 
    if (indexname=="ActCUCode") then begin res = 0; end;
  end;
  HCUDVcRecordInIndex = res;
  return;
end;  

global
function LongInt HCUDVcRecordCheck(record HCUDVc HCUDr,record HCUDVc oHCUDr,LongInt stat,LongInt long4)
begin
  LongInt res;
  record CUVc CUr;
  record PDVc PDr;
  record CNDVc CNDr;
  record HCUDVc lHCUDr;
  Boolean found,testf;
      
  res = 0;
  if (blank(HCUDr.CUCode)) then begin
    RecordCheckError(1120,"",-1,"CUCode");      
    res = -1; 
    goto LHCUDVcRecordCheck;
  end;  
  if (blankdate(HCUDr.StartDate)) then begin
    RecordCheckError(1120,"",-1,"StartDate");      
    res = -1; 
    goto LHCUDVcRecordCheck;
  end;  
  if (nonblankdate(HCUDr.EndDate)) then begin
    if (HCUDr.StartDate>HCUDr.EndDate) then begin
      RecordCheckError(2246,"",-1,"EndDate");      
      res = -1; 
      goto LHCUDVcRecordCheck;     
    end;
  end;
  found = true;
  lHCUDr.CUCode = HCUDr.CUCode;
  while (LoopKey("ActCUCode",lHCUDr,1,found)) begin
    if (lHCUDr.CUCode!=HCUDr.CUCode) then begin found = false; end;
    if (found) then begin
      testf = true;
      switch (stat) begin
        case Rs_update:
          if ((lHCUDr.StartDate==oHCUDr.StartDate) and (lHCUDr.EndDate==oHCUDr.EndDate)) then begin 
            if ((HCUDr.StartDate==oHCUDr.StartDate) and (HCUDr.EndDate==oHCUDr.EndDate)) then begin 
              testf = false; 
            end;
          end;
      end;
      if (testf) then begin
        if (DateInRange(HCUDr.StartDate,lHCUDr.StartDate,lHCUDr.EndDate)) then begin
          RecordCheckError(20159,"",-1,"StartDate");      
          res = -1; 
          goto LHCUDVcRecordCheck;     
        end;  
        if (DateInRange(HCUDr.EndDate,lHCUDr.StartDate,lHCUDr.EndDate)) then begin
          RecordCheckError(20159,"",-1,"StartDate");      
          res = -1; 
          goto LHCUDVcRecordCheck;     
        end;  
        if (HCUDr.StartDate<lHCUDr.StartDate) and (HCUDr.EndDate>lHCUDr.EndDate) then begin
          RecordCheckError(20159,"",-1,"StartDate");      
          res = -1; 
          goto LHCUDVcRecordCheck;     
        end;
        if (lHCUDr.StartDate<HCUDr.StartDate) and (lHCUDr.EndDate>HCUDr.EndDate) then begin
          RecordCheckError(20159,"",-1,"StartDate");      
          res = -1; 
          goto LHCUDVcRecordCheck;     
        end;
      end;
    end;
  end;    
  CUr.Code = HCUDr.CUCode;
  if (ReadFIrstMain(CUr,1,true)==false) then begin
    RecordCheckError(20859,"",-1,"CUCode");
    res = -1;
    goto LHCUDVcRecordCheck;
  end;
  if (nonblank(HCUDr.DownPayDeal)) then begin
    PDr.Code = HCUDr.DownPayDeal;
    if (ReadFIrstMain(PDr,1,true)==false) then begin
      RecordCheckError(1958,"",-1,"DownPayDeal");
      res = -1;
      goto LHCUDVcRecordCheck;
    end;
  end;
  if (nonblank(HCUDr.CNDCode)) then begin
    CNDr.Code = HCUDr.CNDCode;
    if (ReadFIrstMain(CNDr,1,true)==false) then begin
      RecordCheckError(2213,"",-1,"CNDCode");
      res = -1;
      goto LHCUDVcRecordCheck;
    end;
  end;
LHCUDVcRecordCheck:;  
  HCUDVcRecordCheck = res;
  return;
end;  

global
function string 255 FindReservationCancellationTerms(string custcode,Date td,string resstatus)
begin
  string 255 cnterm;
  record CUVc CUr;
  record HCUDVc HCUDr;
  record HotelBlock Hotelb;

  BlockLoad(Hotelb);
  if (resstatus==Hotelb.ChInSt) or (resstatus==Hotelb.ChOutSt) or (resstatus==Hotelb.BlockStatus) then begin
    goto LFindReservationCancellationTerms;
  end;
  FindHCUD(custcode,td,resstatus,HCUDr);
  cnterm = HCUDr.CNDCode;
  if (blank(cnterm)) then begin
    CUr.Code = custcode;
    if (ReadFirstMain(CUr,1,true)) then begin
      if (CUr.CustCat==Hotelb.WebDefCustCat) then begin
        cnterm = Hotelb.WebCNDCode;
      end;
    end;
  end;
  if (blank(cnterm)) then begin
    cnterm = Hotelb.CNDCode;
  end;
LFindReservationCancellationTerms:;  
  FindReservationCancellationTerms = cnterm;
  return;
end;  
