external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt SalesForSerBlockCheck(var record SalesForSerBlock SalesForSerr,record SalesForSerBlock SalesForSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row SalesForSerBlock SalesForSerrw;
  row SalesForSerBlock SalesForSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(SalesForSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(SalesForSerr,i,SalesForSerrw);
    if (SalesForSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LSalesForSerBlockCheck;        
    end;
    if (SalesForSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LSalesForSerBlockCheck;        
    end;
    if ((nonblankdate(SalesForSerrw.StartDate)) and (nonblankdate(SalesForSerrw.EndDate))) then begin
      if (SalesForSerrw.StartDate>SalesForSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LSalesForSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(SalesForSerr,j,SalesForSer2rw);
      res = SerBlockCheckOverlap(SalesForSerrw.TSerStart,SalesForSerrw.TSerEnd,SalesForSer2rw.TSerStart,SalesForSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LSalesForSerBlockCheck;        
      end;
    end;        
  end;  
LSalesForSerBlockCheck:;  
  SalesForSerBlockCheck = res;
  return;
end;
