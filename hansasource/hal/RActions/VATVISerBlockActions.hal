external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt VATVISerBlockCheck(var record VATVISerBlock VATVISerr,record VATVISerBlock VATVISer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt;
  row VATVISerBlock VATVISerrw;
  row VATVISerBlock VATVISer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(VATVISerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(VATVISerr,i,VATVISerrw);
    if (VATVISerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LVATVISerBlockCheck;        
    end;
    if (VATVISerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LVATVISerBlockCheck;        
    end;
    if ((nonblankdate(VATVISerrw.StartDate)) and (nonblankdate(VATVISerrw.EndDate))) then begin
      if (VATVISerrw.StartDate>VATVISerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LVATVISerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(VATVISerr,j,VATVISer2rw);
      res = SerBlockCheckOverlap(VATVISerrw.TSerStart,VATVISerrw.TSerEnd,VATVISer2rw.TSerStart,VATVISer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LVATVISerBlockCheck;        
      end;
    end;        
  end;  
LVATVISerBlockCheck:;  
  VATVISerBlockCheck = res;
  return;
end;
