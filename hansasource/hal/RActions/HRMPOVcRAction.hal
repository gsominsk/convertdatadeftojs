external function Boolean IsUnOKAllowed(string,Date);

global
updating function LongInt HRMJPVcRecordCheck(record HRMJPVc HRMJPr,record HRMJPVc HRMJP2r,LongInt stat,LongInt check)
begin
  LongInt oldnr;
  LongInt res;
  record CUVc CUr;
  Date td;
  row HRMJPVc HRMJPrw;
  Integer i,rwcnt;
  record DepVc Depr;
  Boolean transf,unokf;

  res = 0;
  oldnr = HRMJPr.SerNr;

  transf = false;
  if (HRMJPr.OKFlag==1) then begin
    if (stat==Rs_insert) then begin transf = true; end;
    if (stat==Rs_update) then begin
      if (HRMJP2r.OKFlag==0) then begin transf = true; end;
    end;
  end;
  if (HRMJPr.OKFlag==0) then begin//unok
    if (stat==Rs_update) then begin
      if (HRMJP2r.OKFlag==1) then begin unokf = true; end;
    end;
  end;
  if (unokf) then begin
    if (IsUnOKAllowed("HRMJPVc",HRMJPr.StartDate)==false) then begin
      RecordCheckError(1046,"",-1,"StartDate");      
      res = -1; 
    end;    
    goto LHRMJPVcRecordCheck;
  end;
  if (transf) then begin
    if (UserCanAction("HRMJPOK",true)==false) then begin
      RecordCheckError(1274,StringFromStringSet(3,"HRMJPOK"),-1,"SerNr");      
      res = -1; 
      goto LHRMJPVcRecordCheck;
    end;
  end;  

  if (HRMJPr.SerNr<=0) then begin
    td = HRMJPr.StartDate;
    if (blankdate(td)) then begin
      td = CurrentDate;
    end;
    HRMJPr.SerNr = NextSerNr("HRMJPVc",td,-1,false,"");
  end;
  if (nonblankdate(HRMJPr.EndDate)) then begin
    if (HRMJPr.EndDate<HRMJPr.StartDate) then begin
      RecordCheckError(1142,"",-1,"EndDate");      
      res = -1; 
      goto LHRMJPVcRecordCheck;
    end;
  end;
  if (nonblank(HRMJPr.DepCode)) then begin
    Depr.Code = HRMJPr.DepCode;
    if (ReadFirstMain(Depr,1,true)==false) then begin
      RecordCheckError(1231,"",i,"DepCode");      
      res = -1;
      goto LHRMJPVcRecordCheck;
    end;
  end;
  rwcnt = MatRowCnt(HRMJPr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(HRMJPr,i,HRMJPrw);
    CUr.Code = HRMJPrw.Employee;
    if (ReadFirstMain(CUr,1,true)==false) then begin
      RecordCheckError(1120,HRMJPrw.Employee,i,"Employee");      
      res = -1;
      goto LHRMJPVcRecordCheck;
    end;
    if (CUr.EmployeeType==0) then begin
      RecordCheckError(1120,HRMJPrw.Employee,i,"Employee");      
      res = -1; 
      goto LHRMJPVcRecordCheck;
    end;
    if (CUr.blockedFlag!=0) then begin
      RecordCheckError(1265,HRMJPrw.Employee,i,"Employee");      
      res = -1;
      goto LHRMJPVcRecordCheck;
    end;
  end;
LHRMJPVcRecordCheck:;  
  if (res!=0) then begin HRMJPr.SerNr = oldnr; end;
  HRMJPVcRecordCheck = res;
  return;
end;