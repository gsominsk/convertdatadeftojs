external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt PayPlanSerBlockCheck(var record PayPlanSerBlock PayPlanSerr,record PayPlanSerBlock PayPlanSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row PayPlanSerBlock PayPlanSerrw;
  row PayPlanSerBlock PayPlanSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(PayPlanSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(PayPlanSerr,i,PayPlanSerrw);
    if (PayPlanSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LPayPlanSerBlockCheck;        
    end;
    if (PayPlanSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LPayPlanSerBlockCheck;        
    end;
    if ((nonblankdate(PayPlanSerrw.StartDate)) and (nonblankdate(PayPlanSerrw.EndDate))) then begin
      if (PayPlanSerrw.StartDate>PayPlanSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LPayPlanSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(PayPlanSerr,j,PayPlanSer2rw);
      res = SerBlockCheckOverlap(PayPlanSerrw.TSerStart,PayPlanSerrw.TSerEnd,PayPlanSer2rw.TSerStart,PayPlanSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LPayPlanSerBlockCheck;        
      end;
    end;        
  end;  
LPayPlanSerBlockCheck:;  
  PayPlanSerBlockCheck = res;
  return;
end;
