external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function string 255 GetNextValue(var string);

global
function LongInt MultiBuyRebVcRecordCheck(var record MultiBuyRebVc MBRr,record MultiBuyRebVc MBR2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record INVc INr;
  record PLDefVc PLDefr;
  record LocationVc Locationr;
  string 10 mlocation,location;
  Integer i,rwcnt;
  row MultiBuyRebVc MBRrw;

  res = 0;
  if (blank(MBRr.Code)) then begin
    RecordCheckError(1058,"",-1,"Code");    
    res = -1;
    goto LMultiBuyRebVcRecordCheck;
  end;  
  if (blank(MBRr.RebArtCode)) then begin
    RecordCheckError(1058,"",-1,"RebArtCode");    
    res = -1;
    goto LMultiBuyRebVcRecordCheck;
  end;  
  if (nonblank(MBRr.PLCode)) then begin
    PLDefr.Code = MBRr.PLCode;
    if (ReadFIrstMain(PLDefr,1,true)==false) then begin
      RecordCheckError(1290,"",-1,"PLCode");    
      res = -1;
      goto LMultiBuyRebVcRecordCheck;
    end;
  end;  
  if (nonblank(MBRr.FixedPrice)) then begin
    if (nonblank(MBRr.FixedDiscount)) then begin
      RecordCheckError(25602,"",-1,"FixedDiscount");    
      res = -1;
      goto LMultiBuyRebVcRecordCheck;
    end;
  end;

  mlocation = MBRr.Location;
  location = GetNextValue(mlocation);
  while (nonblank(location)) begin
    Locationr.Code = location;
    if (ReadFirstMain(Locationr,1,true)==false) then begin
      RecordCheckError(1120,location,-1,"Location");    
      res = -1;
      goto LMultiBuyRebVcRecordCheck;
    end;
    location = GetNextValue(mlocation);
  end;  
  if (ReadFirstItem(MBRr.RebArtCode,INr,true,false)==false) then begin
    RecordCheckError(1120,MBRrw.RebArtCode,-1,"RebArtCode");      
    res = -1; 
    goto LMultiBuyRebVcRecordCheck;
  end;
  rwcnt = MatRowCnt(MBRr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(MBRr,i,MBRrw);
    if (ReadFirstItem(MBRrw.ArtCode,INr,true,false)==false) then begin
      RecordCheckError(1120,MBRrw.ArtCode,i,"ArtCode");      
      res = -1; 
      goto LMultiBuyRebVcRecordCheck;
    end;
    if (nonblank(MBRrw.RebArtCode)) then begin
      if (ReadFirstItem(MBRrw.RebArtCode,INr,true,false)==false) then begin
        RecordCheckError(1120,MBRrw.RebArtCode,i,"RebArtCode");      
        res = -1; 
        goto LMultiBuyRebVcRecordCheck;
      end;
    end;
  end;
LMultiBuyRebVcRecordCheck:;
  MultiBuyRebVcRecordCheck = res;
  return;
end;

global
function LongInt MultiBuyRebVcRecordInIndex(record MultiBuyRebVc MBRr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (MBRr.Closed!=0) then begin 
    if (indexname=="ActArtCode") then begin res = 0; end;
  end;
  MultiBuyRebVcRecordInIndex = res;
  return;
end;
