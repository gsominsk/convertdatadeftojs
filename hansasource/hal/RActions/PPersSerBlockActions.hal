external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt PPersSerBlockCheck(var record PPersSerBlock PPersSerr,record PPersSerBlock PPersSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row PPersSerBlock PPersSerrw;
  row PPersSerBlock PPersSer2rw;
  record MainCLBlock MainCLRec;
  record PMBlock PMRec;
  row PMBlock PMrw;  
  
  res = 0;
  BlockLoad(PMRec);
  BlockLoad(MainCLRec);
  pmrwcnt = MatRowCnt(PMRec);
  rwcnt = MatRowCnt(PPersSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(PPersSerr,i,PPersSerrw);
    if (PPersSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LPPersSerBlockCheck;        
    end;
    if (PPersSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LPPersSerBlockCheck;        
    end;
    if ((nonblankdate(PPersSerrw.StartDate)) and (nonblankdate(PPersSerrw.EndDate))) then begin
      if (PPersSerrw.StartDate>PPersSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LPPersSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(PPersSerr,j,PPersSer2rw);
      res = SerBlockCheckOverlap(PPersSerrw.TSerStart,PPersSerrw.TSerEnd,PPersSer2rw.TSerStart,PPersSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LPPersSerBlockCheck;        
      end;
    end;    
    if (MainCLRec.CommonNoSer!=0) then begin
      for (j=0; j<pmrwcnt;j=j+1) begin
        MatRowGet(PMRec,j,PMrw);
        if ((PPersSerrw.TSerStart>=PMrw.TSerStartIn) and (PPersSerrw.TSerStart<=PMrw.TSerEndIn)) then begin
          RecordCheckError(1564,"",i,"TSerStart");   
          res = -1;
          goto LPPersSerBlockCheck;        
        end;
        if ((PPersSerrw.TSerEnd>=PMrw.TSerStartIn) and (PPersSerrw.TSerEnd<=PMrw.TSerEndIn)) then begin
          RecordCheckError(1564,"",i,"TSerEnd");   
          res = -1;
          goto LPPersSerBlockCheck;        
        end;
        if ((PPersSerrw.TSerStart>=PMrw.TSerStartOut) and (PPersSerrw.TSerStart<=PMrw.TSerEndOut)) then begin
          RecordCheckError(1564,"",i,"TSerStart");   
          res = -1;
          goto LPPersSerBlockCheck;        
        end;
        if ((PPersSerrw.TSerEnd>=PMrw.TSerStartOut) and (PPersSerrw.TSerEnd<=PMrw.TSerEndOut)) then begin
          RecordCheckError(1564,"",i,"TSerEnd");   
          res = -1;
          goto LPPersSerBlockCheck;        
        end;        
      end;      
    end;  
  end;  
LPPersSerBlockCheck:;  
  PPersSerBlockCheck = res;
  return;
end;
