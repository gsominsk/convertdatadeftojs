global
function LongInt INEffVcRecordCheck(var record INEffVc INEffr,record INEffVc INEff2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record INVc INr;
  record CUVc VEr;  
  Integer insertmode,updatemode;
  Boolean transf;
 
  res = 0;
  insertmode = 1;
  updatemode = 2;
  transf = false;
  if (INEffr.OKFlag==1) then begin
    if (stat==insertmode) then begin transf = true; end;
    if (stat==updatemode) then begin
      if (INEff2r.OKFlag==0) then begin transf = true; end;
    end;  
  end;  
  if (blank(INEffr.ArtCode)) then begin
    RecordCheckError(1130,"",-1,"ArtCode");    
    res = -1;
    goto LINEffVcRecordCheck;
  end;  
  if (blank(INEffr.VECode)) then begin
    RecordCheckError(1205,"",-1,"VECode");    
    res = -1;
    goto LINEffVcRecordCheck;
  end;  
  if (INEffr.EndDate<INEffr.TransDate) then begin
    RecordCheckError(1142,"",-1,"EndDate");    
    res = -1;
    goto LINEffVcRecordCheck;
  end;
  if (transf) then begin
    if (blankdate(INEffr.TransDate)) then begin
      RecordCheckError(1024,"",-1,"TransDate");    
      res = -1;
      goto LINEffVcRecordCheck;
    end;
    if (INEffr.UsedQty==blankval) then begin
      RecordCheckError(1854,"",-1,"UsedQty");    
      res = -1;
      goto LINEffVcRecordCheck;
    end;
    if (INEffr.RcvQty==blankval) then begin
      RecordCheckError(1854,"",-1,"RcvQty");    
      res = -1;
      goto LINEffVcRecordCheck;
    end;
  end;
  INr.Code = INEffr.ArtCode;
  if (ReadFirstMain(INr,1,true)==false) then begin
    RecordCheckError(1233,"",-1,"ArtCode");    
    res = -1;
    goto LINEffVcRecordCheck;
  end;
  VEr.Code = INEffr.VECode;
  if (ReadFirstMain(VEr,1,true)==false) then begin
    RecordCheckError(1233,"",-1,"VECode");    
    res = -1;
    goto LINEffVcRecordCheck;
  end;
LINEffVcRecordCheck:;  
  INEffVcRecordCheck = res;
  RETURN;
END;

global
function LongInt INEffVcRecordDefaults(var record INEffVc INEffr,record INEffVc INEff2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Time blankt;
    
  if (SingleUserMode) then begin
    INEffr.SerNr = NextSerNr("INEffVc",INEffr.TransDate,-1,false,"");
  end;
  INEffr.UsedQty = blankval;
  INEffr.RcvQty = blankval;
  INEffr.TransDate = CurrentDate;
  INEffr.StartTime = blankt;
  INEffr.EndTime = blankt;
  INEffVcRecordDefaults = res; 
  RETURN;
END;

global
function LongInt INEffVcRecordDuplicate(var record INEffVc INEffr,record INEffVc INEff2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
    
  if (SingleUserMode) then begin
    INEffr.SerNr = NextSerNr("INEffVc",INEffr.TransDate,-1,false,"");
  end;
  INEffr.TransDate = CurrentDate;
  INEffr.OKFlag = 0;
  INEffVcRecordDuplicate = res; 
  RETURN;
END;

global
function LongInt INEffVcRecordRemoveTest(var record INEffVc INEffr,record INEffVc INEff2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  res = 1;
  if (INEffr.OKFlag!=0) then begin
    res = 0;
  end;    
  INEffVcRecordRemoveTest = res; 
  RETURN;
END;
