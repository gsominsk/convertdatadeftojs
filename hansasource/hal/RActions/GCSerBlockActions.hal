external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt GCSerBlockCheck(var record GCSerBlock GCSerr,record GCSerBlock GCSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row GCSerBlock GCSerrw;
  row GCSerBlock GCSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(GCSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(GCSerr,i,GCSerrw);
    if (GCSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LGCSerBlockCheck;        
    end;
    if (GCSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LGCSerBlockCheck;        
    end;
    if ((nonblankdate(GCSerrw.StartDate)) and (nonblankdate(GCSerrw.EndDate))) then begin
      if (GCSerrw.StartDate>GCSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LGCSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(GCSerr,j,GCSer2rw);
      res = SerBlockCheckOverlap(GCSerrw.TSerStart,GCSerrw.TSerEnd,GCSer2rw.TSerStart,GCSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LGCSerBlockCheck;        
      end;
    end;        
  end;  
LGCSerBlockCheck:;  
  GCSerBlockCheck = res;
  return;
end;
