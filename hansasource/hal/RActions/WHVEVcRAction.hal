external procedure ApplyWHVETaxExcemption(Date,var record WHVEVc);
external function Boolean GetWHTaxRow(string,var row WHTaxBlock);
external function Boolean GetPMRow(string,var row PMBlock);

global
function Boolean FindWHVETax(string vecode,Date td,var record WHVEVc resWHVEr)
begin
  record WHVEVc WHVEr;
  Boolean found,res;

  RecordNew(resWHVEr);
  found = true;
  WHVEr.VECode = vecode;
  while (LoopBackKey("ActVECode",WHVEr,1,found)) begin
    if (WHVEr.VECode!=vecode) then begin found = false; end;
    if (found) then begin
      if (nonblankdate(WHVEr.EndDate)) then begin
        if (DateInRange(td,WHVEr.StartDate,WHVEr.EndDate)) then begin
          res = true;
          RecordCopy(resWHVEr,WHVEr);
          goto LFindWHVE;
        end;
      end else begin
        if (td>=WHVEr.StartDate) then begin
          res = true;
          RecordCopy(resWHVEr,WHVEr);
          goto LFindWHVE;
        end;
      end;
    end;
  end;
LFindWHVE:;  
  if (res) then begin
    ApplyWHVETaxExcemption(td,resWHVEr);
  end;
  FindWHVETax = res;
  return;
end;

global
function Boolean FindWHVETaxInclClosed(string vecode,Date td,var record WHVEVc resWHVEr)
begin
  record WHVEVc WHVEr;
  Boolean found,res;

  RecordNew(resWHVEr);
  found = true;
  WHVEr.VECode = vecode;
  while (LoopBackKey("VECode",WHVEr,1,found)) begin
    if (WHVEr.VECode!=vecode) then begin found = false; end;
    if (found) then begin
      if (nonblankdate(WHVEr.EndDate)) then begin
        if (DateInRange(td,WHVEr.StartDate,WHVEr.EndDate)) then begin
          res = true;
          RecordCopy(resWHVEr,WHVEr);
          goto LFindWHVETaxInclClosed;
        end;
      end else begin
        if (td>=WHVEr.StartDate) then begin
          res = true;
          RecordCopy(resWHVEr,WHVEr);
          goto LFindWHVETaxInclClosed;
        end;
      end;
    end;
  end;
LFindWHVETaxInclClosed:;  
  FindWHVETaxInclClosed = res;
  return;
end;

global
function LongInt WHVEVcRecordInIndex(record WHVEVc WHVEr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (WHVEr.Closed!=0) then begin 
    if (indexname=="ActVECode") then begin res = 0; end;
  end;
  WHVEVcRecordInIndex = res;
  return;
end;  

global
function LongInt WHVEVcRecordCheck(record WHVEVc WHVEr,record WHVEVc oWHVEr,LongInt stat,LongInt long4)
begin
  LongInt res;
  record CUVc VEr;
  record PDVc PDr;
  record CNDVc CNDr;
  record WHVEVc lWHVEr;
  Boolean found,testf;
  record APAccBlock APb;
  row WHTaxBlock WHTbrw;
  row PMBlock PMrw;
  row WHVEVc WHVErw;
  Integer i,rwcnt;
      
  BlockLoad(APb);    
  res = 0;
  if (blank(WHVEr.VECode)) then begin
    RecordCheckError(1120,"",-1,"VECode");      
    res = -1; 
    goto LWHVEVcRecordCheck;
  end;  
  if (blankdate(WHVEr.StartDate)) then begin
    RecordCheckError(32006,"",-1,"StartDate");      
    res = -1; 
    goto LWHVEVcRecordCheck;
  end;  
  if (nonblankdate(WHVEr.EndDate)) then begin
    if (WHVEr.StartDate>WHVEr.EndDate) then begin
      RecordCheckError(2246,"",-1,"EndDate");      
      res = -1; 
      goto LWHVEVcRecordCheck;     
    end;
  end;
  found = true;
  lWHVEr.VECode = WHVEr.VECode;
  while (LoopKey("ActVECode",lWHVEr,1,found)) begin
    if (lWHVEr.VECode!=WHVEr.VECode) then begin found = false; end;
    if (found) then begin
      testf = true;
      switch (stat) begin
        case Rs_update:
          if ((lWHVEr.StartDate==oWHVEr.StartDate) and (lWHVEr.EndDate==oWHVEr.EndDate)) then begin 
            testf = false; 
          end;
      end;
      if (testf) then begin
        if (DateInRange(WHVEr.StartDate,lWHVEr.StartDate,lWHVEr.EndDate)) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHVEVcRecordCheck;     
        end;  
        if (DateInRange(WHVEr.EndDate,lWHVEr.StartDate,lWHVEr.EndDate)) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHVEVcRecordCheck;     
        end;  
        if (WHVEr.StartDate<lWHVEr.StartDate) and (WHVEr.EndDate>lWHVEr.EndDate) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHVEVcRecordCheck;     
        end;
        if (lWHVEr.StartDate<WHVEr.StartDate) and ((lWHVEr.EndDate>WHVEr.EndDate) and (nonblankdate(WHVEr.EndDate))) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHVEVcRecordCheck;     
        end;
      end;
    end;
  end;    
  VEr.Code = WHVEr.VECode;
  if (ReadFIrstMain(VEr,1,true)==false) then begin
    RecordCheckError(20859,"",-1,"VECode");
    res = -1;
    goto LWHVEVcRecordCheck;
  end;
  if (APb.PMControlWithTax==1) then begin 
    rwcnt = MatRowCnt(WHVEr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(WHVEr,i,WHVErw);
      if nonblank(WHVErw.Region) then begin 
        if blank(WHVErw.WHTax) then begin 
          RecordCheckError(1058,"",i,"WHTax");
          res = -1;
          goto LWHVEVcRecordCheck;
        end else begin
          GetWHTaxRow(WHVErw.WHTax,WHTbrw);
          if blank(WHTbrw.PayMode) then begin 
            RecordCheckError(28703,"",i,"WHTax");
            res = -1;
            goto LWHVEVcRecordCheck;
          end else begin  
            GetPMRow(WHTbrw.PayMode,PMrw);
            if (PMrw.CheckType!=kPayModeTypeRegionalWithholding) then begin 
              RecordCheckError(28703,"",i,"WHTax");
              res = -1;
              goto LWHVEVcRecordCheck;
            end;
          end;  
        end;
      end else begin
        if nonblank(WHVErw.WHTax) then begin 
          GetWHTaxRow(WHVErw.WHTax,WHTbrw);
          if nonblank(WHTbrw.PayMode) then begin 
            GetPMRow(WHTbrw.PayMode,PMrw);
            if (PMrw.CheckType==kPayModeTypeRegionalWithholding) then begin 
              RecordCheckError(28704,"",i,"Region");
              res = -1;
              goto LWHVEVcRecordCheck;
            end;
          end;  
        end;
      end;
    end;
  end;  
LWHVEVcRecordCheck:;  
  WHVEVcRecordCheck = res;
  return;
end;  
