global
function LongInt RPTaxVcRecordCheck(record RPTaxVc RPr,record RPTaxVc RP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record RPTaxVc oldRPTaxr;
  record RegionVc Regionr;
  record CommodityVc Commodityr;
  Integer i,j,rwcnt;
  row RPTaxVc RPTaxrw;
  row RPTaxVc RPTax2rw;
  Boolean found;      
  
  res = 0;
  if (blank(RPr.Code)) then begin
    RecordCheckError(1120,"",-1,"Code");      
    res = -1; 
    goto LRPTaxVcRecordCheck;
  end;  
  Regionr.Code = RPr.Region;
  if (ReadFirstMain(Regionr,1,true)==false) then begin
    RecordCheckError(1120," " & RPr.Region,-1,"Region");      
    res = -1; 
    goto LRPTaxVcRecordCheck;
  end;
  rwcnt = MatRowCnt(RPr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(RPr,i,RPTaxrw);
    Commodityr.Number = RPTaxrw.EUCodex;
    if (ReadFirstMain(Commodityr,1,true)==false) then begin
      RecordCheckError(1120," " & RPTaxrw.EUCodex,i,"EUCodex");      
      res = -1; 
      goto LRPTaxVcRecordCheck;
    end;
    for (j=i+1;j<rwcnt;j=j+1) begin
      MatRowGet(RPr,j,RPTax2rw);
      if (RPTax2rw.EUCodex==RPTaxrw.EUCodex) then begin
        RecordCheckError(1547," " & RPTaxrw.EUCodex,i,"EUCodex");      
        res = -1; 
        goto LRPTaxVcRecordCheck;
      end;
    end;
  end;
  oldRPTaxr.Region = RPr.Region;
  found = true;
  while (LoopKey("Region",oldRPTaxr,1,found)) begin
    if (oldRPTaxr.Code!=RPr.Code) then begin
      if (oldRPTaxr.Region==RPr.Region) then begin
        RecordCheckError(1547," " & oldRPTaxr.Code,-1,"Region");      
        res = -1; 
        goto LRPTaxVcRecordCheck;
      end;
    end;
  end;
LRPTaxVcRecordCheck:;  
  RPTaxVcRecordCheck = res;
  RETURN;
END;  
