external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt AT2TakeSerBlockCheck(var record AT2TakeSerBlock AT2TakeSerr,record AT2TakeSerBlock AT2TakeSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row AT2TakeSerBlock AT2TakeSerrw;
  row AT2TakeSerBlock AT2TakeSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(AT2TakeSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(AT2TakeSerr,i,AT2TakeSerrw);
    if (AT2TakeSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LAT2TakeSerBlockCheck;        
    end;
    if (AT2TakeSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LAT2TakeSerBlockCheck;        
    end;
    if ((nonblankdate(AT2TakeSerrw.StartDate)) and (nonblankdate(AT2TakeSerrw.EndDate))) then begin
      if (AT2TakeSerrw.StartDate>AT2TakeSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LAT2TakeSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(AT2TakeSerr,j,AT2TakeSer2rw);
      res = SerBlockCheckOverlap(AT2TakeSerrw.TSerStart,AT2TakeSerrw.TSerEnd,AT2TakeSer2rw.TSerStart,AT2TakeSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LAT2TakeSerBlockCheck;        
      end;      
    end;        
  end;  
LAT2TakeSerBlockCheck:;  
  AT2TakeSerBlockCheck = res;
  return;
end;
