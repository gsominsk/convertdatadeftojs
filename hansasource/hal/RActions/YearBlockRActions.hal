global
updating function LongInt YearBlockCheck(var record YearBlock Yearr,record YearBlock Year2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer i,j,rwcnt;
  row YearBlock Yearrw;
  row YearBlock Year2rw;
  Date nd;
  record TRVc TRr;
  
  res = 0;     
  rwcnt = MatRowCnt(Yearr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Yearr,i,Yearrw);
    if (blank(Yearrw.YearCode)) then begin     
      RecordCheckError(1058,"",i,"YearCode");   
      res = -1;
      goto LYearBlockCheck;
    end;
    if (i==(rwcnt-1)) then begin
      nd.year = 2200;
      nd.month = 12;
      nd.day = 31;
      TRr.TransDate = nd;
      LoopBackKey("TransDate",TRr,1,true);
      if (nonblankdate(TRr.TransDate)) and (TRr.TransDate!=nd) then begin
        if (DateInRange(TRr.TransDate,Yearrw.StartDate,nd)) then begin
          if (TRr.TransDate>Yearrw.EndDate) then begin
            RecordCheckError(22262,"",i,"EndDate");   
            res = -1;
            goto LYearBlockCheck;
          end;
        end;
      end;
    end;
    
    for (j=0;j<i;j=j+1) begin
      MatRowGet(Yearr,j,Year2rw);
      if (Year2rw.YearCode==Yearrw.YearCode) then begin
        RecordCheckError(1545,"",i,"YearCode");      
        res = -1;
        goto LYearBlockCheck;
      end;
    end;
    if (i>0) then begin
      MatRowGet(Yearr,i-1,Year2rw);
      nd = AddDay(Year2rw.EndDate,1);
      if (nd!=Yearrw.StartDate) then begin
        RecordCheckError(1076,"",i,"StartDate");      
        res = -1;
        goto LYearBlockCheck;
      end;
    end;
    if (Yearrw.StartDate>=Yearrw.EndDate) then begin
      RecordCheckError(1076,"",i,"EndDate");      
      res = -1;
      goto LYearBlockCheck;
    end;
  end;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Yearr,i,Yearrw);
    if (Yearrw.IntYc==0) then begin
      if (i>0) then begin
        MatRowGet(Yearr,i-1,Year2rw);
        if (Year2rw.IntYc!=0) then begin
          Yearrw.IntYc = Year2rw.IntYc + 5;
          MatRowPut(Yearr,i,Yearrw);
        end;
      end;
    end;
  end;
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(Yearr,i,Yearrw);
    if (Yearrw.IntYc==0) then begin
      if ((i==rwcnt-1)) then begin
        Yearrw.IntYc = 50+(rwcnt-1)*5;
      end;
      if (i<rwcnt-1) then begin
        MatRowGet(Yearr,i+1,Year2rw);
        if (Year2rw.IntYc!=0) then begin
          Yearrw.IntYc = Year2rw.IntYc - 5;
        end;
      end;
      MatRowPut(Yearr,i,Yearrw);
    end;
  end;
LYearBlockCheck:;
  YearBlockCheck = res;
  RETURN;
END;
