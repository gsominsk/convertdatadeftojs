external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt TSSerBlockCheck(var record TSSerBlock TSSerr,record TSSerBlock TSSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row TSSerBlock TSSerrw;
  row TSSerBlock TSSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(TSSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(TSSerr,i,TSSerrw);
    if (TSSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LTSSerBlockCheck;        
    end;
    if (TSSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LTSSerBlockCheck;        
    end;
    if ((nonblankdate(TSSerrw.StartDate)) and (nonblankdate(TSSerrw.EndDate))) then begin
      if (TSSerrw.StartDate>TSSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LTSSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(TSSerr,j,TSSer2rw);
      res = SerBlockCheckOverlap(TSSerrw.TSerStart,TSSerrw.TSerEnd,TSSer2rw.TSerStart,TSSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LTSSerBlockCheck;        
      end;
    end;        
  end;  
LTSSerBlockCheck:;  
  TSSerBlockCheck = res;
  return;
end;
