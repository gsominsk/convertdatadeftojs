external function Boolean SerNrTestDAVc(LongInt,Date,var Boolean);
external procedure GetCurUser(var record UserVc);

global
function LongInt DAVcRecordDefaults(var record DAVc DAr,record DAVc DA2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record UserVc Userr;
  record DABlock DAb;
  
  BlockLoad(DAb);
  DAr.SerNr = -1;
  DAr.TransDate = CurrentDate;
  DAr.Person = CurrentUser;
  GetCurUser(Userr);
  DAr.Name = Userr.Name;
  DAr.RateType = DAb.RateType;
  if (SingleUserMode) then begin
    DAr.SerNr = NextSerNr("DAVc",DAr.TransDate,-1,false,"");
  end;
  DAVcRecordDefaults = res; 
  return;
end;

global
function LongInt DAVcRecordDuplicate(var record DAVc DAr,record DAVc DA2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record UserVc Userr;
  
  DAr.SerNr = -1;
  DAr.OKFlag = 0;
  DAr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    DAr.SerNr = NextSerNr("DAVc",DAr.TransDate,-1,false,"");
  end;
  DAVcRecordDuplicate = res; 
  return;
end;

global
function LongInt DAVcRecordCheck(var record DAVc DAr,record DAVc DA2r,LongInt stat,LongInt check)
begin
  LongInt res;
  Boolean unokf,transf,gentrans;
  LongInt oldnr;
  record UserVc Userr;
  record PRVc PRr;
  record CountryVc Countryr;
  Integer i,rwcnt;
  row DAVc DArw;

  res = 0;  
  oldnr = DAr.SerNr; 
  if (DAr.OKFlag==0) then begin
    if (stat==Rs_update) then begin
      if (DA2r.OKFlag==1) then begin unokf = true; end;
    end;
  end;
  transf = false;
  if (DAr.OKFlag==1) then begin
    if (stat==Rs_insert) then begin transf = true; end;
    if (stat==Rs_update) then begin
      if (DA2r.OKFlag==0) then begin transf = true; end;
    end;
  end;
  if (DAr.SerNr<=0) then begin
    DAr.SerNr = NextSerNr("DAVc",DAr.TransDate,-1,false,"");
  end;
  if (SerNrTestDAVc(DAr.SerNr,DAr.TransDate,gentrans)==false) then begin
    if (check>0) then begin
      RecordCheckError(1557,"",-1,"SerNr");      
      res = -1; 
      goto LDAVcRecordCheck;
    end;
  end;
  Userr.Code = DAr.Person;
  if (ReadFirstMain(Userr,1,true)==false) then begin
    RecordCheckError(1290,"",-1,"Person");      
    res = -1;
    goto LDAVcRecordCheck;
  end;
  if (nonblank(DAr.PRCode)) then begin
    PRr.Code = DAr.PRCode;
    if (ReadFirstMain(PRr,1,true)==false) then begin
      RecordCheckError(1290,"",-1,"PRCode");      
      res = -1;
      goto LDAVcRecordCheck;
    end;
  end;
  rwcnt = MatRowCnt(DAr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(DAr,i,DArw);
    Countryr.Code = DArw.Country;
    if (ReadFirstMain(Countryr,1,true)==false) then begin
      RecordCheckError(1290,"",i,"Country");      
      res = -1;
      goto LDAVcRecordCheck;
    end;
  end;
LDAVcRecordCheck:;
  if (res!=0) then begin
    DAr.SerNr = oldnr;
  end;
  DAVcRecordCheck = res; 
  return;
end;
