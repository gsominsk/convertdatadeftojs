external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
function LongInt DASerBlockCheck(var record DASerBlock DASerr,record DASerBlock DASer2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row DASerBlock DASerrw;
  row DASerBlock DASer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(DASerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(DASerr,i,DASerrw);
    if (DASerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LDASerBlockCheck;        
    end;
    if (DASerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LDASerBlockCheck;        
    end;
    if ((nonblankdate(DASerrw.StartDate)) and (nonblankdate(DASerrw.EndDate))) then begin
      if (DASerrw.StartDate>DASerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LDASerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(DASerr,j,DASer2rw);
      res = SerBlockCheckOverlap(DASerrw.TSerStart,DASerrw.TSerEnd,DASer2rw.TSerStart,DASer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LDASerBlockCheck;        
      end;      
    end;        
  end;  
LDASerBlockCheck:;  
  DASerBlockCheck = res;
  return;
end;
