external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt RestAccSerBlockCheck(var record RestAccSerBlock RestAccSerr,record RestAccSerBlock RestAccSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row RestAccSerBlock RestAccSerrw;
  row RestAccSerBlock RestAccSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(RestAccSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(RestAccSerr,i,RestAccSerrw);
    if (RestAccSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LRestAccSerBlockCheck;        
    end;
    if (RestAccSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LRestAccSerBlockCheck;        
    end;
    if ((nonblankdate(RestAccSerrw.StartDate)) and (nonblankdate(RestAccSerrw.EndDate))) then begin
      if (RestAccSerrw.StartDate>RestAccSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LRestAccSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(RestAccSerr,j,RestAccSer2rw);
      res = SerBlockCheckOverlap(RestAccSerrw.TSerStart,RestAccSerrw.TSerEnd,RestAccSer2rw.TSerStart,RestAccSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LRestAccSerBlockCheck;        
      end;
    end;        
  end;  
LRestAccSerBlockCheck:;  
  RestAccSerBlockCheck = res;
  return;
end;
