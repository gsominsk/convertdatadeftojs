external procedure B1ToB2StrValRM(string,val,val,var string,roundmode);
external procedure B1ToB2ValRM(val,val,val,var val,roundmode);
external function roundmode GetCostRoundModeRB();
external updating function LongInt POVcRecordUpdate(var record POVc,record POVc,LongInt,LongInt);
external updating function LongInt POVcRecordUpdateAfter(var record POVc,record POVc,LongInt,LongInt);
external procedure FindTempStockRow(LongInt,string,Boolean,record TempStockVc,var row TempStockVc);
external function Boolean StockRecordForLocationAllowed(string,string,string,date,integer,var Integer,var string);
external function Boolean HasIntegratedNL();
external procedure VerifyRowObjects(String,String,String,String,var Integer,var String,var Boolean,Array string,Array string,var Integer);
external updating function Integer CreateVIFromPO(LongInt,record RcVc,var record VIVc);
external updating procedure FindAcptRulesAndCreateAcceptanceAlert(Integer,Integer,string,string,string,val,string,string,string,string);
external updating procedure CancelApprovalRequestActivities(Integer,string,string,string);
external function Boolean IsDigit(string);
external function string 255 NextSerialNumber(string,string,record SerNrTrackBlock);
external function Boolean SerialNrEverinStock(string,string);
external function Boolean AcceptanceRulesExists(Integer,string);
external updating procedure PUVc_UpdateTR(record PUVc,record PUVc);
external function Boolean DisallowFutureDateCheck(Boolean,Date,string,Integer);
external updating procedure UpdateTrans_Stock(record TRVc);
external function Integer IsUnOKAllowed_PUVc(record PUVc);
external updating procedure DeleteTransaction(LongInt,Integer);
external function Boolean ItemHistExists(string,LongInt);
external updating procedure StoreUnOKHistory(string,LongInt,Date,Time,string);
external updating procedure UpdateRecalcStockNeeded(Integer);
external updating function Boolean BA_GRNCostVarianceWarning(record PUVc,var Integer);
external procedure FindBatchBestBeforeDate(string,string,var Date);
external procedure B1ToB2StrVal(string,val,val,var string);
external procedure B1ToB2Val(val,val,val,var val);
external procedure SwapM4Val(var val,var val);
external function Integer ArtCodePrimaryCostModel(string);
external function Integer FIFOPerSerialNr(record INVc,record CostAccBlock);
external function Boolean CanOKStockRecord(var Integer);
external function Boolean IsStockSettingsOK();
external updating procedure AddTTrans_PUVc(record TRVc,record PUVc);
external updating procedure UpdateStockResFromPU(record PUVc,record PUVc);
external function Integer CheckRates(string,val,val,val,val,val,var string);
external procedure FillAmForVariancePU(record PUVc);
external updating procedure UpdateVarianceStatusPU(record PUVc);
external function Integer CheckObjs(string,string,var string);
external procedure StockMovSumUp(var record StockMovVc);
external function Integer IsPositionFree(string);
external updating procedure RemovePU_PORows(var record PUVc);
external updating procedure SetPositionStatus(string,Integer);
external function string 20 FindFreePositionInLocArea(record INVc,string,record LocationVc,string,string);
external function Integer GetPO(LongInt,var record POVc,string,Boolean);
external updating procedure UpdatePOFromPURows(record PUVc,Boolean,record PUVc,Boolean,Boolean);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function Boolean DateWarned(Date,string);
external function Boolean IsSerialNrCorrect(string);
external updating procedure UpdateSerStock(string,string,string,val,val);
external updating procedure InvalidatePU(record PUVc);
external updating procedure InvalidateTR(Integer,LongInt);
external updating procedure PurUpdateOvst(record PUVc,record PUVc);
external function string 255 CheckTrans(var record TRVc,Integer,Boolean);
external updating procedure SaveTrans(record TRVc);
external function Integer MakeTransFromPU(record TRVc,record PUVc,record LocationVc,Boolean);
external updating procedure PurUpdateCostPrice(record PUVc,Boolean);
external updating function Boolean UpdatePOFromPU(record PUVc,record PUVc,Boolean);
external updating procedure PurUpdateSerialNr(record PUVc,Boolean,Boolean);
external updating procedure PurUpdateItemHist(record PUVc);
external updating procedure UpdateInstock(string,string,string,string,date,val,val,val,val,val,val,val,val,val);
external function Boolean ShouldItemUpdateStock(string);
external procedure ConvertToDualBase(var string,date,var val,var val,var val,var val,var val,var val,Boolean);
external updating function val NextLocOKNr(string);
external function Boolean ExistStockTrans(string,Date,var Integer,var string,string,LongInt,record MainStockBlock);
external function Integer SerialNrOnThisPU(record PUVc,string,string);
external function Boolean SerialNrAvail2(string,string,val);
external function Boolean CheckAllowedSize(record INVc,val,val,val);
external function Integer CheckPosition(string,string,Integer,val,val,val);
external function Boolean IsOffice(Boolean);
external function Boolean Date2Test(string,Date,string,Integer);
external function Boolean SerNrTestPUVc(LongInt,Date,var Boolean);
external function LongInt GetCurUserLastNr(string);
external procedure PUSumUp(var record PUVc);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external procedure GetCurUser(var record UserVc);
external function val GetAcceptanceSum_PUVc(record PUVc);

global
function LongInt PUVcRecordDefaults(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record UserVc Userr;
  string 10 curcode;
  val fr,to1,to2,br1,br2;
  record AccBlock ARAccb;
  record MainStockBlock MSb;
  
  BlockLoad(ARAccb);
  BlockLoad(MSb);
  PUr.SerNr = -1;  
  PUr.RegDate = CurrentDate;
  PUr.TransDate = CurrentDate;
  PUr.Invalid = 0;
  if (SingleUserMode) then begin
    PUr.SerNr = NextSerNr("PUVc",PUr.TransDate,-1,false,"");
  end;
  PUr.PONr = -1;
  PUr.POCOSerNr = -1;
  curcode = PUr.CurncyCode;
  GetFullCurncyRate(curcode,PUr.TransDate,fr,to1,to2,br1,br2);
  PUr.CurncyCode = curcode;
  PUr.FrRate = fr;
  PUr.ToRateB1 = to1; 
  PUr.ToRateB2 = to2;
  PUr.BaseRate1 = br1;
  PUr.BaseRate2 = br2;
  PUr.Cost1 = blankval;
  PUr.Cost2 = blankval;
  PUr.Cost3 = blankval;
  PUr.Cost4 = blankval;
  PUr.Cost5 = blankval;
  PUr.SumQuant = blankval;
  PUr.ShipCost = blankval;
  PUr.CustomsCost = blankval;
  PUr.SumCostPrice = blankval;
  PUr.SubTotal = blankval;
  PUr.VATVal = blankval;
  PUr.PayVal = blankval;
  GetCurUser(Userr);
  PUr.Location = Userr.Location;
  PUr.NoTAXonVAT = ARAccb.NoTAXonVAT;
  PUr.BranchID = CurBranchID;
  PUr.ExtraCostsCalculation = MSb.ExtraCostsCalculation;
  PUVcRecordDefaults = res; 
  return;
end;

global
function LongInt PUVcRecordDuplicate(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  row PUVc PUrw;   
  Integer rwcnt,i;
  string 10 curcode;
  val fr,to1,to2,br1,br2;
      
  PUr.SerNr = -1;
  PUr.PONr = -1;
  PUr.OKFlag = 0;
  PUr.Invalid = 0;
  rwcnt = MatRowCnt(PUr);
  for (i=0; i<rwcnt; i=i+1) begin
    MatRowGet(PUr,i,PUrw);
    if ((PUrw.ovst!=0) or (PUrw.stp==3)) then begin
      MatRowDelete(PUr,i);
      rwcnt = MatRowCnt(PUr);
      i = i - 1;
    end else begin
      PUrw.OrdRow = -1;
      PUrw.ChargeNumber = -1;
      PUrw.SerialNr = "";
      MatRowPut(PUr,i,PUrw);
    end;
  end;
  if (SingleUserMode) then begin
    PUr.SerNr = NextSerNr("PUVc",PUr.TransDate,-1,false,"");
  end;
  curcode = PUr.CurncyCode;
  GetFullCurncyRate(curcode,PUr.TransDate,fr,to1,to2,br1,br2);
  PUr.CurncyCode = curcode;
  PUr.FrRate = fr;
  PUr.ToRateB1 = to1; 
  PUr.ToRateB2 = to2;
  PUr.BaseRate1 = br1;
  PUr.BaseRate2 = br2;
  PUr.LocOKNr = blankval;
  PUr.BranchID = CurBranchID;
  PUr.AcceptanceBy = "";
  PUr.AcceptanceFYI = "";
  PUSumUp(PUr); 
  PUVcRecordDuplicate = res; 
  return;
end;

global
function LongInt PUVcRecordRemoveTest(var record PUVc PUr,record PUVc PU2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  record DBLockBlock DBLockRec;
  record IVCashVc IVCashr;
  Integer actnr;
  record ActVc Actr;
  record RLinkVc RLr;

  res = 1;
  BlockLoad(DBLockRec);
  if (PUr.TransDate<=DBLockRec.DeleteBeforeDate) then begin
    res = 1;
    goto LPUVcRecordRemoveTest;
  end;
  if (PUr.OKFlag!=0) then begin
    if (long3>0) then begin
      MessageBox(1544,"");
    end;
    res = 0;
  end;    
  if (res!=0) then begin
    IVCashr.PUNr = PUr.SerNr;
    if (ReadFirstKey("PUNr",IVCashr,1,true)) then begin
      if (long3>0) then begin MessageBox(1560,"") end;
      res = 0;
    end;
  end;
  actnr = 1;
  while (ReadRecordLink(PUr,actnr,Actr,RLr)) begin
    if (Actr.TodoFlag==kTodoFlagApproval) then begin
      if (long3>0) then begin MessageBox(22408,""); end;
      res = 0;
      goto LPUVcRecordRemoveTest;
    end;
    actnr = actnr + 1;
  end;
LPUVcRecordRemoveTest:;
  PUVcRecordRemoveTest = res; 
  return;
end;

global
function LongInt PUVcRecordReset(var record PUVc PUr,record PUVc PU2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  PUr.OKFlag = 0;
  PUVcRecordReset = res; 
  return;
end;

procedure AddToStockMovRow(row StockMovVc StockMovrw,row PUVc PUrw)
begin  
  StockMovrw.ArtCode = PUrw.ArtCode;
  StockMovrw.Quant = PUrw.Quant;
  StockMovrw.Spec = PUrw.Spec;
  StockMovrw.SerialNr = PUrw.SerialNr;
  StockMovrw.OldPrice = PUrw.CostPrice;
  StockMovrw.ExtraSCost = blankval;
  StockMovrw.NewPrice = PUrw.CostPrice;
  StockMovrw.BasePrice = PUrw.UPrice;
  StockMovrw.Coefficient = PUrw.Coefficient;
  StockMovrw.UnitXval = PUrw.UnitXval;
  StockMovrw.UnitYval = PUrw.UnitYval;
  StockMovrw.UnitZval = PUrw.UnitZval;
  StockMovrw.FrPosCode = PUrw.PosCode;
  return;
end;

updating procedure PUMakeStockMovement(record PUVc PUr,Boolean negf)
begin
  record StockMovVc StockMovr;
  row StockMovVc StockMovrw;
  row PUVc PUrw;
  Integer i,rwcnt,smrwcnt;
  record MainStockBlock MainRec;
  string 20 toposcode,lastpalletitem;
  record INVc INr;
  record LocationVc LocRec;
  Boolean storesmf;  
  
  BlockLoad(MainRec);  
  LocRec.Code = PUr.Location;
  if (blank(LocRec.Code)) then begin 
    LocRec.Code = MainRec.MainStock;
  end;
  if (ReadFirstMain(LocRec,1,true)) then begin end;
  if (LocRec.RequirePos==0) then begin goto LPUMakeStockMovement; end;
  if (PUr.OKFlag==0) then begin goto LPUMakeStockMovement; end;
  if (LocRec.RequirePos==0) then begin goto LPUMakeStockMovement; end;
  RecordNew(StockMovr);  
  StockMovr.FrLocation = PUr.Location;
  StockMovr.ToLocation = PUr.Location;
  if (blank(PUr.Location)) then begin
    StockMovr.FrLocation = MainRec.MainStock;
    StockMovr.ToLocation = MainRec.MainStock;
  end;
  StockMovr.ToForkLiftQue = 0;
  StockMovr.TransNr = PUr.SerNr;
  StockMovr.FileName = "PUVc";
  StockMovr.TransDate = CurrentDate;
  StockMovr.SerNr = NextSerNr("StockMovVc",StockMovr.TransDate,-1,false,"");

  rwcnt = MatRowCnt(PUr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUr,i,PUrw);
    if (blank(PUrw.PosCode)) then begin goto LSKIPROW; end;
//    if ((nonblank(PUrw.PosCode)) and (blank(PUrw.ToPosCode))) then begin
    if (nonblank(PUrw.PosCode)) then begin
      SetPositionStatus(PUrw.PosCode,1);
    end;
//    if (blank(PUrw.ToPosCode)) then begin goto LSKIPROW; end; //to pos code is assigned below if missing
    storesmf = false;
    ClearRow(StockMovr,StockMovrw,1);
    AddToStockMovRow(StockMovrw,PUrw);
    if (ReadFirstItem(StockMovrw.ArtCode,INr,true,true)) then begin end;
    if (blank(INr.DefPalletItem)) and (INr.Code!=lastpalletitem) then begin
      storesmf = true;
      StockMovr.ToForkLiftQue = 100;//items without pallet don't goto automatic delivery que
    end;
    if (blank(toposcode)) then begin
      toposcode = PUrw.ToPosCode;
      if (blank(toposcode)) then begin
        toposcode = FindFreePositionInLocArea(INr,INr.LocArea,LocRec,StockMovrw.FrPosCode,"");
      end;
    end;
    StockMovrw.ToPosCode = toposcode;
    if (blank(StockMovrw.ToPosCode)) then begin goto LSKIPROW; end;
    MatRowPut(StockMovr,smrwcnt,StockMovrw);
    SetPositionStatus(toposcode,2);    
    PUrw.BarCode = StockMovr.SerNr;    
    //BS, if next PUrw doesn«t contain pallet item and the item has a pallet item
    // we should create a new StockMovVc.
    // or else labels will not be printed for 2nd/4th/6th item.
    MatRowGet(PUr,i+1,PUrw);
    if (PUrw.ArtCode!=INr.DefPalletItem) then begin
      storesmf = true;
    end;
    MatRowGet(PUr,i,PUrw);
    lastpalletitem = INr.DefPalletItem;
    smrwcnt = smrwcnt + 1;
    if ((Mod(smrwcnt,2)==0) or (storesmf)) then begin
      if (MatRowCnt(StockMovr)>0) then begin
        StockMovSumUp(StockMovr);    
        if (RecordStore(StockMovr,false)) then begin
          CreateRecordLink(StockMovr,CurrentCompany,PUr,CurrentCompany);  
          CreateRecordLink(PUr,CurrentCompany,StockMovr,CurrentCompany);  
        end;
      end;
      RecordNew(StockMovr);
      StockMovr.FrLocation = PUr.Location;
      StockMovr.ToLocation = PUr.Location;
      if (blank(PUr.Location)) then begin
        StockMovr.FrLocation = MainRec.MainStock;
        StockMovr.ToLocation = MainRec.MainStock;
      end;
      StockMovr.ToForkLiftQue = 0;
      StockMovr.TransNr = PUr.SerNr;
      StockMovr.FileName = "PUVc";
      StockMovr.TransDate = CurrentDate;
      StockMovr.SerNr = NextSerNr("StockMovVc",StockMovr.TransDate,-1,false,"");

      if (StockMovr.SerNr<=0) then begin goto LPUMakeStockMovement; end;

      smrwcnt = 0;
      toposcode = "";
      lastpalletitem = "";
    end;
    MatRowPut(PUr,i,PUrw);
LSKIPROW:;    
  end;
  if (MatRowCnt(StockMovr)>0) then begin
    StockMovSumUp(StockMovr);    
    if (RecordStore(StockMovr,false)) then begin
      CreateRecordLink(StockMovr,CurrentCompany,PUr,CurrentCompany);  
      CreateRecordLink(PUr,CurrentCompany,StockMovr,CurrentCompany);  
    end;
  end;
LPUMakeStockMovement:;  
  return;
end;

global
updating function LongInt PUVcRecordSave(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  if (PUr.OKFlag!=0) then begin
    PUr.LocOKNr = NextLocOKNr(PUr.Location);
  end;
  PUVcRecordSave = res;
  return;
end;

global
updating procedure PurUpdateStock2(record PUVc PUp,Boolean negf,record TempStockVc TSr,Boolean usetmpstkf)
begin
  record MainStockBlock MainStockRec;
  row PUVc PUrw;
  Integer i,rwcnt;
  val t,t2,q2;
  string 255 location;
  string 255 thelocation;
  Boolean testf;
  row TempStockVc TSrw;
  
  if (PUp.Invalid!=0) then begin // not finnished
    goto LPurUpdateStock;
  end;
  location = PUp.Location;
  BlockLoad(MainStockRec);
  if (blank(location)) then begin
    location = MainStockRec.MainStock;
  end;
  rwcnt = MatRowCnt(PUp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    testf = true;
    if (usetmpstkf) then begin
      FindTempStockRow(i,PUrw.ArtCode,true,TSr,TSrw);
      if (TSrw.StockAffectf==0) then begin testf = false; end;
      if (TSrw.ItemType!=kItemTypeStocked) then begin testf = false; end;
    end;
    if (testf) then begin
      if (blank(PUrw.Location)) then begin
        thelocation = location;
      end else begin
        thelocation = PUrw.Location;
      end;
      switch (PUrw.ovst) begin
        case 0:
          t2 = PUrw.Quant;
        case 1:
          t2 = 0;        
      end;
      if (ShouldItemUpdateStock(PUrw.ArtCode)==false) then begin t2 = blankval; end;
      if (MainStockRec.UnitConvCalc==0) then begin
        q2 = t2*PUrw.Coefficient;
      end else begin
        q2 = t2/PUrw.Coefficient;
      end;
      if (negf) then begin 
        t2 = -t2; 
        q2 = -q2; 
      end;
      UpdateInstock("PUVc",PUp.SerNr,PUrw.ArtCode,thelocation,PUp.TransDate,t2,t,t,t,t,q2,t,t,t);
    end;
  end;
LPurUpdateStock:;
  return;
end;

global
updating procedure PurUpdateStock(record PUVc PUp,Boolean negf)
begin
  record TempStockVc TSr;
  
  PurUpdateStock2(PUp,negf,TSr,false);
  return;
end;

updating procedure SavePurchase(var record PUVc PUp,Boolean saverec)
begin
  record PUVc PU2r;
  
  if (PUp.OKFlag!=0) then begin
    PurUpdateStock(PUp,false);
    PurUpdateCostPrice(PUp,false);  // this should never be called during import!!! 
    PurUpdateSerialNr(PUp,false,false);//before ItemHist to get consigment stock right
    PurUpdateItemHist(PUp);
  end;
  if (PUp.PONr!=-1) then begin
    PU2r.SerNr = -1;
    if (UpdatePOFromPU(PUp,PU2r,saverec)) then begin end;
  end else begin
    UpdatePOFromPURows(PUp,true,PU2r,false,true);
//Check PU rows PONr 
  end;
  return;
end;

updating procedure UpdatePurchase(var record PUVc PUp,record PUVc PU2p,Boolean negf)
begin  
  if (PUp.PONr!=-1) then begin
    if (UpdatePOFromPU(PUp,PU2p,negf)) then begin end;
  end else begin    
    if (negf) then begin
      UpdatePOFromPURows(PUp,false,PU2p,true,false);
    end else begin
      UpdatePOFromPURows(PUp,true,PU2p,true,true);
    end;
//Check PU rows PONr 
  end;
  return;
end;

global
updating function LongInt PUVcRecordSaveAfter(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record UserVc Userr;

  Userr.Code = PUr.Sign;
  ReadFirstMain(Userr,1,true);
  PUr.SalesGroup = Userr.SalesGroup;  
  SavePurchase(PUr,false);
  if (PUr.OKFlag!=0) then begin
    PUMakeStockMovement(PUr,false);
    UpdateVarianceStatusPU(PUr);
  end;
  PUVcRecordSaveAfter = res;
  return;
end;

global
updating function LongInt PUVcRecordUpdate(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  if ((PUr.OKFlag!=0) and (PU2r.OKFlag==0)) then begin
    if (ItemHistExists("PUVc",PUr.SerNr)==false) then begin
      PUr.LocOKNr = NextLocOKNr(PUr.Location);
    end;
  end;
LPUVcRecordUpdate:;  
  PUVcRecordUpdate = res;
  return;
end;

global
updating procedure UnOKGoodsReceipt(record PUVc PUr,record PUVc PU2r,Boolean deltrf)
begin
  LongInt oldpunr;
  
  PurUpdateStock(PUr,true);
  PurUpdateSerialNr(PUr,false,true);
  oldpunr = PUr.SerNr;
  PUr.SerNr = -1;
  UpdatePurchase(PUr,PU2r,true);
  PUr.SerNr = oldpunr;
  
//  PUMakeStockMovement(PUr,true);//JJRECALCSTOCK
//    UpdateVarianceStatusPU(PUr);//JJRECALCSTOCK
//    UpdateStockResFromPU(PUr,PU2r);//JJRECALCSTOCK
  
  UpdateRecalcStockNeeded(1);
  StoreUnOKHistory("PUVc",PUr.SerNr,CurrentDate,CurrentTime,CurrentUser);    
  if (deltrf) then begin
    DeleteTransaction(PUr.SerNr,PUYc);
  end;
  return;
end;

global
updating function LongInt PUVcRecordUpdateAfter(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  Boolean ihf;
  record VIVc VIr;
  record VIVc oldVIr;
  record POSettingBlock POSb;
  record RcVc RepSpec;
  val bc1v;

  ihf = ItemHistExists("PUVc",PUr.SerNr);
  if ((PUr.OKFlag!=0) and (PU2r.OKFlag!=0)) then begin
    // Comment-field has been changed 
    PurUpdateOvst(PUr,PU2r);
  end else begin
    if ((PU2r.OKFlag==0) and (PUr.OKFlag!=0)) then begin
      PurUpdateStock(PUr,false);
      PurUpdateSerialNr(PUr,false,false);//before ItemHist to get consigment stock right
      if (ihf==false) then begin
        PurUpdateCostPrice(PUr,false);
        PurUpdateItemHist(PUr);
      end;
    end;
  end;
  if ((PUr.Invalid==0) and (PU2r.Invalid==0)) then begin
    if ((PUr.OKFlag==0) and (PU2r.OKFlag!=0)) then begin//unok
      //done below
    end else begin
      UpdatePurchase(PUr,PU2r,false);
    end;
  end;
  if ((PUr.OKFlag!=0) and (PU2r.OKFlag==0)) then begin
    if (ihf==false) then begin
      BlockLoad(POSb);
      PUMakeStockMovement(PUr,false);
      UpdateVarianceStatusPU(PUr);
      UpdateStockResFromPU(PUr,PU2r);
      if (POSb.CreateVIonPUOK!=0) and (POSb.OpenCreateVIFromPO==0) and (PUr.PONr>0) then begin
        RepSpec.FirstVer = PUr.PONr;
        RepSpec.flags[0] = 1;
        RepSpec.flags[1] = 1;
        RepSpec.flags[2] = 1;
        RepSpec.flags[3] = 1;
        RepSpec.flags[4] = 1;
        RepSpec.flags[5] = 1;
        RepSpec.flags[6] = 1;
        RepSpec.flags[7] = 1;
        if (CreateVIFromPO(RepSpec.FirstVer,RepSpec,VIr)==0) then begin
          RecordCopy(oldVIr,VIr);
          VIr.OKFlag = 1;
          RecordUpdate(oldVIr,VIr,true);
        end;
      end;
    end;
  end;
  if ((PU2r.Invalid==0) and (PUr.Invalid!=0)) then begin
    if (PU2r.OKFlag!=0) then begin
      UpdatePurchase(PUr,PU2r,false);//not stocked items suport only atm
//      InvalidateTR(PUYc,PUr.SerNr);
//      InvalidatePU(PUr);
    end;
  end;  
  if ((PUr.Invalid==0) and (PU2r.Invalid==0)) then begin
    if ((PUr.OKFlag==0) and (PU2r.OKFlag!=0)) then begin//unok
      UnOKGoodsReceipt(PUr,PU2r,true);
    end;
  end;
  if ((PUr.OKFlag!=0) and (PU2r.OKFlag!=0)) then begin//overstrike
    PUVc_UpdateTR(PUr,PU2r);
  end;
  PUVcRecordUpdateAfter = res;
  return;
end;

updating procedure RemovePU(record PUVc PUr)
begin
  Integer rwcnt,i;
  row PUVc PUrw;
  record POVc oldPOr;
  record POVc POr;
  row POVc POrw;
  Integer porwcnt;
  
  if (PUr.PONr!=-1) then begin
    POr.SerNr = PUr.PONr;
    if (ReadFirstMain(POr,1,true)) then begin
      RecordCopy(oldPOr,POr);
      porwcnt = MatRowCnt(POr);
      rwcnt = MatRowCnt(PUr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(PUr,i,PUrw);
        if ((PUrw.OrdRow>=0) and (PUrw.OrdRow<porwcnt) and (PUrw.ovst==0)) then begin
          MatRowGet(POr,PUrw.OrdRow,POrw);
          POrw.Shipd1 = POrw.Shipd1 - PUrw.Quant;
          if (POrw.Shipd1==0) then begin
            POrw.Shipd1 = blankval;
          end;
          MatRowPut(POr,PUrw.OrdRow,POrw);
        end;
      end;
      if (RecordUpdate(oldPOr,POr,false)==0) then begin //true calls recordcheck which is not needed
        POVcRecordUpdate(POr,oldPOr,Rs_update,0);
        POVcRecordUpdateAfter(POr,oldPOr,Rs_update,0);
      end;
    end;    
  end else begin
    RemovePU_PORows(PUr);
  end;
  return;
end;

global
updating function LongInt PUVcRecordRemoveAfter(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  RemovePU(PUr);
  PUVcRecordRemoveAfter = res;
  return;
end;

function Integer ValidateBulkSerialNoRow(record SerNrTrackBlock SNrb,record PUVc PUr,row PUVc PUrw,Integer currow,var string gotofield,var Integer gotorow)
begin
  Integer res;
  Integer j,rwcnt;
  row PUVc PU2rw;
  Boolean testf;
  string 255 serialnr;
  string 255 serialnr2;
  val qty,qty2;

  gotorow = -1;
  gotofield = "";  
  if (PUrw.Quant>1) then begin
    rwcnt = len(PUrw.SerialNr);
    for (j=0;j<rwcnt;j=j+1) begin
      if ((IsDigit(Mid(PUrw.SerialNr,j,1))==false) and (Mid(PUrw.SerialNr,j,1)!=":")) then begin
        res = 20434;
        gotorow = currow;
        gotofield = "SerialNr";
        goto LValidateBulkSerialNoRow;
      end;
    end;
  end;
  rwcnt = MatRowCnt(PUr);
  serialnr = FirstInRange(PUrw.SerialNr,60);
  while (nonblank(serialnr)) begin
    if (SerialNrAvail2(PUrw.ArtCode,serialnr,1.00)==true) then begin
      res = 1241;
      gotorow = currow;
      gotofield = "SerialNr";
      goto LValidateBulkSerialNoRow;
    end;

    for (j=0;j<currow;j=j+1) begin  
      MatRowGet(PUr,j,PU2rw);
      if (PU2rw.Quant!=0) then begin
        if (PUrw.ArtCode==PU2rw.ArtCode) then begin
          qty2 = blankval;
          serialnr2 = FirstInRange(PU2rw.SerialNr,60);
          while (nonblank(serialnr2)) begin
            if (serialnr==serialnr2) then begin
              res = 1241;
              gotorow = currow;
              gotofield = "SerialNr";
              goto LValidateBulkSerialNoRow;
            end;
            qty2 = qty2 + 1;
            serialnr2 = NextSerialNumber(PU2rw.ArtCode,serialnr2,SNrb);
            if (qty2>=PU2rw.Quant) then begin serialnr2 = ""; end;
          end;
        end;
      end;
    end;
    qty = qty + 1;
    serialnr = NextSerialNumber(PUrw.ArtCode,serialnr,SNrb);
    if (qty>=PUrw.Quant) then begin serialnr = ""; end;
  end;
LValidateBulkSerialNoRow:;  
  ValidateBulkSerialNoRow = res;
  return;
end;

global
updating function LongInt PUVcRecordCheck(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt check)
begin
  LongInt res;
  record POVc POr;
  row POVc POrw;
  record PUVc localPUr;
  record INVc INr;
  record LocationVc LocRec;
  record CostAccBlock CAb;
  record MainStockBlock MSb;
  record SRBlock SRRec;
  row PUVc PUrw;
  row PUVc PU2rw;
  Integer rwcnt;
  Integer i,j;
  LongInt oldnr;
  LongInt newnr;
  Boolean transf,gentrans,transvariancef;
  val t;
  Integer errcode;
  LongInt sernr;
  record TRVc gTRp,oTRr;
  string 20 location,loc;
  val w,h,d,m;
  record INVc IN2r;
  string 255 errstr,objstr;
  record StockMovVc StockMovr;
  string 20 lastpalletitem;
  record PosVc Posr;
  val prev;
  Date bbd;
  record CUVc VEr;
  Boolean unokf;
  record AccVc Accr;
  string 255 gotofield;
  Integer gotorow;
  transaction string 255 gRuniningMaint;
  record SerNrTrackBlock SNrb;
  Array string 255 otcheckaccs;
  Array string 255 otcheckobjtyps;
  Integer otcheckcnt;
  Boolean initotcheckf,testf;    

  res = 0;
  oldnr = PUr.SerNr;
  transf = false;
  if (PUr.OKFlag==1) then begin
    if (stat==Rs_insert) then begin transf = true; end;
    if (stat==Rs_update) then begin
      if (PU2r.OKFlag==0) then begin transf = true; end;
    end;
  end;
   if (stat==Rs_update) then begin
    if (PUr.SerNr<=0) and (PU2r.OKFlag==0) then begin
      PUr.SerNr = PU2r.SerNr;
    end;
  end;    

  if (PUr.OKFlag==0) then begin
    if (stat==Rs_update) then begin
      if (PU2r.OKFlag==1) then begin unokf = true; end;
    end;
  end;
  if (unokf) then begin
    errcode = IsUnOKAllowed_PUVc(PUr);
    if (errcode!=0) then begin
      RecordCheckError(errcode,"",-1,"TransDate");      
      res = -1; 
    end;
    goto L99PUVcRecordCheck;
  end;  
  if (stat==Rs_update) then begin
    if (PU2r.OKFlag!=0) then begin
      goto L99PUVcRecordCheck;
    end;
  end;

  BlockLoad(SRRec);
  BlockLoad(CAb);
  BlockLoad(MSb);
  BlockLoad(SNrb);

  if (PUr.SerNr<=0) then begin
    newnr = GetCurUserLastNr("PUVc");
    if (newnr==-1) then begin
      newnr = SRRec.LastPurNr;
    end;
    PUr.SerNr = NextSerNr("PUVc",PUr.TransDate,newnr,false,"");
  end;
  
  if ((stat==Rs_insert) or (PUr.SerNr!=PU2r.SerNr)) then begin
    localPUr.SerNr = PUr.SerNr;
    if (ReadFirstMAin(localPUr,1,true)) then begin
      RecordCheckError(1547,"",-1,"SerNr");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
  end;
  if (SerNrTestPUVc(PUr.SerNr,PUr.TransDate,gentrans)==false) then begin
    if (check>0) then begin
    RecordCheckError(1557,"",-1,"SerNr");      
    res = -1;
    goto L99PUVcRecordCheck;
  end;
  end;
  if (check==0) then begin
    goto LPUVcRecordCheck_GenTrans;
  end;
  if (Date2Test("PUVc",PUr.TransDate,"TransDate",-1)==false) then begin
    res = -1;
    goto L99PUVcRecordCheck;
  end; 
  if (DisallowFutureDateCheck(true,PUr.TransDate,"TransDate",-1)) then begin
    res = -1;
    goto L99PUVcRecordCheck;
  end;
  if (transf) then begin
    if (UserCanAction("PUOK",true)==false) then begin
      RecordCheckError(1274,StringFromStringSet(3,"PUOK"),-1,"SerNr");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
  end;  
  rwcnt = MatRowCnt(PUr);
  if (blank(PUr.Location)) then begin
    if (MSb.requireLocation!=0) then begin
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(PUr,i,PUrw);
        if (blank(PUrw.Location)) then begin
          RecordCheckError(1058,PUr.Location,-1,"Location");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
  end;
  loc = PUr.Location;
  if (blank(loc)) then begin
    loc = MSb.MainStock;
  end;  
  if (nonblank(loc)) then begin
    LocRec.Code = loc;
    if (ReadFirstMain(LocRec,1,true)==false) then begin
      RecordCheckError(1120,PUr.Location,-1,"Location");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
  end;
  if (nonblank(PUr.VECode)) then begin     
    VEr.Code = PUr.VECode;
    if (ReadFirstMain(VEr,1,true)==false) then begin
      RecordCheckError(1120,PUr.VECode,-1,"VECode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (VEr.blockedFlag!=0 or VEr.OnHoldFlag!=0) then begin
      RecordCheckError(1265,PUr.VECode,-1,"VECode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
  end;
  if (nonblank(PUr.Objects)) then begin     
    errcode = CheckObjs("",PUr.Objects,errstr);
    if (errcode!=0) then begin
      RecordCheckError(errcode,errstr,-1,"Objects");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
  end;  
  errcode = CheckRates(PUr.CurncyCode,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,errstr);
  if (errcode!=0) then begin
    RecordCheckError(errcode,"",-1,errstr);      
    res = -1; 
    goto L99PUVcRecordCheck;
  end;          
  if (transf) then begin
    if (PUr.OKFlag==1) then begin
      if (BA_GRNCostVarianceWarning(PUr,i)) then begin
        if (UserCanAction("DisallowCostVariance",false)) then begin
          RecordCheckError(1274,StringFromStringSet(3,"DisallowCostVariance"),i,"CostPrice");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
  end;
  if (HasLocalization("PRT")) then begin
    VEr.Code = PUr.VECode;
    if (ReadFirstMain(VEr,1,true)) then begin
      if (blank(VEr.VATNr)) then begin
        RecordCheckError(20275,"",-1,"VECode");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
  end;

  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUr,i,PUrw);
    testf = true;
    if (IsStandardProduct) then begin
      testf = HasIntegratedNL and transf;
    end;
    if (testf) then begin
      if (nonblank(PUrw.CostAcc)) then begin
        Accr.AccNumber = PUrw.CostAcc;
        if (ReadFirstMain(Accr,1,true)==false) then begin
          RecordCheckError(1007,PUrw.CostAcc,i,"CostAcc");      
          res = -1; 
          goto L99PUVcRecordCheck;
        end else begin
          if ((Accr.blockedFlag!=0) or (Accr.GroupAcc!=0)) then begin
            RecordCheckError(1007,PUrw.CostAcc,i,"CostAcc");      
            res = -1; 
            goto L99PUVcRecordCheck;
          end;
        end;          
      end;
      if (nonblank(PUrw.CredAcc)) then begin
        Accr.AccNumber = PUrw.CredAcc;
        if (ReadFirstMain(Accr,1,true)==false) then begin
          RecordCheckError(1007,PUrw.CredAcc,i,"CredAcc");      
          res = -1; 
          goto L99PUVcRecordCheck;
        end else begin
          if ((Accr.blockedFlag!=0) or (Accr.GroupAcc!=0)) then begin
            RecordCheckError(1007,PUrw.CredAcc,i,"CredAcc");      
            res = -1; 
            goto L99PUVcRecordCheck;
          end;          
        end;
      end;        
    end;
    location = PUrw.Location;
    if (blank(PUrw.Location)) then begin
      location = loc;
    end;
    if (nonblank(location)) then begin
      LocRec.Code = location;
      if (ReadFirstMain(LocRec,1,true)==false) then begin
        RecordCheckError(1120,PUrw.Location,i,"Location");
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (StockRecordForLocationAllowed("PUVc",location,PUrw.ArtCode,PUr.TransDate,PUr.OKFlag,errcode,errstr)==false) then begin
      RecordCheckError(errcode,errstr,i,"ArtCode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    
    if (nonblank(PUrw.Location)) then begin
      location = PUrw.Location;
      if (MSb.Chronology==1) then begin//Chronology per Location
        if (PUrw.Location!=loc) then begin
          RecordCheckError(20857,"",i,"Location");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
  if (PUrw.stp==1) then begin
    if (IsOffice(false)) then begin
      if (PUrw.Quant<0) then begin
        RecordCheckError(1574,"",i,"Quant");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (PUrw.PONr>0) then begin
      errcode = GetPO(PUrw.PONr,POr,PUr.VECode,false);
      switch (errcode) begin
        case 1: 
          RecordCheckError(1281,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        case 2: 
          RecordCheckError(1215,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        case 3: 
          RecordCheckError(1138,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        case 4: 
          RecordCheckError(1026,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        case 5: 
          RecordCheckError(1459,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        case 6:
          RecordCheckError(22062,"",i,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;        
      end;
      if (PUr.PONr>0) then begin
        if (PUrw.PONr!=PUr.PONr) then begin
          RecordCheckError(2010,"",-1,"PONr");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
      if (PUrw.OrdRow<0) then begin
        RecordCheckError(1058,"",i,"OrdRow");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;            
    end;
    if (PUrw.PONr>0) then begin
      errcode = GetPO(PUrw.PONr,POr,PUr.VECode,false);
      if (POr.TransDate>PUr.TransDate) then begin
        RecordCheckError(20855,"",i,"PONr");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end else begin
      errcode = GetPO(PUr.PONr,POr,PUr.VECode,false);
      if (POr.TransDate>PUr.TransDate) then begin
        RecordCheckError(20855,"",-1,"PONr");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (PUr.PONr>0 or PUrw.PONr>0) then begin
      if (check!=0) then begin
        if (PUrw.OrdRow<MatRowCnt(POr)) and (PUrw.OrdRow>=0) then begin
          if (PUr.OKFlag!=0 and PU2r.OKFlag!=0) then begin
            MatRowGet(PU2r,i,PU2rw);
            if (PUrw.ArtCode==PU2rw.ArtCode and PUrw.Quant==PU2rw.Quant and PUrw.OrdRow==PU2rw.OrdRow) then begin
              goto Lskiprecvcheck;
            end;
          end;
          if (PUrw.ovst==0) and (PUr.Invalid==0) then begin
            MatRowGet(POr,PUrw.OrdRow,POrw);
            if (MSb.dontAllowOverreceive==1) then begin
              prev = blankval;
              for (j=0;j<rwcnt;j=j+1) begin
                if (j!=i) then begin
                  MatRowGet(PUr,j,PU2rw);
                  if (PU2rw.OrdRow==PUrw.OrdRow and PU2rw.PONr==PUrw.PONr) then begin
                    if (PU2rw.ovst==0) then begin
                      prev = prev + PU2rw.Quant;
                    end;
                  end;
                end;
              end;
              prev = prev + PUrw.Quant;
              if (((POrw.Quant - POrw.Shipd2)<prev) and (prev>0)) then begin
                RecordCheckError(20055,"",i,"Quant");      
                res = -1; 
                goto L99PUVcRecordCheck;
              end;
            end;
          end;
          Lskiprecvcheck:;
        end;
      end;  
    end;  
    if (blank(PUrw.ArtCode)) then begin
      RecordCheckError(1130,"",i,"ArtCode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (ReadFirstItem(PUrw.ArtCode,INr,true,true)==false) then begin
      RecordCheckError(1120," " & PUrw.ArtCode,i,"ArtCode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (PUrw.StockType==kStockTypeConsigment) then begin
      if (INr.SerNrf<1) then begin
        RecordCheckError(1953,"",i,"StockType");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (PUrw.TREO==kTREO) then begin
      if (INr.SerNrf<1) then begin
        RecordCheckError(1953,"",i,"TREO");
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if ((HasLocalization("EST")) or (HasLocalization("POL"))) then begin//vatEstonian,vatPolish
      if (INr.ItemType!=1) then begin
        RecordCheckError(1301,"",i,"ArtCode");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (CheckAllowedSize(INr,PUrw.UnitXval,PUrw.UnitYval,PUrw.UnitZval)==false) then begin
      RecordCheckError(1480,"",i,"UnitXval");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (check!=0) then begin
      VerifyRowObjects("PL",PUr.Objects,PUrw.Objects,PUrw.CredAcc,errcode,errstr,initotcheckf,otcheckaccs,otcheckobjtyps,otcheckcnt);                              
      if (errcode!=0) then begin
        RecordCheckError(errcode,errstr,i,"Objects");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (nonblank(PUrw.Objects)) then begin 
      errcode = CheckObjs("",PUrw.Objects,errstr);
      if (errcode!=0) then begin
        RecordCheckError(errcode,errstr,i,"Objects");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
    end;
    if (blank(PUrw.Quant)) then begin
      RecordCheckError(1058,"",i,"Quant");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (INr.ItemType==2) then begin
      RecordCheckError(1826,"",i,"ArtCode");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if (LocRec.RequirePos!=0) then begin
/*occupied is not important, height is important    
      if (nonblank(PUrw.ToPosCode)) then begin
        res = IsPositionFree(PUrw.ToPosCode);
        if (res>0) then begin
          RecordCheckError(res," " & PUrw.ToPosCode,i,"ToPosCode");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
*/
      w = 0;
      h = 0;
      d = 0;
      m = PUrw.Quant/INr.QtyonPallet;
      if (INr.PalletWidth!=0) then begin w = w + INr.PalletWidth*m; end;
      if (INr.PalletHeight!=0) then begin h = h + INr.PalletHeight*m; end;
      if (INr.PalletDepth!=0) then begin d = d + INr.PalletDepth*m; end;
      for (j=0;j<i;j=j+1) begin
        MatRowGet(PUr,j,PU2rw);
        if (PUrw.PosCode==PU2rw.PosCode) then begin
          if (ReadFirstItem(PU2rw.ArtCode,IN2r,true,true)) then begin
            m = 0;
            if (IN2r.QtyonPallet>0) then begin
              m = PU2rw.Quant/IN2r.QtyonPallet;
            end;                
            if (IN2r.PalletWidth!=0) then begin
              w = w + IN2r.PalletWidth*m;
            end;
            if (IN2r.PalletHeight!=0) then begin
              h = h + IN2r.PalletHeight*m;
            end;
            if (IN2r.PalletDepth!=0) then begin
              d = d + IN2r.PalletDepth*m;
            end;
          end;
        end;
/*only height matters
        if (PUrw.ArtCode!=lastpalletitem) then begin
          if (nonblank(PUrw.ToPosCode)) then begin
            if (PUrw.ToPosCode==PU2rw.ToPosCode) then begin
              RecordCheckError(1729,PUrw.ToPosCode,i,"ToPosCode");      
              res = -1;
              goto L99PUVcRecordCheck;
            end;
          end;
        end;
*/        
      end;    
      lastpalletitem = INr.DefPalletItem;
      
      if (nonblank(PUrw.PosCode)) then begin
        errcode = CheckPosition(PUrw.PosCode,location,LocRec.RequirePos,w,h,d);
        if (errcode!=0) then begin
          RecordCheckError(errcode,PUrw.PosCode,i,"PosCode");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
      if (nonblank(PUrw.ToPosCode)) then begin
        w = 0;
        h = 0;
        d = 0;
        m = PUrw.Quant/INr.QtyonPallet;
        if (INr.PalletWidth!=0) then begin w = w + INr.PalletWidth*m; end;
        if (INr.PalletHeight!=0) then begin h = h + INr.PalletHeight*m; end;
        if (INr.PalletDepth!=0) then begin d = d + INr.PalletDepth*m; end;
        for (j=0;j<i;j=j+1) begin
          MatRowGet(PUr,j,PU2rw);
          if (PUrw.ToPosCode==PU2rw.ToPosCode) then begin
            if (ReadFirstItem(PU2rw.ArtCode,IN2r,true,true)) then begin
              m = 0;
              if (IN2r.QtyonPallet>0) then begin
                m = PU2rw.Quant/IN2r.QtyonPallet;
              end;                
              if (IN2r.PalletWidth!=0) then begin
                w = w + IN2r.PalletWidth*m;
              end;
              if (IN2r.PalletHeight!=0) then begin
                h = h + IN2r.PalletHeight*m;
              end;
              if (IN2r.PalletDepth!=0) then begin
                d = d + IN2r.PalletDepth*m;
              end;
            end;
          end;
        end;    
        errcode = CheckPosition(PUrw.ToPosCode,location,LocRec.RequirePos,w,h,d);
        if (errcode!=0) then begin
          RecordCheckError(errcode,PUrw.ToPosCode,i,"ToPosCode");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end else begin
      if (nonblank(PUrw.PosCode)) then begin
        Posr.Code = PUrw.PosCode;
        Posr.Location = location;
        if (ReadFirstKey("Location",Posr,2,true)==false) then begin
          RecordCheckError(1120,PUrw.PosCode,i,"PosCode");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
      if (nonblank(PUrw.ToPosCode)) then begin
        Posr.Code = PUrw.ToPosCode;
        Posr.Location = location;
        if (ReadFirstKey("Location",Posr,2,true)==false) then begin
          RecordCheckError(1120,PUrw.ToPosCode,i,"ToPosCode");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
    if (LocRec.RequirePos!=0) then begin
      if (blank(PUrw.ToPosCode)) then begin
        RecordCheckError(1854,"",i,"ToPosCode");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
      if (StockMovr.SerNr<=0) then begin
        StockMovr.SerNr = NextSerNr("StockMovVc",CurrentDate,-1,false,"");
        if (StockMovr.SerNr<=0) then begin 
          RecordCheckError(2291,"",-1,"SerNr");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
/*    
    if (nonblank(LocRec.Code)) then begin
      if (nonblank(MainWHM.Location)) then begin
        if (LocRec.RequirePos!=0) then begin
          if (blank(PUrw.ToPosCode)) then begin
            RecordCheckError(1854,"",i,"PosCode");      
            res = -1;
            goto L99PUVcRecordCheck;
          end;

//*          if (PUrw.ToPosCode!=MainWHM.DefPUPosCode) then begin
//*            res = IsPositionFree(PUrw.ToPosCode);
//*            if (res>0) then begin
//*              RecordCheckError(res," " & PUrw.ToPosCode,i,"ToPosCode");      
//*              res = -1;
//*              goto L99PUVcRecordCheck;
//*            end;
//*          end;
        end;
      end else begin
        if (LocRec.RequirePos!=0) then begin
          if (blank(PUrw.ToPosCode)) then begin
            RecordCheckError(1854,"",i,"ToPosCode");      
            res = -1;
            goto L99PUVcRecordCheck;
          end;
        end;
      end;
    end;
*/    
    if (transf) then begin
      if (((INr.SerNrf>0) and (INr.SerNrf<3)) and (MSb.NoSerOnPU==0)) then begin
        if (PUrw.Quant!=0) then begin
          if (blank(PUrw.SerialNr)) then begin
            RecordCheckError(1239,"",i,"SerialNr");      
            res = -1;
            goto L99PUVcRecordCheck;
          end;
        end;
        if (Mid(PUrw.SerialNr,len(PUrw.SerialNr),1)==" ") then begin
          RecordCheckError(1239,"",i,"SerialNr");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
        if (SNrb.BulkSerialNos==0) then begin
          if (IsSerialNrCorrect(PUrw.SerialNr)==false) then begin
            RecordCheckError(24154,PUrw.SerialNr,i,"SerialNr");      
            res = -1; 
            goto L99PUVcRecordCheck;
          end;        
        end;        
        switch (INr.SerNrf) begin
          case 1:  // serial nr. 
            // is the serial number available at any location 
            if (SNrb.BulkSerialNos!=0) then begin
              errcode = ValidateBulkSerialNoRow(SNrb,PUr,PUrw,i,gotofield,gotorow);
              if (errcode) then begin
                RecordCheckError(errcode,"",gotorow,gotofield);      
                res = -1;
                goto L99PUVcRecordCheck;
              end;
            end else begin
              for (j=0;j<i;j=j+1) begin              
                MatRowGet(PUr,j,PU2rw);
                if (PUrw.Quant!=0) then begin
                  if ((PUrw.ArtCode==PU2rw.ArtCode) and
                      (PUrw.SerialNr==PU2rw.SerialNr)) then begin
                    RecordCheckError(1241,"",i,"SerialNr");      
                    res = -1;
                    goto L99PUVcRecordCheck;
                  end;
                end;
              end;
              if (PUrw.Quant!=0) then begin
                if (SerialNrAvail2(PUrw.ArtCode,PUrw.SerialNr,PUrw.Quant)) then begin
                  RecordCheckError(1243,"",i,"SerialNr");      
                  res = -1;
                  goto L99PUVcRecordCheck;
                end;
              end;
              if (PUrw.Quant!=1 and PUrw.Quant!=0) then begin
                RecordCheckError(1242,"",i,"Quant");      
                res = -1;
                goto L99PUVcRecordCheck;
              end;
              if (SerialNrOnThisPU(PUr,PUrw.ArtCode,PUrw.SerialNr)>1) then begin
                RecordCheckError(1242,"",i,"Quant");      
                res = -1;
                goto L99PUVcRecordCheck;
              end;
/*              
              if (SerialNrEverinStock(PUrw.ArtCode,PUrw.SerialNr)) then begin
                RecordCheckError(1243,"",i,"SerialNr");      
                res = -1;
                goto L99PUVcRecordCheck;
              end;
*/              
            end;
          case 2:  ;// batch nr. 
            for (j=0;j<i;j=j+1) begin              
              MatRowGet(PUr,j,PU2rw);
              if ((PUrw.ArtCode==PU2rw.ArtCode) and
                  (PUrw.SerialNr==PU2rw.SerialNr)) then begin
                  if (PUrw.CostPrice!=PU2rw.CostPrice) then begin
                    RecordCheckError(20568,"",i,"SerialNr");      
                    res = -1;
                    goto L99PUVcRecordCheck;
                  end;
                  if (nonblank(PUrw.BestBefore)) or (nonblank(PU2rw.BestBefore)) then begin
                    if (PUrw.BestBefore!=PU2rw.BestBefore) then begin
                      RecordCheckError(20765,"",i,"SerialNr");      
                      res = -1;
                      goto L99PUVcRecordCheck;
                    end;
                  end;
              end;
            end;
            if (FIFOPerSerialNr(INr,CAb)!=0) then begin//otherwise cost price on deliveries is completely wrong
              if (SerialNrAvail2(PUrw.ArtCode,PUrw.SerialNr,1)) then begin
                RecordCheckError(1243,"",i,"SerialNr");      
                res = -1;
                goto L99PUVcRecordCheck;
              end;              
            end;
            FindBatchBestBeforeDate(PUrw.ArtCode,PUrw.SerialNr,bbd);
            if (nonblankdate(bbd)) then begin
              if (PUrw.BestBefore!=bbd)then begin
                RecordCheckError(20765,"",i,"SerialNr");      
                res = -1;
                goto L99PUVcRecordCheck;
              end;
            end;
        end;
      end;
    end;
  end;
  end;
  if (DateWarned(PUr.TransDate,"PUVc")) then begin
    MessageBox(1045,"");
  end;    
  if (transf) then begin
    if (stat==Rs_update) then begin
      sernr = PU2r.SerNr;
    end;
    if (check>0) then begin
      if (MSb.Chronology!=0) then begin
        if (ExistStockTrans(location,PUr.TransDate,errcode,errstr,"PUVc",sernr,MSb)==true) then begin
          RecordCheckError(errcode,errstr,-1,"TransDate");      
          res = -1;
          goto L99PUVcRecordCheck;
        end;
      end;
    end;
    if (CanOKStockRecord(errcode)==false) then begin
      RecordCheckError(errcode,"",-1,"SerNr");      
      res = -1; 
      goto L99PUVcRecordCheck;
    end;
/*done in other place    
    if (IsStockSettingsOK==false) then begin
      RecordCheckError(1767,"",-1,"SerNr");      
      res = -1; 
      goto L99PUVcRecordCheck;
    end;
*/    
  end;
  if (transf) then begin
    FillAmForVariancePU(PUr);
  end;
LPUVcRecordCheck_GenTrans:;
  if (gentrans==false) then begin transf = false; end;
  if (IsStandardProduct) then begin
    transf = HasIntegratedNL and transf;
  end;
  if (transf) then begin
    errcode = MakeTransFromPU(gTRp,PUr,LocRec,false);
    if (errcode>0) then begin
      RecordCheckError(errcode,"",-1,"SerNr");      
      res = -1;
      goto L99PUVcRecordCheck;
    end;
    if ((gTRp.Number>0) and (gTRp.IntYc==PUYc)) then begin
      errstr = CheckTrans(gTRp,2,true);
      if (nonblank(errstr)) then begin
        RecordCheckError(1085,errstr,-1,"SerNr");      
        res = -1;
        goto L99PUVcRecordCheck;
      end;
      switch (gRuniningMaint) begin    
        case "RecalcStockMn":
          UpdateTrans_Stock(gTRp);          
        otherwise
          SaveTrans(gTRp);
          AddTTrans_PUVc(gTRp,PUr);
      end;
    end;    
  end;  
L99PUVcRecordCheck:;
  if (res!=0) then begin PUr.SerNr = oldnr; end;
  PUVcRecordCheck = res;
  return;
end;
  
global
updating function LongInt PUCheckIfSaveAllowed(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  SetRecordCheckVc("PUVc");
  res = PUVcRecordCheck(PUr,PU2r,stat,long4);  
  PUCheckIfSaveAllowed = res;
  return;
end;
  
procedure PUVcConvertB1ToB2(var record PUVc PUp,string curp,val frp,var val to1p,var val to2p,var val br1p,var val br2p)
begin
  row PUVc PUrw;
  Integer rwcnt,i;
  val fr,to1,to2,br1,br2;
  string 20 curncy;
  Boolean base2inv;
  Boolean treated;
  Date curdate;
  val t;
  string 255 tstr;

  curdate = CurrentDate;
  curncy = curp;
  GetFullCurncyRate(curncy,PUp.TransDate,fr,to1,to2,br1,br2);
  if (curncy==curp) then begin base2inv = true; end;
  SwapM4Val(br1p,br2p);
  SwapM4Val(to1p,to2p);
  rwcnt = MatRowCnt(PUp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    B1ToB2StrValRM(PUrw.Extra,br1p,br2p,tstr,GetCostRoundModeRB);
    PUrw.Extra = tstr;
    B1ToB2ValRM(PUrw.CostPrice,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.CostPrice = t;
    B1ToB2ValRM(PUrw.Sum,br1p,br2p,t,GetCostRoundModeRB); //Sum always in base 1, isnt it
    PUrw.Sum = t;
    B1ToB2ValRM(PUrw.ShipCost,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.ShipCost = t;
    B1ToB2ValRM(PUrw.RowCost1,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.RowCost1 = t;
    B1ToB2ValRM(PUrw.RowCost2,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.RowCost2 = t;
    B1ToB2ValRM(PUrw.RowCost3,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.RowCost3 = t;
    B1ToB2ValRM(PUrw.RowCost4,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.RowCost4 = t;
    B1ToB2ValRM(PUrw.RowCost5,br1p,br2p,t,GetCostRoundModeRB);
    PUrw.RowCost5 = t;
    B1ToB2StrValRM(PUrw.CustomsCost,br1p,br2p,tstr,GetCostRoundModeRB);
    PUrw.CustomsCost = tstr;
    MatRowPut(PUp,i,PUrw);
  end;
  return;
end;

global
function LongInt PUVcRecordImport(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record ConvMasterBlock cvm;
  Boolean gToDualBase,gBase1ToBase2;
  val fr,to1,to2,br1,br2,t;
  string 10 curncy;

  BlockLoad(cvm);
  if (cvm.DualBaseCurrencyFlag!=0) then begin gToDualBase = true; end;
  if (cvm.Base1ToBase2Flag!=0) then begin gBase1ToBase2 = true; end;
  if (gToDualBase) then begin
    curncy = PUr.CurncyCode;
    fr = PUr.FrRate;
    to1 = PUr.ToRateB1;
    to2 = PUr.ToRateB2;
    br1 = PUr.BaseRate1;
    br2 = PUr.BaseRate2;
    ConvertToDualBase(curncy,PUr.TransDate,fr,to1,to2,br1,br2,t,false);
    PUr.CurncyCode = curncy;
    PUr.FrRate = fr;
    PUr.ToRateB1 = to1;
    PUr.ToRateB2 = to2;
    PUr.BaseRate1 = br1;
    PUr.BaseRate2 = br2;
  end;
  if (gBase1ToBase2) then begin
    to1 = PUr.ToRateB1;
    to2 = PUr.ToRateB2;
    br1 = PUr.BaseRate1;
    br2 = PUr.BaseRate2;
    PUVcConvertB1ToB2(PUr,PUr.CurncyCode,PUr.FrRate,to1,to2,br1,br2);
    PUr.ToRateB1 = to1;
    PUr.ToRateB2 = to2;
    PUr.BaseRate1 = br1;
    PUr.BaseRate2 = br2;
    PUSumUp(PUr);
  end;
  if (PUr.NoTAXonVAT==-1) then begin
    PUr.NoTAXonVAT = 0;
  end;
  PUVcRecordImport = res;
  return;
end;

global
updating function LongInt PUVcRecordImportAfter(var record PUVc PUr,record PUVc PU2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  Integer err;
    
  if (PUr.OKFlag!=0) then begin
    PurUpdateSerialNr(PUr,true,false);
    if (ImportingTextBackup==false and CanOKStockRecord(err)==true) then begin
      PurUpdateStock(PUr,false);
      PurUpdateCostPrice(PUr,false);  // this should be called during import since records are from other systems
      PurUpdateItemHist(PUr);
    end;
  end;
  PUVcRecordImportAfter = res; 
  return;
end;

function
Boolean InvalidPUOK(record PUVc PUp)
begin
  Boolean res;
  Boolean noinvalidf;
  record ItemHistVc IHr;
  row PUVc PUrw;
  Integer i,rwcnt;
  Integer primary;
  record INVc INr;

  noinvalidf = false;
  res = true;
  rwcnt = MatRowCnt(PUp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    if (ReadFirstItem(PUrw.ArtCode,INr,false,false)) then begin
      if (INr.ItemType==1) then begin
        noinvalidf = true;
      end;
    end;
/*    
    primary = ArtCodePrimaryCostModel(PUrw.ArtCode);  
    switch (primary) begin
      case 0:
      case 9:
      case 3:
        IHr.FileName = "PUVc";
        IHr.ArtCode = PUrw.ArtCode;
        if (ReadFirstKey("FNArtCode",IHr,2,true)) begin
          noinvalidf = true;
        end;
      otherwise
        IHr.FileName = "PUVc";
        IHr.TransNr = PUp.SerNr;
        IHr.Row = i;
        if (ReadFirstKey("FNTransNr",IHr,3,true)) begin
          if (IHr.RemQty!=PUrw.Quant) then begin
            noinvalidf = true;
          end;
        end else begin
          noinvalidf = true;
        end;
    end;
*/    
    if (noinvalidf) then begin
      res = false;
      MessageBox(34500,"");
      goto LInvalidPUOK;
    end;
  end;
LInvalidPUOK:;  
  InvalidPUOK = res;
  return;
end;

global
function LongInt PUVcRecordInvalidateTest(var record PUVc PUr,record PUVc PU2r,LongInt long3,LongInt long4)
begin
  LongInt res; 

  res = 0;
  if (UserCanAction("PUInvalid",true)) then begin
    if ((PUr.Invalid==0) and (PUr.OKFlag!=0)) then begin
      if (InvalidPUOK(PUr)) then begin
        PUr.Invalid = 1;     
        res = 1;
      end;
    end else begin
      if (PUr.Invalid==0) then begin
        MessageBox(34500,"");
      end;
    end;
  end;  
//  res = 0;// more code needed
  PUVcRecordInvalidateTest = res; 
  return;
end;

/* not possible
global
function LongInt PUVcRecordTransDateOff(var record PUVc PUr,record PUVc PU2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  PUVcRecordTransDateOff = res; 
  return;
end;
*/
