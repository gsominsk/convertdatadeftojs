external function val HasObjectsMaxNumber();
external function Boolean HasIntegratedNL();
external function Integer GetVATLaw();
external function Integer CheckVATCodes(string,string,string,var string);
external function Boolean TransInFiscal(Date);
external function Boolean IsControlAccount(string,Boolean,Boolean);
external function Boolean DisallowFutureDateCheck(Boolean,Date,string,Integer);
external procedure TrBal(record TRVc,var val,var val,Integer);
external function Boolean ReadTAcc(string,string,Boolean,var record TAccVc);
external procedure SetTRObjectRows(var record TRVc);
external function LongInt GetCurUserLastNr(string);
external procedure TRSumup(var record TRVc,var val);
external function LongInt TestTranspCorst2(record TRVc);
external function Boolean RecordSupportExists(string,string,LongInt);
external function Boolean VATCodeExists(string);
external function Integer CheckObjs(string,string,var string);
external function Boolean SLAccTest(string,Integer);
external function Boolean DateLocked(Date,string);
external function Integer GetIntYc(Date);
external function Boolean SerNrTestTRVc(LongInt,Date,var Boolean);

procedure ChangeLastTrRow(record TRVc TRp,val temp2)
begin
  row TRVc TRrw;
  Integer rwcnt;
  
  rwcnt = MatRowCnt(TRp);
  if (rwcnt>0) then begin
    MatRowGet(TRp,rwcnt-1,TRrw);
    if (temp2!=0) then begin
      if (TRrw.DebVal2==0) then begin
        TRrw.CredVal2 = TRrw.CredVal2 + temp2;
      end else begin
        TRrw.DebVal2 = TRrw.DebVal2 - temp2;
      end;
    end;
    MatRowPut(TRp,rwcnt-1,TRrw);
  end;
  return;
end;

global
function LongInt TRVcRecordCheck(record TRVc TRp,record TRVc TR2p,LongInt stat,LongInt long4)
begin
  LongInt res;
  record BaseCurBlock BaseCurr;
  record TRGenBlock TRGb;
  record CYBlock CYb;
  record SRBlock SRb;
  record IPrsVc IPrsr;  
  record OPrsVc OPrsr;  
  record AccVc Accr;
  record CUVc CUr;  
  record CUVc VEr;  
  record TRVc lTRr;
  record VIVc VIr;  
  record IVVc IVr;  
  row TRVc TR2rw;
  row TRVc TRrw;
  row TRVc TRtrw;
  val temp,temp2;
  LongInt oldnr;
  LongInt newnr;
  Boolean gentrans,entinvnrf;
  string 255 tstr;
  Integer i,orwcnt,rwcnt,onaccf,errcode;
  Integer kk,j,starti;
  string 255 errstr;
  Boolean testf;
  record TAccVc TAccr;
  record CurncyCodeVc CurncyCoder;
  record APAccBlock APAccb;
  record APVc APr;
  record AccBlock ARAccb;
  record ARVc ARr;
  record VITBlock VITb;
  val amount,koef;
  string 255 errfieldname;
  transaction Boolean gTROnEnterKey;
  transaction string 255 gTROnEnterKey_fieldname;
  transaction Integer gTROnEnterKey_rownr;
  
  res = 0;
  BlockLoad(SRb);
  BlockLoad(CYb);
  BlockLoad(TRGb);
  BlockLoad(BaseCurr);
  BlockLoad(APAccb);
  BlockLoad(VITb);
  SetTRObjectRows(TRp);
  if (TransInFiscal(TRp.TransDate)==false) then begin
    RecordCheckError(1075,"",-1,"Number");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  oldnr = TRp.Number;
  if (TRp.Number<=0) then begin
    newnr = GetCurUserLastNr("TRVc");    
    if (newnr==-1) then begin
      newnr = SRb.LastTRNr;
    end;    
    TRp.Number = NextSerNr("TRVc",TRp.TransDate,newnr,false,"");
  end;
  if (TRp.IntYc<=200) then begin// Powerfull 
    if ((stat==Rs_insert) or (TRp.Number!=Tr2p.Number) or (TRp.TransDate!=Tr2p.TransDate)) then begin
      if (SerNrTestTRVc(TRp.Number,TRp.TransDate,gentrans)==false) then begin
        RecordCheckError(1557,"",-1,"Number");      
        res = -1; 
        goto LTRVcRecordCheck;
      end;
    end;
  end;
  if (blankdate(TRp.TransDate)) then begin
    RecordCheckError(1024,"",-1,"TransDate");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  if (DisallowFutureDateCheck(true,TRp.TransDate,"TransDate",-1)) then begin
    res = -1;
    goto LTRVcRecordCheck;
  end;
  if (TRp.IntYc<=200) then begin
    TRp.IntYc = GetIntYc(TRp.TransDate);
  end;
  if (TRp.IntYc<=1) then begin
    RecordCheckError(1047,"",-1,"TransDate");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  if (DateLocked(TRp.TransDate,"TRVc")) then begin
    RecordCheckError(1046,"",-1,"TransDate");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  if (stat==Rs_update) then begin  
    if (DateLocked(TR2p.TransDate,"TRVc")) then begin
      RecordCheckError(1046,"",-1,"TransDate");      
      res = -1; 
      goto LTRVcRecordCheck;
    end;
  end;
  rwcnt = MatRowCnt(TRp);
  if (stat==Rs_update) then begin
    orwcnt = MatRowCnt(Tr2p);
  end;
  if (rwcnt==0) then begin
    RecordCheckError(1030,"",0,"AccNumber");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  if (stat==Rs_update) then begin
    for (i=rwcnt-1;i>=0;i=i-1) begin
      MatRowGet(TRp,i,TRrw);
      if (TRrw.stp==2) then begin
        starti = i;
        i = -1;
      end;
    end;
  end;
  for (i=starti;i<rwcnt;i=i+1) begin
    onaccf = 0;
    MatRowGet(TRp,i,TRrw);
    if (stat==Rs_update) then begin
      if (orwcnt>i) then begin
        MatRowGet(Tr2p,i,TR2rw);
      end;
    end;
    if (TRrw.Typ==kTransactionRowTypeAny) then begin
      RecordCheckError(20849,"",i,"Typ");      
      res = -1; 
      goto LTRVcRecordCheck;
    end;
    if (TRrw.Typ==kTransactionRowTypeCustomer) then begin
      if (TRGb.IVGenTrans==1) then begin
        if ((nonblankdate(TRGb.IVStartDate)) and (TRp.TransDate<TRGb.IVStartDate)) then begin
          RecordCheckError(20856,"",-1,"TransDate");      
          res = -1; 
          goto LTRVcRecordCheck;
        end;   
      end else begin
        RecordCheckError(20856,"",-1,"TransDate");      
        res = -1; 
        goto LTRVcRecordCheck;
      end;
    end;
    if (TRrw.Typ==kTransactionRowTypeSupplier) then begin
      if (TRGb.VIGenTrans==1) then begin
        if ((nonblankdate(TRGb.VIStartDate)) and (TRp.TransDate<TRGb.VIStartDate)) then begin
          RecordCheckError(20856,"",-1,"TransDate");      
          res = -1; 
          goto LTRVcRecordCheck;
        end;
      end else begin
        RecordCheckError(20856,"",-1,"TransDate");      
        res = -1; 
        goto LTRVcRecordCheck;
      end;
    end;
    switch (TRrw.stp) begin
      case 1:
        if (TRrw.ovst==0) then begin
          if (stat==Rs_update) then begin
            if (CYb.TRBlockSubsytemRows!=0) then begin
              if (TRrw.Typ!=0) then begin
                RecordCheckError(22003,"",i,"Typ");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
            end;          
          end;
          if (nonblank(TRrw.AccNumber)) then begin
            Accr.AccNumber = TRrw.AccNumber;
            if (ReadFirstMain(Accr,1,true)==false) then begin
              RecordCheckError(1007,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            if ((Accr.blockedFlag!=0) or (Accr.GroupAcc!=0)) then begin
              RecordCheckError(1084,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            if (HasIntegratedNL) then begin
              if (SLAccTest(TRrw.AccNumber,0)) then begin
                if (TRrw.Typ==0) then begin
                  if (CYb.usePrel==0) then begin
                    if (stat==Rs_insert) then begin
                      RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
                      res = -1; 
                      goto LTRVcRecordCheck;
                    end else begin
                      if (i>=orwcnt) then begin
                        RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
                        res = -1; 
                        goto LTRVcRecordCheck;
                      end;
                    end;
                  end;
                end;
              end;
            end;
            if (nonblank(TRrw.Curncy)) then begin
              CurncyCoder.CurncyCode = TRrw.Curncy;
              if (ReadFirstMain(CurncyCoder,1,true)==false) then begin
                RecordCheckError(1260,TRrw.Curncy,i,"Curncy");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
            end;            
            if (CYb.SkipObjectTypeControlforYearEnd==0) or (CYb.SkipObjectTypeControlforYearEnd!=0 and (TRrw.SAFTType<kTransactionFiscalTypeYearEnd or TRrw.SAFTType>kTransactionFiscalTypeYearEndStage2)) then begin
              errcode = CheckObjs(TRrw.AccNumber,TRrw.Objects,errstr);
              if (errcode!=0) then begin
                errfieldname = "Objects";
                RecordCheckError(errcode,errstr,i,"Objects");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
            end;
            errcode = CheckVATCodes(TRrw.AccNumber,TRrw.VATCode,"",errstr);
            if (errcode!=0) then begin
              RecordCheckError(errcode,errstr & USetStr(24219) & TRrw.AccNumber,i,"VATCode");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;

            if ((blank(TRrw.DebVal)) and (blank(TRrw.CredVal))) then begin
              RecordCheckError(1009,"",i,"DebVal");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            if (nonblank(TRrw.VATCode)) then begin
              if (VATCodeExists(TRrw.VATCode)==false) then begin
                RecordCheckError(1120,TRrw.VATCode,i,"VATCode");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
            end;
            if (nonblank(Accr.Curncy)) then begin
              if (TRrw.Curncy!=Accr.Curncy) then begin
                RecordCheckError(1217,Accr.Curncy,i,"Curncy");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
              if (BaseCurr.BaseCur1!=Accr.Curncy) and (BaseCurr.BaseCur2!=Accr.Curncy) then begin
                if ((blank(TRrw.CurDebVal)) and (blank(TRrw.CurCredVal))) then begin
                  RecordCheckError(1009,"",i,"CurDebVal");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end;
              end;
            end;          
            if (nonblank(TRrw.CompCode)) then begin
              if (TRrw.Typ==0) then begin
                RecordCheckError(1249,"",i,"CompCode");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;
              if (TRrw.Typ==1) then begin              
                CUr.Code = TRrw.CompCode;
                if (ReadFirstMain(CUr,1,true)==false) then begin
                  RecordCheckError(1120,TRrw.CompCode,i,"CompCode");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end else begin
                  onaccf = CUr.OnAccount;
                end;
                if (TRrw.SerNr>0) then begin
                  if (TRrw.CredVal!=0) then begin//recipt, credit notes
                    IVr.SerNr = TRrw.SerNr;
                    if (ReadFirstMain(IVr,1,true)) then begin
                      if (IVr.InvDate>TRp.TransDate) then begin
                        RecordCheckError(20852,"",i,"SerNr");      
                        res = -1; 
                        goto LTRVcRecordCheck;
                      end;
                    end;

                    if (stat!=Rs_update) then begin
                    if (ARAccb.NoOverPayIV!=0) then begin
                      ARr.InvoiceNr = TRrw.SerNr;
                      ReadFirstMain(ARr,1,true);
                      amount = TRrw.CredVal;
                      for (j=0;j<i;j=j+1) begin
                        MatRowGet(TRp,j,TR2rw);
                        if (TR2rw.SerNr==TRrw.SerNr) and (TR2rw.Typ==1) then begin
                          amount = amount + TR2rw.CredVal;
                        end;
                      end;
                      if ((ARr.BookRVal<0) and (amount<0)) then begin
                        koef = -1.00;
                      end else begin
                        koef = 1.00;
                      end;
                      if ((ARr.BookRVal*koef) < (amount*koef)) then begin
                        tstr = " " & amount & USetStr(20061);
                        tstr = tstr & " " & ARr.RVal;
                        tstr = tstr & USetStr(20064) & " " & (amount-ARr.RVal) & USetStr(20063);
                        RecordCheckError(20065,tstr,i,"SerNr");      
                        res = -1; 
                        goto LTRVcRecordCheck;  
                      end;
                    end;
                    end;
                    
                  end;
                end;
              end;
              if (TRrw.Typ==2) then begin
                VEr.Code = TRrw.CompCode;
                if (ReadFirstMain(VEr,1,true)==false) then begin
                  RecordCheckError(1120,TRrw.CompCode,i,"CompCode");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end else begin
                  onaccf = VEr.OnAccount;
                end;
                if (TRrw.SerNr>0) then begin
                  if (TRrw.DebVal!=0) then begin//payment, credit notes
                    VIr.SerNr = TRrw.SerNr;
                    if (ReadFirstMain(VIr,1,true)) then begin
                      if (VIr.InvType==kInvoiceTypeCredit or VIr.InvType==kInvoiceTypeCreditSpecialSales) then begin
                        if (VIr.CredInv>0) then begin
                        if (VITb.DisallowInvDateAfterTransDate!=0 and VIr.InvDate>TRp.TransDate) then begin
                          RecordCheckError(20852,"",i,"SerNr");      
                          res = -1; 
                          goto LTRVcRecordCheck;
                        end;
                        end;
                      end else begin
                        if (VITb.DisallowInvDateAfterTransDate!=0 and VIr.InvDate>TRp.TransDate) then begin
                          RecordCheckError(20852,"",i,"SerNr");      
                          res = -1; 
                          goto LTRVcRecordCheck;
                        end;
                        if (stat!=Rs_update) then begin
                        if (APAccb.NoOverPayVI!=0) then begin
                          APr.SerNr = TRrw.SerNr;
                          ReadFirstMain(APr,1,true);
                          amount = TRrw.DebVal;
                          for (j=0;j<i;j=j+1) begin
                            MatRowGet(TRp,j,TR2rw);
                            if (TR2rw.SerNr==TRrw.SerNr) and (TR2rw.Typ==2) then begin
                              amount = amount + TR2rw.DebVal;
                            end;
                          end;
                          if ((APr.BookRVal<0) and (amount<0)) then begin
                            koef = -1.00;
                          end else begin
                            koef = 1.00;
                          end;
                          if ((APr.BookRVal*koef) < (amount*koef)) then begin
                            tstr = " " & amount & USetStr(20061);
                            tstr = tstr & " " & APr.RVal;
                            tstr = tstr & USetStr(20064) & " " & (amount-APr.RVal) & USetStr(20063);
                            RecordCheckError(20065,tstr,i,"SerNr");      
                            res = -1; 
                            goto LTRVcRecordCheck;  
                          end;
                        end;
                        end;
                      end;
                    end;
                  end;
                end;
              end;
            end;
            if (TRrw.SerNr!=-1) then begin
              if (TRrw.Typ==0) then begin                
                for (kk=0;kk<rwcnt;kk=kk+1) begin
                   MatRowGet(TRp,kk,TRtrw);
                   if (TRtrw.SerNr==TRrw.SerNr) then begin
                     if (TRtrw.Typ!=0) then begin goto LTRVcRecordCheck11; end;
                   end;
                end;  
                RecordCheckError(1123,"",kk,"SerNr");      
                res = -1; 
                goto LTRVcRecordCheck;
LTRVcRecordCheck11:;                
              end;
              if (GetVATLaw()!=28) then begin//checkbox needed,LangLatvian
                if (TRrw.Typ!=0) then begin
                  for (kk=0;kk<rwcnt;kk=kk+1) begin
                    MatRowGet(TRp,kk,TRtrw);
                    if (TRtrw.SerNr==TRrw.SerNr) then begin
                      if (kk!=i) then begin
                        if (TRtrw.Typ!=0) then begin
                          if (TRtrw.Typ==TRrw.Typ) then begin
                            testf = false;
                            switch (TRtrw.Typ) begin
                              case 1:
                                IVr.SerNr = TRtrw.SerNr;
                                testf = !ReadFirstMain(IVr,1,true);
                              case 1:
                                VIr.SerNr = TRtrw.SerNr;
                                testf = !ReadFirstMain(VIr,1,true);
                            end;
                            if (testf) then begin//only invoices issued in NL
                              RecordCheckError(1115,"",kk,"SerNr");      
                              res = -1; 
                              goto LTRVcRecordCheck;
                            end;
                          end;
                        end;
                      end;
                    end;
                  end; 
                end;
              end;  
              if (TRrw.Typ==kTransactionRowTypeCustomer) then begin                
                RecordNew(IPrsr);
                IPrsr.IVNr = TRrw.SerNr;
                IPrsr.CustCode = "";
                IPrsr.AccNr = "";
                if (ReadFirstKey("IVKey",IPrsr,1,true)==true) then begin
                  if (TRrw.CompCode!=IPrsr.CustCode) then begin
                    RecordCheckError(1250,IPrsr.CustCode,i,"CompCode");      
                    res = -1; 
                    goto LTRVcRecordCheck;
                  end;
                  if (RecordSupportExists("ARVc","TRVc",TRrw.SerNr)) then begin
                    RecordCheckError(1115,"",i,"SerNr");      
                    res = -1; 
                    goto LTRVcRecordCheck;
                  end;
                end else begin
                  if (TRp.IntYc<201) then begin
                    if (TRrw.CredVal!=0) then begin
                      RecordCheckError(1133,TRrw.SerNr,i,"SerNr");      
                      res = -1; 
                      goto LTRVcRecordCheck;
                    end;
                  end;
                end;
              end;
              if (TRrw.Typ==kTransactionRowTypeSupplier) then begin              
                RecordNew(OPrsr);
                OPrsr.VINr = TRrw.SerNr;
                OPrsr.VECode = "";
                if (ReadFirstKey("VIKey",OPrsr,1,true)==true) then begin
                  if (TRrw.CompCode!=OPrsr.VECode) then begin
                    RecordCheckError(1250,OPrsr.VECode,i,"CompCode");      
                    res = -1; 
                    goto LTRVcRecordCheck;
                  end;
                  if (RecordSupportExists("APVc","TRVc",TRrw.SerNr)) then begin
                    RecordCheckError(1560,"",i,"SerNr");      
                    res = -1; 
                    goto LTRVcRecordCheck;
                  end;
                end else begin
                  if (TRp.IntYc<201) then begin
                    if (TRrw.DebVal!=0) then begin
                      RecordCheckError(1133,OPrsr.VINr,i,"SerNr");      
                      res = -1; 
                      goto LTRVcRecordCheck;
                    end;
                  end;
                end;
              end;
            end else begin
              entinvnrf = false;            
              switch (TRrw.Typ) begin
                case kTransactionRowTypeCustomer:
                  if (TRrw.CredVal!=0) then begin
                    if (onaccf==0) then begin entinvnrf = true; end;
                  end else begin
                    if (TRrw.DebVal!=0) then begin 
                      if (onaccf==0) then begin entinvnrf = true; end;
                    end;
                  end; 
                  if (nonblank(CUr.CurncyCode)) then begin
                    if (TRrw.Curncy!=CUr.CurncyCode) then begin
                      RecordCheckError(1217,"",i,"Curncy");
                      res = -1; 
                      goto LTRVcRecordCheck;
                    end;
                  end;                   
                case kTransactionRowTypeSupplier:
                  if (TRrw.DebVal!=0) then begin
                    if (onaccf==0) then begin entinvnrf = true; end;
                  end else begin
                    if (TRrw.CredVal!=0) then begin
                      if (onaccf==0) then begin entinvnrf=true; end;
                    end;
                  end;  
                  if (nonblank(VEr.VECurncyCode)) then begin
                    if (TRrw.Curncy!=VEr.VECurncyCode) then begin
                      RecordCheckError(1217,"",i,"Curncy");
                      res = -1; 
                      goto LTRVcRecordCheck;
                    end;
                  end;                   
                otherwise
                  ;
              end;
              if (entinvnrf==true) then begin
                RecordCheckError(1116,TRrw.SerNr,i,"SerNr");      
                res = -1; 
                goto LTRVcRecordCheck;
              end;  
            end;
          end else begin            
            if ((TRrw.DebVal!=0) or (TRrw.CredVal!=0)) then begin
              RecordCheckError(1010,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            if (TRrw.Typ!=0) then begin
              RecordCheckError(1010,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            RecordCheckError(1010,"",i,"AccNumber");      
            res = -1; 
            goto LTRVcRecordCheck;            
          end;
          if (nonblank(TRrw.TAccNumber)) then begin
            if (ReadTAcc(TRrw.TAccNumber,TRrw.Objects,nonblank(TRrw.DebVal),TAccr)) then begin
              RecordCheckError(1007,TRrw.AccNumber,i,"TAccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
            if (TAccr.Closed!=0) then begin
              RecordCheckError(1084,TAccr.AccNumber,i,"TAccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
          end;
          testf = true;
          if (stat==Rs_update) then begin
            if (i<orwcnt) then begin
              MatRowGet(TR2p,i,TR2rw);
              if (TR2rw.AccNumber==TRrw.AccNumber) and (TR2rw.Typ==TRrw.Typ) and (TR2rw.DebVal==TRrw.DebVal) and (TR2rw.CredVal==TRrw.CredVal) and (TR2rw.DebVal2==TRrw.DebVal2) and (TR2rw.CredVal2==TRrw.CredVal2) then begin
                testf = false;
              end;
            end;
          end;
          if (HasIntegratedNL==false) then begin testf = false; end;
          if (testf) then begin
            switch (TRrw.Typ) begin
              case 0:
                if (IsControlAccount(TRrw.AccNumber,true,false)) then begin
                  RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end;
                if (IsControlAccount(TRrw.AccNumber,false,false)) then begin
                  RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end;
              case 1:
                if (IsControlAccount(TRrw.AccNumber,true,true)==false) then begin
                  RecordCheckError(22082,TRrw.AccNumber,i,"AccNumber");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end;
              case 2:
                if (IsControlAccount(TRrw.AccNumber,false,true)==false) then begin
                  RecordCheckError(22082,TRrw.AccNumber,i,"AccNumber");      
                  res = -1; 
                  goto LTRVcRecordCheck;
                end;
            end;
          end;
/*          
          if (TRrw.Typ==0) then begin
            if (SLAccTest(TRrw.AccNumber,0)) then begin
              RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
          end;          
*/
        end else begin
          if (HasIntegratedNL) then begin
            if (SLAccTest(TRrw.AccNumber,0)) then begin
              RecordCheckError(1082,TRrw.AccNumber,i,"AccNumber");      
              res = -1; 
              goto LTRVcRecordCheck;
            end;
          end;
        end;
      case 2:
        if (blank(TRrw.Sign)) then begin
          RecordCheckError(1025,"",i,"Sign");      
          res = -1; 
          goto LTRVcRecordCheck;
        end;
      case 4:
        lTRr.Number = TRrw.CorrNr;
        lTRr.IntYc = TRrw.CorrYc;
        if (lTRr.IntYc==0) then begin
          lTRr.IntYc = TRp.IntYc;
          TRrw.CorrYc = TRp.IntYc;
        end;        
        if (ReadFirstMain(lTRr,2,true)==false) then begin
          RecordCheckError(1026,"",i,"CorrNr");      
          res = -1; 
          goto LTRVcRecordCheck;
        end;
    end;
  end;
  if (CYb.TRCheckCorsp!=0) then begin
    res = TestTranspCorst2(TRp);
    if (res!=0) then begin
      switch (CYb.TRCheckCorsp) begin
        case 1:
          RecordCheckError(res,"",rwcnt-1,"DebVal");      
          res = -1; 
          goto LTRVcRecordCheck;
        case 2:
          MessageBox(res,"");
      end;
    end;
  end;
  TrBal(TRp,temp,temp2,-1);
  if (temp!=0) then begin
    RecordCheckError(1011,"",rwcnt-1,"DebVal");      
    res = -1; 
    goto LTRVcRecordCheck;
  end;
  if (temp2!=0) then begin
    RecordCheckError(1011,"",rwcnt-1,"DebVal2");      
    res = -1; 
    goto LTRVcRecordCheck;
//    ChangeLastTrRow(TRp,temp2);//software changes amounts entered by user ? where is Update Note for this ? 
//    TRSumup(TRp,temp);
  end;
LTRVcRecordCheck:;
  if (gTROnEnterKey) then begin
    gTROnEnterKey_fieldname = errfieldname;
    gTROnEnterKey_rownr = i;
  end;
  if (res!=0) then begin TRp.Number = oldnr; end;
  TRVcRecordCheck = res;
  return;
end;
