external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt OffHireSerBlockCheck(var record OffHireSerBlock OffHireSerr,record OffHireSerBlock OffHireSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row OffHireSerBlock OffHireSerrw;
  row OffHireSerBlock OffHireSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(OffHireSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(OffHireSerr,i,OffHireSerrw);
    if (OffHireSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LOffHireSerBlockCheck;        
    end;
    if (OffHireSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LOffHireSerBlockCheck;        
    end;
    if ((nonblankdate(OffHireSerrw.StartDate)) and (nonblankdate(OffHireSerrw.EndDate))) then begin
      if (OffHireSerrw.StartDate>OffHireSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LOffHireSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(OffHireSerr,j,OffHireSer2rw);
      res = SerBlockCheckOverlap(OffHireSerrw.TSerStart,OffHireSerrw.TSerEnd,OffHireSer2rw.TSerStart,OffHireSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LOffHireSerBlockCheck;        
      end;
    end;        
  end;  
LOffHireSerBlockCheck:;  
  OffHireSerBlockCheck = res;
  return;
end;
