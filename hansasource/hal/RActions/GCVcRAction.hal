external function Boolean IsMarketplaceServer();
external function Boolean SerNrTestGCVc(LongInt,Date,var Boolean);

global
function LongInt GCVcRecordCheck(var record GCVc GCr,record GCVc GC2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record GCVc locGCr;
  Boolean gentrans;
  record GCTypeVc GCTr;

  res = 0;
  if (GCr.SerNr<=0) then begin
    GCr.SerNr = NextSerNr("GCVc",GCr.TransDate,-1,true,"");
  end;
  if (SerNrTestGCVc(GCr.SerNr,GCr.TransDate,gentrans)==false) then begin
    RecordCheckError(1557,"",-1,"SerNr");      
    res = -1; 
    goto LGCVcRecordCheck;
  end;
  locGCr.SerNr = GCr.SerNr;
  if (ReadFirstMain(locGCr,1,true)) then begin    
    if (locGCr.SerNr!=GCr.SerNr) then begin
      RecordCheckError(1547,"",-1,"SerNr");   
      res = -1; 
      goto LGCVcRecordCheck;  
    end;
  end;
  if (nonblank(GCr.BarCode)) then begin
    locGCr.BarCode = GCr.BarCode;
    if (ReadFirstKey("BarCode",locGCr,1,true)) then begin    
      if (locGCr.SerNr!=GCr.SerNr) then begin
        RecordCheckError(1547,"",-1,"BarCode");   
        res = -1; 
        goto LGCVcRecordCheck;  
      end;
    end;
  end;
  if (IsMarketplaceServer==false) then begin
    if (GCr.Amount==0) then begin
      RecordCheckError(1058,"  " & GCr.Amount,-1,"Amount");
      res = -1;
      goto LGCVcRecordCheck;
    end; 
  end;
  if (blankdate(GCr.TransDate)) then begin
    RecordCheckError(1058," " & GCr.TransDate,-1,"TransDate");
    res = -1;
    goto LGCVcRecordCheck;
  end; 
  if (blankdate(GCr.ExpiryDate)) then begin
    RecordCheckError(1058," " & GCr.ExpiryDate,-1,"ExpiryDate");
    res = -1;
    goto LGCVcRecordCheck;
  end; 
  if (GCr.TransDate>GCr.ExpiryDate) then begin
    RecordCheckError(1142," " & GCr.ExpiryDate,-1,"ExpiryDate");
    res = -1;
    goto LGCVcRecordCheck;
  end;
  if (nonblank(GCr.Type)) then begin
    GCTr.Code = GCr.Type;
    if (ReadFirstMain(GCTr,1,true)==false) then begin
      RecordCheckError(1290,"",-1,"Type");
      res = -1;
      goto LGCVcRecordCheck;
    end;
  end;
/*  
  if (GCTyper.RequireGuarantor!=0) then begin
    if (blank(GCr.GuarantorCode)) then begin
      RecordCheckError(1058,"",-1,"GuarantorCode");
      res = -1;
      goto LGCVcRecordCheck;
    end;
  end;
*/  
LGCVcRecordCheck:;
  GCVcRecordCheck = res;
  return;
end;

global
function LongInt GCVcRecordInIndex(record GCVc GCr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (GCr.Closed!=0) then begin 
    if (indexname=="ActSerNr")  then begin res = 0; end;
    if (indexname=="ActTransDate")  then begin res = 0; end;
    if (indexname=="ActComment")  then begin res = 0; end;
    if (indexname=="ActOKFlag")  then begin res = 0; end;
  end;
  GCVcRecordInIndex = res;
  return;
end;

global
function LongInt GCVcRecordDefaults(var record GCVc GCr,record GCVc GC2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record GCBlock GCb;

  BlockLoad(GCb);
  GCr.Closed = 0;
  GCr.OKFlag = 0;
  GCr.TransDate = CurrentDate;
  if (GCb.ExpiryMonths>0) then begin 
    GCr.ExpiryDate = AddMonth(GCr.TransDate,GCb.ExpiryMonths);
  end;  
  if (SingleUserMode) then begin
    GCr.SerNr = NextSerNr("GCVc",GCr.TransDate,-1,true,"");
  end;
  GCVcRecordDefaults = res; 
  return;
end;

global
function LongInt GCVcRecordDuplicate(var record GCVc GCr,record GCVc GC2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  GCr.Closed = 0;
  GCr.OKFlag = 0;
  GCr.SerNr = -1;
  GCr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    GCr.SerNr = NextSerNr("GCVc",GCr.TransDate,-1,true,"");
  end;
  GCVcRecordDuplicate = res; 
  return;
end;

global
updating function LongInt GCVcRecordSaveAfter(var record GCVc GCr,record GCVc GC2r,LongInt long3,LongInt long4)
begin
  LongInt res;

  GCVcRecordSaveAfter = res; 
  return;
end;

global
updating function LongInt GCVcRecordUpdateAfter(var record GCVc GCr,record GCVc GC2r,LongInt long3,LongInt long4)
begin
  LongInt res;

  GCVcRecordUpdateAfter = res; 
  return;
end;

