external procedure NextM4Number(string,var string);  

procedure GetNextGuestNr(var string guestnr)
BEGIN
  record ProfileVc Guestr;
  record SRBlock SRRec;
  Boolean res;
  record GuestOptBlock GuestOptRec;
  LongInt gnr,frgnr,tognr;

  BlockLoad(GuestOptRec);
  BlockLoad(SRRec);
  NextM4Number(SRRec.LastGuestCode,guestnr);
  if (nonblank(guestnr)) then begin
    Guestr.Code = guestnr;
    if (ReadFirstMain(Guestr,1,true)==false) then begin
      SRRec.LastGuestCode = guestnr;
    end else begin
      Guestr.Code = "……………………………………………………";
      if (ReadLastMain(Guestr,1,false)) then begin
        NextM4Number(Guestr.Code,guestnr);
        if (nonblank(guestnr)) then begin
          SRRec.LastGuestCode = guestnr;
        end else begin
          guestnr = "1";
        end;  
      end;
    end;
  end;
  if (GuestOptRec.GuestCodeFromRange!=0) then begin
    gnr = StringToLongInt(guestnr);
    frgnr = StringToLongInt(GuestOptRec.FromGuestCode);
    tognr = StringToLongInt(GuestOptRec.ToGuestCode);
    if ((gnr<frgnr) or (gnr>tognr)) then begin
      Guestr.Code = "……………………………………………………";
      if (ReadLastMain(Guestr,1,false)) then begin
        guestnr = Guestr.Code;
        gnr = StringToLongInt(guestnr);
        frgnr = StringToLongInt(GuestOptRec.FromGuestCode);
        if (gnr<frgnr) then begin
          guestnr = GuestOptRec.FromGuestCode;
        end;
      end;
    end;
  end;
  RETURN;
END;

global
function LongInt ProfileVcRecordDuplicate(var record ProfileVc Profiler,record ProfileVc Profile2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  string 255 tstr;

  if (SingleUserMode) then begin
    GetNextGuestNr(tstr);
    Profiler.Code = tstr;
  end;
  ProfileVcRecordDuplicate = res;  
  return;
end;

global
function LongInt ProfileVcRecordDefaults(var record ProfileVc Profiler,record ProfileVc Profile2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  string 255 tstr;

  if (SingleUserMode) then begin
    GetNextGuestNr(tstr);
    Profiler.Code = tstr;
  end;
  ProfileVcRecordDefaults = res;  
  return;
end;

function Boolean NewspaperExists(string newspapercode)
BEGIN
  record NewspaperBlock NewspaperRec;
  row NewspaperBlock Newspaperrw;
  Integer i,rwcnt;
  Boolean res;
 
  BlockLoad(NewspaperRec);
  rwcnt = MatRowCnt(NewspaperRec);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(NewspaperRec,i,Newspaperrw);
    if (newspapercode==Newspaperrw.NewsCode) then begin
      res = true;
      goto LNewspaperExists;
    end;
  end;
LNewspaperExists:;  
  NewspaperExists = res;
  RETURN;
END;

global
function LongInt ProfileVcRecordCheck(record ProfileVc Profiler,record ProfileVc Profile2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer insertmode;
  record ProfileVc locProfiler;
  string 255 tstr;
  record GuestOptBlock GuestOptRec;

  BlockLoad(GuestOptRec);
  res = 0;
  insertmode = 1;
  if (blank(Profiler.Code)) then begin
    GetNextGuestNr(tstr);
    Profiler.Code = tstr;
    locProfiler.Code = Profiler.Code;
    if (ReadFirstMain(locProfiler,1,true)) then begin
      RecordCheckError(1547,Profiler.Code,-1,"Code");
      res = -1;
      goto LProfileVcRecordCheck;    
    end;
  end; 
  if (blank(Profiler.Code)) then begin
    RecordCheckError(1854,Profiler.Code,-1,"Code");
    res = -1;
    goto LProfileVcRecordCheck;    
  end; 
  if (GuestOptRec.GuestCodeFromRange!=0) then begin
    if ((Profiler.Code<GuestOptRec.FromGuestCode) or (Profiler.Code>GuestOptRec.ToGuestCode)) then begin
      RecordCheckError(2155,Profiler.Code,-1,"Code");
      res = -1;
      goto LProfileVcRecordCheck;    
    end;
  end;  
  if (stat==insertmode) then begin
    locProfiler.Code = Profiler.Code;
    if (ReadFirstMain(locProfiler,1,true)) then begin
      RecordCheckError(1547,Profiler.Code,-1,"Code");
      res = -1;
      goto LProfileVcRecordCheck;    
    end;
  end;
  if (nonblank(Profiler.Newspaper)) then begin
    if (NewspaperExists(Profiler.Newspaper)==false) then begin
      RecordCheckError(1120,Profiler.Newspaper,-1,"Newspaper");
      res = -1;
      goto LProfileVcRecordCheck;    
    end;
  end;
LProfileVcRecordCheck:;
  ProfileVcRecordCheck = res;
  RETURN;
END;

global
function LongInt ProfileVcRecordSave(record ProfileVc Profiler,record ProfileVc Profile2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  
  Profiler.DateChanged = CurrentDate;
  ProfileVcRecordSave = res;
  RETURN;
END;

global
updating function LongInt ProfileVcRecordSaveAfter(record ProfileVc Profiler,record ProfileVc Profile2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record SRBlock SRRec;

  BlockLoad(SRRec);
  SRRec.LastGuestCode = Profiler.Code;
  BlockStore(SRRec); 
LProfileVcRecordSaveAfter:;
  ProfileVcRecordSaveAfter = res;
  RETURN;
END;
  
global
function LongInt ProfileVcRecordUpdate(record ProfileVc Profiler,record ProfileVc Profile2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  
  Profiler.DateChanged = CurrentDate;
  ProfileVcRecordUpdate = res;
  RETURN;
END;

