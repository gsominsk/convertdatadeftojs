external procedure GetCustomsFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external function Boolean SerNrTestCustomsVc(LongInt,Date,var Boolean);

procedure SetInsPrc(record CustomsVc Customsr,var val ins)
begin
  record CustomsDefaultBlock cdrp;
  row CustomsDefaultBlock cdrw;
  Integer i,rwcnt;
  
  ins = BlankVal;
  BlockLoad(cdrp);
  rwcnt = MatRowCnt(cdrp);
  for (i=0; i<rwcnt; i=i+1) begin
    MatRowGet(cdrp,i,cdrw);
    if (i==0) then begin
      ins = cdrw.InsurancePrc;
    end;
    if (CurrentUser==cdrw.Signature) then begin
      ins = cdrw.InsurancePrc;
      goto LSetInsPrc;
    end;
  end;
LSetInsPrc:;
  return;
end;

global
function LongInt CustomsVcRecordDefaults(var record CustomsVc Customsr,record CustomsVc Customs2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res;
  val fr,to1,to2,br1,br2;
  string 255 tstr;
  val vp;

  Customsr.SerNr = -1;
  Customsr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    Customsr.SerNr = NextSerNr("CustomsVc",Customsr.TransDate,-1,false,"");
  end;
  Customsr.OKFlag = 0;
  Customsr.OrderedFlag = 0;
  tstr = "";
  GetCustomsFullCurncyRate(tstr,Customsr.TransDate,fr,to1,to2,br1,br2);
  Customsr.CurncyCode = tstr;
  Customsr.InsCurncy = tstr;
  Customsr.ShpCurncy = tstr;
  Customsr.Div1Curncy = tstr;
  Customsr.Div2Curncy = tstr;
  Customsr.FrRate = fr;
  Customsr.ToRateB1 = to1; 
  Customsr.ToRateB2 = to2;
  Customsr.BaseRate1 = br1;
  Customsr.BaseRate2 = br2;    
  SetInsPrc(Customsr,vp);
  Customsr.InsPrc = vp;
  CustomsVcRecordDefaults = res; 
  RETURN;
END;

global
function LongInt CustomsVcRecordDuplicate(var record CustomsVc Customsr,record CustomsVc Customs2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res;
  val fr,to1,to2,br1,br2;
  string 255 tstr;
  val vp;

  Customsr.SerNr = -1;
  Customsr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    Customsr.SerNr = NextSerNr("CustomsVc",Customsr.TransDate,-1,false,"");
  end;
  Customsr.OKFlag = 0;
  Customsr.OrderedFlag = 0;
  tstr = Customsr.CurncyCode;
  GetCustomsFullCurncyRate(tstr,Customsr.TransDate,fr,to1,to2,br1,br2);
  Customsr.FrRate = fr;
  Customsr.ToRateB1 = to1; 
  Customsr.ToRateB2 = to2;
  Customsr.BaseRate1 = br1;
  Customsr.BaseRate2 = br2;    
  CustomsVcRecordDuplicate = res; 
  RETURN;
END;

global
updating function LongInt CustomsVcRecordCheck(record CustomsVc Customsp,record CustomsVc Customs2p,LongInt stat,LongInt long4)
begin
  LongInt res;
  row CustomsVc Customsrw;
  record POVc POr;
  row POVc POrw;
  record INVc INr;
  LongInt oldnr,newnr;
  Boolean found,gentrans;
  Integer rwcnt,i;
  Integer porowcnt;
  string 200 lancode;

  res = 0;
  rwcnt = MatRowCnt(Customsp);
  oldnr = Customsp.SerNr;
  if (stat==2) then begin
    if (Customsp.SerNr<=0) and (Customs2p.OKFlag==0) then begin
      Customsp.SerNr = Customs2p.SerNr;
    end;
  end;  
  if (Customsp.SerNr==-1) then begin
    newnr = -1;
    Customsp.SerNr = NextSerNr("CustomsVc",Customsp.TransDate,newnr,false,lancode);
  end;
  if (SerNrTestCustomsVc(Customsp.SerNr,Customsp.TransDate,gentrans)==false) then begin
    RecordCheckError(1557,"",-1,"SerNr");      
    res = -1;
    goto LCustomsVcRecordCheck;
  end;
  for (i = 0 ; i<rwcnt;i=i+1) begin
    MatRowGet(Customsp,i,Customsrw);
    switch (Customsrw.stp) begin
      case 1:
        if (nonblank(Customsrw.ArtCode)) then begin
          INr.Code = Customsrw.ArtCode;
          found = ReadFirstMain(INr,1,true);
          if (found==false) then begin
            RecordCheckError(1120,Customsrw.ArtCode,i,"ArtCode");      
            res = -1;
            goto LCustomsVcRecordCheck;
          end;
          if (INr.Terminated<>0) then begin
            RecordCheckError(1266,Customsrw.ArtCode,i,"ArtCode");      
            res = -1;
            goto LCustomsVcRecordCheck;
          end;
          if (INr.ItemType==2) then begin
            RecordCheckError(1826,Customsrw.ArtCode,i,"ArtCode");      
            res = -1;
            goto LCustomsVcRecordCheck;
          end;
        end;
        if (blank(Customsrw.VATCode) and (Customsrw.Sum<>0)) then begin
          RecordCheckError(1134,"",i,"VATCode");      
          res = -1;
          goto LCustomsVcRecordCheck;
        end;
        if (Customsrw.PONr!=-1) then begin
          POr.SerNr = Customsrw.PONr;
          if (ReadFirstMain(POr,1,true)==false) then begin
            RecordCheckError(1458,"",i,"PONr");      
            res = -1;
            goto LCustomsVcRecordCheck;
          end;
          porowcnt = MatRowCnt(POr);
          if ((Customsrw.PORowNr>porowcnt) or (Customsrw.PORowNr<1)) then begin
            RecordCheckError(1459,"",i,"PORowNr");      
            res = -1;
            goto LCustomsVcRecordCheck;
          end;
          MatRowGet(POr,Customsrw.PORowNr-1,POrw);
        end;
    end;
  end;
LCustomsVcRecordCheck:;
  if (res!=0) then begin
    Customsp.SerNr = oldnr;
  end;
  CustomsVcRecordCheck = res;
end;


