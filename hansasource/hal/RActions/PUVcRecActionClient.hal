external updating procedure VIFromPODExecute(LongInt);
external procedure ShowUnOkWarn(string,Integer);

global
function LongInt PUVcRecordDefaultsClient(LongInt wn,var record PUVc PUr,record PUVc PU2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  record LocalMachineBlock LocalMachineRec;

  BlockLoad(LocalMachineRec);  
  if (blank(PUr.Location)) then begin
    PUr.Location = LocalMachineRec.DefLocation;
  end;
  PUVcRecordDefaultsClient = res;
  return;
end;


procedure PrintPalletLabels(record PUVc PUp)
BEGIN
  record MainStockBlock MainStockRec;
  record RcVc RepSpec;
  Boolean testf;
  record StockMovVc StockMovr;
  string 255 location;
  record LocationVc Locr;

  if (PUp.OKFlag!=0) then begin
    BlockLoad(MainStockRec);
    testf = true;
    location = PUp.Location;
    if (blank(location)) then begin
      location = MainStockRec.MainStock;
    end;
    Locr.Code = location;
    ReadFirstMain(Locr,1,true);
    if (Locr.RequirePos==0) then begin testf = false; end;
    StockMovr.TransNr = PUp.SerNr;
    StockMovr.FileName = "PUVc";
    if (ReadFirstKey("FileName",StockMovr,2,true)==false) then begin testf = false; end;
    if (testf) then begin
      RepSpec.f2 = PUp.SerNr;
      RepSpec.Media = mtPrinter;
      RepSpec.repname = "PalletLabForm";
      RunReport(RepSpec,0);
    end;
  end;
  RETURN;
END;

global
updating function LongInt PUVcRecordSaveClient(LongInt wn,var record PUVc PUp,record PUVc PU2p,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record AutoActionBlock AutoActionRec;

  BlockLoad(AutoActionRec);
  if (AutoActionRec.PUPalletLabelPrinting!=0) then begin
    PrintPalletLabels(PUp);
  end;      
  ShowUnOkWarn("PUVc",PUp.OKFlag);
  res = 1;
  PUVcRecordSaveClient = res;
  RETURN;
END;

global
updating function LongInt PUVcRecordUpdateClient(LongInt wn,var record PUVc PUp,record PUVc PU2p,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record AutoActionBlock AutoActionRec;
  record VIVc VIr;
  record POSettingBlock POSb;
  Integer nwn;

  if (PUp.OKFlag!=0) and (PU2p.OKFlag!=0) then begin
    goto LPUVcRecordUpdateClient;
  end;
  if (PUp.OKFlag!=PU2p.OKFlag and PUp.PONr!=-1) then begin
    UpdateBrowses("POVc");
  end;
  BlockLoad(POSb);
  BlockLoad(AutoActionRec);
  if (AutoActionRec.PUPalletLabelPrinting!=0) then begin
    PrintPalletLabels(PUp);
  end;      
  if (POSb.CreateVIonPUOK!=0) and (PUp.PONr>0) then begin
    if (POSb.OpenCreateVIFromPO!=0) then begin
      VIFromPODExecute(PUp.PONr);
    end else begin
      VIr.POSerNr = PUp.PONr;
      if (ReadLastKey("POSerNr",VIr,1,true)) then begin
        nwn = OpenWindow("VIDClass",0,0,"","",VIr);
      end;
    end;
  end;
  ShowUnOkWarn("PUVc",PUp.OKFlag);
LPUVcRecordUpdateClient:;  
  res = 1;
  PUVcRecordUpdateClient = res;
  RETURN;
END;
