global
updating function LongInt AccPeriodVcRecordCheck(var record AccPeriodVc AccPeriodr,record AccPeriodVc Acc2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer i,j,rwcnt;
  row AccPeriodVc AccPeriodrw;
  row AccPeriodVc AccPeriod2rw;
  Date nd;
  
  res = 0;
  if ((blankdate(AccPeriodr.FiscalStartDate)) or (blankdate(AccPeriodr.FiscalEndDate))) then begin
    RecordCheckError(1718,"",-1,"Code");
    res = -1;
    goto LAccPeriodVcRecordCheck;
  end;
  rwcnt = MatRowCnt(AccPeriodr);
  if (rwcnt<1) then begin
    RecordCheckError(1075,"",-1,"Code");      
    res = -1;
    goto LAccPeriodVcRecordCheck;
  end;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(AccPeriodr,i,AccPeriodrw);
    if (i>0) then begin
      MatRowGet(AccPeriodr,i-1,AccPeriod2rw);
      nd = AddDay(AccPeriod2rw.EndDate,1);
      if (nd!=AccPeriodrw.StartDate) then begin
        RecordCheckError(2274,"",i,"StartDate");      
        res = -1;
        goto LAccPeriodVcRecordCheck;
      end;
    end;
    if (AccPeriodrw.StartDate>=AccPeriodrw.EndDate) then begin
      RecordCheckError(2274,"",i,"EndDate");      
      res = -1;
      goto LAccPeriodVcRecordCheck;
    end;
  end;
  if (rwcnt>0) then begin
    MatRowGet(AccPeriodr,0,AccPeriodrw);
    if (AccPeriodrw.StartDate!=AccPeriodr.FiscalStartDate) then begin
      RecordCheckError(2274,"",0,"StartDate");      
      res = -1;
      goto LAccPeriodVcRecordCheck;
    end;
    MatRowGet(AccPeriodr,rwcnt-1,AccPeriodrw);
    if (AccPeriodrw.EndDate!=AccPeriodr.FiscalEndDate) then begin
      RecordCheckError(2274,"",rwcnt-1,"EndDate");      
      res = -1;
      goto LAccPeriodVcRecordCheck;
    end;
  end;
LAccPeriodVcRecordCheck:;
  AccPeriodVcRecordCheck = res;
  RETURN;
END;

global
function Boolean FindAccPeriod(Date tdp,var record AccPeriodVc AccPeriodr)
begin
  Boolean res;
  row AccPeriodVc AccPeriodrw;
  Integer rwcnt,i;
  Boolean foundf;
  
  AccPeriodr.FiscalStartDate = AddYear(tdp,-2);
  foundf = true;
  while (LoopKey("FiscalStartDate",AccPeriodr,1,foundf)) begin
    if (DateInRange(tdp,AccPeriodr.FiscalStartDate,AccPeriodr.FiscalEndDate)) then begin
      res = true;
      goto LFindAccPeriod;
    end;
  end;
LFindAccPeriod:;
  FindAccPeriod = res;
  return;
end;

global
function Date FindDueDateInAccPeriod(Date tdp)
begin
  Date res;
  record AccPeriodVc AccPeriodr;
  row AccPeriodVc AccPeriodrw;
  Integer rwcnt,i;
  Boolean foundf;
  
  if (FindAccPeriod(tdp,AccPeriodr)) then begin
    rwcnt = MatRowCnt(AccPeriodr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(AccPeriodr,i,AccPeriodrw);
      if (DateInRange(tdp,AccPeriodrw.StartDate,AccPeriodrw.EndDate)) then begin
        if (nonblankdate(AccPeriodrw.DueDate)) then begin
          res = AccPeriodrw.DueDate;
        end else begin
          res = AccPeriodrw.EndDate;
        end;
        goto LFindDueDateInAccPeriod;
      end;
    end;
  end;
LFindDueDateInAccPeriod:;
  FindDueDateInAccPeriod = res;
  return;
end;

