global
function Boolean FindWHITTax(string itcode,Date td,var record WHITVc resWHITr)
begin
  record WHITVc WHITr;
  Boolean found,res;

  RecordNew(resWHITr);
  found = true;
  WHITr.ITCode = itcode;
  while (LoopBackKey("ActITCode",WHITr,1,found)) begin
    if (WHITr.ITCode!=itcode) then begin found = false; end;
    if (found) then begin
      if (nonblankdate(WHITr.EndDate)) then begin
        if (DateInRange(td,WHITr.StartDate,WHITr.EndDate)) then begin
          res = true;
          RecordCopy(resWHITr,WHITr);
          goto LFindWHIT;
        end;
      end else begin
        if (td>=WHITr.StartDate) then begin
          res = true;
          RecordCopy(resWHITr,WHITr);
          goto LFindWHIT;
        end;
      end;
    end;
  end;
LFindWHIT:;  
  FindWHITTax = res;
  return;
end;

global
function Boolean FindWHITTaxInclClosed(string itcode,Date td,var record WHITVc resWHITr)
begin
  record WHITVc WHITr;
  Boolean found,res;

  RecordNew(resWHITr);
  found = true;
  WHITr.ITCode = itcode;
  while (LoopBackKey("ITCode",WHITr,1,found)) begin
    if (WHITr.ITCode!=itcode) then begin found = false; end;
    if (found) then begin
      if (nonblankdate(WHITr.EndDate)) then begin
        if (DateInRange(td,WHITr.StartDate,WHITr.EndDate)) then begin
          res = true;
          RecordCopy(resWHITr,WHITr);
          goto LFindWHITTaxInclClosed;
        end;
      end else begin
        if (td>=WHITr.StartDate) then begin
          res = true;
          RecordCopy(resWHITr,WHITr);
          goto LFindWHITTaxInclClosed;
        end;
      end;
    end;
  end;
LFindWHITTaxInclClosed:;  
  FindWHITTaxInclClosed = res;
  return;
end;

global
function LongInt WHITVcRecordInIndex(record WHITVc WHITr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (WHITr.Closed!=0) then begin 
    if (indexname=="ActITCode") then begin res = 0; end;
  end;
  WHITVcRecordInIndex = res;
  return;
end;  

global
function LongInt WHITVcRecordCheck(record WHITVc WHITr,record WHITVc oWHITr,LongInt stat,LongInt long4)
begin
  LongInt res;
  record ITVc ITr;
  record PDVc PDr;
  record CNDVc CNDr;
  record WHITVc lWHITr;
  Boolean found,testf;
      
  res = 0;
  if (blank(WHITr.ITCode)) then begin
    RecordCheckError(1120,"",-1,"ITCode");      
    res = -1; 
    goto LWHITVcRecordCheck;
  end;  
  if (blankdate(WHITr.StartDate)) then begin
    RecordCheckError(1120,"",-1,"StartDate");      
    res = -1; 
    goto LWHITVcRecordCheck;
  end;  
  if (nonblankdate(WHITr.EndDate)) then begin
    if (WHITr.StartDate>WHITr.EndDate) then begin
      RecordCheckError(2246,"",-1,"EndDate");      
      res = -1; 
      goto LWHITVcRecordCheck;     
    end;
  end;
  found = true;
  lWHITr.ITCode = WHITr.ITCode;
  while (LoopKey("ActITCode",lWHITr,1,found)) begin
    if (lWHITr.ITCode!=WHITr.ITCode) then begin found = false; end;
    if (found) then begin
      testf = true;
      switch (stat) begin
        case Rs_update:
          if ((lWHITr.StartDate==oWHITr.StartDate) and (lWHITr.EndDate==oWHITr.EndDate)) then begin 
            testf = false; 
          end;
      end;
      if (testf) then begin
        if (DateInRange(WHITr.StartDate,lWHITr.StartDate,lWHITr.EndDate)) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHITVcRecordCheck;     
        end;  
        if (DateInRange(WHITr.EndDate,lWHITr.StartDate,lWHITr.EndDate)) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHITVcRecordCheck;     
        end;  
        if (WHITr.StartDate<lWHITr.StartDate) and (WHITr.EndDate>lWHITr.EndDate) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHITVcRecordCheck;     
        end;
        if (lWHITr.StartDate<WHITr.StartDate) and ((lWHITr.EndDate>WHITr.EndDate) and (nonblankdate(WHITr.EndDate))) then begin
          RecordCheckError(22030,"",-1,"StartDate");      
          res = -1; 
          goto LWHITVcRecordCheck;     
        end;
      end;
    end;
  end;    
  ITr.Code = WHITr.ITCode;
  if (ReadFIrstMain(ITr,1,true)==false) then begin
    RecordCheckError(20859,"",-1,"ITCode");
    res = -1;
    goto LWHITVcRecordCheck;
  end;
LWHITVcRecordCheck:;  
  WHITVcRecordCheck = res;
  return;
end;  
