external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt CUSerBlockCheck(var record CUSerBlock CUSerr,record CUSerBlock CUSer2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row CUSerBlock CUSerrw;
  row CUSerBlock CUSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(CUSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(CUSerr,i,CUSerrw);
    if (CUSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LCUSerBlockCheck;        
    end;
    if (CUSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LCUSerBlockCheck;        
    end;
    if ((nonblankdate(CUSerrw.StartDate)) and (nonblankdate(CUSerrw.EndDate))) then begin
      if (CUSerrw.StartDate>CUSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LCUSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(CUSerr,j,CUSer2rw);
      res = SerBlockCheckOverlap(CUSerrw.TSerStart,CUSerrw.TSerEnd,CUSer2rw.TSerStart,CUSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LCUSerBlockCheck;        
      end;
    end;        
  end;  
LCUSerBlockCheck:;  
  CUSerBlockCheck = res;
  return;
end;
