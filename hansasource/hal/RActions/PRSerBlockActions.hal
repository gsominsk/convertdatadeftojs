external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt PRSerBlockCheck(var record PRSerBlock PRSerr,record PRSerBlock PRSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row PRSerBlock PRSerrw;
  row PRSerBlock PRSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(PRSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(PRSerr,i,PRSerrw);
    if (PRSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LPRSerBlockCheck;        
    end;
    if (PRSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LPRSerBlockCheck;        
    end;
    if ((nonblankdate(PRSerrw.StartDate)) and (nonblankdate(PRSerrw.EndDate))) then begin
      if (PRSerrw.StartDate>PRSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LPRSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(PRSerr,j,PRSer2rw);
      res = SerBlockCheckOverlap(PRSerrw.TSerStart,PRSerrw.TSerEnd,PRSer2rw.TSerStart,PRSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LPRSerBlockCheck;        
      end;
    end;        
  end;  
LPRSerBlockCheck:;  
  PRSerBlockCheck = res;
  return;
end;
