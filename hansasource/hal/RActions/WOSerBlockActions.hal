external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt WOSerBlockCheck(var record WOSerBlock WOSerr,record WOSerBlock WOSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row WOSerBlock WOSerrw;
  row WOSerBlock WOSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(WOSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(WOSerr,i,WOSerrw);
    if (WOSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LWOSerBlockCheck;        
    end;
    if (WOSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LWOSerBlockCheck;        
    end;
    if ((nonblankdate(WOSerrw.StartDate)) and (nonblankdate(WOSerrw.EndDate))) then begin
      if (WOSerrw.StartDate>WOSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LWOSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(WOSerr,j,WOSer2rw);
      res = SerBlockCheckOverlap(WOSerrw.TSerStart,WOSerrw.TSerEnd,WOSer2rw.TSerStart,WOSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LWOSerBlockCheck;        
      end;
    end;        
  end;  
LWOSerBlockCheck:;  
  WOSerBlockCheck = res;
  return;
end;
