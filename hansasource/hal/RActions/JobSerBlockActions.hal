external function Integer SerBlockCheckOverlap(LongInt,LongInt,LongInt,LongInt);

global
updating function LongInt JobSerBlockCheck(var record JobSerBlock JobSerr,record JobSerBlock JobSer2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer j,i,rwcnt,pmrwcnt;
  row JobSerBlock JobSerrw;
  row JobSerBlock JobSer2rw;
  
  res = 0;
  rwcnt = MatRowCnt(JobSerr);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(JobSerr,i,JobSerrw);
    if (JobSerrw.TSerStart<=0) then begin
      RecordCheckError(2210,"",i,"TSerStart");   
      res = -1;
      goto LJobSerBlockCheck;        
    end;
    if (JobSerrw.TSerEnd<=0) then begin
      RecordCheckError(2210,"",i,"TSerEnd");   
      res = -1;
      goto LJobSerBlockCheck;        
    end;
    if ((nonblankdate(JobSerrw.StartDate)) and (nonblankdate(JobSerrw.EndDate))) then begin
      if (JobSerrw.StartDate>JobSerrw.EndDate) then begin
        RecordCheckError(20851,"",i,"EndDate");   
        res = -1;
        goto LJobSerBlockCheck;        
      end;
    end;
    for (j=0;j<i;j=j+1) begin
      MatRowGet(JobSerr,j,JobSer2rw);
      res = SerBlockCheckOverlap(JobSerrw.TSerStart,JobSerrw.TSerEnd,JobSer2rw.TSerStart,JobSer2rw.TSerEnd);      
      if (res!=0) then begin
        RecordCheckError(res,"",i,"TSerStart");   
        res = -1;
        goto LJobSerBlockCheck;        
      end;
    end;        
  end;  
LJobSerBlockCheck:;  
  JobSerBlockCheck = res;
  return;
end;
