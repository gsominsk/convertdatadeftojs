external function Integer CheckObjs(string,string,var string);

global
updating function LongInt ResUsageVcRecordCheck(record ResUsageVc ResUsager,record ResUsageVc ResUsage2r,LongInt stat,LongInt long4)
begin
  record ResUsageTypeVc RUTr;
  record INVc INr;
  row ResUsageVc ResUsagerw;
  LongInt res;
  integer i,rowcnt,errcode;
  string 255 errstr;

  res = 0;
  if blank(ResUsager.Code) then begin
     RecordCheckError(1959,"",-1,"Code");
     res = -1;
     goto LResUsageVcRecordCheck;
  end;
  if nonblank(ResUsager.Type) then begin
    RUTr.Code = ResUsager.Type;
    if (ReadFirstMain(RUTr,1,True)==false) then begin
      RecordCheckError(1120,ResUsager.Type,-1,"Type");      
      res = -1; 
      goto LResUsageVcRecordCheck;
    end;
  end;
  errcode = CheckObjs("",ResUsager.Objects,errstr);
  if (errcode!=0) then begin
    RecordCheckError(errcode,errstr,-1,"Objects");      
    res = -1; 
    goto LResUsageVcRecordCheck;
  end;
  rowcnt = MatRowCnt(ResUsager);
  for (i=0;i<rowcnt;i=i+1) begin
    MatRowGet(ResUsager,i,ResUsagerw);
    INr.Code = ResUsagerw.ArtCode;
    if (ReadFirstMain(INr,1,true)==false) then begin
      RecordCheckError(1120,ResUsagerw.ArtCode,i,"ArtCode");      
      res = -1; 
      goto LResUsageVcRecordCheck;
    end;
  end;
LResUsageVcRecordCheck:;
  ResUsageVcRecordCheck = res;
  return;
end;
