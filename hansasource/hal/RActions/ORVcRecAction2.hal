external function string 255 GetLegalInvoiceNrSerie(row LegalInvNrBlock,string);
external procedure GetLegalInvNrRow(string,var row LegalInvNrBlock);
external procedure TaxMatrixConvertB1ToB2(record TaxMatrixVc,var val,var val,var val,var val);
external function roundmode DefaultRoundMode();
external function Boolean CheckCYBank1CorrectForEInvoicing(string);
external function Boolean CheckInternetEnabler();
external function Boolean CurencyCodeIsISO(string);
external function Boolean CheckAddr3CorrectforEInvoicing(string);
external function Boolean EInvoiceForCustomer(Integer,string,record CUVc);
external function Boolean CanOKStockRecord(var Integer);
external procedure SetORFlags(record ORVc);
external updating procedure UpdatePlanned(string,string,string,LongInt,Date,val,Integer,val,val,Boolean);
external function Date ConvertPlanShipString(string);
external function Boolean PasteActTypeInAct(string,var record ActVc);
external updating procedure MakeActFromSubSys_ORVc(var record ORVc,Boolean,Boolean);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure FindSalesExVat(record TaxMatrixVc,string,val,Integer,Integer,var val);
external procedure ORSumup(var record ORVc);
external updating procedure UpdateOrdOut(record ORVc,Boolean,Boolean);
external updating procedure UpdatePRSO(record ORVc,Boolean);
external procedure ConvertToDualBase(var string,date,var val,var val,var val,var val,var val,var val,Boolean);
external procedure SwapM4Val(var val,var val);
external procedure B1ToB2Val(val,val,val,var val);
external function Boolean FindItemVAR(string,var string,var string,var string,var string);
external function Boolean GetItemPriceDiscount3(string,val,var record INVc,string,val,val,val,val,val,string,string,string,string,
                                                var val,var string,var val,var string,var val,var string,Integer,var Boolean,Date,Time,
                                                string,Boolean,var Boolean,string,var string,var val,string,string,var string);
global
updating procedure TestCustOR(record ORVc ORp)
BEGIN
/* This is the logic from C, I think it is more correct.
  INView INr;
  Integer rwcnt,i;
  OR2RowType ORrw;
  m4val t;
  
  INr.Math.h = NIL;
  rwcnt = MatRowCnt(ORVc,1,ORp);
  for (i=0; i<rwcnt; i++) begin
    MatRowGet(ORVc,1,ORp,i,&ORrw);
    if (FindItemVAR(INr.Code,NIL,NIL)==false) then begin
      M4FromStr(INr.Code,M4Code,20,ORrw.ArtCode);
      if (nonblankstrp(INr.Code)) then begin
        if (ReadFirstItem(INr.Code,&INr,false,true)==false) then begin
          M4FromStr(INr.Code,M4Code,20,ORrw.ArtCode);
          M4FromStr(INr.Name,M4Str,60,ORrw.Spec);
          strpblank(INr.Unittext);
          SetM4ValBlank(&INr.UPrice1);
          SetM4ValBlank(&INr.MinLevel);
          strpblank(INr.SalesAcc);
          INr.ItemType = 0;
          strpblank(INr.Group);
          m4_RecordStore(INVc,&INr,false);
        end else begin
          if (M4ValBlankTest(&ORrw.BasePrice)) then begin
            AddM4Val(&INr.InPrice,&INr.ExtraCost,&ORrw.BasePrice);
            MulM4Val(&ORrw.Quant,&ORrw.BasePrice,&t);
            SubM4Val(&ORrw.Sum,&t,&ORrw.rowGP);
          end;
        end;
      end;
    end;
    MatRowPut(ORVc,1,ORp,i,&ORrw);
  end;
  M4DisposeHandle(&INr.Math.h);
*/
  record CUVc CUr;
  record CUVc testCUr;
  
  CUr.Code = ORp.CustCode;
  if (nonblank(CUr.Code)) then begin
  testCUr.Code = CUr.Code;
  if (ReadFirstMain(testCUr,1,true)==false) then begin
    RecordNew(CUr);
    CUr.Code = ORp.CustCode;
    CUr.Name = ORp.Addr0;
    CUr.InvAddr0 = ORp.Addr1;
    CUr.InvAddr1 = ORp.Addr2;
    CUr.InvAddr2 = ORp.Addr3;
    CUr.InvAddr3 = ORp.InvAddr3;
    CUr.InvAddr4 = ORp.InvAddr4;
    CUr.DelAddr0 = ORp.ShipAddr1;
    CUr.DelAddr1 = ORp.ShipAddr2;
    CUr.DelAddr2 = ORp.ShipAddr3;
    CUr.DelAddr3 = ORp.DelAddr3;
    CUr.DelAddr4 = ORp.DelAddr4;
    CUr.Phone = ORp.Phone;
    CUr.Person = ORp.CustContact;
    CUr.Fax = ORp.Fax;
    CUr.VATNr = ORp.VATNr;
    CUr.RebCode = ORp.RebCode;
    CUr.PLCode = ORp.PriceList;
    CUr.VATCode = ORp.CustVATCode;
//    CUr.CurncyCode = ORp.CurncyCode;
    CUr.LangCode = ORp.LangCode;
    CUr.Comment = "";
    CUr.InvoiceToCode = "";
    CUr.SearchKey = "";
    CUr.ExportFlag = ORp.ExportFlag;
    CUr.AccFlag = 0;
    CUr.PayDeal = ORp.PayDeal;
    CUr.CustCat = ORp.CustCat;
    CUr.Objects = "";
//    impCustomers = true;//HAL
    if (RecordStore(CUr,false)) then begin end;
  end;
  end;
  RETURN;
END;

global
updating procedure TestORArts(record ORVc ORp)
BEGIN
  record INVc INr;
  Integer rwcnt,i;
  row ORVc ORrw;
  val t;
  string 60 sz,msk,mskrep;
  string 200 varsubset;
  
  rwcnt = MatRowCnt(ORp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORp,i,ORrw);
    if (FindItemVAR(INr.Code,sz,msk,mskrep,varsubset)==false) then begin
      INr.Code = ORrw.ArtCode;
      if (nonblank(INr.Code)) then begin
        if (ReadFirstItem(INr.Code,INr,false,true)==false) then begin
          INr.Code = ORrw.ArtCode;
          INr.Name = ORrw.Spec;
          INr.Unittext = "";
          INr.UPrice1 = blankval;
          INr.MinLevel = blankval;
          INr.SalesAcc = "";
          INr.ItemType = 0;
          INr.Group = "";
          if (RecordStore(INr,false)) then begin end;
        end else begin
          if (blank(ORrw.BasePrice)) then begin
            ORrw.BasePrice = INr.InPrice + INr.ExtraCost;
            t = ORrw.Quant*ORrw.BasePrice;
            ORrw.rowGP = ORrw.Sum - t;
          end;
        end;
      end;
    end;
    MatRowPut(ORp,i,ORrw);
  end;
  return;
end;

procedure ORVcConvertB1ToB2(record ORVc ORr,string curncy,val fr,var val to1,var val to2,var val br1,var val br2)
BEGIN
  row ORVc ORrw;
  val t;
  Integer i,rwcnt;
  record TaxMatrixVc TMr;

  SwapM4Val(br1,br2);
  SwapM4Val(to1,to2);
  rwcnt = MatRowCnt(ORr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORr,i,ORrw);
    B1ToB2Val(ORrw.rowGP,br1,br2,t);
    ORrw.rowGP = t;
    B1ToB2Val(ORrw.BasePrice,br1,br2,t);
    ORrw.BasePrice = t;
    MatRowPut(ORr,i,ORrw);
  end;
  RETURN;
END;

procedure CalculateTotGP(var record ORVc ORr)
begin
  Integer i,rwcnt;
  row ORVc ORrw;

  rwcnt = MatRowCnt(ORr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORr,i,ORrw);
    ORr.TotGP = ORr.TotGP + ORrw.rowGP;
  end;
  return;
end;

global
updating function LongInt ORVcRecordImport(var record ORVc ORr,record ORVc OR2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res;
  row ORVc ORrw;
  Integer i,rwcnt;
  record ORVc locORr;
  Boolean gBase1ToBase2,gToDualBase;
  record ConvMasterBlock cvm;
  string 10 curncy;
  val fr,to1,to2,br1,br2,t;
  record AccBlock Accb;
  row LegalInvNrBlock LINrbrw;
  
  BlockLoad(Accb);
  if (ORr.SerNr==-99) then begin
    ORr.SerNr = NextSerNr("ORVc",ORr.OrdDate,-1,false,"");
  end;
  locORr.SerNr = ORr.SerNr;
  if ((ReadFirstMain(locORr,1,true)==false) or (Importing==false)) then begin
    BlockLoad(cvm);
    if (cvm.DualBaseCurrencyFlag!=0) then begin gToDualBase = true; end;
    if (cvm.Base1ToBase2Flag!=0) then begin gBase1ToBase2 = true; end;
    if (gToDualBase) then begin
      curncy = ORr.CurncyCode;
      fr = ORr.FrRate;
      to1 = ORr.ToRateB1;
      to2 = ORr.ToRateB2;
      br1 = ORr.BaseRate1;
      br2 = ORr.BaseRate2;
      t = ORr.Sum4;
      ConvertToDualBase(curncy,ORr.OrdDate,fr,to1,to2,br1,br2,t,true);
      ORr.CurncyCode = curncy;
      ORr.FrRate = fr;
      ORr.ToRateB1 = to1;
      ORr.ToRateB2 = to2;
      ORr.BaseRate1 = br1;
      ORr.BaseRate2 = br2;
      ORr.Sum4 = t;
    end;
    if (gBase1ToBase2) then begin
      to1 = ORr.ToRateB1;
      to2 = ORr.ToRateB2;
      br1 = ORr.BaseRate1;
      br2 = ORr.BaseRate2;  
      ORVcConvertB1ToB2(ORr,ORr.CurncyCode,ORr.FrRate,to1,to2,br1,br2);
      ORr.ToRateB1 = to1;
      ORr.ToRateB2 = to2;
      ORr.BaseRate1 = br1;
      ORr.BaseRate2 = br2;    
      CalculateTotGP(ORr);
      ORr.BaseSum4 = MulRateToBase1(ORr.CurncyCode,ORr.Sum4,ORr.FrRate,ORr.ToRateB1,ORr.ToRateB2,ORr.BaseRate1,ORr.BaseRate2,DefaultCurRoundOff);
    end;

//    if (gTBmark==false) then begin
//      TestCustOR(ORr);
//      TestORArts(ORr);
//    end;
    if (ORr.QuoteNr==0) then begin
      ORr.QuoteNr = -1;
    end;
    if (ORr.TotGP==0) then begin    
      rwcnt = MatRowCnt(ORr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(ORr,i,ORrw);
        if (nonblank(ORrw.ArtCode)) then begin
          ORr.TotGP = ORr.TotGP + ORrw.rowGP;
        end;
      end;
    end;
  end;
  if (blankdate(ORr.PlanShipDate)) then begin
    ORr.PlanShipDate = ConvertPlanShipString(ORr.PlanShip);
  end;  
  if (ORr.NoTAXonVAT==-1) then begin
    ORr.NoTAXonVAT = Accb.NoTAXonVAT;
  end;
  if (blank(ORr.OfficialSerNrSerie)) then begin
    GetLegalInvNrRow(ORr.OfficialSerNr,LINrbrw);
    ORr.OfficialSerNrSerie = GetLegalInvoiceNrSerie(LINrbrw,ORr.OfficialSerNr);
  end;
  ORr.PrepaidAmount = blankval;
  ORVcRecordImport = res; 
  RETURN;
END;

global
updating function LongInt ORVcRecordImportAfter(var record ORVc ORr,record ORVc OR2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer err;

  if (ImportingTextBackup==false and CanOKStockRecord(err)==true) then begin
    UpdatePRSO(ORr,true);
    UpdateOrdOut(ORr,true,true);
  end;
  ORVcRecordImportAfter = res; 
  RETURN;
END;

global
updating procedure CreateActFromOR(record ORVc ORp)
BEGIN
  record OrderClassVc OrderClasr;
  record CUVc CUr;
  record ActVc Actr;
  record ActTypeVc ActTyper;
  record ActTypeGrVc ActTypeGrr;
  record ASTBlock ASTRec; 
  Integer curcomp; 
  Boolean skipf;
  
  curcomp = CurrentCompany;
  OrderClasr.Code = ORp.OrderClass;
  if (ReadFirstMain(OrderClasr,1,true)) then begin
    switch (OrderClasr.OrderAct) begin
      case 0: 
        skipf = true;
        goto LCreateActFromOR;
      case 1: Actr.MainPersons = ORp.SalesMan;
      case 2:       
        Actr.MainPersons = OrderClasr.OrderActUser;
        if (blank(Actr.MainPersons)) then begin
          Actr.MainPersons = ORp.SalesMan;
        end;
    end;
    Actr.TransDate = ORp.OrdDate;
    Actr.SerNr = NextSerNr("ActVc",Actr.TransDate,-1,false,"");
    Actr.Invalid = 0;
    Actr.TodoFlag = 1;
    Actr.CUCode = ORp.CustCode;
    Actr.Comment = ORp.Comment;
    Actr.Contact = ORp.CustContact;
    CUr.Code = ORp.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
      Actr.CUName = CUr.Name;
      Actr.Phone = CUr.Phone;
      if (blank(Actr.Contact)) then begin Actr.Contact = ORp.SalesMan; end;
    end;
    BlockLoad(ASTRec);
    Actr.ActType = OrderClasr.OrderActCode;
    if (blank(Actr.ActType)) then begin
      Actr.ActType = ASTRec.GenSalesOrd;
    end;
    ActTyper.Code = Actr.ActType;
    if (ReadFirstMain(ActTyper,1,true)) then begin
      Actr.ItemCode = ActTyper.ItemCode;
      ActTypeGrr.Code = ActTyper.ActTypeGr;
      if (ReadFirstMain(ActTypeGrr,1,true)) then begin
        Actr.CalTimeFlag = ActTypeGrr.DefTimeFlag;
      end;
    end;
    if (PasteActTypeInAct("",Actr)) then begin
    end;    
    Actr.OKFlag = ASTRec.OrderDone;
    if (RecordInsert(Actr,false)) then begin
      CreateRecordLink(Actr,curcomp,ORp,curcomp);  
      CreateRecordLink(ORp,curcomp,Actr,curcomp);  
    end;
    skipf = true;
  end; 
LCreateActFromOR:;  
  if (skipf==false) then begin
    MakeActFromSubSys_ORVc(ORp,false,false); 
  end;
  RETURN;
END;

global
updating procedure UpdateORPlanned(record ORVc ORp,string location,Boolean addf)
BEGIN
  row ORVc ORrw;
  Integer rwcnt,i;
  Date theplandat,plandat,blankd;
  val planqty;
  string 255 theloc;
  
  if (StringIsDate(ORp.PlanShip)) then begin
    theplandat = StringToDate(ORp.PlanShip);
  end else begin
    theplandat = blankd;
  end;
  rwcnt = MatRowCnt(ORp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORp,i,ORrw);
    if (ORrw.OrderType!=kOrderTypeDropShip) then begin
    if (ORrw.stp==1) or (ORrw.stp==kInvoiceRowTypeStructuredItemComponent) then begin
      if (nonblank(ORrw.Location)) then begin
        theloc = ORrw.Location;
      end else begin
        theloc = location;
      end;
      planqty = ORrw.Quant - ORrw.Shipd2;//when ORrw.Shipd1 used: make PO 20 , make GR, do not ok, gone from Item Hist
      plandat = theplandat;
//plshrow      
      if (nonblank(ORrw.PlanShipRow)) then begin
        if (StringIsDate(ORrw.PlanShipRow)) then begin
          plandat = StringToDate(ORrw.PlanShipRow);
        end;
      end;
      if (planqty > 0) then begin  //Over delivery (minus) should not be stored in INrsVc Bjšrn
        UpdatePlanned(ORrw.ArtCode,theloc,"Fut3ORVc",ORp.SerNr,plandat,-planqty,i,-planqty,ORrw.Quant*ORrw.BasePrice,addf);
      end;
    end;
    end;
  end;
  RETURN;
END;

global
updating function Boolean UpdateOrderFromShip(record SHVc SHp,Boolean shf,record SHVc SH2p,Boolean sh2f,Boolean sh2only)
BEGIN
  record OrdSettBlock OSb;
  record ORVc oldORr;
  record ORVc ORr;
  row SHVc SHrw;
  row ORVc ORrw;
  Integer i,rwcnt;
  Integer orw,orcnt;
  val t,s,rowinbase,shipd2,v;
  Boolean orsumupf;
  record TaxMatrixVc TMr;

  BlockLoad(OSb);
  if (shf) then begin
    ORr.SerNr = SHp.OrderNr;
  end else begin
    ORr.SerNr = SH2p.OrderNr;
  end;
  if (ReadFirstMain(ORr,1,true)) then begin
    RecordCopy(oldORr,ORr);
    orcnt = MatRowCnt(ORr);
    UpdateORPlanned(ORr,ORr.Location,false);
    if (sh2f) then begin
      rwcnt = MatRowCnt(SH2p);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(SH2p,i,SHrw);
        if (SHrw.OrdRow!=-1) then begin
          orw = SHrw.OrdRow;
          if ((orw<orcnt) and (orw>-1)) then begin
            MatRowGet(ORr,orw,ORrw);
            if (sh2only==false) then begin
              ORrw.Shipd1 = ORrw.Shipd1 - SHrw.Ship;
            end;
            if (SH2p.OKFlag!=0) then begin
              ORrw.Shipd2 = ORrw.Shipd2 - SHrw.Ship;
            end;
            if (ORrw.Shipd1==0) then begin
              ORrw.Shipd1 = blankval;
            end;
            if (ORrw.Shipd2==0) then begin
              ORrw.Shipd2 = blankval;
            end;
//?            CalcSum(&ORrw.Quant,&ORrw.Price,&ORrw.PriceFactor,&ORrw.vRebate,&ORrw.Sum);              
            MatRowPut(ORr,orw,ORrw);
          end;
        end;
      end;
    end;
    if (shf) then begin
      rwcnt = MatRowCnt(SHp);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(SHp,i,SHrw);
        if (SHrw.OrdRow!=-1) then begin
          orw = SHrw.OrdRow;
          if ((orw<orcnt) and (orw>-1)) then begin
            MatRowGet(ORr,orw,ORrw);
            shipd2 = ORrw.Shipd2;
            if (sh2only==false) then begin
              ORrw.Shipd1 = ORrw.Shipd1 + SHrw.Ship;
            end;
            if (SHp.OKFlag!=0) then begin
              ORrw.Shipd2 = ORrw.Shipd2 + SHrw.Ship;
              if (ORrw.Shipd2==0) then begin
                ORrw.Shipd2 = blankval;
              end;
//?it is to recognize when we should create invoice
//?If you have text lines on orders, invoices are generated even if the order is not delivered. 
            end;
//            ORrw.SerialNr = SHrw.SerialNr; // This field is NOT protected, we shouldn't set it!, it is now
//what if many deliveries from 1 orders ? 
            if (ORrw.Shipd1==0) then begin
              ORrw.Shipd1 = blankval;
            end;
            if (OSb.UpdateGPWhenDel!=0 and SHp.OKFlag!=0) then begin
              if (SHrw.FIFORowVal>=0 and SHrw.Ship>0 and SHrw.InStock>0) then begin
                v = SHrw.FIFORowVal;
                if (ORrw.PriceFactor!=0) then begin
                  v = v*ORrw.PriceFactor;
                end;
                
                t = v + shipd2*ORrw.BasePrice;
                t = t/(SHrw.Ship+shipd2);
                ORrw.BasePrice = Round(t,DefaultRoundMode);
              end;
              t = ORrw.Quant*ORrw.BasePrice;
              if (ORrw.PriceFactor!=0) then begin
                t = t/ORrw.PriceFactor;
              end;
              s = MulRateToBase1(ORr.CurncyCode,ORrw.Sum,ORr.FrRate,ORr.ToRateB1,ORr.ToRateB2,ORr.BaseRate1,ORr.BaseRate2,DefaultCurRoundOff);
              UnpackRowFieldMatrix(ORrw,"TaxMatrix",TMr);
              FindSalesExVat(TMr,ORrw.VATCode,s,ORr.InclVAT,ORr.NoTAXonVAT,rowinbase);
              ORrw.rowGP = rowinbase - t;
              orsumupf = true;
            end;
            MatRowPut(ORr,orw,ORrw);
          end;
        end;
      end;
    end;
    if (orsumupf) then begin
      ORSumup(ORr);
    end;
    SetORFlags(ORr);
    UpdateORPlanned(ORr,ORr.Location,true);
    if (RecordUpdate(oldORr,ORr,false)==0) then begin end;
  end;
  UpdateOrderFromShip = true;
  RETURN;
END;

function Boolean ValidEInvoiceData_OR_Finland(record ORVc ORr,record CYBlock CYb,record CUVc CUr,record BackupBlock Bkpb,
                                           record BaseCurBlock BCb,
                                           var Integer errcode,var string gotofield)
begin
  Boolean res;
  Integer RcvInvoicePref;
  record EInvoiceBlock EIb;
  string 255 curncycode;
  
  res = true;
  RcvInvoicePref = CUr.eInvRcvPref;
  if (RcvInvoicePref==kEInvoiceRcvPreferenceDefault) then begin
    BlockLoad(EIb);
    RcvInvoicePref = EIb.RcvInvoicePref;
  end;
  if (len(ORr.CustContact)>35) then begin
    errcode = 20793;
    gotofield = "CustContact";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (len(ORr.CustOrdNr)>35) then begin
    errcode = 20793;
    gotofield = "CustOrdNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (CheckCYBank1CorrectForEInvoicing(CYb.Bank1)==false) then begin
    errcode = 20584;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (RcvInvoicePref!=2) then begin
    if (blank(CUr.ANACode)) then begin
      errcode = 20587;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_OR_Finland;
    end;	  
  end;
  if (blank(CYb.VATNr)) then begin
    errcode = 20586;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (blank(CYb.OrgNr)) then begin
    errcode = 20586;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (RcvInvoicePref!=2) then begin
    if (blank(CYb.ANACode)) then begin
      errcode = 20585;
      gotofield = "VATNr";
      res = false;
     goto LValidEInvoiceData_OR_Finland;
    end;
  end;
  if (blank(CYb.EInvoiceAccount)) then begin
    errcode = 20583;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (blank(CYb.Bank1)) then begin
    errcode = 20598;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
//  if (blank(Bkpb.HTSConnectionTime)) then begin
//    errcode = 20599;
//    gotofield = "SerNr";
//    res = false;
//    goto LValidEInvoiceData_OR_Finland;
//  end;
//  if (Bkpb.HTSBackgroundTimer<=0) then begin
//    errcode = 20599;
//    gotofield = "SerNr";
//    res = false;
//    goto LValidEInvoiceData_OR_Finland;
//  end;
  if (CheckAddr3CorrectforEInvoicing(ORr.Addr3)==false) then begin
    errcode = 20582;
    gotofield = "Addr3";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  curncycode = ORr.CurncyCode;
  if (blank(curncycode)) then begin
    curncycode = BCb.BaseCur1;
  end;
/*JJCUR  
  if (blank(ORr.CurncyCode)) then begin
    errcode = 1582;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
*/  
  if (curncycode!=BCb.BaseCur1) then begin
    errcode = 1582;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (CurencyCodeIsISO(curncycode)==false) then begin
    errcode = 20591;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
/*
  if (CheckInternetEnabler==false) then begin
    errcode = 20242;
    gotofield = "SerNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
*/
  if (blank(ORr.VATNr)) then begin
    errcode = 20592;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (blank(CUr.ANACode)) then begin
    errcode = 20588;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
  if (blank(ORr.Addr2)) or (blank(ORr.Addr3)) then begin
    errcode = 1058;
    gotofield = "Addr1";
    res = false;
    goto LValidEInvoiceData_OR_Finland;
  end;
LValidEInvoiceData_OR_Finland:;  
  ValidEInvoiceData_OR_Finland = res;
  return;
end;

global
function Boolean ValidEInvoiceData_OR(record ORVc ORr,record CUVc CUr,var Integer errcode,var string gotofield)
begin
  Boolean res;
  record InternetEnablerBlock IEb;
  record CYBlock CYb;
  record BaseCurBlock BCb;
  record BackupBlock Bkpb;
  
  res = true;
  if (EInvoiceForCustomer(0,ORr.CurncyCode,CUr)) then begin
    BlockLoad(IEb);
    BlockLoad(CYb);
    BlockLoad(BCb);
    BlockLoad(Bkpb);
    switch (IEb.RegInCountry) begin
      case 2:
//        res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
      case 3:
//        res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
      case 4:
        res = ValidEInvoiceData_OR_Finland(ORr,CYb,CUr,Bkpb,BCb,errcode,gotofield);
      case 26:
//        res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
      case 99://HansaMail
        switch (CurrentCompany) begin
          case 24:
//            res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
          case 39:
//            res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
          case 40:
            res = ValidEInvoiceData_OR_Finland(ORr,CYb,CUr,Bkpb,BCb,errcode,gotofield);
          case 82:
//            res = ValidEInvoiceData_OR_Sweden(IVr,CYb,CUr,errcode,gotofield);
        end;
    end;
  end;  
LValidEInvoiceData_OR:;  
  ValidEInvoiceData_OR = res;
  return;
end;

global
function Boolean ORCheckForSalesmanPrice(record ORVc ORr, Integer i, string location, string pricelist, var string errstr)
begin 
  record INVc INr;
  val t,s,price,vreb;
  val baseprice,calcprice;
  val temp,tax2prc;
  string 255 curitemname,salesacc,vatcode,tax2code,taxtemplatecode;
  Boolean dummyf,calcpricef,res;
  row ORVc ORrw;
  Time blankt;
  
  res = true;
  if nonblank(pricelist) then begin
    MatRowGet(ORr,i,ORrw);
    if (GetItemPriceDiscount3(ORrw.ArtCode,ORrw.Quant,INr,ORr.CurncyCode,
                                ORr.FrRate,ORr.ToRateB1,ORr.ToRateB2,ORr.BaseRate1,ORr.BaseRate2,
                                ORr.LangCode,ORr.CustCat,pricelist,ORr.RebCode,
                                price,curitemname,vreb,vatcode,baseprice,salesacc,
                                ORr.ExportFlag,calcpricef,ORr.OrdDate,blankt,ORr.CustCode,true,dummyf,ORr.PayDeal,tax2code,tax2prc,ORr.Region,location,taxtemplatecode)) then begin
      calcprice = ORrw.Sum/ORrw.Quant;
      if (price>calcprice) then begin
        errstr = calcprice;
        errstr = errstr & USetStr(20111) & price;
        res = false;
      end;  
    end;              
  end;
  ORCheckForSalesmanPrice = res;        
  return;
end;  